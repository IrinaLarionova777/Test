<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Class name="TCLEx.Function.Barcode">
<Super>%RegisteredObject</Super>
<TimeCreated>62194,66920.125898</TimeCreated>

<UDLText name="T">
<Content><![CDATA[
/*#USER>Set javagwconf=##class(%Net.Remote.ObjectGateway).%OpenId("BarCodeGateWay")
USER>D $SYSTEM.OBJ.Dump(javagwconf)
+----------------- general information ---------------
|      oref value: 1
|      class name: %Net.Remote.ObjectGateway
|           %%OID: $lb("BarCodeGateWay","%Net.Remote.ObjectGateway")
| reference count: 1
+----------------- attribute values ------------------
|       %Concurrency = 1  <Set>
| AllowedIPAddresses = "0.0.0.0"
|          ClassPath = "C:\BarCode\barcode.jar"
|  ConnectionTimeout = 5
|           FilePath = ""
|HeartbeatFailureAction = "R"
|HeartbeatFailureRetry = 300
|HeartbeatFailureTimeout = 30
|  HeartbeatInterval = 10
|InitializationTimeout = 5
|         JDKVersion = "JDK16"
|            JVMArgs = "-Djava.library.path=C:\BarCode;."
|           JavaHome = "C:\Program Files\Java\jdk1.6.0_24"
|            LogFile = ""
|               Name = "BarCodeGateWay"
|               Port = 55555
|             Server = "127.0.0.1"
|               Type = 1
*/
]]></Content>
</UDLText>

<Method name="Recognize">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[pFileName:%String,&pBarCodes:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	Set pBarCodes=""
	If '##class(%File).Exists(pFileName) Quit $$$ERROR($$$GeneralError,"File "_pFileName_" doesn't exist !")
	Set javagwid="BarCodeGateWay"
	Set javagwconf=##class(%Net.Remote.ObjectGateway).%OpenId(javagwid)
	If '$IsObject(javagwconf) Quit $$$ERROR($$$GeneralError,"Incorrect Java GateWay configuration !")
	Set result=$$$OK
	Try
	{
		; запуск шлюза, если он не запущен
		If $$$ISERR(##class(%Net.Remote.Service).PingGateway(javagwconf.Port,javagwconf.Server,javagwconf.InitializationTimeout)) {
			$$$ThrowOnError(##class(%Net.Remote.Service).StartGateway(javagwid))
		}	
	
		Set javagateway=##class(%Net.Remote.Gateway).%New()
		; подключение к шлюзу
		$$$ThrowOnError(javagateway.%Connect(javagwconf.Server,javagwconf.Port)) ;,,ClassPath
		Try
		{
			Set barcodeobj=##class(Softek.Barcode).%New(javagateway)
			Do barcodeobj.setReadCode128(1) ;распознавать code128
			Do barcodeobj.setMultipleRead(1) ;искать больше одного штрих-кода
			Do barcodeobj.setSkewTolerance(5) ;распознавать штрих-коды под углом до 45°
			Do barcodeobj.setPrefOccurrence(2) ;
			Do barcodeobj.setQuietZoneSize(10) ; маленькая неизспользуемая при анализе область
			Set count=barcodeobj.ScanBarCode(pFileName)
			For i=1:1:count {
				Set rect=barcodeobj.GetBarStringRect(i)
				Set vals=$ListBuild(barcodeobj.GetBarString(i),rect.getX(),rect.getY())
				Set pBarCodes=pBarCodes_$ListBuild(vals)
			}
			
		}
		Catch exception
		{
			Set result=exception.AsStatus()
		}
		; отключение от шлюза
		Do javagateway.%Disconnect()
	}
	Catch exception
	{
		Set result=exception.AsStatus()
	}
	Quit result
]]></Implementation>
</Method>
</Class>
</Export>
