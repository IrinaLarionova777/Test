<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF407RU03" type="INT" languagemode="0" generated="1"><![CDATA[
MIF407RU03(mi) ; ASTM protocol  -  DXI (Bi-Directional) (Russion BION))
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),nak=$c(21)
 s lf=$c(10),cr=$c(13),(result,epis,containernumber)=""
 i $$start^MIF000() q
 d trace^MIF000(mi,"Started job="_$j,"DEBUG")
 f  d Main i $$stop^MIF000() q
 c par4
 d trace^MIF000(mi,"Closed job="_$j,"DEBUG")
 q
Main r *R:10
 ;i $c(R)'=enq d trace^MIF000(mi,"Waiting <ENQ> but received <"_R_">","H<--M") q
 i $c(R)'=enq q
 d trace^MIF000(mi,"ENQ","H<--M")
 d ACK
 f  r *R:10 q:$c(R)=eot  d
 . i $c(R)'=stx q
 . s record=$$read^MIF000("",lf) q:'$l(record)
 . s record=$e(record,1,$l(record)-1)
 . d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
 . ; check record validity
 . s chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
 . i $$CHSUM(chrecord)'=chsum d NAK q
 . d ACK
 . ; query record
 . i $e(record,2)="Q" d  q
 .. Set containernumber=$Piece($Piece(record,"|",3),"^",2)
 .. Set epis=$$Episode4Container^KSMIF000(containernumber)
 .. If epis="" Quit
 .. Set ^TMP($zn,$j,"ENQ",epis)=containernumber
 . ; patient record
 .i $e(record,2)="P" d  q
 .. d Last s (sample,epis,containernumber,surname,result,date,time,QC)=""
 .. s surname=$tr($tr($p(record,"|",6),""),"^"," ") q
 . ; order record
 .i $e(record,2)="O" d  q
 .. Set containernumber=$Piece($Piece(record,"|",3),"^",1)
 .. If containernumber="" Set containernumber=$tr($p($p(record,"|",4),"^",1)," ")
 .. ; check if received id is QC
 .. s QC="" i $l(containernumber) s QC=$$findQC^MIF000(mi,containernumber) 
 .. if QC'="" set epis=containernumber
 .. else  Set epis=$$Episode4Container^KSMIF000(containernumber)
 .. ;Set QC=$tr($p(record,"|",12)," ")
 .. ;i QC="Q" s QC="Y"
 .. ;
 . ; result record
 . i $e(record,2)="R" d  q
 .. ; strip trailing spaces
 .. s x1=$p($p(record,"|",3),"^",4) s xx1=x1 f i=$l(x1):-1 q:$e(x1,i)'=" "  s xx1=$e(x1,1,i-1)
 .. s x1=xx1,x2=$tr($p(record,"|",4),", ","."),x3=$tr($p(record,"|",7)," ")
 .. i x3="N" s x3="" ; normal flag
 .. i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 .. ; date and time
 .. i $l($tr($p(record,"|",13)," ")) d
 ... s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
 ... s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
 . ; last record
 . i $e(record,2)="L" d Last q
 d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 d ORDER
 q
Last ; file result if exists
 i $get(epis)'="",$l(result) {
	 do trace^MIF000(mi,"Write results episode="_$g(epis)_": "_$g(result)_" date="_$g(date)_" time="_$g(time)_" QC="_$g(QC),"H<--H")
	 d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 }
 s (sample,epis,containernumber,surname,result,date,time,QC)=""
 q
ORDER ; create list of orders if exists
 s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis),1,containernumber) q:epis=""  d
 . d scanone^MIF000(mi,epis)
 . i $$select^LVBEPVIS(epis,"Y") k ^TMP($zn,$j,"ENQ",epis) q
 . s sex=$s($g(PLIST(5))="лю":"M",$g(PLIST(5))="лќ":"F",1:"U")
 . s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||||||||||"
 . s tc="" 
 . f N=1:1 s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d 
 .. If (epis'=containernumber)&&('$$IsTestCode4Container^KSMIF000(containernumber,mi,tc)) Quit
 .. Do SET(containernumber,tc)
 . s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|"_$s(N>1:"F",1:"I")
 . k ^TMP($zn,$j,"ENQ",epis)
 . d Send
 q
SET(containernumber,tcx) ; set record to be sent to instrument
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_N_"|"_containernumber_"||^^^"_tcx_"|||||||N||||||||||||||Q"
 q
Send ; send list of orders if exists
 d ENQ f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 i $c(R)=enq q
 i $c(R)'=ack d EOT q
 q:$$SEND("1H|\^&||parol|LIS|||||DXI||P|1")
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 d EOT
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK w ack,*-3 d trace^MIF000(mi,"ACK","H-->M") q
NAK w nak,*-3 d trace^MIF000(mi,"NAK","H-->M") q
EOT w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
ENQ w enq,*-3 d trace^MIF000(mi,"EEQ","H-->M") q
]]></Routine>
</Export>
