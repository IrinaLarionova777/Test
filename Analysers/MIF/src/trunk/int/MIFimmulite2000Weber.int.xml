<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFimmulite2000Weber" type="INT" languagemode="0"><![CDATA[
MIFimmulite2000Weber(mi) ; ASTM protocol  -  IMMULITE 2000 (Bi-Directional) (Russion BION))
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),nak=$c(21)
 s lf=$c(10),cr=$c(13),(result,epis,containernumber)=""
 i $$start^MIF000() q
 d trace^MIF000(mi,"Started job="_$j,"DEBUG")
 for {
	 d Main 
	 if $$stop^MIF000() quit
 }
 close par4
 d trace^MIF000(mi,"Closed job="_$j,"DEBUG")
 quit
Main 
 read *R:10
 ;i $c(R)'=enq d trace^MIF000(mi,"Waiting <ENQ> but received <"_R_">","H<--M") q
 if $c(R)'=enq q
 do receiveENQ
 quit
 ;обработка полученного от прибора ENQ
receiveENQ 
 do trace^MIF000(mi,"ENQ","H<--M")
 do ACK
 set timeSilenceStart=$piece($h,",",2),timeSilenceLimit=60
 set ok=1
 for {
	 read *R:10 
	 if $c(R)=eot quit
	 if $char(R)=enq d trace^MIF000(mi,"ENQ is recieved at the wrong time","H-->M") do EOT set ok=0 quit
	 set timeSilence=$piece($h,",",2)-timeSilenceStart
	 if timeSilence>timeSilenceLimit d trace^MIF000(mi,"timeSilence>timeSilenceLimit ("_timeSilence_">"_timeSilenceLimit_")","H-->M") do EOT set ok=0 quit
     if $c(R)'=stx continue
     set timeSilenceStart=$piece($h,",",2)
     set record=$$read^MIF000("",lf) if '$l(record)  continue
     set record=$e(record,1,$l(record)-1)
     do trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
     ; check record validity
     set chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
     if $$CHSUM(chrecord)'=chsum do NAK continue
     do ACK
     set typerecord=$e(record,2)
     ; head record
     if typerecord="H" {
        set nameMachine=$piece(record,"|",5),nameLIS=$piece(record,"|",10)  
        continue
     }
     ; query record
     if typerecord="Q" {
	    Set containernumber=$Piece($Piece(record,"|",3),"^",2)
        Set epis=$$Episode4Container^KSMIF000(containernumber)
        If epis="" continue
        Set ^TMP($zn,$j,"ENQ",epis)=containernumber
        continue
     }    
     ; patient record
     if typerecord="P" {
        do Last s (sample,epis,containernumber,surname,result,date,time,QC)=""
        set surname=$tr($tr($p(record,"|",6),""),"^"," ") 
        continue
     }
     ; order record
     if typerecord="O" {
	     Set containernumber=$tr($Piece($Piece(record,"|",3),"^",1)," ","")
         If containernumber="" Set containernumber=$tr($p($p(record,"|",4),"^",1)," ")
         set QC="" if $l(containernumber) set QC=$$findQC^MIF000(mi,containernumber)
         if QC="" {
	         Set epis=$$Episode4Container^KSMIF000(containernumber)
         } else {
	         set epis=containernumber   
         }
         continue
     }
     ; result record
     if typerecord="R" {
	      ; strip trailing spaces
          set x1=$p($p(record,"|",3),"^",4) s xx1=x1 f i=$l(x1):-1 q:$e(x1,i)'=" "  s xx1=$e(x1,1,i-1)
          set x1=xx1,x2=$tr($p(record,"|",4),", ","."),x3=$tr($p(record,"|",7)," ")
          if x3="N" s x3="" ; normal flag
          if $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
          ; date and time
          if $l($tr($p(record,"|",13)," ")) {
              s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
              s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
          } 
          continue
     }
     ; last record
     if typerecord="L" {
	     d Last 
	     continue
     }
     set timeSilenceStart=$piece($h,",",2)
 }
 if ok=1 { 
    do trace^MIF000(mi,$s($c(R)=eot:"EOT",$c(R)=enq:"ENQ",1:R),"H<--M")
    do ORDER
 }
 quit
Last ; file result if exists
 if $get(epis)'="",$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,containernumber,surname,result,date,time,QC)=""
 q
ORDER ; create list of orders if exists
 s epis="" 
 f  s epis=$o(^TMP($zn,$j,"ENQ",epis),1,containernumber) q:epis=""  d
 . d scanone^MIF000(mi,epis)
 . i $$select^LVBEPVIS(epis,"Y") k ^TMP($zn,$j,"ENQ",epis) q
 . s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 . s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_containernumber_"|||^|||"_sex_"|||||"_$g(PLIST(15))
 . s tc="" 
 . f N=1:1 s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d 
 .. If (epis'=containernumber)&&('$$IsTestCode4Container^KSMIF000(containernumber,mi,tc)) Quit
 .. Do SET(containernumber,tc)
 . s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|"_$s(N>1:"F",1:"I")
 . k ^TMP($zn,$j,"ENQ",epis)
 . d Send
 q
SET(containernumber,tcx) ; set record to be sent to instrument
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_N_"|"_containernumber_"||^^^"_tcx_"|||||||N||||||||||||||Q"
 q
Send ; send list of orders if exists
 d ENQ f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 i $c(R)=enq q
 i $c(R)'=ack d EOT q
 q:$$SEND("1H|\^&||parol|LIS|||||"_$select($get(nameMachine)'="":nameMachine,1:"IMMULITE")_"||P|1")
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 d EOT
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK w ack,*-3 d trace^MIF000(mi,"ACK","H-->M") q
NAK w nak,*-3 d trace^MIF000(mi,"NAK","H-->M") q
EOT w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
ENQ w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M") q
]]></Routine>
</Export>
