<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF999RU02" type="INT" languagemode="0" generated="1"><![CDATA[
MIF999RU02(mi) ;Bayer Clinitek Status EMC Russia
	Set mi=$Get(mi) 
	If mi="" Quit
	Kill ^TMIF(mi,10) 
	Set $ZTRAP="err^MIF000"
	If $$select^LVBMIMP(mi) Quit
	For j=1:1:PLIST Set @("par"_j)=PLIST(j)
	If $$start^MIF000() Quit
	Do trace^MIF000(mi,"Started job="_$Job,"DEBUG")
	Kill ^||data
	For  {
		Do Main(mi)
		If $$stop^MIF000() Quit
	}	
	Close par4
	Do trace^MIF000(mi,"Stopped job="_$Job,"DEBUG")
	Quit
	// Основной цикл опроса прибора
	// Прибор кидает строки с разделителем $Char(13)
	// Одна строка - данные одного эпизода.
	// Внутри строки разделитель ","
	// 5 - я позиция - номер эпизода
	// с 13 позиции идут данные тестов. Каждый тест - это 3 значения
	// n+1 - код
	// n+2 - значение
	// n+3 -альтернативное значение (мы не используем) 
Main(mi) [par10,par11] public {
	Set record=$Piece($$ReadRecord(mi),$Char(13),1)
	If record="" Quit
	Set epis=$Translate($Piece(record,",",5)," ")
	If epis="" Quit
	Kill ^||data
	Set val=$$DeleteLeftAndRightSpaces^KSUTIL1($Piece(record,",",11))
	If val'="" {
		Set ^||data("color")=""""_val_""""
	}	
	Set val=$$DeleteLeftAndRightSpaces^KSUTIL1($Piece(record,",",12))
	If val'="" {
		Set ^||data("clarity")=""""_val_""""
	}	
	Set cnt=$Length(record,",") 
	For i=13:3:cnt {
		Set testcode=$$DeleteLeftAndRightSpaces^KSUTIL1($Piece(record,",",i))
		Set val=$$DeleteLeftAndRightSpaces^KSUTIL1($Piece(record,",",i+1))
		// Set alternativeval=$$DeleteLeftAndRightSpaces^KSUTIL1($Piece(record,",",i+2)) - в EMC нам не нужно
		If (testcode'="")&&(val'="") {
			Set ^||data(testcode)=""""_val_""""
		}	
	}
	Set result=""
	Set testcode=""
	For  {
		Set testcode=$Order(^||data(testcode),1,val)
		If testcode="" Quit
		If val="" Continue
		Set result=result_testcode_$Char(par10)_val_$Char(par11)
	}
	If result'="" {
		Do file^MIF000(mi,"",epis,"",result,"","","")
	}		
}	
	// Чтение записи с прибора, пока не кочится данные или не встретиться $Char(13)
ReadRecord(mi) public {
	Set result=""
	For  {
		Read *Y:5
		If Y=-1 Quit
		Set char=$Char(Y)
		Set result=result_char
		If char=$Char(13) Quit
	}
	If result'="" Do trace^MIF000(mi,$Horolog_" : "_result,"H<--M")
	Quit result
}
 
]]></Routine>
</Export>
