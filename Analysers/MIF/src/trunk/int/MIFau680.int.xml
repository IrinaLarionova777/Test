<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFau680" type="INT" languagemode="0"><![CDATA[
MIFau680(mi) ; OLYMPUS AU680 (Bi-Directional) (Russion EMC)
 set mi=$get(mi) if '$length(mi) quit
 kill ^TMIF(mi,10) set $ztrap="err^MIF000"
 if $$select^LVBMIMP(mi) quit
 for j=1:1:PLIST set @("par"_j)=PLIST(j)
 set stx=$char(2),etx=$char(3),ack=$char(6),nak=$char(15),etb=$char(23)
 set (sample,epis,surname,result,date,time,QC)=""
 if $$start^MIF000() quit
 do trace^MIF000(mi,"Started job="_$job,"DEBUG")
 //write stx,etx,*-3 do trace^MIF000(mi,"ping","H-->M")
 //read *a:5 do trace^MIF000(mi,"response "_a,"M-->H")
 for {
	 do Main 
	 if $$stop^MIF000() quit
 }
 close par4
 quit
Main 
 NEW mainrecord,nrecord,nn
 set mainrecord=$$read^MIF000(stx,etb_","_etx) 
 quit:'$length(mainrecord)
 do trace^MIF000(mi,mainrecord,"H<--M")
 do ACK
 Set nrecord=$LENGTH(mainrecord,stx)
 For nn=1:1:nrecord {
 	Set record=$PIECE(mainrecord,stx,nn)
 	If '$Length(record) CONTINUE
 	Do SubMain
 }	 
 QUIT
SubMain
 set specimenLetters="UXYW" 
 set type=$extract(record,1,2)
 // "R " запрос тестов для анализа, "RH" - повторный запрос тестов для анализа 
 if (type="R ")||(type="RH") {
	 kill ^TMIF(mi,10)
     ; create order to send to instrument
     set str="S "_$extract(record,3,25)_"    E"
     Set containernumber=+$tr($e(record,14,25)," ","")
     Set epis=$$Episode4Container^KSMIF000(containernumber)
     If epis="" Quit
     s tg="" s sampid=$e(record,12),samptype=$tr($e(record,9)," ","") //set tg="" set sampid=$extract(record,12),samptype=$extract(record,11)
     if sampid="0",samptype=" " set tg=sampid
     if sampid="E" set tg=sampid
     if samptype="U" set tg=samptype
     do scanone^MIF000(mi,epis,"")
     //if $$select^LVBEPVIS(epis,"Y") kill ^TMP("MIF-SINGLE",$job,mi,epis) quit
     set (tc,tcx)="" 
     if $data(^TMP("MIF-SINGLE",$job,mi,epis)) {  
        Set i="" 
        For {
	        Set i=$Order(^TMP("MIF-SINGLE",$job,mi,epis,i)) Quit:i="" 
	        //If (epis'=containernumber)&&('$$IsTestCode4Container^KSMIF000(containernumber,mi,i,samptype)) Quit 
	        Set tc=i
	        set len=$length(tc)
	        set pp=$Extract(tc,len)
	        // U X Y W
	        if (samptype'="")&&(samptype'="N")&&(pp=samptype) {
		         Set tc=$Extract(tc,1,len-1)
		         Set tcx=tcx_tc
		         continue
	        }
	        // specimen type is not defined
	        if samptype="N" {
		         if $find(specimenLetters,pp) set tc=$extract(tc,1,len-1)
		         Set tcx=tcx_tc
		         continue
	        }
	        // Serum
	        if samptype="" {
		         if $find(specimenLetters,pp) continue
		         Set tcx=tcx_tc
	        }
        }
        kill ^TMP("MIF-SINGLE",$job,mi,epis)
     }
     set str=str_tcx 
     set line=$order(^TMIF(mi,10,""),-1)+1
     set ^TMIF(mi,10,line)=str
     if $data(^TMIF(mi,10)) do SEND kill ^TMIF(mi,10)
     quit
 }
 // auto repeat
 if (type="Rh") {
	 set line=$order(^TMIF(mi,10,""),-1)+1
     set ^TMIF(mi,10,line)="SE"
     if $data(^TMIF(mi,10)) do SEND kill ^TMIF(mi,10)
     quit
 }
 // end query
 /*if (type="RE") {
	 set line=$order(^TMIF(mi,10,""),-1)+1
     set ^TMIF(mi,10,line)="SE"
     if $data(^TMIF(mi,10)) do SEND kill ^TMIF(mi,10)
     quit
 }*/
 ; process result record
 if type="D "!(type="d ")!(type="DQ")!(type="DH")!(type="dH") {
     for i=1:1:$length(record,etb) {
		 set rec=$piece(record,etb,i)
		 quit:rec=""  
		 set:$extract(rec)=$char(2) rec=$piece(rec,stx,2)
		 set (sample,epis,surname,result,date,time,QC)=""
         Set containernumber=+$Translate($Extract(rec,14,25)," ")
         set epis=$$Episode4Container^KSMIF000(containernumber)
         set samptype=$extract(rec,9),xrec=$extract(rec,31,999) ; 12 digit epis
         for {
	         set res=$extract(xrec,1,14) 
	         set xrec=$extract(xrec,15,999)
	         Set samptype=$Select(samptype="U":"U",samptype="X":"X",samptype="Y":"Y",samptype="W":"W",1:"")
	         //If epis'=containernumber {
		     //    Set conttype=$$ContainerType4Container^KSMIF000(epis,containernumber)
		     //    If conttype="T051" Set samptype="Z" ; Глюкоза с нагрузкой...
	         //}
	         set test=$translate($extract(res,1,3)," ")_samptype
	         set res=$translate($extract(res,4,12)," ")
	         set flag=$translate($extract(res,13,14)," ")
	         ; strip flags
	         set flag=$translate(flag,"1234567FGHLNPrpx","")
	         set result=result_test_$char(par10)_res_$char(par10)_flag_$char(par11)
	         quit:$length(xrec)<14
         }
         ; extract QC details
         if type="DQ" set QC="Y"
         
         if $length(epis),$length(result) {
	         do trace^MIF000(mi,"Write results episode="_epis_": "_result_" date="_date_" time="_time_" QC="_QC,"H<--H")
	         do file^MIF000(mi,sample,epis,surname,result,date,time,QC)  
         }
     }
  }
 quit
 ; send 'ack' to instrument
ACK write ack,*-3
 do trace^MIF000(mi,"ACK","H-->M")
 quit
 ; send string to instrument
SEND 
 hang 1 ; Olympus need little delay to be read recieve orders 
 set x="" for  set x=$order(^TMIF(mi,10,x)) quit:x=""  do
 .set str=^(x) for i=1:1:2 do  quit:ok
 ..set ok=""
 ..write stx,str,etx,*-3 do trace^MIF000(mi,str,"H-->M")
 ..read *a:5 do trace^MIF000(mi,$select($char(a)=ack:"ACK",$char(a)=nak:"NAK",1:$char(a)),"H<--M")
 ..if $char(a)=ack set ok=1
 quit
]]></Routine>
</Export>
