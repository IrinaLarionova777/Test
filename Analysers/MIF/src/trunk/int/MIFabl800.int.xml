<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFabl800" type="INT" languagemode="0"><![CDATA[
MIFabl800(mi) ; ASTM protocol  -  ABL 800 (RADIOMETER) (Bi-Directional) (Russion Burdenko))
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),nak=$c(21),etb=$c(23)
 s lf=$c(10),cr=$c(13)
 set (sample,epis,surname,result,date,time,QC)=""
 i $$start^MIF000() q
 d trace^MIF000(mi,"Started job="_$j,"DEBUG")
 for {
	 d Main 
	 if $$stop^MIF000() quit
 }
 c par4
 quit
Main 
 read *R:10
 ;i $c(R)'=enq d trace^MIF000(mi,"Waiting <ENQ> but received <"_R_">","H<--M") q
 i $c(R)'=enq q
 set nameMachine="",nameLIS=""
 do receiveENQ
 quit
 ;обработка полученного от прибора ENQ
receiveENQ 
 do trace^MIF000(mi,"ENQ","H<--M")
 do ACK
 set timeSilenceStart=$piece($h,",",2),timeSilenceLimit=60,ok=1
 set buffer=""
 for {
	 read *R:10 
	 if $c(R)=eot quit
	 if $char(R)=enq d trace^MIF000(mi,"ENQ is recieved at the wrong time","H-->M") do EOT set ok=0 quit
	 set timeSilence=$piece($h,",",2)-timeSilenceStart
	 if timeSilence>timeSilenceLimit d trace^MIF000(mi,"timeSilence>timeSilenceLimit ("_timeSilence_">"_timeSilenceLimit_")","H-->M") do EOT set ok=0 quit
     if $c(R)'=stx continue
     set timeSilenceStart=$piece($h,",",2)
     do trace^MIF000(mi,"STX","H<--M")
     set record=$$read^MIF000("",lf) if '$l(record)  continue
     do trace^MIF000(mi,record,"H<--M")
     set record=$e(record,1,$l(record)-1) // LF отбрасываем
     ; check record validity
     set endtext=$select('$find(record,etx):etb,1:etx)
     set chsum=$p(record,endtext,2),chrecord=$piece(record,endtext,1)_endtext
     if $$CHSUM(chrecord)'=chsum do NAK continue
     do ACK
     set record=$piece(record,endtext,1)
     set record=$e(record,2,$l(record)) ;на 1-ом месте стоит номер записи - на записях-продолжениях он мешает
     if endtext=etb {
	     set buffer=buffer_record
	     continue
     }
     set buffer=buffer_record
     if buffer="" continue
     do trace^MIF000(mi,"buffer="_buffer,"H<--M")
     set (sample,epis,surname,result,date,time,QC)=""
     for pp=1:1:$length(buffer,$c(13)) {
	     set record=$piece(buffer,$c(13),pp)
	     if record="" continue 
	     set typerecord=$piece(record,"|",1)
	     if typerecord="" continue
	     set typerecord=$select($length(typerecord)=1:typerecord,1:$e(typerecord,2))
	     do trace^MIF000(mi,typerecord_" "_record,"H<--M")
	     ; head record
         if typerecord="H" {
            set nameMachine=$piece(record,"|",5),nameLIS=$piece(record,"|",10)  
            continue
         }
         ; query record
         if typerecord="Q" {
	        set epis=$p($p(record,"|",3),"^",1)
	        //If epis="ALL" Do PrepareAllOrders(mi) continue
	        if $length(epis) set ^TMP($zn,$j,"ENQ",epis)="" 
            continue
         }    
         ; patient record
         if typerecord="P" {
             do Last s (sample,epis,surname,result,date,time,QC)=""
             //set surname=$tr($tr($p(record,"|",6),""),"^"," ") 
             set epis=$tr($p($p(record,"|",4),"^",1)," ") 
             continue
         }
         ; order record
         if typerecord="O" {
	         //set epis=$tr($p($p(record,"|",3),"^",1)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",2)," ")
	         set QC=$tr($p(record,"|",12)," ") 
             if QC="Q" s QC="Y"
             continue
         }
         ; result record
         if typerecord="R" {
             ; strip trailing spaces
             set test=$p($p(record,"|",3),"^",4) set test=$$DeleteLeftAndRightSpaces^KSUTIL1(test)
             set res=$tr($p(record,"|",4),", ",".")
             if $length(res),$isvalidnum(res) set res=+res
             set flag=$tr($p(record,"|",7)," ")
             if flag="N" s flag="" ; normal flag
             if $l(test),$l(res) s result=result_test_$c(par10)_res_$c(par10)_flag_$c(par11)
             ; date and time
             set datetime=$p(record,"|",12)
             if $length($tr(datetime," ")) {
	           s date=$$intdate^SSUTIL4($e(datetime,5,6)_"/"_$e(datetime,7,8)_"/"_$e(datetime,1,4))
               s time=($e(datetime,9,10)*3600)+($e(datetime,11,12)*60)
             }
             continue
         }
         ; last record
         if typerecord="L" {
	        d Last 
	        continue
         }
    }
    d Last 
    set buffer=""
    set timeSilenceStart=$piece($h,",",2)
 } 
 if ok=1 {
	 do trace^MIF000(mi,$s($c(R)=eot:"EOT",$c(R)=enq:"ENQ",1:R),"H<--M")
	 do ORDER
 }
 quit

Last ; file result if exists
 do trace^MIF000(mi,"epis="_epis_" result="_result_" date="_date_" time="_time,"H<--H")
 if epis?1.99AN,$l(result) {
 	 do file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 }
 set (sample,epis,surname,result,date,time,QC)=""
 quit
 
  ;создание глобали ^TMIF(pInstrument,10) с заказами к прибору 
PrepareAllOrders(pInstrument) public {
	Do scanall^MIF000(pInstrument)
	Set epis="" 
	For  {
		Set epis=$Order(^TMP("MIF-SCAN",$Job,pInstrument,epis))
		If epis="" Quit
		if $$select^LVBEPVIS(epis,"Y") continue
		set ^TMP($zn,$j,"ENQ",epis)="" 
	}
}	 
 
ORDER ; create list of orders if exists
 s epis="" 
 for {
	 set epis=$o(^TMP($zn,$j,"ENQ",epis)) quit:epis="" 
     do scanone^MIF000(mi,epis)
     if $$select^LVBEPVIS(epis,"Y") k ^TMP($zn,$j,"ENQ",epis) continue
     ;
     ;do trace^MIF000(mi,"epis="_epis_"$data="_$data(^TMP("MIF-SINGLE",$j,mi,epis))_" $o="_$order(^TMP("MIF-SINGLE",$j,mi,epis,"")),"H<--H")
     ;
     set sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
     ;s sex=$s($g(PLIST(5))="М":"M",$g(PLIST(5))="Ж":"F",1:"U")
     set surname=$get(PLIST(3)),name=$get(PLIST(4)),birthday=$zdate($get(PLIST(6)),8) 
     ;s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_birthday_"|"_sex_"|||||" ;_$g(PLIST(15))
     set line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||"_epis_"||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_birthday_"|"_sex_"|||||" ;_$g(PLIST(15))
     ;s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||||||||||"
     ;set ^TMIF(mi,10,line)="P|1||"_epis_"||^|||U||||||^|^^^|^|^||||||||"
     set tc="" 
     for N=1:1 {
	     set tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) 
	     if tc="" quit
	     do trace^MIF000(mi,"epis="_epis_"tc="_tc,"H<--H")
	     do SET(epis,tc)
     }
     set line=$order(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|"_$s(N>1:"F",1:"I")
     kill ^TMP($zn,$j,"ENQ",epis)
     d Send
 }
 quit
SET(epis,tcx) ; set record to be sent to instrument
 set line=$order(^TMIF(mi,10,""),-1)+1
 set ^TMIF(mi,10,line)="O|"_N_"|"_epis_"||^^^"_tcx_"|||||||A||||||||||||||Q"
 quit
Send ; send list of orders if exists
 do ENQ 
 for j=1:1:10 {
	 read *R:1 
	 quit:$char(R)=ack!($char(R)=enq)  
	 //d trace^MIF000(mi,"Waiting <ACK> or <EOT> (10) but received <"_R_">","H<--M")
 }
 if $c(R)=enq quit
 if $c(R)'=ack do EOT quit
 do trace^MIF000(mi,"ACK","H<--M")
 set recordHead="1H|\^&|||"_$select($get(nameLIS)'="":nameLIS,1:"LIS")_"|||||"_$select($get(nameMachine)'="":nameMachine,1:"DEVICE")_"||P|1|"_$Translate($ZDateTime($Horolog,8)," :")
 quit:$$SEND(recordHead)
 set x="" for FRAME=2:1 set x=$Order(^TMIF(mi,10,x)) quit:x=""  quit:$$SEND(FRAME#8_^TMIF(mi,10,x))
 if x="" kill ^TMIF(mi,10)
 do EOT
 quit
SEND(str) ; send string to instrument
 set pp=$piece(str,"|",1),pp=$extract(pp,$length(pp))
 set endsymbol=$select(pp="L":etx,1:etb)
 set str=str_cr_endsymbol,chsum=$$CHSUM(str)
 write stx,str,chsum,cr,lf,*-3 
 do trace^MIF000(mi,str_chsum,"H-->M")
 for j=1:1:6 {
	 read *R:1 
	 quit:($c(R)=ack)!($c(R)=eot)
	 //d trace^MIF000(mi,"Waiting <ACK> or <EOT> (6) but received <"_R_">","H<--M")
 }
 if $c(R)=ack do trace^MIF000(mi,"ACK","H<--M") quit 0
 if $c(R)=eot do trace^MIF000(mi,"EOT","H<--M") quit 1
 do trace^MIF000(mi,R,"H<--M")
 quit 2
CHSUM(x) ; calculate check sum
 new (x) 
 set z=0 for y=1:1:$length(x) set z=z+$a(x,y)
 quit $tr($j($zhex(z#256),2)," ","0")
 
ACK write ack,*-3 do trace^MIF000(mi,"ACK","H-->M") quit
NAK write nak,*-3 do trace^MIF000(mi,"NAK","H-->M") quit
EOT write eot,*-3 do trace^MIF000(mi,"EOT","H-->M") quit
ENQ write enq,*-3 do trace^MIF000(mi,"ENQ","H-->M") quit
]]></Routine>
</Export>
