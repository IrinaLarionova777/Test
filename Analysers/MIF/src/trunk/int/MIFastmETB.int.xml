<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFastmETB" type="INT" languagemode="0"><![CDATA[
MIFastmETB(mi) ; ASTM protocol с обработкой ETB
 set mi=$get(mi) if '$length(mi) quit
 kill ^TMIF(mi,10) 
 //set $ztrap="err^MIF000dop"
 if $$select^LVBMIMP(mi) quit
 for j=1:1:PLIST set @("par"_j)=PLIST(j)
 set stx=$char(2),etx=$char(3),ack=$char(6),enq=$char(5),eot=$char(4),nak=$char(21),etb=$char(23)
 set lf=$char(10),cr=$char(13)
 set (sample,epis,surname,result,date,time,QC)=""
 if $$start^MIF000() quit
 do trace^MIF000(mi,"Started job="_$job,"DEBUG")
 //set compiled=$listget(^rINDEX($zname,"OBJ"),1)
 for {
	 do Main 
	 if $$stop^MIF000() quit
	 //if $listget(^rINDEX($zname,"OBJ"))'=compiled ztrap "EDIT"  // эмуляция ошибки EDIT, если программа-драйвер изменилась
 }
 close par4
 quit
Main 
 read *R:10
 ;i $c(R)'=enq d trace^MIF000(mi,"Waiting <ENQ> but received <"_R_">","H<--M") q
 if $char(R)'=enq quit
 set nameMachine="",nameLIS=""
 set (sample,epis,surname,result,date,time,QC)=""
 do receiveENQ
 do ORDER
 quit
 ;обработка полученного от прибора ENQ
receiveENQ 
 do trace^MIF000(mi,"ENQ","H<--M")
 do ACK
 set buffer=""
 for {
	 read *R:10 
	 if $char(R)=eot quit
     if $char(R)'=stx continue
     set record=$$read^MIF000("",lf) if '$length(record)  continue
     do trace^MIF000(mi,record,"H<--M")
     set record=$extract(record,1,$length(record)-1)
     ;do trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
     ; check record validity
     set endtext=$select('$find(record,etx):etb,1:etx)
     set chsum=$piece(record,endtext,2),chrecord=$piece(record,endtext,1)_endtext
     if $$CHSUM(chrecord)'=chsum do NAK continue
     do ACK
     set record=$piece(record,endtext,1)
     set record=$extract(record,2,$length(record)) ;на 1-ом месте стоит номер записи - на записях-продолжениях он мешает
     if endtext=etb {
	     set buffer=buffer_record
	     continue
     }
     set buffer=buffer_record
     if buffer="" continue
     do trace^MIF000(mi,"buffer="_buffer,"H<--M")
     for pp=1:1:$length(buffer,$char(13)) {
	     set record=$piece(buffer,$char(13),pp)
	     if record="" continue 
	     set typerecord=$piece(record,"|",1)
	     if typerecord="" continue
	     set typerecord=$select($length(typerecord)=1:typerecord,1:$extract(typerecord,2))
	     do trace^MIF000(mi,typerecord_" "_record,"H<--M")
	     ; head record
         if typerecord="H" {
             set nameMachine=$piece(record,"|",5),nameLIS=$piece(record,"|",10)  
             continue
         }
         ; query record
         if typerecord="Q" {
	        set epis=$piece($piece(record,"|",3),"^",2)
	        if $length(epis) set ^TMP($zname,$job,"ENQ",epis)="" continue
            ;if $l(epis),$d(^TEPI(epis,1)) set ^TMP($zn,$j,"ENQ",epis)="" continue
            ;set suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
            ;if $l(epis),$d(^TEPI(epis,1)) set ^TMP($zn,$j,"ENQ",epis)=suffix continue
            ;set epis=$p($p(record,"|",3),"^",2) if $l(epis) set ^TMP($zn,$j,"ENQ",epis)=""
            continue
         }    
         ; patient record
         if typerecord="P" {
            do Last set (sample,epis,surname,result,date,time,QC)=""
            set surname=$translate($translate($piece(record,"|",6),""),"^"," ") 
            continue
         }
         ; order record
         if typerecord="O" {
	        set epis=$translate($piece($piece(record,"|",3),"^",1)," "),QC=$translate($piece(record,"|",12)," ") //s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
	        // проверяем , не есть ли это контроль качества
            set QC="" if $length(epis) set QC=$$findQC^MIF000(mi,epis) ; check if received id is QC
            if QC="Q" set QC="Y"
            continue
         }
         ; result record
         if typerecord="R" {
            ; strip trailing spaces
            set x1=$piece($piece(record,"|",3),"^",4) set xx1=x1 for i=$length(x1):-1 quit:$extract(x1,i)'=" "  set xx1=$extract(x1,1,i-1)
            set x1=xx1,x2=$translate($piece(record,"|",4),", ","."),x3=$translate($piece(record,"|",7)," ")
            set resultaspect=$translate($piece($piece(record,"|",3),"^",8)," ")
            if x3="N" set x3="" ; normal flag
            if resultaspect="DOSE" if $length(x1),$length(x2) set result=result_x1_$char(par10)_x2_$char(par10)_x3_$char(par11)
            ; date and time
            set datetime=$piece(record,"|",13)
            if $length($translate(datetime," ")) {
	            set date=$$intdate^SSUTIL4($extract(datetime,5,6)_"/"_$extract(datetime,7,8)_"/"_$extract(datetime,1,4))
                set time=($extract(datetime,9,10)*3600)+($extract(datetime,11,12)*60)
            }
            continue
         }
         ; last record
         if typerecord="L" {
	        do Last 
	        continue
         }
    }
    set buffer=""
 } 
 do trace^MIF000(mi,$select($char(R)=eot:"EOT",1:R),"H<--M")
 quit
Last ; file result if exists
 do trace^MIF000(mi,"Write results episode="_epis_": "_result_" date="_$get(date)_" time="_$get(time)_" QC="_$get(QC),"H<--H")
 if epis?1.99AN,$length(result) do file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 set (sample,epis,surname,result,date,time,QC)=""
 quit
ORDER ; create list of orders if exists
  // посылка ранее не посланных заданий 
 set epis=""
 for {
	 set epis=$order(^TMIF(mi,10,epis)) quit:epis="" 
	 do Send(epis)
 }
  // создание и посылка новых заданий 
 set epis="" 
 for {
	 set epis=$order(^TMP($zname,$job,"ENQ",epis)) quit:epis="" 
     do scanone^MIF000(mi,epis)
     if $$select^LVBEPVIS(epis,"Y") kill ^TMP($zname,$job,"ENQ",epis) continue
     set sex=$select($get(PLIST(5))="M":"M",$get(PLIST(5))="F":"F",1:"U")
     ;s sex=$s($g(PLIST(5))="М":"M",$g(PLIST(5))="Ж":"F",1:"U")
     set surname=$get(PLIST(3)),name=$get(PLIST(4)),birthday=$zdate($get(PLIST(6)),8) 
     // header record
     set line=$order(^TMIF(mi,10,epis,""),-1)+1,^TMIF(mi,10,epis,line)="H|\^&|||"_$select($get(nameLIS)'="":nameLIS,1:"LIS")_"|||||"_$select($get(nameMachine)'="":nameMachine,1:"Centaur")_"||P|1|" 
     // patient record
     ;s line=$o(^TMIF(mi,10,epis,""),-1)+1,^TMIF(mi,10,epis,line)="P|1|"_epis_"|||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_birthday_"|"_sex_"|||||" ;_$g(PLIST(15))
     set line=$order(^TMIF(mi,10,epis,""),-1)+1,^TMIF(mi,10,epis,line)="P|1|"_epis_"|||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_"|"_sex_"|||||" ;_$g(PLIST(15))
     ;s line=$o(^TMIF(mi,10,epis,""),-1)+1,^TMIF(mi,10,epis,line)="P|1|"_epis_"|||||||||||"
     // order records
     set tc="" 
     for N=1:1 {
	     set tc=$order(^TMP("MIF-SINGLE",$job,mi,epis,tc)) 
	     if tc="" quit
	     do SET(epis,tc)
     }
     // terminate record
     set line=$order(^TMIF(mi,10,epis,""),-1)+1,^TMIF(mi,10,epis,line)="L|1|"_$select(N>1:"F",1:"I")
     kill ^TMP($zname,$job,"ENQ",epis)
     do Send(epis)
 }
 quit
SET(epis,tcx) ; set record to be sent to instrument
 set line=$order(^TMIF(mi,10,epis,""),-1)+1
 set ^TMIF(mi,10,epis,line)="O|"_N_"|"_epis_"||^^^"_tcx_"|||||||A||||||||||||||Q"
 quit
Send(epis) ; send list of orders if exists
 do ENQ 
 for j=1:1:10 {
	 read *R:1 
	 quit:$char(R)=ack!($char(R)=enq)  
	 //d trace^MIF000(mi,"Waiting <ACK> or <EOT> (10) but received <"_R_">","H<--M")
 }
 if $char(R)=enq quit
 if $char(R)'=ack do EOT quit
 do trace^MIF000(mi,"ACK","H<--M")
 set x="" for FRAME=1:1 set x=$Order(^TMIF(mi,10,epis,x)) quit:x=""  quit:$$SEND(FRAME#8_^TMIF(mi,10,epis,x))
 if x="" kill ^TMIF(mi,10,epis)
 do EOT
 quit
 
SEND(str) ; send string to instrument
 set str=str_cr_etx,chsum=$$CHSUM(str)
 write stx,str,chsum,cr,lf,*-3 
 do trace^MIF000(mi,str_chsum,"H-->M")
 for j=1:1:6 {
	 read *R:1 
	 quit:($char(R)=ack)!($char(R)=eot)
	 //d trace^MIF000(mi,"Waiting <ACK> or <EOT> (6) but received <"_R_">","H<--M")
 }
 if $char(R)=ack do trace^MIF000(mi,"ACK","H<--M") quit 0
 if $char(R)=eot do trace^MIF000(mi,"EOT","H<--M") quit 1
 do trace^MIF000(mi,R,"H<--M")
 quit 2
 
CHSUM(x) ; calculate check sum
 new (x) set z=0 for y=1:1:$length(x) set z=z+$ascii(x,y)
 set z=$extract("0123456789ABCDEF",z#256\16+1)_$extract("0123456789ABCDEF",z#16+1)
 quit z
ACK write ack,*-3 do trace^MIF000(mi,"ACK","H-->M") quit
NAK write nak,*-3 do trace^MIF000(mi,"NAK","H-->M") quit
EOT write eot,*-3 do trace^MIF000(mi,"EOT","H-->M") quit
ENQ write enq,*-3 do trace^MIF000(mi,"ENQ","H-->M") quit
]]></Routine>
</Export>
