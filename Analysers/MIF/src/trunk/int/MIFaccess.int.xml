<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFaccess" type="INT" languagemode="0"><![CDATA[
MIFaccess(mi) ; ASTM protocol  -  Access (Bi-Directional) (Russion EMC))
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),nak=$c(21)
 s lf=$c(10),cr=$c(13),(result,epis)=""
 i $$start^MIF000() q
 d trace^MIF000(mi,"Started job="_$j,"DEBUG")
 for {
	 d Main 
	 if $$stop^MIF000() quit
 }
 c par4
 quit
Main 
 read *R:10
 ;i $c(R)'=enq d trace^MIF000(mi,"Waiting <ENQ> but received <"_R_">","H<--M") q
 i $c(R)'=enq q
 set nameMachine="",nameLIS=""
 do receiveENQ
 do ORDER
 quit
 ;обработка полученного от прибора ENQ
receiveENQ 
 do trace^MIF000(mi,"ENQ","H<--M")
 do ACK
 for {
	 read *R:10 
	 if $c(R)=eot quit
     if $c(R)'=stx continue
     set record=$$read^MIF000("",lf) if '$l(record)  continue
     set record=$e(record,1,$l(record)-1)
     do trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
     ; check record validity
     set chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
     if $$CHSUM(chrecord)'=chsum do NAK continue
     do ACK
     set typerecord=$e(record,2)
     ; head record
     if typerecord="H" {
        set nameMachine=$piece(record,"|",5),nameLIS=$piece(record,"|",10)  
        continue
     }
     ; query record
     if typerecord="Q" {
	     set epis=$p($p(record,"|",3),"^",2)
	     if $l(epis) set ^TMP($zn,$j,"ENQ",epis)="" continue
         ;if $l(epis),$d(^TEPI(epis,1)) set ^TMP($zn,$j,"ENQ",epis)="" continue
         ;set suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
         ;if $l(epis),$d(^TEPI(epis,1)) set ^TMP($zn,$j,"ENQ",epis)=suffix continue
         ;set epis=$p($p(record,"|",3),"^",2) if $l(epis) set ^TMP($zn,$j,"ENQ",epis)=""
         continue
     }    
     ; patient record
     if typerecord="P" {
        do Last s (sample,epis,surname,result,date,time,QC)=""
        set surname=$tr($tr($p(record,"|",6),""),"^"," ") 
        continue
     }
     ; order record
     if typerecord="O" {
	     set epis=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
         if QC="Q" s QC="Y"
         continue
     }
     ; result record
     if typerecord="R" {
         ; strip trailing spaces
         set x1=$p($p(record,"|",3),"^",4) set xx1=x1 for i=$l(x1):-1 quit:$e(x1,i)'=" "  s xx1=$e(x1,1,i-1)
         set x1=xx1,x2=$tr($p(record,"|",4),", ","."),x3=$tr($p(record,"|",7)," ")
         if x3="N" s x3="" ; normal flag
         if $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
         ; date and time
         if $l($tr($p(record,"|",13)," ")) {
	         s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
             s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
         }
         continue
     }
     ; last record
     if typerecord="L" {
	     d Last 
	     continue
     }
 } 
 do trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 quit
Last ; file result if exists
 if epis?1.99AN,$l(result) {
	 set len=$length(result,$char(par11))
	 set resultnew=result
	 for ii=1:1:len {
		 set resdata=$piece(result,$char(par11),ii) if resdata="" continue
		 set test=$piece(resdata,$char(par10),1),res=$piece(resdata,$char(par10),2)
		 set resultnum=$piece(res,"^",1),resultstring=$piece(res,"^",2)
		 set testdop=test_"_i"
		 ;если в настройках есть только строка для интерпретации (test_i) (качественный результат resultstring), то подмениваем код теста и резудьтат на качественный
		 if '$data(^TMIF(mi,3,test)),$data(^TMIF(mi,3,testdop)) {
			     set $piece(resdata,$char(par10),1)=testdop
			     set $piece(resdata,$char(par10),2)=$select(resultstring'="":resultstring,1:res)
			     set $piece(resultnew,$char(par11),ii)=resdata
			     do trace^MIF000(mi,"1:result="_result_" resultnew="_resultnew,"H<--H")
		 }
		 ;если в настройках есть только строка для интерпретации только численного результата (нет test_i), то записываем только численный результат
		 if $data(^TMIF(mi,3,test)),'$data(^TMIF(mi,3,testdop)) {
			     set $piece(resdata,$char(par10),2)=$select(resultnum'="":resultnum,1:res)
			     set $piece(resultnew,$char(par11),ii)=resdata
			     do trace^MIF000(mi,"2:result="_result_" resultnew="_resultnew,"H<--H")
		 }
		 ;если в результате есть и колич и качест результаты 
	     if (resultnum'="")&&(resultstring'="") {
		     set testdoplis=""
		     ;если в настройках есть и тест test и test_i, то пишем оба результата
		     if $data(^TMIF(mi,3,test)),$data(^TMIF(mi,3,testdop)) {
			     set ti=""
			     for {
				       set ti=$order(^TMIF(mi,3,testdop,ti)) quit:ti="" 
			           set xx=$get(^TMIF(mi,2,ti)),direction=$piece(xx,"\",3) ;,suffix=$piece(xx,"\",4) 
			           if '(" IL "[(" "_direction_" ")) continue
			           ;if '$data(^TMIF(mi,2,ti,1,resultstring)) continue
			           set testdoplis=ti quit
		         }
		         if testdoplis'="" {
			         set $piece(resdata,$char(par10),2)=resultnum
		             set $piece(resultnew,$char(par11),ii)=resdata
			         set resultnew=resultnew_testdop_$c(par10)_resultstring_$c(par10)_$piece(resdata,$char(par10),3)_$c(par11)
			         do trace^MIF000(mi,"3:result="_result_" resultnew="_resultnew,"H<--H")
		         }
		     }
		
	     }
	 }
	 set result=resultnew
	 do trace^MIF000(mi,"4:write result="_result,"H<--H")
	 do file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 }
 set (sample,epis,surname,result,date,time,QC)=""
 quit
ORDER ; create list of orders if exists
 s epis="" 
 for {
	 set epis=$o(^TMP($zn,$j,"ENQ",epis)) quit:epis="" 
     do scanone^MIF000(mi,epis)
     if $$select^LVBEPVIS(epis,"Y") k ^TMP($zn,$j,"ENQ",epis) continue
     s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
     ;s sex=$s($g(PLIST(5))="М":"M",$g(PLIST(5))="Ж":"F",1:"U")
     set surname=$get(PLIST(3)),name=$get(PLIST(4)),birthday=$zdate($get(PLIST(6)),8) 
     ;s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_birthday_"|"_sex_"|||||" ;_$g(PLIST(15))
     s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_"|"_sex_"|||||" ;_$g(PLIST(15))
     ;s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||||||||||"
     s tc="" f N=1:1 s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d SET(epis,tc)
     s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|"_$s(N>1:"F",1:"I")
     k ^TMP($zn,$j,"ENQ",epis)
     d Send
 }
 quit
SET(epis,tcx) ; set record to be sent to instrument
 set ll=$length(tcx)
 if $e(tcx,ll-1,ll)="_i" set tcx=$e(tcx,1,ll-2)
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_N_"|"_epis_"||^^^"_tcx_"|||||||A||||||||||||||Q"
 q
Send ; send list of orders if exists
 d ENQ f j=1:1:10 r *R:1 quit:$c(R)=ack!($c(R)=enq)  ;d trace^MIF000(mi,"Waiting <ACK> or <EOT> (10) but received <"_R_">","H<--M")
 i $c(R)=enq q
 i $c(R)'=ack d EOT q
 d trace^MIF000(mi,"ACK","H<--M")
 set recordHead="1H|\^&|||"_$select($get(nameLIS)'="":nameLIS,1:"LIS")_"|||||"_$select($get(nameMachine)'="":nameMachine,1:"ACCESS^505081")_"||P|1|"_$Translate($ZDateTime($Horolog,8)," :")
 q:$$SEND(recordHead)
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 d EOT
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 q:($c(R)=ack)!($c(R)=eot)  ;d trace^MIF000(mi,"Waiting <ACK> or <EOT> (6) but received <"_R_">","H<--M")
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK w ack,*-3 d trace^MIF000(mi,"ACK","H-->M") q
NAK w nak,*-3 d trace^MIF000(mi,"NAK","H-->M") q
EOT w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
ENQ w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M") q
]]></Routine>
</Export>
