<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF435RU01" type="INT" languagemode="0" generated="1"><![CDATA[
MIF435RU01(mi) ; ML 14/11/01 ; ASTM protocol  -  BECKMAN ACL 9000/10000/Elite Pro (Russion BION)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),mID=""
 s lf=$c(10),cr=$c(13),nak=$c(21),(result,epis,containernumber)="" k ^TMIF(mi,10) 
 i $$start^MIF000() q
 Do trace^MIF000(mi,"Started job="_$Job,"DEBUG")
 f  d Main i $$stop^MIF000() q
 c par4
 Do trace^MIF000(mi,"Stopped job="_$Job,"DEBUG")
 q
Main r *R:10 /* e  d  q
 .d:" L "[(" "_par9_" ") loadlist^MIF000(mi)
 .d:" BU "[(" "_par9_" ") SCAN*/
 i $c(R)'=enq q
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK
 .f  r *R:10 q:$c(R)=eot  q:R=-1  d
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",lf) q:'$l(record)
 ..s record=$e(record,1,$l(record)-1)
 ..d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
 .. ; check record validity
 ..s chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
 ..i $$CHSUM(chrecord)'=chsum d  q
 ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 ..d ACK
 .. ; query record
 ..i $e(record,2)="Q" d  q
 ... /*i $p($p(record,"|",3),"^",1)="ALL" s ^TMP($zn,$j,"ENQ")="ALL" q*/
 ... Set containernumber=$Piece($Piece(record,"|",3),"^",2)
 ... Set epis=$$Episode4Container^KSMIF000(containernumber)
 ... If (epis="")||('$Data(^TEPI(epis,1))) Quit
 ... Set ^TMP($zn,$j,"ENQ",epis)=containernumber
 .. ; header record
 ..i $e(record,2)="H" d  q
 ...s mID=$p(record,"|",5)
 ...d Last 
 ... Set (sample,epis,containernumber,surname,result,date,time,QC)=""
 .. ; patient record
 ..i $e(record,2)="P" d  q
 ...d Last 
 ... Set (sample,epis,containernumber,surname,result,date,time,QC)=""
 ...s surname=$tr($tr($p(record,"|",6)," "),"^"," "),surname=$p(surname,cr,1),surname="Surname"
 .. ; order record
 ..i $e(record,2)="O" d  q
 ... set QC=$tr($p(record,"|",12)," ") 
 ... if QC="Q" s QC="Y"
 ... if QC'="Y" do  
 .... Set containernumber=$Piece($Piece(record,"|",3),"^",1)
 .... If containernumber="" Set containernumber=$tr($p($p(record,"|",4),"^",1)," ")
 .... Set epis=$$Episode4Container^KSMIF000(containernumber)
 ... s cup=$p($p(record,"|",4),"^",3)
 ... ; remove punctuation from QC id
 ... i QC="Y" do  ;
 .... Set epis=$Piece($Piece(record,"|",3),"^",1)
 .... s epis=$tr(epis,"-,.<>?/':;{[}]+-=)(*&^%$#@!|\ ",""),QC=cup_"\"_epis
 .. ; result record
 ..i $e(record,2)="R" d  q
 ...s x1=$tr($p($p(record,"|",3),"^",4)," ")_$p(record,"|",5),x2=$tr($p(record,"|",4)," "),x3=$tr($p(record,"|",7)," ")
 ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 ... ; date and time
 ...i $l($tr($p(record,"|",13)," ")) d
 ....s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
 ....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
 .. ; last record
 ..i $e(record,2)="L" d Last q
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 .d ORDER
 q
Last ; file result if exist
 i $get(epis)'="",$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,containernumber,surname,result,date,time,QC)=""
 q
 /*
SCAN ; check all episodes for tests to be performed
 d scanall^MIF000(mi) q:'$d(^TMP("MIF-SCAN",$j,mi))
 s epis="" 
 f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
 .d PATDET(epis)
 .s mode="SCAN" d SET(epis,mode)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 .d Send
 .k ^TMP("MIF-SCAN",$j,mi,epis)
 q
 */
ORDER ; create list of orders if exists
 s (header,NN)=0 k ^TMIF(mi,10)
 i $g(^TMP($zn,$j,"ENQ"))'="ALL" d
 .s epis="" 
 .f  s epis=$o(^TMP($zn,$j,"ENQ",epis),1,containernumber) q:epis=""  d
 ..d scanone^MIF000(mi,epis)
 ..If $Data(^TMP("MIF-SINGLE",$J,mi,epis))<10 Quit ; *** There aren't tests enabled for instrument
 ..If '$$IsEpisode4Instrument(mi,epis,containernumber) Quit
 ..i 'header d
 ...s header=1
 ...s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="H|\^&||||||||ACL9000||P|1|"_$TR($ZDT($H,8)," :")
 ..s mode="QUERY" 
 ..d SET(epis,containernumber,mode)
 .i header s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
 .d Send
 .k ^TMP("MIF-SINGLE",$j,mi),^TMP($zn,$j,"ENQ")
 /*i $g(^TMP($zn,$j,"ENQ"))="ALL" d
 .d scanall^MIF000(mi)
 .s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
 ..i 'header d
 ...s header=1
 ...s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="H|\^&||||||||ACL9000||P|1|"_$TR($ZDT($H,8)," :")
 ..s mode="SCAN" d SET(epis,mode)
 .i header s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
 .d Send
 .k ^TMP($zn,$j,"ENQ")
 */
 q
IsEpisode4Instrument(mi,epis,containernumber) public {
	If epis=containernumber Quit 1
	Set tc="",ok=0
	For  {
		Set tc=$Order(^TMP("MIF-SINGLE",$j,mi,epis,tc))
		If tc="" Quit
		If $$IsTestCode4Container^KSMIF000(containernumber,mi,tc) {
			Set ok=1
			Quit
		}	 
	}	
	Quit ok
}	
 /* 
SAVE(mi) ; create loadlist records
 s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s sample=^TMP("MIF000",$j,"LL","SAMPLE"),NN=1
 s mode="LOAD" d SET(epis,mode)
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
 q 0
 */
PATDET(epis) ; set patient details record
 i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 .s line=$o(^TMIF(mi,10,""),-1)+1
 . Set ^TMIF(mi,10,line)="P|1|||||||U||||||||||||||||||||||||||"
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|"_NN_"||||^^^^||||||||"
 q
SET(epis,containernumber,mode) ; set record to be sent to instrument
 k tc s tc=""
 ; process a single episode scan
 i mode="QUERY",$d(^TMP("MIF-SINGLE",$j,mi,epis)) d
 .s NO=1 f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d
 ..s xtc=$$tc(tc) i $l(xtc) d
 ...If (epis'=containernumber)&&('$$IsTestCode4Container^KSMIF000(containernumber,mi,tc)) Quit
 ...i NO=1 s NN=NN+1 d PATDET(epis)
 ...i '$d(tc(xtc)) d
 ....s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_NO_"|"_containernumber_"||"_"^^^"_xtc_"|||||||||||^||||||||||O||||||"
 ....s NO=NO+1,tc(xtc)=""
 ; process an all episode scan
 /*i mode="SCAN",$d(^TMP("MIF-SCAN",$j,mi,epis)) d
 .s NO=1 f  s tc=$o(^TMP("MIF-SCAN",$j,mi,epis,tc)) q:tc=""  d
 ..s xtc=$$tc(tc) i $l(xtc) d
 ...i NO=1 s NN=NN+1 d PATDET(epis)
 ...i '$d(tc(xtc)) d
 ....s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_NO_"|"_epis_"||"_"^^^"_xtc_"|||||||||||^||||||||||O||||||"
 ....s NO=NO+1,tc(xtc)=""
 */
 ; process a load list episode scan
 /*i mode="LOAD" d
 .s NO=1 f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  d
 ..s xtc=$$tc(tc) i $l(xtc) d
 ...i NO=1 s NN=NN+1 d PATDET(epis)
 ...i '$d(tc(xtc)) d
 ....s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_NO_"|"_epis_"||"_"^^^"_xtc_"|||||||||||^||||||||||O||||||"
 ....s NO=NO+1,tc(xtc)=""
 */
 q
tc(tc) n (tc) s tc=$g(tc),result=""
 f j=1:1:$l(tc) q:$e(tc,j)'?1N  s result=result_$e(tc,j)
 q result
Send ; send list of orders if exists
 If '$D(^TMIF(mi,10)) Quit
 w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M")
 f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=nak:"NAK",1:R),"H<--M")
 i $c(R)=enq q
 i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 s x="" f FRAME=1:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 ;H 1
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
 /*
NEXTTRAY(tray) 
 q tray*/
]]></Routine>
</Export>
