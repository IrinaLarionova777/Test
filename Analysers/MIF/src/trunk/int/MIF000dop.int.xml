<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF000dop" type="INT" languagemode="0"><![CDATA[
MIF000dop ; extra functions MIF000

 // обработка ошибок с перезагрузкой программы на ошибку READ and EDIT (ошибку EDIT надо сэмулировать)
err
	set $zt=""
	if $get(mi)'="" {
		do trace^MIF000(mi,$ze,"ERROR")
	    do trace^MIF000(mi,"Finished","DEBUG")
	    k ^TMP(mi,$j)
 
	    if $find($ze,"<ZEDIT>")||$find($ze,"<READ>") {
		    set programname=$piece($get(^TMIF(mi)),"\",3)
		    if programname'="" {
		          do trace^MIF000(mi,"Autostarting...","DEBUG") x "job ^"_programname
		    }
	    }
	}
	quit

 // обработка ошибок с перезагрузкой программы на любую ошибку 
err2
	set $zt=""
	if $get(mi)'="" {
		do err^MIF000
		do trace^MIF000(mi,$ze,"ERROR")
	    do trace^MIF000(mi,"Finished","DEBUG")
	    k ^TMP(mi,$j)
 
	    set programname=$piece($get(^TMIF(mi)),"\",3)
		if programname'="" {
	         do trace^MIF000(mi,"Autostarting...","DEBUG") x "job ^"_programname
		}
	}
	quit

 // по коду теста для анализатора получить соответсвующую строку epis||ts||tscnt в эпизоде 
 // если один код теста анализатора настроен на два набора тестов и оба присутсвуют в одном эпизоде, то берем первый попавшийся
getRowIdTestSet(mi,epis,testcodeAnal) public {
	set rowid=""
	// определяем код теста ЛИС
	set testcode=""
	for {
		set testcode=$order(^TMIF(mi,3,testcodeAnal,testcode)) if testcode="" quit
		// ищем набор тестов
	    set ts="",rowid=""
        for { 
	        set ts=$order(^TMIF(mi,2,testcode,0,ts))
	        if (ts="") quit
	        if '$data(^TEPI(epis,1,ts)) continue
		    set tscnt=$order(^TEPI(epis,1,ts,""))
            if tscnt'="" set rowid=epis_"||"_ts_"||"_tscnt quit
        }
        if rowid'="" quit
    }
    quit rowid
}


traceWithZTS(mi,record,way) n (mi,record,way) s mi=$g(mi),record=$g(record),way=$g(way)
 s max=$$seldata^LVBMIMP(mi,23) i 'max s max=200
 s:'$l(way) way="H<--M"
 s z=$o(^TMPmif(mi,99,""),-1)+1,^TMPmif(mi,99,z)=way_" : "_$$zts^MIF000()_" : "_record
 //s z=$o(^TMPmif(mi,99,""),-1)+1,^TMPmif(mi,99,z)=way_" : "_$tr($h,","," ")_"."_$e($p($zts,".",2),1,3)_" : "_record
 //s z=$o(^TMPmif(mi,99,""),-1)+1,^TMPmif(mi,99,z)=way_" : "_$zd(+$h,4)_" "_$zt($piece($h,",",2))_"."_$e($p($zts,".",2),1,3)_" : "_record
 f j=z-max:-1 q:'$d(^TMPmif(mi,99,j))  k ^TMPmif(mi,99,j)
 q

  // Если это номер пробирки, то вернуть номер эпизода
transEpisodeNumber(pEpisode)
	if $get(pEpisode)="" quit ""
	if $data(^TEPI(pEpisode)) quit pEpisode
	//set epis=$extract(pEpisode,1,$length(pEpisode)-2) 
	// если pEpisode это номер контейнера и к этому номеру контейнера привязан только один эпизод, то подмениваем на номер этого эпизода
	if $data(^TEPIi("Specimen-SP",pEpisode)) {
		kill PLIST
		s x="" f  s x=$o(^TEPIi("Specimen-SP",pEpisode,x)) q:x=""  s PLIST=$o(PLIST(""),-1)+1,PLIST(PLIST)=x
	    if $d(PLIST(1)),'$d(PLIST(2)) s epis=PLIST(1) if $data(^TEPI(epis)) quit epis
	    k PLIST
	}
	quit pEpisode
 
  // Если это номер пробирки, то вернуть номер эпизода
GetEpisodeByNumberContainer(pNumberContainer,pErrors)
    set pNumberContainer=$get(pNumberContainer)
    set pErrors=""
	if pNumberContainer="" quit ""
	set episode=pNumberContainer
	set classmethod="TCLEx.Function.Container||GetEpisodeByNumberContainer"
	if ##class(%Dictionary.CompiledMethod).%ExistsId(classmethod) {
		set episode=$ClassMethod($piece(classmethod,"||",1),$piece(classmethod,"||",2), pNumberContainer, .pErrors )
		if pErrors'="" set episode=pNumberContainer
	}
	quit episode

  // Соответсвует ли этот тест номеру контейнера в эпизоде (1-соотвествует, 0 - нет)
IsTestCode4Container(pEpisode,pContainerNumber,pInstrumentCode,pTestItem,pErrors)
    set pEpisode=$get(pEpisode)
    set pContainerNumber=$get(pContainerNumber)
    set pInstrumentCode=$get(pInstrumentCode)
    set pTestItem=$get(pTestItem)
    set pErrors=""
    set returnValue=1
	set classmethod="TCLEx.Function.Container||IsTestCode4Container"
	if ##class(%Dictionary.CompiledMethod).%ExistsId(classmethod) {
		set returnValue=$ClassMethod($piece(classmethod,"||",1),$piece(classmethod,"||",2), pEpisode, pContainerNumber, pInstrumentCode, pTestItem, .pErrors )
		if pErrors'="" set returnValue=1
	}
	quit returnValue

  ; copy from MIF000 and change a little
scanone(mi,epis,tg,test,suffix,all,specimen) 
 n (mi,epis,tg,test,suffix,all,specimen) 
 k ^||TMP($zn,$j),^TMP("MIF-SINGLE",$j,mi),xxVTS
 s mi=$g(mi),epis=$g(epis),tg=$g(tg),test=$g(test),suffix=$g(suffix),all=$g(all),specimen=$get(specimen)
 s site=$$seldata^LVBMIMP(mi,21)
 
 s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 . ; check if TestSet exist in TestGroup
 .i $l(test),ts'=test q
 . ; not for this instrument
 .i '$d(^TMIF(mi,0,ts)) q
 .i $l(tg),$$select^LVBMITGS(mi_"||"_tg_"||"_ts,"Y") q
 .s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ..s vtsRID=epis_"||"_ts_"||"_tscnt
 .. ; test is on hold
 ..s hold=$$seldata^LVBVISTS(vtsRID,45)
 .. ; different user site
 ..i $l(site),site'=$$site^LVBVISTS(vtsRID) q
 .. ; send only if test set has not been authorised yet
 ..i $$status^LVBVIS1(vtsRID)="A" quit
 .. ; проверка соответсвия материала образца
 ..set ok=1 if (specimen'="") do  if ok=0 quit
 ...set spec=$$seldata^LVBVISTS(vtsRID,47) if spec="" set spec=##class(TCLEx.Function.TestSet).DefaultSpecimen(ts)
 ...if spec="" quit  // Если у набора тестов нельзя определить код материала образца, то лучше этот набор тестов оставить в списке 
 ...if specimen'=spec set ok=0 quit
 ..;
 ..s ^||TMP($zn,$j,1,epis,ts,$p(vtsRID,"||",3))=$s(hold="Y":"Y",1:"")
 
 i '$d(^TMIF(mi,20,epis)),$d(^TMPmif(mi,20,epis)) m ^TMIF(mi,20,epis)=^TMPmif(mi,20,epis) k ^TMPmif(mi,20,epis)
 k test,list s (test(epis),list)=""
 s sfx="" f  s sfx=$o(^TMIF(mi,20,epis,"SFX",sfx)) q:sfx=""  s test(epis_sfx)=""
 s ts="" f  s ts=$o(^||TMP($zn,$j,1,epis,ts)) q:ts=""  d
 .i '$$getall1^LVBCTTC(ts,,,,,,"Y") k FORMAT m FORMAT=PLIST d
 .. ; check hold
 ..s tscnt="" f  s tscnt=$o(^||TMP($zn,$j,1,epis,ts,tscnt)) q:tscnt=""  s hold=^(tscnt) i hold="Y" d
 ...s ok=1,z="" f  s z=$o(FORMAT(z)) q:z=""  s tc=$p(FORMAT(z),$c(2),1) i $l(tc) d
 ....i $p($g(^TEPI(epis,1,ts,tscnt,"DATA",tc)),"\",10)="Y" s ok=0 q
 ...i ok k ^||TMP($zn,$j,1,epis,ts,tscnt)
 ..s z="" f  s z=$o(FORMAT(z)) q:z=""  s tc=$p(FORMAT(z),$c(2),1) i $l(tc) d
 ... ; check if result already has been entered
 ...s ok=1,tscnt="" f  s tscnt=$o(^||TMP($zn,$j,1,epis,ts,tscnt)) q:tscnt=""  d
 ....i '$d(^TEPI(epis,1,ts,tscnt,"DATA",tc)) s ok=0 q
 ....i $p($g(^TEPI(epis,1,ts,tscnt,"DATA",tc)),"\",10)="Y" s ok=0 q
 ...i 'ok s xx=$g(^TMIF(mi,2,tc)),ch=$p(xx,"\",1),direction=$p(xx,"\",3),sfx=$p(xx,"\",4) i $l(ch)," Y LI "[(" "_direction_" ") d
 ....i $l(suffix),suffix'=sfx q
 ....i $l(tg) s ok=0 d  q:'ok
 .....i '$$select^LVBMITGI(mi_"||"_tg_"||"_tc,"Y"),PLIST(3)="Y" s ok=1
 ....i all="Y",$d(^TMIF(mi,20,epis,"TI",tc)) q
 ....i $l(sfx) s ^TMIF(mi,20,epis,"SFX",sfx)=""
 ....s (test(epis_sfx,ch),test(epis_sfx))="",xxVTS(epis_sfx,ch,epis_"||"_ts_"||"_tscnt)=""
 ....i '$d(list(tc)) s syn=$p($g(^TTAB("TC",tc)),"\",12),list=list_$s($l(list):",",1:"")_$s($l(syn):syn,1:tc),list(tc)=""
 
 k ^||TMP($zn,$j) m ^TMP("MIF-SINGLE",$j,mi)=test
 i $d(^TMP("MIF-SINGLE",$j,mi)) d
 .s $p(^TMIF(mi,20,epis),"\",2)=$$zts^MIF000()
 .set erriii=##class(TCLEx.MIF.Functions).writeInfoOrder(mi,epis,$get(sfx)) if +erriii=0 do trace^MIF000(mi,$System.Status.GetErrorText(erriii),"H-->H")
 .i all'="Y",$l(list) d events^LVBCOM01(epis,"IL","Sample loaded on instrument <"_mi_"> for tests : "_list)
 .i all="Y",$l(list) d events^LVBCOM01(epis,"IL","Request sent to the instrument <"_mi_"> for tests : "_list)
 q
 
 /*
  // Пробежать по всем отправленным тестам и записать данные о заказе в класс TCLEx.Data.MIFInfoEpisode, TCLEx.Data.MIFInfoEpisodeTests
writeInfoOrder(mi,epis,suffix) public {
	
	set ok=1
	try {
		set suffix=$get(suffix)
		set info=$lb(+$h,$piece($h,",",2))
		set test=""
	    for {
		    set test=$order(^TMP("MIF-SINGLE",$j,mi,epis_suffix,test)) if test="" quit
		    set ok=$$setMIFinfo(mi,epis,test,info)
		}
	    }
     catch(exception) {
	     set ok="0"_$c(1)_$System.Status.GetErrorText(exception.AsStatus())  
     }	
     quit ok
     
}

 // Записать данные о заказе в класс TCLEx.Data.MIFInfoEpisode, TCLEx.Data.MIFInfoEpisodeTests
setMIFinfo(mi,epis,test,info) public 
{
  set ok=1
  try {
	set idepis=mi_"||"_epis
  	set objepis=##class(TCLEx.Data.MIFInfoEpisode).%OpenId(idepis)
  	set idtest=mi_"||"_epis_"||"_test
	set objepistest=##class(TCLEx.Data.MIFInfoEpisodeTest).%OpenId(idtest)
  	
	set dateOrder=$li(info,1)
	set timeOrder=$li(info,2)
	set dateResult=$li(info,3)
	set timeResult=$li(info,4)	
	
	if (dateOrder'="")||(timeOrder'="")||(dateResult'="")||(timeResult'="") {
		set createepis=0,createepistest=0
		if '$IsObject(objepis) {
			kill PLIST
			set PLIST(1)=mi_"||"_epis
			set PLIST(2)=mi
			set PLIST(3)=epis
			set PLIST(4)=dateOrder
			set PLIST(5)=timeOrder
			set PLIST(6)=dateResult
			set PLIST(7)=timeResult
		    &sql(INSERT INTO TCLEx_Data.MIF_InfoEpisode VALUES :PLIST())
		    if SQLCODE'=0 $$$ThrowOnError($$$ERROR($$$GeneralError,$get(%msg)))	
		    set createepis=1
		}
	    if '$IsObject(objepistest) {
		    set PLIST(0)=mi_"||"_epis
		    set PLIST(1)=mi_"||"_epis_"||"_test
			set PLIST(2)=test
			set PLIST(3)=dateOrder
			set PLIST(4)=timeOrder
			set PLIST(5)=dateResult
			set PLIST(6)=timeResult
		    &sql(INSERT INTO TCLEx_Data.MIF_InfoEpisodeTest VALUES :PLIST())   
		    if SQLCODE'=0 $$$ThrowOnError($$$ERROR($$$GeneralError,$get(%msg)))
		    set createepistest=1
	    }
	    if (createepistest=0) {
			kill PLIST
		    &sql(SELECT * INTO :PLIST() FROM TCLEx_Data.MIF_InfoEpisodeTest WHERE MIFinfoTest_RowId=:idepistest)
		    if dateOrder'="" set PLIST(3)=dateOrder,PLIST(4)=timeOrder
		    if dateResult'="" set PLIST(5)=dateResult,PLIST(6)=timeResult
		    &sql(UPDATE TCLEx_Data.MIF_InfoEpisodeTest VALUES :PLIST() WHERE MIFinfoTest_RowId=:idepistest)
		    if SQLCODE'=0 $$$ThrowOnError($$$ERROR($$$GeneralError,$get(%msg)))
		}
		if (createepis=0) {
			kill PLIST
		    &sql(SELECT * INTO :PLIST() FROM TCLEx_Data.MIF_InfoEpisode WHERE MIFinfo_RowId=:idepis)
		    if SQLCODE'=0 $$$ThrowOnError($$$ERROR($$$GeneralError,$get(%msg)))
		    if dateOrder>PLIST(4) {
			    set PLIST(4)=dateOrder,PLIST(5)=timeOrder
		    }
		    if (dateOrder=PLIST(4))&&(timeOrder>PLIST(5)) {
			    set PLIST(5)=timeOrder
		    }
		    if dateResult>PLIST(6) {
			    set PLIST(6)=dateResult,PLIST(7)=timeResult
		    }
		    if (dateResult=PLIST(6))&&(timeResult>PLIST(7)) {
			    set PLIST(7)=timeResult
		    }
		    &sql(UPDATE TCLEx_Data.MIF_InfoEpisode VALUES :PLIST() WHERE MIFinfo_RowId=:idepis)
		    if SQLCODE'=0 $$$ThrowOnError($$$ERROR($$$GeneralError),$get(%msg))
		}
	}
  }
  catch(exception) {
	  set ok="0"_$c(1)_$System.Status.GetErrorText(exception.AsStatus())  
  }	
  quit ok
	
}*/

]]></Routine>
</Export>
