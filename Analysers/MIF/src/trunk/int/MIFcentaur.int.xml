<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFcentaur" type="INT" languagemode="0" generated="1"><![CDATA[
MIFcentaur(mi) ; ASTM protocol  -  Centaur (Bi-Directional) (Russion EMC))
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),nak=$c(21),etb=$c(23)
 s lf=$c(10),cr=$c(13)
 s (sample,episode,surname,result,date,time,QC)=""
 i $$start^MIF000() q
 d trace^MIF000(mi,"Started job="_$j,"DEBUG")
 for {
	 d Main 
	 if $$stop^MIF000() quit
 }
 c par4
 quit
Main 
 read *R:10
 ;i $c(R)'=enq d trace^MIF000(mi,"Waiting <ENQ> but received <"_R_">","H<--M") q
 i $c(R)'=enq q
 do receiveENQ
 do ORDER
 quit
 ;обработка полученного от прибора ENQ
receiveENQ 
 do trace^MIF000(mi,"ENQ","H<--M")
 do ACK
 set buffer=""
 for {
	 read *R:10 
	 if $c(R)=eot quit
     if $c(R)'=stx continue
     set record=$$read^MIF000("",lf) if '$l(record)  continue
     do trace^MIF000(mi,record,"H<--M")
     set record=$e(record,1,$l(record)-1)
     ;do trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
     ; check record validity
     set endtext=$select('$find(record,etx):etb,1:etx)
     set chsum=$p(record,endtext,2),chrecord=$piece(record,endtext,1)_endtext
     if $$CHSUM(chrecord)'=chsum do NAK continue
     do ACK
     set record=$piece(record,endtext,1)
     set record=$e(record,2,$l(record)) ;на 1-ом месте стоит номер записи - на записях-продолжениях он мешает
     if endtext=etb {
	     set buffer=buffer_record
	     continue
     }
     set buffer=buffer_record
     if buffer="" continue
     do trace^MIF000(mi,"buffer="_buffer,"H<--M")
     for pp=1:1:$length(buffer,$c(13)) {
	     set record=$piece(buffer,$c(13),pp)
	     if record="" continue 
	     set typerecord=$piece(record,"|",1)
	     if typerecord="" continue
	     set typerecord=$select($length(typerecord)=1:typerecord,1:$e(typerecord,2))
	     do trace^MIF000(mi,typerecord_" "_record,"H<--M")
         ; query record
         if typerecord="Q" {
	        set episode=$p($p(record,"|",3),"^",2)
	        if $l(episode) set ^TMP($zn,$j,"ENQ",episode)="" continue
            continue
         }    
         ; patient record
         if typerecord="P" {
            do Last s (sample,episode,surname,result,date,time,QC)=""
            set surname=$tr($tr($p(record,"|",6),""),"^"," ") 
            continue
         }
         ; order record
         if typerecord="O" {
	        set episode=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," ") s:'$l(episode) episode=$tr($p($p(record,"|",4),"^",1)," ")
            if QC="Q" s QC="Y"
            continue
         }
         ; result record
         if typerecord="R" {
            ; strip trailing spaces
            set x1=$p($p(record,"|",3),"^",4) set xx1=x1 for i=$l(x1):-1 quit:$e(x1,i)'=" "  s xx1=$e(x1,1,i-1)
            set x1=xx1,x2=$tr($p(record,"|",4),", ","."),x3=$tr($p(record,"|",7)," ")
            set resultaspect=$p($p(record,"|",3),"^",8)
            if x3="N" s x3="" ; normal flag
            if resultaspect="DOSE" if $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
            ; date and time
            set datetime=$p(record,"|",13)
            if $l($tr(datetime," ")) {
	            s date=$$intdate^SSUTIL4($e(datetime,5,6)_"/"_$e(datetime,7,8)_"/"_$e(datetime,1,4))
                s time=($e(datetime,9,10)*3600)+($e(datetime,11,12)*60)
            }
            continue
         }
         ; last record
         if typerecord="L" {
	        d Last 
	        continue
         }
    }
    set buffer=""
 } 
 do trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 quit
Last ; file result if exists
 do trace^MIF000(mi,"episode="_episode_" result="_result_" date="_date_" time="_time,"H<--H")
 if episode?1.99AN,$l(result) do file^MIF000(mi,sample,episode,surname,result,date,time,QC)
 set (sample,episode,surname,result,date,time,QC)=""
 quit
ORDER ; create list of orders if exists
 s epis=""
 for {
	 set epis=$o(^TMP($zn,$j,"ENQ",epis)) quit:epis="" 
     do scanone^MIF000(mi,epis)
     if $$select^LVBEPVIS(epis,"Y") k ^TMP($zn,$j,"ENQ",epis) continue
     s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
     ;s sex=$s($g(PLIST(5))="М":"M",$g(PLIST(5))="Ж":"F",1:"U")
     set surname=$get(PLIST(3)),name=$get(PLIST(4)),birthday=$zdate($get(PLIST(6)),8) 
     ;s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_birthday_"|"_sex_"|||||" ;_$g(PLIST(15))
     s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||"_$$Translit^KSUTIL1(surname_"^"_name)_"||"_"|"_sex_"|||||" ;_$g(PLIST(15))
     ;s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_epis_"|||||||||||"
     s tc="" f N=1:1 s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d SET(epis,tc)
     s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|"_$s(N>1:"N",1:"I")
     k ^TMP($zn,$j,"ENQ",epis)
     d Send
 }
 quit
SET(epis,tcx) ; set record to be sent to instrument
 set ll=$length(tcx)
 if $e(tcx,ll-1,ll)="_i" set tcx=$e(tcx,1,ll-2)
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_N_"|"_epis_"||^^^"_tcx_"|||||||||||||||||||||O"
 q
Send ; send list of orders if exists
 d ENQ f j=1:1:10 r *R:1 quit:$c(R)=ack!($c(R)=enq)  ;d trace^MIF000(mi,"Waiting <ACK> or <EOT> (10) but received <"_R_">","H<--M")
 i $c(R)=enq q
 i $c(R)'=ack d EOT q
 d trace^MIF000(mi,"ACK","H<--M")
 q:$$SEND("1H|\^&|||LIS|||||Centaur||P|1|") //_$Translate($ZDateTime($Horolog,8)," :"))
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 d EOT
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 q:($c(R)=ack)!($c(R)=eot)  ;d trace^MIF000(mi,"Waiting <ACK> or <EOT> (6) but received <"_R_">","H<--M")
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK w ack,*-3 d trace^MIF000(mi,"ACK","H-->M") q
NAK w nak,*-3 d trace^MIF000(mi,"NAK","H-->M") q
EOT w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
ENQ w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M") q
]]></Routine>
</Export>
