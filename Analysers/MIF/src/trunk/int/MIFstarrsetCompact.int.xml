<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFstarrsetCompact" type="INT" languagemode="0" generated="1"><![CDATA[
MIF999RU01(mi) ;Combi Scan 100 Bion Russia
	Set mi=$Get(mi) 
	If mi="" Quit
	Kill ^TMIF(mi,10) 
	Set $ZTRAP="err^MIF000"
	If $$select^LVBMIMP(mi) Quit
	For j=1:1:PLIST Set @("par"_j)=PLIST(j)
	If $$start^MIF000() Quit
	Do trace^MIF000(mi,"Started job="_$Job,"DEBUG")
	Kill ^||data
	For  {
		Do Main(mi)
		If $$stop^MIF000() Quit
	}	
	Close par4
	Do trace^MIF000(mi,"Stopped job="_$Job,"DEBUG")
	Quit
Main(mi) [par10,par11] public {
	Set record=$Piece($$ReadRecord(mi),$Char(13),1)
	If record="" Quit
	If $Translate(record," ")["Pat.ID:" {
		Kill ^||data
		Set record=$Translate(record," ")
		Set containernumber=$Piece(record,"Pat.ID:",2)
		Set epis=$$Episode4Container^KSMIF000(containernumber)
		If epis'="" {
			Set ^||data("Epis")=epis
		}
		Quit
	}
	Set testcode=$Translate($Extract(record,2,4)," ")
	Set val=$Extract(record,5,$Length(record))
	Set val=$$DeleteLeftSpaces^KSUTIL1(val)
	Set val=$Piece(val," ",1)
	If (testcode'="")&&(",BIL,UBG,KET,GLU,PRO,ERY,NIT,pH,SG,LEU,"[(","_testcode_",")) {
		Set ^||data("Result",testcode)=val
		Quit
	}
	If record["--------" {
		Set epis=$Get(^||data("Epis"))
		If epis="" Quit
		Set result=""
		Set testcode=""
		For  {
			Set testcode=$Order(^||data("Result",testcode),1,val)
			If testcode="" Quit
			If val="" Continue
			Set result=result_testcode_$Char(par10)_val_$Char(par11)
		}
		If result'="" {
			Do file^MIF000(mi,"",epis,"",result,"","","")
		}		
	}		 
}	

ReadRecord(mi) public {
	Set result=""
	For  {
		Read *Y:5
		If Y=-1 Quit
		Set char=$Char(Y)
		Set result=result_char
		If char=$Char(13) Quit
	}
	If result'="" Do trace^MIF000(mi,$Horolog_" : "_result,"H<--M")
	Quit result
}	 
]]></Routine>
</Export>
