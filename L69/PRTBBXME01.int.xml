<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBXME01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBXME01 ; IG 22/09/99 ; XM Expiry Report
 q
 ; Parameters :  1. Site
 ;               2. Date from
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="XM Expiry Report"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s('$l(PAR1):"All Sites",1:CTUSL(3))
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR2,+$h,$p($h,",",2)),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 i $l(PAR1),'$$select^LVBCTUSL(PAR1,"Y") m CTUSL=PLIST
 q
BODY ; create temporary printing file
 ; find all XM transactions
 k XMtran i '$$getall^LVBBBTR(4,,"ANY") m XMtran=PLIST
 s date=PAR2-1 f  s date=$o(^TBBPi("TR-Date",date)) q:date=""  d
 .s xx0=0 f  s xx0=$o(XMtran(xx0)) q:xx0=""  s tran=$p(XMtran(xx0),$c(2)) d
 ..s xx1="" f  s xx1=$o(^TBBPi("TR-Date",date,tran,xx1)) q:xx1=""  d
 ...s xx2="" f  s xx2=$o(^TBBPi("TR-Date",date,tran,xx1,xx2)) q:xx2=""  d
 ....s xx3="" f  s xx3=$o(^TBBPi("TR-Date",date,tran,xx1,xx2,xx3)) q:xx3=""  s stop=0 d
 ..... ; find vtsRID
 .....i 'stop,'$$select^LVBBBPT(xx1_"||"_xx2_"||"_xx3) d
 ......i PLIST(13)="Y" s stop=1 q
 ......s vtsRID=$p(PLIST(9),$c(1)) i '$l(vtsRID) s stop=1
 ..... ; check if it is still the same vtsRID
 .....i 'stop k PACK i '$$select^LVBBBP(xx1_"||"_xx2) m PACK=PLIST i vtsRID'=$p(PLIST(12),$c(1)) s stop=1
 ..... ; check current location
 .....i 'stop,$l(PAR1),PAR1'=$p(PACK(11),$c(1),4) s stop=1
 ..... ; check Hold date/time
 .....i 'stop,'$$select^LVBVISTE(vtsRID_"||"_xx1_"||"_xx2) d
 ......i $p(PLIST(8),$c(1))>$p($h,",",1) s stop=1
 ......i $p(PLIST(8),$c(1))=$p($h,",",1),$p(PLIST(9),$c(1))>$p($h,",",2) s stop=1
 .....i 'stop f xx4=xx3+1:1 q:$$select^LVBBBPT(xx1_"||"_xx2_"||"_xx4)  i PLIST(13)'="Y",$p(PLIST(3),$c(1))=$p(XMtran(0),$c(1)) s stop=1 q
 .....i 'stop d
 ......s site=$p(PACK(11),$c(1),4) s:site="" site=" "
 ......s loc=$p(PACK(11),$c(1),1) s:loc="" loc=" "
 ......s ^TMP($zn,$j,"REPORT",site,loc,xx1_"||"_xx2)=""
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s site="" f  s site=$o(^TMP($zn,$j,"REPORT",site)) q:site=""  d
 ..s xxx(1)=site i '$$select^LVBCTUSL(site,"Y") s xxx(1)=PLIST(2)_$c(1)_PLIST(3)
 ..s loc="" f  s loc=$o(^TMP($zn,$j,"REPORT",site,loc)) q:loc=""  d
 ...s pack="" f  s pack=$o(^TMP($zn,$j,"REPORT",site,loc,pack)) q:pack=""  d
 ....s epis="" i '$$select^LVBBBP(pack) d
 .....s xxx(2)=PLIST(11)
 .....s xxx(3)=PLIST(8)
 .....s xxx(4)=PLIST(9)
 .....s xxx(5)=PLIST(2)
 .....s xxx(6)=$$extdate2^SSUTIL4(PLIST(4))
 .....s xxx(7)=$$extdate2^SSUTIL4(PLIST(13))
 .....s epis=$p(PLIST(12),"||",1) i '$l(epis) s epis=$$seldata^LVBDEB(PLIST(6),4)
 ....k PLIST i $$select^LVBEPVIS(epis)
 ....d PRINT^PRTREP00(.PLIST,.N) k PLIST,xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="BBXME"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
