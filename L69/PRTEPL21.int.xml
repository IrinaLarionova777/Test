<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPL21" type="INT" languagemode="0" generated="1"><![CDATA[
PRTEPL21	; ML 12/12/2002 ; Episode Patient Label - Histopath/Dermatopath
	q
	; Parameters :  1. Episode number
	;               2. Label code
	;               3. Quantity
	;
CREATE(REPID,PAR,USER,%msql)	; Create report
	n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
	d INIT,BODY,PRINT
CREND	i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR1 i $$update^LVBPRPR(REPID)
	k ^TMP($zn,$j),^TMP("PRTREP00",$j)
	q
	; initialize parameters
INIT	f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
	q
BODY	; create temporary printing file
	; quantity entered (default 3 labels)
	s (quantity,leftover)=$s(PAR3'="":PAR3,1:3)
	i $$select^LVBEPVIS(PAR1) q
	k VISIT m VISIT=PLIST s ReqLabel=""
	s name=$g(VISIT(3))_", "_$e($g(VISIT(4)))
	s date="" i $l(VISIT(61)) s date=$$extdate2^SSUTIL4(VISIT(61))
	s row=$s(quantity=3:1,1:(quantity\3)+1) f i=1:1:row d
	.i i=1,leftover>3 s quantity=3,leftover=leftover-3
	.i i>1 s quantity=$s(leftover>3:3,1:leftover),leftover=$s(leftover>3:leftover-3,1:"")
	.f col=1:1:quantity d
	..s ^TMP($zn,$j,"REPORT",i,col,1)=PAR1
	..s ^TMP($zn,$j,"REPORT",i,col,2)=name
	..s ^TMP($zn,$j,"REPORT",i,col,3)=date
	..i $e(PAR1)="H" s ^TMP($zn,$j,"REPORT",i,col,4)="HISTOPATH"
	..i $e(PAR1)="M" s ^TMP($zn,$j,"REPORT",i,col,4)="HISTOPATH"
	..i $e(PAR1)="D" s ^TMP($zn,$j,"REPORT",i,col,4)="DERMATOPATH"
	q
PRINT	i $d(^TMP($zn,$j,"REPORT")) d
	.s row="" f  s row=$o(^TMP($zn,$j,"REPORT",row)) q:row=""  d
	..s LF=$C(10),labnum=1
	..d ADD^SSPRINT(REPID,"PRT",LF)
	..d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
	..d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_labnum_""""_LF)
	..d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_labnum_""""_LF)
	..d ADD^SSPRINT(REPID,"PRT","Q184,24"_LF)
	..d ADD^SSPRINT(REPID,"PRT","R16,0"_LF)
	..s col="" f  s col=$o(^TMP($zn,$j,"REPORT",row,col)) q:col=""  d
	...i $d(^TMP($zn,$j,"REPORT",row,col,1)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(col-1*232))_",5,0,4,1,1,N,"""_^TMP($zn,$j,"REPORT",row,col,1)_""""_LF)
	...i $d(^TMP($zn,$j,"REPORT",row,col,2)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(col-1*232))_",40,0,2,1,1,N,"""_^TMP($zn,$j,"REPORT",row,col,2)_""""_LF)
	...i $d(^TMP($zn,$j,"REPORT",row,col,3)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(col-1*232))_",75,0,2,1,1,N,"""_^TMP($zn,$j,"REPORT",row,col,3)_""""_LF)
	...i $d(^TMP($zn,$j,"REPORT",row,col,4)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(col-1*232))_",100,0,2,1,1,N,"""_^TMP($zn,$j,"REPORT",row,col,4)_""""_LF)
	..d ADD^SSPRINT(REPID,"PRT","FE"_LF)
	..d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_labnum_""""_LF)
	..d ADD^SSPRINT(REPID,"PRT","P1"_LF)
	q
]]></Routine>
</Export>
