<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF427" type="INT" languagemode="0" generated="1"><![CDATA[
MIF427(mi) ; ML 23/10/02 ; ASTM protocol  -  Vital Triturus (Bi-Directional) ; QC - Yes
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),etb=$c(23),ack=$c(6),enq=$c(5),eot=$c(4)
 s lf=$c(10),cr=$c(13),nak=$c(21),(result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  q
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK k record
 .f  r *R:10 q:$c(R)=eot  q:R=-1  i $c(R)=stx d
 ..s record=$$read^MIF000("",lf) i $l(record) d
 ...s cnt=$o(record(""),-1)+1
 ...s record(cnt)=$e($p(record,$s(record[etx:etx,1:etb),1),2,999)
 ...d trace^MIF000(mi,$tr(record(cnt),etx_etb_cr,"   "),"H<--M")
 ...d ACK
 . ; reunite split records
 .s (record,cnt)="" f  s cnt=$o(record(cnt)) q:cnt=""  s record=record_record(cnt)
 .f i=1:1:$l(record,$c(13)) s rec=$p(record,$c(13),i) d
 .. ; header record
 ..i $e(rec,2)="H" d  q
 ...d Last s (sample,epis,surname,result,date,time,QC)=""
 .. ; patient record
 ..i $e(rec)="P" d  q
 ...d Last s (sample,epis,surname,result,date,time,QC)=""
 ...s epis=$tr($p($p(rec,"|",4),"^",1)," ")
 ...s surname=$tr($tr($p(rec,"|",6)," "),"^"," "),surname=$p(surname,cr,1)
 .. ; order record
 ..i $e(rec)="O" d  q
 ...s QC=$tr($p(rec,"|",12)," ") s:'$l(epis) epis=$tr($p(rec,"|",3)," ")
 ...s cup=$p(rec,"|",4)
 ... ; remove punctuation from QC id
 ...i QC="Q" s epis=$tr(epis,"-,.<>?/':;{[}]+-=)(*&^%$#@!|\ ",""),QC=cup_"\"_epis
 .. ; result record
 ..i $e(rec)="R" d  q
 ...s x1=$p($p(rec,"|",3),"^",4) f  q:'$l(x1)  q:$e(x1,$l(x1))'=" "  s x1=$e(x1,1,$l(x1)-1)
 ...s x2=$tr($p(rec,"|",4)," ")
 ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par11)
 ... ; date and time
 ...i $l($tr($p(rec,"|",13)," ")) d
 ....s date=$$intdate^SSUTIL4($e($p(rec,"|",13),5,6)_"/"_$e($p(rec,"|",13),7,8)_"/"_$e($p(rec,"|",13),1,4))
 ....s time=($e($p(rec,"|",13),9,10)*3600)+($e($p(rec,"|",13),11,12)*60)
 .. ; last record
 ..i $e(rec)="L" d Last q
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)=""
 q
ACK ; send 'ack' to instrument
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 q
]]></Routine>
</Export>
