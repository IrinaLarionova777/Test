<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBCCR01TAS" type="INT" languagemode="0" generated="1"><![CDATA[
LVBCCR01TAS	; IG 31/12/98 ; CCR file creation - Trak (Tasmania)
	;
	; supported calls
	;
	; $$create^LVBCCR01TAS(type,TYPE,DATE)
	;
create(type,TYPE,DATE)	n (type,TYPE,DATE,Start,End) s type=$g(type),TYPE=$g(TYPE),DATE=$g(DATE),file="",state="TAS" k ^TMP($zn,$j)
	; create file
	i $$select^MVBCFLAB(1) q
	f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
	s dir=PLIST(48),labs=PLIST(46),rid=$s(type="P":PLIST(47),1:PLIST(49))
	s lab="" f i=1:1:$l(labs,",") i $p($p(labs,",",i),"=",1)=state s lab=$p($p(labs,",",i),"=",2) q
	f j="\","/" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
	s delim="^",records=0
	i $d(^TCCR(type,"DATA",TYPE,DATE)),$l(lab) d
	.s date=$$extdate2^SSUTIL4(DATE)
	.s curdate=$$extdate2^SSUTIL4($p($h,","))
	.s curtime=$$exttime^SSUTIL4($p($h,",",2))
	.s tt=""
	.i TYPE="D",type="P" s tt="cd"
	.i TYPE="D",type="B" s tt="hd"
	.i TYPE="R",type="P" s tt="cr"
	.i TYPE="R",type="B" s tt="hr"
	.s file=dir_$e($p(curdate,"/",3),3,4)_$p(curdate,"/",2)_$p(curdate,"/",1)_tt_"1"_"."_lab
	.o file:"WNS":5 e  q
	.u file
	.s epis="" f  s epis=$o(^TCCR(type,"DATA",TYPE,DATE,epis)) q:epis=""  d
	..i $g(^(epis))'=state q
	..i $$select^LVBEPVIS(epis,"Y") q
	..f j=1:1:6,8:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
	..k VISIT m VISIT=PLIST
	..i $$select^LVBCTDR(VISIT(15),"Y") q
	..f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
	..k DOC m DOC=PLIST
	..s ts="" f  s ts=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts)) q:ts=""  d
	...s cnt="" f  s cnt=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)) q:cnt=""  d
	....s acc="" f  s acc=$o(^THIR(epis,acc)) q:acc=""  i $d(^THIR(epis,acc,"TEST",epis_"||"_ts_"||"_cnt)) q
 	....s consent="y" i $l(acc) s xx=$$seldata^LVBHIVAC(epis_"||"_acc,4) i $l(xx) s consent=$tr($p(xx,$c(1)),"YN","yn")
	....s rid=rid+1,^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)=rid
	....s index=$o(^TMP($zn,$j,""),-1)+1,dob="" i $l(VISIT(6)) s dob=$$extdate2^SSUTIL4(VISIT(6))
	....i TYPE="R",type="P" d
	.....s ^TMP($zn,$j,index)=lab_$c(9)_epis_$c(9)_"N"_$c(9)_$p(dob,"/",1)_$p(dob,"/",2)_$p(dob,"/",3)_$c(9)_"99"
	.....f j="A","B","S","E","O","R" d
	......s xx=$g(^TEPI(epis,1,ts,cnt,"CCR",j)) 
	......s ^TMP($zn,$j,index)=^TMP($zn,$j,index)_$c(9)_j_xx
	....i TYPE="R",type="B" d
	.....s ^TMP($zn,$j,index)=lab_$c(9)_epis_$c(9)_$p(dob,"/",1)_$p(dob,"/",2)_$p(dob,"/",3)_$c(9)_$$getdesc(epis_"||"_ts_"||"_cnt)
	....i TYPE="D" d
	.....s reqdate=$$extdate2^SSUTIL4(VISIT(14)),flag="G" i $l(DOC(33)) s flag=$s($$seldata^LVBCTSPL($p(DOC(22),$c(1),1),4)="Y":"G",1:"S")
	.....s ^TMP($zn,$j,index)=lab_$c(9)_epis_$c(9)_""_$c(9)_"N"_$c(9)_$e(VISIT(8),1,10)_$c(9)_$p(VISIT(7),$c(1),2)_$c(9)
	.....s ^TMP($zn,$j,index)=^TMP($zn,$j,index)_$e(VISIT(3),1,30)_$c(9)_$e(VISIT(4),1,30)_$c(9)_VISIT(99)_$c(9)_$e(VISIT(32),1,40)_$c(9)_$e(VISIT(33),1,40)_$c(9)
	.....s ^TMP($zn,$j,index)=^TMP($zn,$j,index)_$e(VISIT(34),1,30)_$c(9)_$e(VISIT(36),1,4)_$c(9)_$p(dob,"/",1)_$p(dob,"/",2)_$p(dob,"/",3)_$c(9)
	.....s ^TMP($zn,$j,index)=^TMP($zn,$j,index)_$e(DOC(3),1,30)_$c(9)_$e(DOC(4),1,30)_$c(9)_""_$c(9)_$e(DOC(9),1,40)_$c(9)_$e(DOC(10),1,40)_$c(9)_$e(DOC(11),1,30)_$c(9)
	.....s ^TMP($zn,$j,index)=^TMP($zn,$j,index)_$e(DOC(13),1,4)_$c(9)_$e(DOC(5),1,10)_$c(9)_$e(DOC(8),1,10)_$c(9)_$e(DOC(18),1,8)_$c(9)_flag_$c(9)_$p(reqdate,"/",1)_$p(reqdate,"/",2)_$p(reqdate,"/",3)_$c(9)_""_$c(9)_"I9"
	. ; calculate checksum
	.s (sum,x)="" f  s x=$o(^TMP($zn,$j,x)) q:x=""  f y=1:1:$l(^TMP($zn,$j,x)) s sum=sum+($a($e(^TMP($zn,$j,x),y))*y) 
	.s HEADER=lab_$e($p(file,dir,2),1,9)_$c(9)_$o(^TMP($zn,$j,""),-1)_$c(9)_sum_$c(9)_$p(curdate,"/",1)_$p(curdate,"/",2)_$p(curdate,"/",3)_$p(curtime,":",1)_$p(curtime,":",2)
	.w HEADER,!
	.s x="" f  s x=$o(^TMP($zn,$j,x)) q:x=""  w ^TMP($zn,$j,x),!
	.c file
	k ^TMP($zn,$j)
	q file
txt(xx1,xx2)	n (xx1,xx2) s xx1=$tr($g(xx1),""""),xx2=$g(xx2)
	i xx2 s result=""""_$e(xx1_$j("",xx2),1,xx2)_""""
	i 'xx2 s result=""""_xx1_""""
	q result
date(date)	n (date) q """"_$$extdate2^SSUTIL4($g(date))_""""
julian(date)	n (date) s result=""
	s x1=$p($$extdate2^SSUTIL4(date),"/",3)
	s result=date-$$intdate^SSUTIL4("01/01/"_x1)+1
	q result
getdesc(vrowid)	
	n res,row,l
	s res=""
	k plist m plist=PLIST k PLIST
	i '$$SINGLE^PRTDR00(vrowid,"P") d
	.s l="" f  s l=$o(PLIST(l)) q:l=""  d
	..s row=$$strip(PLIST(l))
	..i ($l(res)+$l(row)+1)<6120 s res=res_" "_row
	k PLIST m PLIST=plist
	q res_"|"
strip(line)	
	n text,l2,j,i
	s text=line
	i text="!" s text=""
	s text=$tr(text,$c(10)_$c(13),"  ")
	i $e(text,1,1)="?" s text=$e(text,5,$l(text))
	f i="~NORMAL","~BOLD","~ITALIC","~INVISIBLE","~UNDERLINE","~ABNORMAL","~RES-AUTHORISEDBY","~RES-TSHEADER" d
	.i text=i s text=""
	.s l2="" i $l(text,i)>1 f j=1:1:$l(text,i) s l2=l2_$p(text,i,j)
	.i $l(l2) s text=l2
	q text
]]></Routine>
</Export>
