<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="zUPLTranslation1" type="MAC" languagemode="0"><![CDATA[
zUPLTranslation1 ; ML 01/05/09 ; Translation export/import for VB messages
	q
 
Start
	w !,"Export(E) or Import(I) > " r type q:'$l(type)
	i "Ii"[type d import
	i "Ee"[type d export
	q
 
export
	w !!,"This will run the VB Messages extract to file",!
ex0	w !,"File (full path) > " r file q:'$l(file)
ex1	w !,"Language > " r lan g:'$l(lan) ex0 i lan="?" d  g ex1
	.s lan=0 f  s lan=$o(^SS("LAN",lan)) q:lan=""  w !,lan,?10,$p(^(lan),"^",2)
	.w !!
	i '$d(^SS("LAN",lan)) w "...???" g ex1
	w !,"Translated(T), Not-Translated(N), All(A) > " r type g:'$l(type) ex1
	s type=$tr(type,"tna","TNA")
	
	o file:("NSW") s delim=$c(9)
	u file w "Code",delim,"English Description",delim,"Translation",!
	i $$open^MVBSSVBM(,,,lan,$s(type="T":"Y",type="N":"N",1:""))
	f  q:$$fetch^MVBSSVBM(,,,lan,$s(type="T":"Y",type="N":"N",1:""))  d
	.u file w PLIST(2),delim,PLIST(3)
	.i '$$select^MVBSSVBN(PLIST(2)_"||"_lan),$l(PLIST(4)) u file w delim,PLIST(4)
	.w !
	i $$close^MVBSSVBM()
	c file
	q
 
import
	w !!,"This will run the VB Messages translation upload from file",!
	w !,"File (full path) > " r file q:'$l(file)  o file::2 e  w "...???" g import+2
	w !,"Language > " r lan g:'$l(lan) import+2 i lan="?" d  g import+3
	.s lan=0 f  s lan=$o(^SS("LAN",lan)) q:lan=""  w !,lan,?10,$p(^(lan),"^",2)
	.w !!
	i '$d(^SS("LAN",lan)) w "...???" g import+3
	
	s delim=$c(9)
	s $zt="error^"_$zn
	f cnt=0:1 u file r data i cnt d
	.s rowid=$p(data,delim,1)_"||"_lan
	.i '$$select^MVBSSVBN(rowid) d  q
	..s PLIST(4)=$p(data,delim,3)
	..i $l(PLIST(4)) s err=$$update^MVBSSVBN(rowid) i err u 0 w !,$p(data,delim,1),"-->",err
	.s PLIST(2)=lan
	.s PLIST(3)=$p(data,delim,1)
	.s PLIST(4)=$p(data,delim,3)
	.i $l(PLIST(4)) s err=$$insert^MVBSSVBN() i err u 0 w !,$p(data,delim,1),"-->",err
	q
 
error c file
	q
	 
]]></Routine>
</Export>
