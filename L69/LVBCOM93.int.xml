<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBCOM93" type="INT" languagemode="0" generated="1"><![CDATA[
LVBCOM93 ; IG 29/4/98 ; Common routines
 ;
 ; supported calls:
 ;
 ; convTI1^LVBCOM93
 ; convTI2^LVBCOM93
 ; convTS1^LVBCOM93
 ; convTS2^LVBCOM93
 ; convTS3^LVBCOM93
 ; convTS4^LVBCOM93
convTI1 s file="file_name",$zt="ERROR^"_$zn
 o file::0 e  q
 ; it will skip first two records
 f cnt=0,0,1:1 u file r MESSAGE i cnt u 0 d
 .k TEST
 .s TEST(2)=$p(MESSAGE,",",1)                    ; code
 .s TEST(3)=$p(MESSAGE,",",2)                    ; description
 .s TEST(4)=$p(MESSAGE,",",4)                    ; units
 .s TEST(5)=$p(MESSAGE,",",5)                    ; result format 
 . ;
 . ; result format must be translated into LabTrak result format which is :
 . ;
 . ; S-Standard comments
 . ; X-Free Text
 . ; N-Numeric
 . ; V-Micro Pathogens
 . ; M-Number*1000
 . ; D-Date field
 . ; Y-Yes/No field
 . ; Z1-Day Book Lab Number
 . ; Z2-Day Book Specimen type
 . ; Z3-Day Book Site
 . ; Z4-Specimen Unlabeled
 . ; Z5-Cancer Council Codes
 . ; Z6-24 Hour Urine Volume
 . ; Z7-24 Hour Urine Time
 . ;
 .s TEST(6)=$p(MESSAGE,",",7)                    ; length
 .s TEST(15)=$p(MESSAGE,",",8)                   ; sig figures
 .i $$select^LVBCTDEP($e(TEST(2))) w !,TEST(2),"--> Department does not exist..." q
 .k PLIST m PLIST=TEST s PLIST=$o(PLIST(""),-1) w !,PLIST(2),"-->",$$insert^LVBCTTC()
 q
 ; TestItems - Misc TAB
convTI2 s file="file_name",$zt="ERROR^"_$zn
 o file::0 e  q
 ; it will skip first two records
 f cnt=0,0,1:1 u file r MESSAGE i cnt u 0 d
 .s TEST(2)=$p(MESSAGE,";",1)                    ; code
 .i '$$select^LVBCTTC(TEST(2)) d
 ..s PLIST(36)=$p(MESSAGE,";",9)                 ; national code
 ..s PLIST(37)=$p(MESSAGE,";",10)                ; national short name
 ..s PLIST(38)=$p(MESSAGE,";",11)                ; national long name
 ..s PLIST=$o(PLIST(""),-1) w !,PLIST(2),"-->",$$update^LVBCTTC(PLIST(2))
 q
 ; TestSets - General TAB
convTS1 s file="file_name",$zt="ERROR^"_$zn
 o file::0 e  q
 ; it will skip first two records
 f cnt=0,0,1:1 u file r MESSAGE i cnt u 0 d
 .k TEST
 .s TEST(2)=$p(MESSAGE,";",1)                    ; code
 .s TEST(3)=$p(MESSAGE,";",2)                    ; description
 .s TEST(4)=$p(MESSAGE,";",7)                    ; synonym 1
 .s TEST(5)=$p(MESSAGE,";",8)                    ; synonym 2
 .s TEST(14)=$p(MESSAGE,";",3)                   ; active
 .s TEST(14)=$tr(TEST(14),"x","Y")
 .i '$l(TEST(4)) w !,TEST(2),"--> Synonym must exist..." q
 .i $l(TEST(4)),$d(^TTAB("SY",TEST(4))),^(TEST(4))'=TEST(2) w !,TEST(2),"--> Synonym "_TEST(4)_" already exist..." q
 .i $l(TEST(5)),$d(^TTAB("SY",TEST(5))),^(TEST(5))'=TEST(2) w !,TEST(2),"--> Synonym "_TEST(5)_" already exist..." q
 .i $$select^LVBCTDEP($e(TEST(2))) w !,TEST(2),"--> Department does not exist..." q
 .k PLIST m PLIST=TEST s PLIST=$o(PLIST(""),-1) w !,PLIST(2),"-->",$$insert^LVBCTTS()
 q
 ; TestSets - Report TAB
convTS3 s file="file_name",$zt="ERROR^"_$zn
 o file::0 e  q
 ; it will skip first two records
 f cnt=0,0,1:1 u file r MESSAGE i cnt u 0 d
 .s TEST(2)=$p(MESSAGE,";",1)                    ; code
 .i '$$select^LVBCTTS(TEST(2)) d
 ..s PLIST(17)=$p(MESSAGE,";",2)                 ; report name
 ..s PLIST=$o(PLIST(""),-1) w !,PLIST(2),"-->",$$update^LVBCTTS(PLIST(2))
 q
 ; TestSets - BillingItems TAB
convTS4 s file="file_name",$zt="ERROR^"_$zn
 o file::0 e  q
 ; it will skip first two records
 f cnt=0,0,1:1 u file r MESSAGE i cnt u 0 d
 .s TEST(0)=$p(MESSAGE,";",1)                    ; code
 .i '$$select^LVBCTTS(TEST(0)) d
 ..s TEST(2)=$p(MESSAGE,";",2)                   ; date
 ..s TEST(3)=$p(MESSAGE,";",3)                   ; billing item
 ..s TEST(5)=$p(MESSAGE,";",5)                   ; billing description
 ..i $$select^LVBCTBI(TEST(3)) w !,TEST(3),"--> Billing Item does not exist..." q
 ..k PLIST m PLIST=TEST s PLIST=$o(PLIST(""),-1) w !,TEST(0),"-->",$$insert^LVBCTTSD(TEST(0))
 q
ERROR u 0 w !,$ze c file q
]]></Routine>
</Export>
