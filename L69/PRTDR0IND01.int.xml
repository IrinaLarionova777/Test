<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR0IND01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR0IND01 ; ML 8/12/00 ; Doctor's Report HEADER - India - Max Laboratories
 q
 ; supported calls
 ;
 ; INIT^PRTDR0IND01
 ; QDR^PRTDR0IND01
 ; Header^PRTDR0IND01(page,par1,par2)
 ; Footer^PRTDR0IND01(page,par1,par2)
 ; $$name^PRTDR0IND01(Title,Name,Surname)
 ;
 ; client specific parameters
INIT s MaxLine=35
 q
 ; print header
Header(page,par1,par2) n (page,par1,par2,VISIT,DOC,REPID)
 i '$d(^PR(REPID,"HDR")) d QDR
 d ADD^PRTREP00(REPID,"PRT","#")
 ; find location
 s location="" i VISIT(22)'="" s location=$p(VISIT(22),$c(1),2)
 s (copyto,copyfrom,specimen)="",epis=$p(VISIT(2),$c(1))
 ; find copy-to
 i $d(DOC),$p(VISIT(15),$c(1))=$p(DOC(2),$c(1)) d
 .i $$open^LVBVISDC(epis)
 .f  q:$$fetch^LVBVISDC(epis)  s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 .i $$close^LVBVISDC()
 i $d(HOSP),$p(VISIT(22),$c(1))=$p(HOSP(2),$c(1)) d
 .i $$open^LVBVISDC(epis)
 .f  q:$$fetch^LVBVISDC(epis)  s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 .i $$close^LVBVISDC()
 ; find copy-from
 i $d(DOC),$p(VISIT(15),$c(1))'=$p(DOC(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$name(PLIST(26),PLIST(4),PLIST(3))
 i $d(HOSP),$p(VISIT(22),$c(1))'=$p(HOSP(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$name(PLIST(26),PLIST(4),PLIST(3))
 ; find specimens
 i $$open^LVBVISSP(epis)
 f  q:$$fetch^LVBVISSP(epis)  s specimen=specimen_$s($l(specimen):", ",1:"")_$p(PLIST(2),$c(1))
 i $$close^LVBVISSP()
 i $l(specimen)>20 f j=1:1 i $l($p(specimen,",",1,j))>20 s specimen=$p(specimen,",",1,j-1) q
 ; collected date and time
 s colldate="" s:$p(VISIT(12),$c(1))'="" colldate=$$extdate2^SSUTIL4($p(VISIT(12),$c(1)))
 s colltime="" s:$p(VISIT(13),$c(1))'="" colltime=$$exttime^SSUTIL4($p(VISIT(13),$c(1)))
 ; clinical history
 i $l(VISIT(53)) s VISIT(53)=$p(VISIT(53),"|") d
 .f i=1:1 q:VISIT(53)<70  s VISIT(53)=$p(VISIT(53)," ",1,$l(VISIT(53)," ")-1)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1300h1600V"_$$name($p(VISIT(7),$c(1),2),VISIT(4),VISIT(3)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4030h1600V"_$p(VISIT(26),$c(1))_$s($p(VISIT(26),$c(1))>1:" yrs",1:" yr"))
 i '$l($p(VISIT(5),$c(1),2)) s $p(VISIT(5),$c(1),2)="Unknown"
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4950h1600V"_$p(VISIT(5),$c(1),2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1300h1800V"_$p(VISIT(20),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h1800V"_$p(VISIT(2),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1300h2000V"_$p(VISIT(65),","))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h2000V"_colldate)
 i $d(DOC) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1300h2200V"_$$name(DOC(26),DOC(4),DOC(3)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h2200V"_$$RepDate($p(VISIT(2),$c(1)),page))
 s RepPageType=$g(^TMP("REPORT",$j,page,"PageType"))
 s RepPageDesc="" i '$$select^LVBCTRG(RepPageType) s RepPageDesc=$g(PLIST(3))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a200h2400V"_RepPageDesc)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a200h2450V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
 ; print footer
Footer(page,par1,par2) n (page,REPID,VISIT,epis) s page=$g(page)
 i page'=$o(^TMP("REPORT",$j,""),-1) q
 s xPage="" f  s xPage=$o(^TMP("REPORT",$j,xPage)) q:xPage=""  d
 .s xvtsRID="" f  s xvtsRID=$o(^TMP("REPORT",$j,xPage,"TEST",xvtsRID)) q:xvtsRID=""  d
 ..s dept=$e($p(xvtsRID,"||",2)) q:$$select^LVBCTDEP(dept)
 ..i $p(PLIST(10),$c(1),2)'="" s AuthPath($p(PLIST(10),$c(1)))=$p(PLIST(10),$c(1),2)
 i $d(AuthPath)\10 d
 .s x="" f i=1:1 s x=$o(AuthPath(x))  q:x=""  d
 ..s ReportDr=AuthPath(x)
 ..s cnt=(i\3)+1,DrFooter(cnt)=$g(DrFooter(cnt))_ReportDr_","
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a200h6890VReported by:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 .f i=1:1:$l(DrFooter(1),",") s ReportDr=$p(DrFooter(1),",",i) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s(i=1:1100,1:3100)_"H"_ReportDr)
 .d ADD^PRTREP00(REPID,"PRT","!")
 .s x=1 f  s x=$o(DrFooter(x)) q:x=""  d
 ..f i=1:1:$l(DrFooter(x),",") s ReportDr=$p(DrFooter(x),",",i) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s(i=1:1100,1:3100)_"H"_ReportDr)
 ..d ADD^PRTREP00(REPID,"PRT","!")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p9v0s0b4101T")
 .d ADD^PRTREP00(REPID,"PRT","!")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a200H(For perusal of registered medical practitioners only)")
 q
 ; printer site header
QDR s QDR="MAX",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
 q
name(Title,Name,Surname) n (Title,Name,Surname) s result=""
 s Title=$g(Title),Name=$g(Name),Surname=$g(Surname)
 s result=$s($l(Title):Title_" ",1:"")_Name_" "_Surname
 q result
Address(x1,x2,x3,x4,x5,Address) n (x1,x2,x3,x4,x5,Address) k Address s cnt=0 f j=1:1:3 s Address(j)=""
 s Address=x1_", "_x2_", "_x5
 f  q:$e(Address)'=","&($e(Address)'=" ")  s Address=$e(Address,2,999)
 q
 ; print orginal reported date and time
RepDate(epis,page) n (epis,page) s epis=$g(epis),page=$g(page),(date,time)=""
 s xvtsRID="" f  s xvtsRID=$o(^TMP("REPORT",$j,page,"TEST",xvtsRID)) q:xvtsRID=""  i $p(xvtsRID,"||",3) d
 .q:$$select^LVBVISTS(xvtsRID)  i $l(PLIST(7)) d
 .i '$l(date) s date=PLIST(7),time=PLIST(8) q
 .i PLIST(7)>date s date=PLIST(7),time=PLIST(8) q
 .i PLIST(7)=date,PLIST(8)>time s date=PLIST(7),time=PLIST(8) q
 i $l(date) s date=$$extdate2^SSUTIL4(date)
 q date
 ; tests to follow
TTF(epis) n (epis) s epis=$g(epis),result=""
 s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 . ; do not look at empty tests
 .i '$d(^TTAB("TS",ts,0)) q
 . ; do not look at non printable tests
 .s (ok,x)="" f  s x=$o(^TTAB("TS",ts,0,x)) q:x=""  i $p(^(x),"\",8)'="",$p(^(x),"\",13)="Y" s ok=1 q
 .i ok s cnt="" f  s cnt=$o(^TEPI(epis,1,ts,cnt)) q:cnt=""  d
 ..s rowid=epis_"||"_ts_"||"_cnt,status=""
 ..s status=$$seldata^LVBVISTS(rowid,31),test=$$seldata^LVBCTTS($$seldata^LVBVISTS(rowid,3),4)
 ..i $p(status,$c(1))'="Y" s result=result_$s($l(result):" ",1:"")_test
 q result
]]></Routine>
</Export>
