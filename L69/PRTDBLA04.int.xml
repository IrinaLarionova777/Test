<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDBLA04" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDBLA04 ; ML 18/08/2000 ; Day Book Labels - Territory Health Services
 q
 ; Parameters :  1.Laboratory
 ;               2.Site
 ;               3.Date from
 ;               4.Date to
 ;               5.List episodes/accesion numbers
 ;               6.Order RowID
 ;               7.Bactec label
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=$g(TYPE),PLIST(9)="M" I $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
 ; check if visit/accession should print labels
CHECK(XX99) n (XX99,PAR3,PAR4,PAR7) s XX99=$g(XX99)
 i $l(PAR7) s ^TMP($zn,$j,"LIST",$e($p(XX99,"||",2)),$p(XX99,"||",2)," "," ")="" q
 ; check all blocks
 i $$open^LVBHIVAB(XX99)
 f  q:$$fetch^LVBHIVAB(XX99)  d
 .f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 .k BLOCK m BLOCK=PLIST
 . ; check all records
 .i $$open^LVBHIVAO(BLOCK(1))
 .f  q:$$fetch^LVBHIVAO(BLOCK(1))  d
 ..f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ..i $l(PAR3),PLIST(8)<PAR3 q
 ..i $l(PAR4),PLIST(8)>PAR4 q
 ..i PLIST(7)'="Y" d
 ...s yn="Y" i $e($p(XX99,"||",2))="M",$l(PLIST(3)) d
 ....s xx=$e($p(XX99,"||",2))_"||"_PLIST(3)
 ....s yn=$$seldata^LVBCTDBP(xx,7)
 ...i $p(yn,$c(1))="Y" s ^TMP($zn,$j,"LIST",$e($p(PLIST(1),"||",2)),$p(PLIST(1),"||",2),$p(PLIST(1),"||",3),PLIST(1))=""
 .i $$close^LVBHIVAO()
 i $$close^LVBHIVAB()
 q
 ; create list of labels to print
BODY k ^TMP($zn,$j)
 ; create list of labels for a laboratory
 i '$l(PAR5) d
 .s x1="" f  s x1=$o(^THIRi("PRINT-LABEL",x1)) q:x1=""  d
 ..i $$select^LVBEPVIS(x1,"Y") q
 ..f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ..k VISIT m VISIT=PLIST
 ..s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 ... ; check laboratory
 ...i $l(PAR1),$e(x2,1)'=PAR1 q
 ... ; check site
 ...i $l(PAR2),$e(x2,2)'=PAR2 q
 ...d CHECK(x1_"||"_x2)
 ; create list of labels for an episodes
 i $l(PAR5) d
 .i '$l(PAR6) d
 ..f j=1:1:$l(PAR5,"|") s x1=$p($p(PAR5,"|",j),"~",1),x2=$p($p(PAR5,"|",j),"~",2) i $l(x1),$l(x2) d
 ...i x2'="*",$d(^THIRi("PRINT-LABEL",x1,x2)) d CHECK(x1_"||"_x2)
 ...i x2="*" s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 .... ; check laboratory
 ....i $l(PAR1),$e(x2,1)'=PAR1 q
 .... ; check site
 ....i $l(PAR2),$e(x2,2)'=PAR2 q
 ....d CHECK(x1_"||"_x2)
 .i $l(PAR6) f j=1:1:$l(PAR6,",") s x=$p(PAR6,",",j) i $l(x) s ^TMP($zn,$j,"LIST",$e($p(x,"||",2)),$p(x,"||",2),$p(x,"||",3),x)=""
 ; sort the list
 s Lab="" f  s Lab=$o(^TMP($zn,$j,"LIST",Lab)) q:Lab=""  d
 .s Accession="" f  s Accession=$o(^TMP($zn,$j,"LIST",Lab,Accession)) q:Accession=""  d
 ..s sort="" f  s sort=$o(^TMP($zn,$j,"LIST",Lab,Accession,sort)) q:sort=""  d
 ...s xROWID="" f  s xROWID=$o(^TMP($zn,$j,"LIST",Lab,Accession,sort,xROWID)) q:xROWID=""  d
 ....i Lab'="M" s ^TMP($zn,$j,"LIST-SORT",Lab," ",$p(xROWID,"||",1),xROWID)=""
 ....i Lab="M" d
 .....s specimen="" i '$$select^LVBHIVAC($p(xROWID,"||",1,2)) s specimen=$p(PLIST(3),$c(1))
 .....i '$l(specimen) s specimen=" "
 .....s ^TMP($zn,$j,"LIST-SORT",Lab,specimen,$p(xROWID,"||",1),xROWID)=""
 ; create labels
 s Lab="" f  s Lab=$o(^TMP($zn,$j,"LIST",Lab)) q:Lab=""  d
 .s row=1
 .s sort1="" f  s sort1=$o(^TMP($zn,$j,"LIST",Lab,sort1)) q:sort1=""  d
 ..s sort2="" f  s sort2=$o(^TMP($zn,$j,"LIST",Lab,sort1,sort2)) q:sort2=""  d
 ...s xROWID="" f  s xROWID=$o(^TMP($zn,$j,"LIST",Lab,sort1,sort2,xROWID)) q:xROWID=""  d
 .... ; order information
 ....i $$select^LVBHIVAO(xROWID) q
 ....f j=1:1:4,6:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k ORDER m ORDER=PLIST
 .... ; update record that it had been printed already
 ....f j=1:1:9 i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....s PLIST(7)="Y" i $$update^LVBHIVAO(xROWID)
 .... ; visit information
 ....i $$select^LVBEPVIS($p(xROWID,"||"),"Y") q
 ....f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k VISIT m VISIT=PLIST
 ....s datecoll=$$extdate2^SSUTIL4(VISIT(12)),datecoll=$p(datecoll,"/",1,2)_"/"_$e(datecoll,9,10)
 .... ; separate accession number into 2 parts
 ....s AccNum="" i '$$select^LVBHIVAC($p(xROWID,"||",1,2)) s specimen=$p(PLIST(3),$c(1)) s AccNum=$p(PLIST(8),$c(1))_$p(PLIST(14),$c(1))_$p(PLIST(9),$c(1))_"-"_$p(PLIST(10),$c(1))
 ....k HIVAC m HIVAC=PLIST
 .... ; build lines
 .... ; micro, Bactec
 ....i Lab="M",$l(PAR7)>0 d
 .....i $l(VISIT(3))>19 s surname=$e(VISIT(3),1,18)_"*"
 .....i $l(VISIT(3))=19 s surname=VISIT(3)
 .....i $l(VISIT(3))<19 s surname=VISIT(3)
 .....s col=$g(^TMP($zn,$j,"REPORT",Lab,row))+1 i col>2 s row=row+1,col=1
 .....s ^TMP($zn,$j,"REPORT",Lab,row)=col
 .....s ^TMP($zn,$j,"REPORT",Lab,row,1,col)=surname_" "_$e(VISIT(4))
 .....s ^TMP($zn,$j,"REPORT",Lab,row,2,col)=AccNum
 .....s ^TMP($zn,$j,"REPORT",Lab,row,3,col)=VISIT(2)
 .....s ^TMP($zn,$j,"REPORT",Lab,row,4,col)=$$UPPER^SSUTIL4(VISIT(2))
 .....s ^TMP($zn,$j,"REPORT",Lab,row,5,col)=$e(PAR7,1,5)_"    "_$$extdate2^SSUTIL4(VISIT(6))
 .... ; micro, non-bactec
 ....i Lab="M",'$l(PAR7) d
 .....s surname=""
 .....i $l(VISIT(3))>19 s surname=$e(VISIT(3),1,18)_"*"
 .....i $l(VISIT(3))=19 s surname=VISIT(3)
 .....i $l(VISIT(3))<19 s surname=VISIT(3) 
 .....s proc=$$UPPER^SSUTIL4($p(ORDER(3),$c(1))) i $l(proc)<8 d
 ......f x=$l(proc)+1:1:8 s proc=proc_" "
 .....s col=$g(^TMP($zn,$j,"REPORT",Lab,row))+1 i col>2 s row=row+1,col=1
 .....s ^TMP($zn,$j,"REPORT",Lab,row)=col
 .....s ^TMP($zn,$j,"REPORT",Lab,row,1,col)=surname_" "_$e(VISIT(4))
 .....s ^TMP($zn,$j,"REPORT",Lab,row,2,col)=AccNum
 .....s ^TMP($zn,$j,"REPORT",Lab,row,3,col)=VISIT(2)
 .....s ^TMP($zn,$j,"REPORT",Lab,row,4,col)=$$UPPER^SSUTIL4(VISIT(2))
 .....s ^TMP($zn,$j,"REPORT",Lab,row,5,col)=proc_" "_$$extdate2^SSUTIL4(VISIT(6))_"  "_specimen
 .... ; non-micro
 ....i Lab'="M" d
 .....s col=$g(^TMP($zn,$j,"REPORT",Lab,row))+1 i col>4 s row=row+1,col=1
 .....s ^TMP($zn,$j,"REPORT",Lab,row)=col
 .....s ^TMP($zn,$j,"REPORT",Lab,row,1,col)=$s(AccNum'="":AccNum,1:$p(xROWID,"||",2))
 .....i $l(VISIT(3)_","_$e(VISIT(4)))<16 s ^TMP($zn,$j,"REPORT",Lab,row,2,col)=VISIT(3)_","_$e(VISIT(4))
 .....e  i $l(VISIT(3))<16 s ^TMP($zn,$j,"REPORT",Lab,row,2,col)=VISIT(3)
 .....e  s ^TMP($zn,$j,"REPORT",Lab,row,2,col)=$e(VISIT(3),1,14)_"."
 .....s ^TMP($zn,$j,"REPORT",Lab,row,3,col)=$p(xROWID,"||",1)
 .....s ^TMP($zn,$j,"REPORT",Lab,row,4,col)=$p($p(ORDER(1),"||",3),$c(1))_" "_$p(ORDER(5),$c(1),1)
 .....s ^TMP($zn,$j,"REPORT",Lab,row,5,col)="RDH "_datecoll
 q
 ; print labels
PRINT s Lab="" f  s Lab=$o(^TMP($zn,$j,"REPORT",Lab)) q:Lab=""  d
 .i Lab="M" d
 ..s row="" f  s row=$o(^TMP($zn,$j,"REPORT",Lab,row)) q:row=""  d
 ...k line m line=^TMP($zn,$j,"REPORT",Lab,row) f j=1:1:5 s line(j,1)=$g(line(j,1)),line(j,2)=$g(line(j,2))
 ...s LF=$c(10),LABnum=1
 ...d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_LABnum_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_LABnum_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","Q200,24"_LF)
 ...d ADD^SSPRINT(REPID,"PRT","R10,0"_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A20,5,0,4,1,1,N,"""_line(1,1)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A450,5,0,4,1,1,N,"""_line(1,2)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A140,35,0,3,1,1,N,"""_line(2,1)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A560,35,0,3,1,1,N,"""_line(2,2)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","B110,60,0,1,2,4,56,N,"""_line(3,1)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","B540,60,0,1,2,4,56,N,"""_line(3,2)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A150,130,0,3,1,1,N,"""_line(4,1)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A570,130,0,3,1,1,N,"""_line(4,2)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A20,160,0,2,1,1,N,"""_line(5,1)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","A450,160,0,2,1,1,N,"""_line(5,2)_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FE"_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_LABnum_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","P1"_LF)
 .i Lab'="M" d
 ..s row="" f  s row=$o(^TMP($zn,$j,"REPORT",Lab,row)) q:row=""  d
 ...s LF=$c(10),LABnum=1
 ...d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_LABnum_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_LABnum_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","Q172,29"_LF)
 ...d ADD^SSPRINT(REPID,"PRT","R10,0"_LF)
 ...s line="" f  s line=$o(^TMP($zn,$j,"REPORT",Lab,row,line)) q:line=""  d
 ....s vertpos=$s(line=1:5,line=2:35,line=3:65,line=4:95,1:145)
 ....s col="" f  s col=$o(^TMP($zn,$j,"REPORT",Lab,row,line,col)) q:col=""  s linex=^(col) d
 .....s horzpos=$s(col=1:20,col=2:230,col=3:430,1:630)
 .....d ADD^SSPRINT(REPID,"PRT","A"_horzpos_","_vertpos_",0,"_$s(line=1:3,1:2)_",1,1,N,"""_linex_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FE"_LF)
 ...d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_LABnum_""""_LF)
 ...d ADD^SSPRINT(REPID,"PRT","P1"_LF)
 q
]]></Routine>
</Export>
