<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF418" type="INT" languagemode="0" generated="1"><![CDATA[
MIF418(mi) ; ML 19/02/2001 ; ASTM protocol  -  DADE Sysmex 9250 (Bi-Directional)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4)
 s lf=$c(10),cr=$c(13),nak=$c(15),(result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  d  q
 .d:" L "[(" "_par9_" ") loadlist^MIF000(mi)
 .d:" BU "[(" "_par9_" ") SCAN
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK
 .f  r *R:10 q:$c(R)=eot  q:R=-1  d
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",lf) q:'$l(record)
 ..s record=$e(record,1,$l(record)-1)
 ..d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
 .. ; check record validity
 ..s chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
 ..i $$CHSUM(chrecord)'=chsum d  q
 ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 ..d ACK
 .. ; query record
 ..i $p(record,"|",2)="Q" d  q
 ...s epis=$p(record,"|",4)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="" q
 ...s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix q
 .. ; qc record
 ..i $p(record,"|",2)="S" d  q
 ... ; do once have seen some QC transmitted
 .. ; patient record
 ..i $p(record,"|",2)="P" d  q
 ...d Last s (sample,epis,surname,result,date,time,QC)=""
 ...s surname=$tr($p($p(record,"|",6),"^",2)," ")
 .. ; order record
 ..i $p(record,"|",2)="OBR" d  q
 ...s epis=$tr($p(record,"|",4)," ") s:'$l(epis) epis=$tr($p(record,"|",5)," ")
 .. ; result record
 ..i $p(record,"|",2)="OBX" d  q
 ...s x1=$tr($p($p(record,"|",5),"^",1)," "),x2=$tr($p($p(record,"|",7),"^",1)," "),x3=$tr($p(record,"|",10)," ")
 ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 ... ; date and time
 ...i $l($tr($p(record,"|",14)," ")) d
 ....s date=$$intdate^SSUTIL4($e($p(record,"|",14),5,6)_"/"_$e($p(record,"|",14),7,8)_"/"_$e($p(record,"|",14),1,4))
 ....s time=($e($p(record,"|",14),9,10)*3600)+($e($p(record,"|",14),11,12)*60)
 .. ; last record
 ..i $p(record,"|",2)="L" d Last q
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 .d ORDER
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)=""
 q
SCAN ; check all episodes for tests to be performed
 d scanall^MIF000(mi) q:'$d(^TMP("MIF-SCAN",$j,mi))
 s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
 .d PATDET(epis)
 .s mode="SCAN" d SET(epis,mode)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 .d Send
 .k ^TMP("MIF-SCAN",$j,mi,epis)
 q
ORDER ; create list of orders if exists
 s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  d
 .d scanone^MIF000(mi,epis)
 .d PATDET(epis)
 .s mode="QUERY" d SET(epis,mode)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 .d Send
 .k ^TMP("MIF-SINGLE",$j,mi,epis),^TMP($zn,$j,"ENQ",epis)
 q
SAVE(mi) ; create loadlist records
 s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s sample=^TMP("MIF000",$j,"LL","SAMPLE")
 d PATDET(epis)
 s mode="LOAD" d SET(epis,mode)
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 q 0
PATDET(epis) ; set patient details record
 s priority="" i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 .s dob="" i PLIST(6)'="" s dob=$$extdate2^SSUTIL4(PLIST(6)),dob=$p(dob,"/",3)_$p(dob,"/",2)_$p(dob,"/",1)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_$p(PLIST(20),$c(1))_"|||"_$g(PLIST(4))_"^"_$g(PLIST(3))_"||"_dob_"|"_sex_"|||||"_$g(PLIST(15))_"|||||||||||"_$p(PLIST(22),$c(1))_"^"_$p(PLIST(22),$c(1))
 .s DateRec="" s DateRec=$s(PLIST(61)'="":$$extdate2^SSUTIL4(PLIST(61)),1:$$extdate2^SSUTIL4(+$h)),DateRec=$p(DateRec,"/",3)_$p(DateRec,"/",2)_$p(DateRec,"/",1)
 .i $p(PLIST(17),$c(1))'="",'$$select^LVBCTPR($p(PLIST(17),$c(1))) s priority=$s(PLIST(4)="Y":"S",1:"")
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||^||||||||"
 q
SET(epis,mode) ; set record to be sent to instrument
 s (tc,test)="",tcx=","
 ; process a single episode scan
 i mode="QUERY",$d(^TMP("MIF-SINGLE",$j,mi,epis)) d
 .f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d
 ..q:tcx=tc  s tcx=tcx_tc_","
 ; process an all episode scan
 i mode="SCAN",$d(^TMP("MIF-SCAN",$j,mi,epis)) d
 .f  s tc=$o(^TMP("MIF-SCAN",$j,mi,epis,tc)) q:tc=""  d
 ..q:tcx=tc  s tcx=tcx_tc_","
 ; process a load list episode scan
 i mode="LOAD" d
 .f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  d
 ..q:tcx=tc  s tcx=tcx_tc_","
 i tcx[",aaa," s test="001^CBC"
 i tcx[",bbb," s test="diff^DIFF"
 i tcx[",ccc," s test="ret^RET"
 i tcx[",aaa,",tcx[",bbb," s test="002^CBC+DIFF"
 i tcx[",aaa,",tcx[",ccc," s test="003^CBC+RET"
 i tcx[",aaa,",tcx[",bbb,",tcx[",ccc," s test="004^CBC+DIFF+RET"
 i $l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="OBR|1|"_epis_"||"_test_"|"_priority_"||||||N|||"_DateRec_"|||||||||||||"_$s(priority="S":"S",1:"R")
 i '$l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="Q|1|^"_epis_"||^^^ALL||||||||X"
 q
Send ; send list of orders if exists
 w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M")
 f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=nak:"NAK",1:R),"H<--M")
 i $c(R)=enq q
 i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 q:$$SEND("1|H|\^&|||||||||||A.2|"_$tr($zd(+$h,3),"-")_$tr($$exttime^SSUTIL4($p($h,",",2)),":"))
 s x="" f FRAME=2:1 s x=$o(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_"|"_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
NEXTTRAY(tray) 
 q tray
]]></Routine>
</Export>
