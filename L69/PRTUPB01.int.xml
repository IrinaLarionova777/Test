<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTUPB01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTUPB01 ; IG 13/10/98 ; UnPosted Batches
 q
 ; Parameters : 1.Batches
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j),^TMP($zn,$j,"index")
 k xHEADER d INIT
 s line=1,xHEADER(line)="UnPosted"_$s($l(PAR1):" EDI ",1:" ")_"Batches"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j),^TMP($zn,$j,"index")
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create report
 i '$l(PAR1) s batch="" f  s batch=$o(^TBAT(batch)) q:batch=""  i $p(^(batch),"\")'="P" s ^TMP($zn,$j,"index",batch)=""
 i $l(PAR1) f j=1:1:$l(PAR1,",") s batch=$p(PAR1,",",j) i $l(batch) s ^TMP($zn,$j,"index",batch)=""
 s batch="" f  s batch=$o(^TMP($zn,$j,"index",batch)) q:batch=""  d
 .s bline="" f  s bline=$o(^TBAT(batch,bline)) q:bline=""  d
 ..s bROWID=batch_"||"_bline,epis=$p(^(bline),"\")
 ..s ^TMP($zn,$j,"REPORT",bROWID)=""
 ..i '$l(epis) d
 ...s epis="" f cnt=1:1 s epis=$o(^TBAT(batch,bline,1,epis)) q:epis=""  d
 ....s ^TMP($zn,$j,"REPORT",bROWID,cnt,"EPISODE")=epis
 ..i $l(epis) s ^TMP($zn,$j,"REPORT",bROWID,1,"EPISODE")=epis
 ..i $$open^LVBCRBLA(bROWID)
 ..f cnt=1:1 q:$$fetch^LVBCRBLA(bROWID)  m ^TMP($zn,$j,"REPORT",bROWID,cnt)=PLIST
 ..i $$close^LVBCRBLA()
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s (bROWID,total)="" f  s bROWID=$o(^TMP($zn,$j,"REPORT",bROWID)) q:bROWID=""  d
 ..s batch=$p(bROWID,"||",1) i $$select^LVBCRBT(batch)
 ..s xxx(1)=batch,xxx(2)=$s(PLIST(3)="U":"Unreconciled",PLIST(3)="R":"Reconciled",1:"")
 ..d PRINT^PRTREP00(,.N) k xxx
 ..s line=$p(bROWID,"||",2),xxx(1)=batch,xxx(3)=line
 ..i '$$select^LVBCRBL(bROWID) d
 ...i $l(PLIST(10)) s xxx(2)=PLIST(10)
 ...e  i $l(PLIST(6)) s xx1=PLIST(6),xxx(2)="" d
 ....f j=1:1:$l(xx1,",") d
 .....s xx2=$p($p(xx1,",",j),"-",1)_"||"_$p($p(xx1,",",j),"-",2)
 .....s xxx(2)=xxx(2)_$s($l(xxx(2)):",",1:"")_$p(xx1,",",j)
 .....i '$$select^LVBINVB(xx2),$l(PLIST(7)) s xxx(2)=xxx(2)_" <"_PLIST(7)_">"
 ..s cnt="" f  s cnt=$o(^TMP($zn,$j,"REPORT",bROWID,cnt)) q:cnt=""  d
 ...k plist m plist=^TMP($zn,$j,"REPORT",bROWID,cnt)
 ...s (epis,xxx(4))=$g(plist("EPISODE")) i $$select^LVBEPVIS(epis)
 ...i $d(plist(3)) d
 ....s xxx(5)=plist(3)
 ....s xxx(6)=plist(4),total=total+xxx(6)
 ....i plist(3)="CQ" s xxx(7)=plist(8),xxx(8)=plist(7),xxx(9)=$p(plist(5),$c(1),2) i '$l(xxx(9)) s xxx(9)=plist(5)
 ....i plist(3)="CC" s xxx(9)=plist(9),xxx(8)=plist(7),xxx(7)=$p(plist(6),$c(1),2) i '$l(xxx(7)) s xxx(7)=plist(6)
 ...d PRINT^PRTREP00(.PLIST,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","Total for all Unposted Batches : "_$j(total,0,2))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="UPB"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
