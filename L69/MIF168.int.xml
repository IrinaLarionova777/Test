<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF168" type="INT" languagemode="0" generated="1"><![CDATA[
MIF168(mi) ; ML 30/6/98 ; Diamed AG Maestro software
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),eot=$c(4),ack=$c(6),cr=$c(13),lf=$c(10),hh=$p($h,",",2)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main i " BU B "[(" "_par9_" "),$p($h,",",2)-hh>60!(hh>$p($h,",",2)) d SCAN s hh=$p($h,",",2)
 s record=$$read^MIF000(stx,etx)
 i record="" q
 d trace^MIF000(mi,record,"H<--M")
 s (sample,epis,surname,result,date,time,QC)=""
 ; checksum checking
 s cs=$e(record,$l(record)-1,$l(record))
 i cs'=$$CHSUM($e(record,1,$l(record)-2)) d NAK q
 d ACK
 s epis=$p(record,"|",10),epis=$tr(epis," ")
 i epis="" q
 s result="RESULT\"_$p(record,"|",34)
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
CHSUM(xx) n (xx) s xx=$g(xx),sum=0
 f j=1:1:$l(xx) s sum=sum+$a(xx,j)
 s sum=$zh(sum),sum=$e(sum,$l(sum)-1,$l(sum))
 q sum
ACK w stx_"OK"_cr_lf_$$CHSUM("OK"_cr_lf)_etx,*-3 
 d trace^MIF000(mi,"OK","H-->M")
 q
NAK w stx_"ER"_cr_lf_$$CHSUM("ER"_cr_lf)_etx,*-3
 d trace^MIF000(mi,"ER","H-->M")
 q
SCAN ; send all available episodes
 d scanall^MIF000(mi)
 s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
 .k PLIST i '$$select^LVBEPVIS(epis,"Y") d
 ..s data="CH05|"_epis_"|"_PLIST(46)_"|"_PLIST(20)_"|"_PLIST(3)_" "_PLIST(4)
 ..s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)=data
 s line="" f  s line=$o(^TMIF(mi,10,line)) q:line=""  s data=^(line) d
 .w stx_data_cr_lf_$$CHSUM(data_cr_lf)_etx,*-3
 .d trace^MIF000(mi,data,"H-->M")
 .s record=$$read^MIF000(stx,etx)
 .d trace^MIF000(mi,record,"H<--M")
 .i $p(record,cr)="OK" k ^TMIF(mi,10,line)
 q
]]></Routine>
</Export>
