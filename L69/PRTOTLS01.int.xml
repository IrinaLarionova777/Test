<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTOTLS01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTOTLS01 ; IG 27/05/98 ;Overdue Test list - Summary
 q
 ; Parameters :  1.Department
 ;               2.Test Set (may be synonym)
 ;               3.Test Group
 ;               4.Site
 ;               5.Report type : OTL - Overdue test list
 ;                               RESP - Result Pending list
 ;                               RESNA - Results Entered Not Authorized list
 ;               6.Sort Order  : PRIO - Priority
 ;                               PT   - Patient type
 ;               7.Patient type : Y  - InPatient
 ;                                N  - OutPatient
 ;                                GP - GP
 ;               8.Priority
 ;               9.Patient location
 ;              11.Received before time
 ;              12.Received before date
 ;              13.Patient location type : Y  - InPatient
 ;                                         N  - OutPatient
 ;                                         GP - GP
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT s line=0
 i PAR5="OTL" s line=line+1,xHEADER(line)="Overdue Test list (Summary)"
 i PAR5="RESP" s line=line+1,xHEADER(line)="Result Pending list (Summary)"
 i PAR5="RESNA" s line=line+1,xHEADER(line)="Results Entered Not Authorized list (Summary)"
 s line=line+1,xHEADER(line)=""
 i $l(SITE1) s line=line+1,xHEADER(line)="Site : "_SITE1_" - "_SITE2
 i $l(DEP1) s line=line+1,xHEADER(line)="Department : "_DEP1_" - "_DEP2
 i $l(GROUP1) s line=line+1,xHEADER(line)="Group : "_DEP1_" - "_DEP2
 i $l(PAR7) s line=line+1,xHEADER(line)="Patient Type : "_$s(PAR7="Y":"Inpatient",PAR7="N":"Outpatient",1:"GP")
 i $l(PAR13) s line=line+1,xHEADER(line)="Patient Location Type : "_$s(PAR13="Y":"Inpatient",PAR13="N":"Outpatient",1:"GP")
 i $l(PRIO1) s line=line+1,xHEADER(line)="Priority : "_PRIO1_" - "_PRIO2
 i $l(PLOC1) s line=line+1,xHEADER(line)="Patient Location : "_PLOC1_" - "_PLOC2
 i $l(PAR11)!$l(PAR12) s line=line+1,xHEADER(line)="Received before "_$$extdate^SSUTIL4(PAR12)_" "_$$exttime^SSUTIL4(PAR11)
 d HEADER^PRTREP00(.xHEADER,,,,,PAR6),BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR5 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 i $l(PAR11),'$l(PAR12) s PAR12=+$h
 k PLIST
 ; set order - Patient type as default
 i '$l(PAR6) s PAR6="PT"
 i PAR6="PRIO",$$getall^LVBCTPR()
 i PAR6="PT" k PLIST d
 .s PLIST(1)=$c(2,2,2)_"Y"_$c(28)_"Y"_$c(28)_"InPatient"
 .s PLIST(2)=$c(2,2,2)_"N"_$c(28)_"N"_$c(28)_"OutPatient"
 .s PLIST(3)=$c(2,2,2)_"GP"_$c(28)_"GP"_$c(28)_"GP Practice"
 s PLIST=$o(PLIST(""),-1)
 f j=1:1:PLIST s ^TMP($zn,$j,"Caption",j)=$p($p(PLIST(j),$c(2),4),$c(28),2,3)
 k Caption m Caption=^TMP($zn,$j,"Caption") s Caption=$o(Caption(""),-1)
 k xSORT d H
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 ; additional parameters
 s (SITE1,SITE2,DEP1,DEP2,GROUP1,GROUP2,PRIO1,PRIO2,PLOC1,PLOC2)=""
 ; find test set by synonym
 i $l(PAR2),'$d(^TTAB("TS",PAR2)) s PAR2=$$select^LVBCTSY(PAR2,"TS")
 ; find site
 i $l(PAR4),'$$select^LVBCTUSL(PAR4,"Y") s SITE1=$p(PLIST(2),"\"),SITE2=$p(PLIST(3),"\")
 ; find test group
 i $l(PAR3),'$$select^LVBCTTWG(PAR3,"Y") s GROUP1=$p(PLIST(2),"\"),GROUP2=$p(PLIST(3),"\"),PAR1=$p(PLIST(4),$c(1))
 ; find department
 d
 .i $l(PAR1),'$$select^LVBCTDEP(PAR1,"Y") s DEP1=$p(PLIST(2),"\"),DEP2=$p(PLIST(3),"\") q
 .i $l(PAR2),'$$select^LVBCTDEP($E(PAR2),"Y") s DEP1=$p(PLIST(2),"\"),DEP2=$p(PLIST(3),"\") q
 ; find priority
 i $l(PAR8),'$$select^LVBCTPR(PAR8,"Y") s PRIO1=$p(PLIST(2),"\"),PRIO2=$p(PLIST(3),"\")
 ; find location
 i $l(PAR9),'$$select^LVBCTHOS(PAR9,"Y") s PLOC1=$p(PLIST(2),"\"),PLOC2=$p(PLIST(3),"\")
 f j="PRIO","PT" s xSORT(j)=$$name(j)
 q
name(par) n (par) s name=""
 i par="PRIO" s name="Priority"
 i par="PT" s name="Patient type"
 q name
H s x="",$p(x,"-",20)="",xLINE(1)=" "_x_"-"_"-----------"_"-----------"
 f j=1:1:Caption s x="",$p(x,"-",10)="",xLINE(1)=xLINE(1)_x_"-"
 s xLINE(1)=$e(xLINE(1),1,$l(xLINE(1))-1)_" "
 s x=$e("Test Set"_$j("",20),1,20),xLINE(2)="|"_x_"|  Total   |"_" No Value |"
 f j=1:1:Caption s x=$e($p(Caption(j),$c(28),2)_$j("",9),1,9),xLINE(2)=xLINE(2)_x_"|"
 s x=$j("",20),xLINE(3)="|"_x_"|"_"          |"_"          |"
 f j=1:1:Caption s x="",$p(x,"-",10)="",xLINE(3)=xLINE(3)_x_"|"
 s x="",$p(x,"-",20)="",xLINE(4)="|"_x_"-"_"-----------"_"-----------"
 f j=1:1:Caption s x="",$p(x,"-",10)="",xLINE(4)=xLINE(4)_x_"-"
 s xLINE(4)=$e(xLINE(4),1,$l(xLINE(4))-1)_"|"
 s cnt=1,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 s cnt=2,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 s cnt=3,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 f j=1:1:Caption s cnt=cnt+1,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 s X=xLINE(3)
 s N(1,"Z")=1 f j=2:1 q:'$F(X,"|",N(j-1,"Z")+1)  s N(j,"Z")=$f(X,"|",N(j-1,"Z")+1)-1
 f j=1:1 q:'$d(N(j+1,"Z"))  s N(j,"L")=N(j+1,"Z")-N(j,"Z")-1
 q
 ; create temporary printing file
BODY ; test set
 s %routine=$p($g(^TTAB("REPORT-GENERIC","OTL")),"\",1)
 i $l(%routine) s LineRoutine="BODY^"_%routine i $l($t(@LineRoutine)) d
 .k ^TMP(%routine,$j)
 .x "d BODY^"_%routine i $d(^TMP(%routine,$j,"REPORT")) m ^TMP($zn,$j,"REPORT")=^TMP(%routine,$j,"REPORT")
 q
PRINT s DEP="" f  s DEP=$o(^TMP($zn,$j,"REPORT",DEP)) q:DEP=""  d
 .i '$$select^LVBCTDEP(DEP,"Y") d
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ..d ADDLINE^PRTREP00(REPID,"PRT","~ITALIC")
 ..d ADDLINE^PRTREP00(REPID,"PRT","?5  "_PLIST(2))
 ..d ADDLINE^PRTREP00(REPID,"PRT","?15 "_PLIST(3))
 ..d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s TEST="" f  s TEST=$o(^TMP($zn,$j,"REPORT",DEP,TEST)) q:TEST=""  d
 ..i '$$selskip^LVBCTTS(TEST) d
 ...s xxx(1)=PLIST(2)_" "_PLIST(3)
 ...s SORT="" f  s SORT=$o(^TMP($zn,$j,"REPORT",DEP,TEST,SORT)) q:SORT=""  d
 ....s (sum,x1)="" f  s x1=$o(^TMP($zn,$j,"REPORT",DEP,TEST,SORT,x1)) q:x1=""  d
 .....s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",DEP,TEST,SORT,x1,x2)) q:x2=""  s sum=sum+1
 ....f j=1:1:Caption i $p(Caption(j),$c(28),1)=SORT s xxx(3+j)=sum,xxx(2)=$g(xxx(2))+sum,sum="" q
 ....i sum s xxx(3)=sum,xxx(2)=$g(xxx(2))+sum
 ...d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="OTLS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" d PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
