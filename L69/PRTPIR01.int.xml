<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTPIR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTPIR01 ; ML 23/10/2000 ; PRODUCT INVENTORY REPORT
 q
 ; Parameters :  1. Site
 ;               2. Product
 ;               3. Date From
 ;               4. Date To
 ;               5. Expiry Date
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Product Inventory Report"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s(PAR1="":"All Sites",1:$p(^TTAB("USLOC",PAR1),"^"))
 s line=4,xHEADER(line)=""
 s line=5,xHEADER(line)="Product : "_$s(PAR2="":"All Products",1:$p(^CT("PR",PAR2),"^",2))
 s line=6,xHEADER(line)=""
 i PAR5'="" s line=7,xHEADER(line)="Expiry Date : "_$$extdate2^SSUTIL4(PAR5)
 i PAR5'="" s line=8,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR3,PAR4),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR4+1 f  s date=$o(^TBBPi("TR-Date",date),-1) q:date=""  q:date<PAR3  d
 .s trans="" f  s trans=$o(^TBBPi("TR-Date",date,trans)) q:trans=""  d
 ..s PackID="" f  s PackID=$o(^TBBPi("TR-Date",date,trans,PackID)) q:PackID=""  d
 ...s SecondID="" f  s SecondID=$o(^TBBPi("TR-Date",date,trans,PackID,SecondID)) q:SecondID=""  d
 ....q:$d(^TMP($zn,$j,"PackID",PackID,SecondID))
 ....s TransCnt="" f  s TransCnt=$o(^TBBPi("TR-Date",date,trans,PackID,SecondID,TransCnt),-1) q:TransCnt=""  i '$$select^LVBBBPT(PackID_"||"_SecondID_"||"_TransCnt),PLIST(13)'="Y" q
 ....i TransCnt d
 .....s LastTran="" f  s LastTran=$o(^TBBP(PackID,SecondID,"TRAN",LastTran),-1) q:LastTran=""  i '$$select^LVBBBPT(PackID_"||"_SecondID_"||"_LastTran),PLIST(13)'="Y" q
 .....i LastTran=TransCnt d
 ......q:$$select^LVBBBPT(PackID_"||"_SecondID_"||"_TransCnt)  s status=$p(PLIST(10),$c(1),2)
 ......q:$$select^LVBBBP(PackID_"||"_SecondID)  m PackDet=PLIST k PLIST
 ......s location=$p(PackDet(11),$c(1),2),site=$p(PackDet(11),$c(1),3),product=$p(PackDet(8),$c(1)),BloodGrp=$p(PackDet(9),$c(1)),DateExp=$p(PackDet(13),$c(1))
 ......i BloodGrp="" s BloodGrp=" "
 ......i PAR5'="" i DateExp<PAR3!(DateExp>PAR5) q
 ......i site="" s site=" "
 ......i PAR1'="",PAR1'=site q
 ......i PAR2'="",PAR2'=product q
 ......s ^TMP($zn,$j,"PackID",PackID,SecondID)="",^TMP($zn,$j,"PackDet",site,product,location,BloodGrp,PackID,SecondID,DateExp)=status
 q
PRINT ; print report
 s site="" f  s site=$o(^TMP($zn,$j,"PackDet",site)) q:site=""  d
 .s xxx(1)=site,product="" f  s product=$o(^TMP($zn,$j,"PackDet",site,product)) q:product=""  d
 ..s xxx(3)=product,location="" f  s location=$o(^TMP($zn,$j,"PackDet",site,product,location)) q:location=""  d
 ...s xxx(2)=location,BloodGrp="" f  s BloodGrp=$o(^TMP($zn,$j,"PackDet",site,product,location,BloodGrp)) q:BloodGrp=""  d
 ....s PackID="" f  s PackID=$o(^TMP($zn,$j,"PackDet",site,product,location,BloodGrp,PackID)) q:PackID=""  d
 .....s SecondID="" f  s SecondID=$o(^TMP($zn,$j,"PackDet",site,product,location,BloodGrp,PackID,SecondID)) q:SecondID=""  d
 ......s DateExp="" f  s DateExp=$o(^TMP($zn,$j,"PackDet",site,product,location,BloodGrp,PackID,SecondID,DateExp)) q:DateExp=""  d
 .......s status=^(DateExp)
 .......s xxx(4)=BloodGrp,xxx(5)=PackID_" "_SecondID,xxx(6)=status,xxx(7)=$$extdate2^SSUTIL4(DateExp)
 .......d PRINT^PRTREP00(,.N)
 .......k xxx f i=1:1:10 s xxx(i)=""
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="PIR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
