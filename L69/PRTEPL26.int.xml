<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPL26" type="INT" languagemode="0" generated="1"><![CDATA[
PRTEPL26 ; IG 21/1/99 ; episode patient label - APP
 q
 ; Parameters :  1. Episode number
 ;               2. Label code
 ;               3. Quantity (not in use)
 ;               4. PC-name
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) K ^TMP($ZN,$J)
 d INIT,BODY
 i '$l(PAR2) d
 .k ^TMP("WS",$j) m ^TMP("WS",$j)=^TMP($zn,$j,"REPORT")
 .s label="" f  s label=$o(^TMP("WS",$j,label)) q:label=""  d START^PRTREP00(USER,,PAR1_"\"_label,"EPL")
 .k ^TMP("WS",$j)
 i $l(PAR2) d PRINT
CREND i '$$select^LVBPRPR(REPID) S PLIST(12)=PAR2_"-"_PAR1,PLIST(9)="M" I $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
BODY ; create temporary printing file
 i $l(PAR2) d
 .i $$open^LVBVISTS(PAR1)
 .f  q:$$fetch^LVBVISTS(PAR1)  s ts=$p(PLIST(3),$c(1)),vtsRID=$p(PLIST(1),$c(1)) d
 ..s (label,max)=""
 ..s label=$$seldata^LVBCTTS(ts,23),max=$$seldata^LVBCTLB(label,4)
 ..s label=$p(label,$c(1)),max=$p(max,$c(1))
 ..i PAR2'=label q
 ..i $l(label),max d
 ...s xxLABEL=$s(max>1:label,1:label_" "_vtsRID)
 ...s ^TMP($zn,$j,"REPORT",xxLABEL,ts)=$g(^TMP($zn,$j,"REPORT",xxLABEL,ts))+1
 ...s ^TMP($zn,$j,"REPORT",xxLABEL)=max
 .i $$close^LVBVISTS()
 .s label="" f  s label=$o(^TMP($zn,$j,"REPORT",label)) q:label=""  d
 ..s max=^TMP($zn,$j,"REPORT",label),^TMP($zn,$j,"REPORT",label)=""
 ..s (cnt,ts)="" f  s ts=$o(^TMP($zn,$j,"REPORT",label,ts)) q:ts=""  d
 ...i label'[" " s cnt=cnt+^(ts) q
 ...s cnt=^(ts),xx=cnt\max s:cnt#max xx=xx+1 s ^TMP($zn,$j,"REPORT",label)=xx
 ..i label'[" " s xx=cnt\max s:cnt#max xx=xx+1 s ^TMP($zn,$j,"REPORT",label)=xx
 i '$l(PAR2) d
 .i $$open^LVBVISTS(PAR1)
 .f  q:$$fetch^LVBVISTS(PAR1)  s ts=$p(PLIST(3),$c(1)),vtsRID=$p(PLIST(1),$c(1)) d
 ..s label="",ts=$p(vtsRID,"||",2)
 ..s label=$$seldata^LVBCTTS(ts,23),max=$$seldata^LVBCTLB(label,4)
 ..s label=$p(label,$c(1))
 ..i $l(label),max s ^TMP($zn,$j,"REPORT",label)="",^TMP($zn,$j,"REPORT",label,ts)=""
 .i $$close^LVBVISTS()
 q
PRINT s label="" f  s label=$o(^TMP($zn,$j,"REPORT",label)) q:label=""  s cnt=^(label) i cnt d
 .s LF=$c(10),LABnum=1,tests=""
 .s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",label,ts)) q:ts=""  s syn=$$seldata^LVBCTTS(ts,4) i $l(syn) s tests=tests_" "_syn
 .f j=1:1:cnt d
 ..d ADD^SSPRINT(REPID,"PRT",LF)
 ..d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
 ..d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_LABnum_""""_LF)
 ..d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_LABnum_""""_LF)
 ..d ADD^SSPRINT(REPID,"PRT","Q136,24"_LF)
 ..d ADD^SSPRINT(REPID,"PRT","R8,0"_LF)
 ..d ADD^SSPRINT(REPID,"PRT","B245,15,0,3,2,7,55,N,"""_PAR1_""""_LF)
 ..d ADD^SSPRINT(REPID,"PRT","A290,80,0,4,1,1,N,""*"_PAR1_"*"""_LF)
 ..d ADD^SSPRINT(REPID,"PRT","A240,110,0,2,1,1,N,"""_tests_""""_LF)
 ..d ADD^SSPRINT(REPID,"PRT","FE"_LF)
 ..d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_LABnum_""""_LF)
 ..d ADD^SSPRINT(REPID,"PRT","P1"_LF)
 q
]]></Routine>
</Export>
