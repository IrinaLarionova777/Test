<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCBR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTCBR01 ; IG 07/10/98 ; Reminder SUMMARY
 q
 ; Parameters :  1.Type
 ;               2.Date for Summons List
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 i "12"[PAR1 s line=1,xHEADER(line)="Reminders SUMMARY (Type "_type_")"
 i "3"[PAR1 s line=1,xHEADER(line)="Summons List SUMMARY"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR2),BODY,PRINT
CREND k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$L(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 s type=""
 i PAR1=1 d
 .s pc="" f  s pc=$o(^TMP("REMINDER",$j,pc)) q:pc=""  d  q:$l(type)
 ..s batch="" f  s batch=$o(^TMP("REMINDER",$j,pc,batch)) q:batch=""  d  q:$l(type)
 ...s seq="" f  s seq=$o(^TMP("REMINDER",$j,pc,batch,seq)) q:seq=""  d  q:$l(type)
 ....s type=$p(^(seq),"\")
 i PAR1=2 d
 .s pc="" f  s pc=$o(^TMP("REMINDER",$j,pc)) q:pc=""  d
 ..s batch="" f  s batch=$o(^TMP("REMINDER",$j,pc,batch)) q:batch=""  d
 ...s type=$p(^(batch),"\")
 q
BODY ; create temporary printing file
 ; invoice summary
 i PAR1=1 d
 .s pc="" f  s pc=$o(^TMP("REMINDER",$j,pc)) q:pc=""  d
 ..s batch="" f  s batch=$o(^TMP("REMINDER",$j,pc,batch)) q:batch=""  d
 ...s seq="" f  s seq=$o(^TMP("REMINDER",$j,pc,batch,seq)) q:seq=""  d
 ....s (ep,am1,am2)=""
 ....s epis="" f  s epis=$o(^TMCB(pc,batch,epis)) q:epis=""  i $p(^(epis),"\",2)=seq d
 .....s am1=am1+^(epis),ep=epis
 .....s dc=$p(^TEPI(epis),"\",10),ur=$p(^TEPI(epis),"\",18)
 .....s am2=am2+^TDEB(ur,dc,0,epis)
 ....i $l(ep) s ^TMP($zn,$j,"REPORT",1,pc,batch,seq)=am1_"\"_am2_"\"_ep
 i PAR1=2 d
 .s pc="" f  s pc=$o(^TMP("REMINDER",$j,pc)) q:pc=""  d
 ..s batch="" f  s batch=$o(^TMP("REMINDER",$j,pc,batch)) q:batch=""  d
 ...s (am1,am2)=""
 ...s epis="" f  s epis=$o(^TMCB(pc,batch,epis)) q:epis=""  d
 ....s am1=am1+^(epis)
 ....s dc=$p(^TEPI(epis),"\",10),ur=$p(^TEPI(epis),"\",18)
 ....s am2=am2+^TDEB(ur,dc,0,epis)
 ...s ^TMP($zn,$j,"REPORT",2,pc,batch)=am1_"\"_am2
 i PAR1=3 d
 .s epis="" f  s epis=$o(^TDAY(3,"S-"_PAR2,epis)) q:epis=""  d
 ..s batchRID=$$seldata^LVBEPVIS(epis,57)
 ..s batchRID=$p(batchRID,$c(1)),pc=$p(batchRID,"||",1),batch=$p(batchRID,"||",2)
 ..s am1=+$g(^TMCB(pc,batch,epis)),seq=$p($g(^TMCB(pc,batch,epis)),"\",2)
 ..s dc=$p(^TEPI(epis),"\",10),ur=$p(^TEPI(epis),"\",18)
 ..s am2=+^TDEB(ur,dc,0,epis)
 ..i $l(seq) s ^TMP($zn,$j,"REPORT",3,pc,batch,seq)=am1_"\"_am2_"\"_epis
 ..i '$l(seq) s ^TMP($zn,$j,"REPORT",3,pc,batch,epis)=am1_"\"_am2_"\"_epis
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s type="" f  s type=$o(^TMP($zn,$j,"REPORT",type)) q:type=""  d
 ..s (x2,x3,cnt)=""
 ..i type=2 d  q
 ...s pc="" f  s pc=$o(^TMP($zn,$j,"REPORT",2,pc)) q:pc=""  d
 ....s batch="" f  s batch=$o(^TMP($zn,$j,"REPORT",2,pc,batch)) q:batch=""  d
 .....s xxx(1)=pc_"-"_batch,xxx(2)=$p(^(batch),"\",1),xxx(3)=$p(^(batch),"\",2),ep=$p(^(batch),"\",3)
 .....k PLIST i $l(ep),$$select^LVBEPVIS(ep)
 .....d PRINT^PRTREP00(.PLIST,.N) k xxx
 ..s pc="" f  s pc=$o(^TMP($zn,$j,"REPORT",type,pc)) q:pc=""  d
 ...s batch="" f  s batch=$o(^TMP($zn,$j,"REPORT",type,pc,batch)) q:batch=""  d
 ....s epseq="" f  s epseq=$o(^TMP($zn,$j,"REPORT",type,pc,batch,epseq)) q:epseq=""  d
 .....s xxx(1)=pc_"-"_batch_"-"_epseq,xxx(2)=$p(^(epseq),"\",1),xxx(3)=$p(^(epseq),"\",2),ep=$p(^(epseq),"\",3)
 .....s x2=x2+xxx(2),x3=x3+xxx(3),cnt=cnt+1
 .....k PLIST i $l(ep),$$select^LVBEPVIS(ep)
 .....d PRINT^PRTREP00(.PLIST,.N) k xxx
 ..d PRINT^PRTREP00(,.N) k xxx
 ..s xxx(1)="Total",xxx(2)=x2,xxx(3)=x3
 ..d PRINT^PRTREP00(,.N) k xxx
 ..s xxx(1)="Average",xxx(2)=$j(x2/cnt,0,2),xxx(3)=$j(x3/cnt,0,2)
 ..d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="CBR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
