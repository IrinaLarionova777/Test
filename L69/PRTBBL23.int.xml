<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBL23" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBL23 ; ML 05/05/2004 ; BloodBank Labels - Goulburn Valley Health
 q
 ; Parameters :  1. vtsRID
 ;               2. packRID
 ;               3. User
 ;               4. R - Blood Group Check
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j),^TMP("PRTDR00",$j),^TMP("REPORT",$j)
 k xHEADER d INIT,BODY,PRINT
 k ^TMP($zn,$j)
 q
 ; initialise parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 i $l(PAR3) d
 .i '$$selrow^MVBSSUSR(PAR3,"Y") s PAR3=PAR3_$c(1)_PLIST(3)
 q
 ; create report
BODY i $l(PAR1) d
 .s epis=$p(PAR1,"||",1)
 .k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 .k DOC i '$$select^LVBCTDR($p(VISIT(15),$c(1))) m DOC=PLIST
 .k VISTS i '$$select^LVBVISTS(PAR1) m VISTS=PLIST
 .k BBBG1 i '$$select^LVBBBBG($p($g(VISIT(69)),$c(1))) m BBBG1=PLIST
 .i $$open^LVBVISTE(PAR1)
 .f  q:$$fetch^LVBVISTE(PAR1)  k VISTE m VISTE=PLIST d
 ..i $l(PAR2),(","_PAR2_",")'[(","_$p(PLIST(2),$c(1))_"||"_$p(PLIST(3),$c(1))_",") q
 ..s order=VISTE(13) i '$l(order) s order=" "
 ..m ^TMP($zn,$j,"REPORT",order,VISTE(1))=VISTE
 .i $$close^LVBVISTE()
 q
PRINT s LabelCount="",Sheet=1 k LABEL
 i $d(^TMP($zn,$j,"REPORT")) d
 .s x1="" f  s x1=$o(^TMP($zn,$j,"REPORT",x1)) q:x1=""  d
 ..s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",x1,x2)) q:x2=""  d
 ...m VISTE=^TMP($zn,$j,"REPORT",x1,x2)
 ...i '$$select^LVBVISTE(x2)  k VISTE m VISTE=PLIST d
 ....s rowid=$p(PLIST(2),$c(1))_"||"_$p(PLIST(3),$c(1))
 ....i '$$select^LVBBBP(rowid) k PACK m PACK=PLIST d
 .....k BBBG2 i '$$select^LVBBBBG($p($g(PACK(9)),$c(1))) m BBBG2=PLIST
 .....d LABEL
 .....k PLIST m PLIST=PACK s PLIST(22)=+$h,PLIST(23)=$p(VISIT(20),$c(1)) i $$update^LVBBBP(rowid)
 i $d(LABEL) d
 .d ADD^SSPRINT(REPID,"PRT","~RESET")
 .d ADD^PRTREP00(REPID,"PRT","~ORIENTATION=0")
 .d ADD^PRTREP00(REPID,"PRT","~FONT-COURIER")
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADD^PRTREP00(REPID,"PRT","~FONTSIZE=10")
 .s CurrSheet=$p($o(LABEL("")),"-")
 .s SheetLabel="" f  s SheetLabel=$o(LABEL(SheetLabel)) q:SheetLabel=""  d
 ..i $p(SheetLabel,"-")'=CurrSheet d
 ...s CurrSheet=$p(SheetLabel,"-") d ADD^SSPRINT(REPID,"PRT","#")
 ..s line="" f  s line=$o(LABEL(SheetLabel,line)) q:line=""  d
 ...s pos="" f  s pos=$o(LABEL(SheetLabel,line,pos)) q:pos=""  d
 ....s cnt=$p(SheetLabel,"-",2)
 ....s label=$s(cnt=1:"1-1",cnt=2:"1-2",cnt=3:"2-1",cnt=4:"2-2",cnt=5:"3-1",cnt=6:"3-2",1:"4-1")
 ....s vertical=$s(line=1:200,line=2:300,line=3:400,line=4:500,line=5:600,line=6:700,line=7:800,line=8:900,1:1000)
 ....s vertical=vertical+$s($p(label,"-")=1:0,$p(label,"-")=2:1100,$p(label,"-")=3:2150,1:3250)
 ....s horizontal=$s($p(label,"-",2)=1:100,1:3000)
 ....i pos'=1 s horizontal=horizontal+$s(line=2:1500,line=3:900,line=6:840,1:420)
 ....i $d(LABEL(SheetLabel,line,pos,"B")) d ADD^SSPRINT(REPID,"PRT","~BOLD")
 ....d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a"_horizontal_"h"_vertical_"V"_LABEL(SheetLabel,line,pos))
 ....i $d(LABEL(SheetLabel,line,pos,"B")) d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 .d ADD^SSPRINT(REPID,"PRT","#")
 q
 ; format label
LABEL s LabelCount=LabelCount+1
 s LABEL(Sheet_"-"_LabelCount,1,1)="Name: "_$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3))
 s LABEL(Sheet_"-"_LabelCount,2,1)="U.R/Ref No.: "_$s(VISIT(16)'="":$p(VISIT(16),$c(1)),1:$p(VISIT(20),$c(1)))
 s LABEL(Sheet_"-"_LabelCount,2,2)="DoB: "_$$extdate2^SSUTIL4(VISIT(6))
 s LABEL(Sheet_"-"_LabelCount,3,1)="Patient Group: "
 s LABEL(Sheet_"-"_LabelCount,3,2)=$g(BBBG1(3))
 s LABEL(Sheet_"-"_LabelCount,3,2,"B")=""
 s LABEL(Sheet_"-"_LabelCount,4,1)=""
 s LABEL(Sheet_"-"_LabelCount,5,1)="Product: "_$p(PACK(8),$c(1),2)
 s LABEL(Sheet_"-"_LabelCount,6,1)="Donation No.: "
 s LABEL(Sheet_"-"_LabelCount,6,2)=$p(PACK(2),$c(1))
 s LABEL(Sheet_"-"_LabelCount,6,2,"B")=""
 s LABEL(Sheet_"-"_LabelCount,7,1)="Group: "
 s LABEL(Sheet_"-"_LabelCount,7,2)=$g(BBBG2(3))
 s LABEL(Sheet_"-"_LabelCount,7,2,"B")=""
 f j=1:1:3 s @("xV"_j)=$p(VISTE(1),"||",j)
 s xx="" i $l(xV1),$l(xV2),$l(xV3) s xx=$p($g(^TEPI(xV1,1,xV2,xV3,"DATA","B0101")),"\")
 s LABEL(Sheet_"-"_LabelCount,8,1)="Valid Until: "_$s(xx?5N:$$extdate2^SSUTIL4(xx),1:"")
 s LABEL(Sheet_"-"_LabelCount,9,1)="Status: "_$p(VISTE(5),$c(1),2)
 i LabelCount#7=0 s Sheet=Sheet+1,LabelCount=""
 q
]]></Routine>
</Export>
