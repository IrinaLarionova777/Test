<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="CLNBQADMIN" type="INT" languagemode="0"><![CDATA[
CLNBQADMIN
	; +----------------+
	; | Binary Queries |
	; | Administration |
	; +----------------+
	;  Nov 24, 2006 by Mc_Fly
	;  TrakHealth BCN
	q
getautho(user,pass) n spass,group,idexe
	; Retorna
	; P0 = Servidor alternatiu
	; P1 = Nom del servidor
	; P2 = Menus amb permisos
	; P3 = Versió rutines
	s P3="1.6"
	i '$l(user) q 0
	i '$d(^SSU("SSUSR",1,user)) q 0
	s spass=$p($g(^SSU("SSUSR",1,user)),"^",3)
	i spass'=$$ENCR^SSUTIL2(pass) q 0
	s group=$p(^SSU("SSUSR",1,user),"^",4)
	i '$l(group) q 0
	s idexe=$o(^SS("SSVBE",0,"Executable","BINARYQUERIES",""))
	i '$l(idexe) q 0
	i '$d(^SSU("SSGRP",group,"VBEX",idexe)) q 0
 
	i $tr($p($p($p($zv,")",2),"("),".")," ","")>4 d
	. s ns=$zu(5)
	. i $zu(5,"%CACHELIB")
	. d $system.License.Login(user)
	. i $zu(5,ns)
 
	s P0=$g(^BQS("MServer"))
	s P1=$zu(110)
	
	s i="",(P2,sep)=""
	f  s i=$o(^SSU("SSGRP",group,"VBEX",idexe,"VBOP",i)) q:i=""  d
	. s ord=$p(i,"||",2) q:ord=""
	. s P2=P2_sep_$p($g(^SS("SSVBE",idexe,"SSVBO",ord)),"^",1),sep="^"
	q 1
getcubes()	n i,n,des,r
	; Retorna els cubs de la pantalla d'Administracio
	k PLIST
	s i="",n=0
	f  s i=$o(^BQC(i)) q:i=""  d
	. s des=$p(^(i),"\",1)
	. s wdes=$p($g(^BQZ(i)),"\") i $l(wdes) s des=wdes
	. s n=n+1,PLIST(n)=i_$c(1)_des
	q n
getgrid(item,cube)
	; Retorna els cubs, families o camps a la pantalla d'administració
	; 
	; Retorna
	; PLIST
	; P0 l'ordre dels camps
	k PLIST
	s i=0,n=0 k tord
	i item=0 d cube
	i item=1 d family
	i item=2 d field
	s (o,i,P0,sep)=""
	f  s o=$o(tord(o)) q:o=""  d
	. f  s i=$o(tord(o,i)) q:i=""  s P0=P0_sep_i,sep=","
	q max
cube
	f  s i=$o(^BQC(i)) q:i=""  d
	. s odes=$p(^(i),"\",1)
	. s r=$g(^BQZ(i)),des=$p(r,"\"),vis=+$p(r,"\",2),ord=+$p(r,"\",3)
	. s n=n+1,PLIST(n)=i_$c(9)_odes_$c(9)_des
	. i vis s tord(ord,i)=""
	s max=$o(^BQC(""),-1)
	q
family
	f  s i=$o(^BQC(cube,1,i)) q:i=""  d
	. s odes=$p(^(i),"\",1)
	. s r=$g(^BQZ(cube,1,i)),des=$p(r,"\",1),vis=+$p(r,"\",2),ord=+$p(r,"\",3)
	. s n=n+1,PLIST(n)=i_$c(9)_odes_$c(9)_des
	. i vis s tord(ord,i)=""
	s max=$o(^BQC(cube,1,""),-1)
	q
field
	f  s i=$o(^BQC(cube,2,i)) q:i=""  d
	. s odes=$p(^(i),"\",1)
	. s r=$g(^BQZ(cube,2,i)),des=$p(r,"\",1),vis=+$p(r,"\",2),ord=+$p(r,"\",3)
	. s n=n+1,PLIST(n)=i_$c(9)_odes_$c(9)_des
	. i vis s tord(ord,i)=""
	s max=$o(^BQC(cube,2,""),-1)
	q
savegrid(item,cube)
	; Salva els canvis de la pantalla d'administracio
	i item=0 s i="" f  s i=$o(PLIST(i)) q:i=""  d
	. s code=$p(PLIST(i),"\"),^BQZ(code)=$p(PLIST(i),"\",2,4)
	i item=1 s i="" f  s i=$o(PLIST(i)) q:i=""  d
	. s code=$p(PLIST(i),"\"),^BQZ(cube,1,code)=$p(PLIST(i),"\",2,4)
	i item=2 s i="" f  s i=$o(PLIST(i)) q:i=""  d
	. s code=$p(PLIST(i),"\"),^BQZ(cube,2,code)=$p(PLIST(i),"\",2,4)
	q 1
]]></Routine>
</Export>
