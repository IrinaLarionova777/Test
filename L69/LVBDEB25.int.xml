<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBDEB25" type="INT" languagemode="0"><![CDATA[
LVBDEB25 ; IG 12/07/98 ; MRN control for client without THOS (auto-allocation only)
 ;
 ; supported calls:
 ;
 ; $$check^LVBDEB25(deb,step)
 ;
 ; check debtor number
 ; return code = ReturnCode_$c(1)_Flag1_$c(1)_Flag2
 ; Return Code - 10 - exist in TDEB              - MRN - last visit
 ;               20 - exist in THOS              - MRN - multiple THOS data
 ;               30 - exist in Labtrak as HRN    -     - multiple Labtrak MRN's and last visits
 ;               50 - does not exist but valid   - MRN - single THOS data
 ;              100 - invalid MRN
 ; Flag1       - Search DB : H - LVBHOSP
 ;                           L - LVBEPVIS
 ; Flag2       - Accept Hospital DB MRN as LabTrak MRN
check(deb,step) n (PLIST,step,deb) k PLIST
 ; existing LabTrak MRN                  - ok=10_$c(1)_$c(1)_"N"
 i '$$select^LVBDEB(deb) s ok=10_$c(1)_$c(1)_"N",last=PLIST(4) k PLIST s PLIST(1)=deb,PLIST(2)=last g end
 ; request for new MRN                   - ok=50_$c(1)_"LH"_$c(1)_"N"
 i deb="." s ok=50_$c(1)_"LH"_$c(1)_"N" k PLIST s PLIST(1)="{New}" g end
 ; request for new MRN with URprefix     - ok=50_$c(1)_"LH"_$c(1)_"N"
 i deb?1U1"." s ok=50_$c(1)_"LH"_$c(1)_"N" d  i ok k PLIST s PLIST(1)=$e(deb)_"{New}" g end
 .i $$select^LVBCTUR($e(deb),"Y") s ok=0 q
 .i PLIST(7)="Y" s ok=0 q
 ; existing LabTrak-Medicare              - ok=30_$c(1)_$c(1)_"N"
 i deb?1"M"1P10N s ok=30_$c(1)_$c(1)_"N" d  i ok g end
 .k ^TMP($zn,$j) s deb=$e(deb,3,99)
 .s epis="" f  s epis=$o(^TIND(1,deb,epis)) q:epis=""  d
 ..s mrn=$$mrn^LVBEPVIS(epis) i $l(mrn),'$d(mrn(mrn)) s mrn(mrn)="" d
 ...i '$$select^LVBDEB(mrn) s cnt=$o(^TMP($zn,$j,""),-1)+1 s:cnt=1 cnt=2 s ^TMP($zn,$j,cnt)=mrn_$c(1)_PLIST(4)
 .i '$d(^TMP($zn,$j)) s ok=0 q
 .k PLIST s PLIST(1)="{New}" m PLIST=^TMP($zn,$j)
 ; invalid MRN
 s ok=100 k PLIST
end s PLIST=$o(PLIST(""),-1)
 q ok
new(pref) s pref=$g(pref)
nx k PLIST s PLIST=1
 i pref="" s PLIST(1)=$$next^LVBDEB("^CF(""DEB-CNT"")")
 i pref'="" s PLIST(1)=pref_$$next^LVBDEB("^CF(""DEB-CNT"",pref)")
 ; check if number already exists
 i PLIST(1)'="" s PLIST(1)=$e(PLIST(1)+1000000,2,8) i $d(^TDEB(PLIST(1))) g nx
 q 0
]]></Routine>
</Export>
