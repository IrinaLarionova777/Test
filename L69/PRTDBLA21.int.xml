<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDBLA21" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDBLA21 ; ML 11/12/2002 ; Day Book Labels - Histopath/Dermatopath
 q
 ; Parameters :  1.Laboratory
 ;               2.Site
 ;               3.Date from
 ;               4.Date to
 ;               5.List episodes/accesion numbers
 ;               6.Order RowID
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=$g(TYPE),PLIST(9)="M" I $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
 ; check if visit/accession should print labels
CHECK(XX99) n (XX99,PAR3,PAR4) s XX99=$g(XX99)
 ; check all blocks
 i $$open^LVBHIVAB(XX99)
 f  q:$$fetch^LVBHIVAB(XX99)  d
 .f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 .k BLOCK m BLOCK=PLIST
 . ; check all records
 .i $$open^LVBHIVAO(BLOCK(1))
 .f  q:$$fetch^LVBHIVAO(BLOCK(1))  d
 ..f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ..i $l(PAR3),PLIST(8)<PAR3 q
 ..i $l(PAR4),PLIST(8)>PAR4 q
 ..i PLIST(7)'="Y" s ^TMP($zn,$j,"LIST",PLIST(1))=""
 .i $$close^LVBHIVAO()
 i $$close^LVBHIVAB()
 q
 ; create list of labels to print
BODY k ^TMP($zn,$j)
 ; create list of labels for a laboratory
 i '$l(PAR5) d
 .s x1="" f  s x1=$o(^THIRi("PRINT-LABEL",x1)) q:x1=""  d
 ..i $$select^LVBEPVIS(x1,"Y") q
 ..f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ..k VISIT m VISIT=PLIST
 ..s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 ... ; check laboratory
 ...i $l(PAR1),$e(x2,1)'=PAR1 q
 ... ; check site
 ...i $l(PAR2),$e(x2,2)'=PAR2 q
 ...D CHECK(x1_"||"_x2)
 ; create list of labels for an episodes
 i $l(PAR5) d
 .i '$l(PAR6) d
 ..f j=1:1:$l(PAR5,"|") s x1=$p($p(PAR5,"|",j),"~",1),x2=$p($p(PAR5,"|",j),"~",2) i $l(x1),$l(x2) d
 ...i x2'="*",$d(^THIRi("PRINT-LABEL",x1,x2)) D CHECK(x1_"||"_x2)
 ...i x2="*" s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 .... ; check laboratory
 ....i $l(PAR1),$e(x2,1)'=PAR1 q
 .... ; check site
 ....i $l(PAR2),$e(x2,2)'=PAR2 q
 ....D CHECK(x1_"||"_x2)
 .i $l(PAR6) f j=1:1:$l(PAR6,",") i $l($p(PAR6,",",j)) s ^TMP($zn,$j,"LIST",$p(PAR6,",",j))=""
 ; create labels
 s Position=0,xROWID="" f  s xROWID=$o(^TMP($zn,$j,"LIST",xROWID)) q:xROWID=""  d
 . ; order information
 .i $$select^LVBHIVAO(xROWID) q
 .f j=1:1:4,6:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 .k ORDER m ORDER=PLIST
 . ; update record that it had been printed already
 .f j=1:1:9 i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 .s PLIST(7)="Y" i $$update^LVBHIVAO(xROWID)
 . ; visit information
 .i $$select^LVBEPVIS($p(xROWID,"||"),"Y") q
 .f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 . ; start build lines
 .s Position=Position+1
 .s LINE(1,Position)=$$extdate2^SSUTIL4(+$h)
 .s LINE(2,Position)=$p(xROWID,"||",1)
 .i $l(VISIT(3)_","_$e(VISIT(4)))<16 s LINE(3,Position)=VISIT(3)_","_$e(VISIT(4))
 .e  i $l(VISIT(3))<16 s LINE(3,Position)=VISIT(3)
 .e  s LINE(3,Position)=$e(VISIT(3),1,14)_"."
 .s LINE(4,Position)=$e($p(xROWID,"||",2),1,3)_"-"_$e($p(xROWID,"||",2),4,99)
 .s LINE(5,Position)="B"_$p($p(ORDER(1),"||",3),$c(1))_" "_"L"_ORDER(10)_" "_$p(ORDER(5),$c(1),1)
 .i Position=3 d PRINT
 d PRINT
 q
 ; print labels
PRINT i $d(LINE) d
 .s LF=$C(10),labnum=1
 .d ADD^SSPRINT(REPID,"PRT",LF)
 .d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
 .d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_labnum_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_labnum_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","Q184,24"_LF)
 .d ADD^SSPRINT(REPID,"PRT","R16,0"_LF)
 .f Position=1:1:3 d
 ..i $d(LINE(1,Position)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(Position-1*232))_",5,0,2,1,1,N,"""_LINE(1,Position)_""""_LF)
 ..i $d(LINE(2,Position)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(Position-1*232))_",35,0,4,1,1,N,"""_LINE(2,Position)_""""_LF)
 ..i $d(LINE(3,Position)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(Position-1*232))_",65,0,2,1,1,N,"""_LINE(3,Position)_""""_LF)
 ..i $d(LINE(4,Position)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(Position-1*232))_",95,0,2,1,1,N,"""_LINE(4,Position)_""""_LF)
 ..i $d(LINE(5,Position)) d ADD^SSPRINT(REPID,"PRT","A"_(88+(Position-1*232))_",146,0,2,1,1,N,"""_LINE(5,Position)_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","FE"_LF)
 .d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_labnum_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","P1"_LF)
 k LINE s Position=0
 q
]]></Routine>
</Export>
