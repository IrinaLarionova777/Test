<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBCCR01VIC" type="INT" languagemode="0" generated="1"><![CDATA[
LVBCCR01VIC ; ML 24/12/99 ; CCR file creation - Trak (Victoria)
 ;
 ; supported calls
 ;
 ; $$create^LVBCCR01VIC(type,TYPE,DATE)
 ;
create(type,TYPE,DATE) n (type,TYPE,DATE) s type=$g(type),TYPE=$g(TYPE),DATE=$g(DATE),file="",state="VIC"
 ; find list of patients
 i $$select^MVBCFLAB(1) q
 f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 s dir=PLIST(48),rid=$s(type="P":PLIST(47),1:PLIST(49)),labs=PLIST(46)
 s lab="" f i=1:1:$l(labs,",") i $p($p(labs,",",i),"=",1)=state s lab=$p($p(labs,",",i),"=",2) q
 f j="\","/" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
 ; create file
 i $d(^TCCR(type,"DATA",TYPE,DATE)),type="P",$l(lab) d
 .s date=$$extdate2^SSUTIL4(DATE)
 .s file=dir_"v"_$s(TYPE="R":"rr",1:"pr")_$e($p(date,"/",3),3,4)_$p(date,"/",2)_$p(date,"/",1)_".txt"
 .o file:"WNS":5 e  q
 .u file
 .s epis="" f  s epis=$o(^TCCR(type,"DATA",TYPE,DATE,epis)) q:epis=""  d
 ..i $g(^(epis))'=state q
 ..i $$select^LVBEPVIS(epis,"Y") q
 ..f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ..k VISIT m VISIT=PLIST
 ..i $$select^LVBCTDR(VISIT(15),"Y") q
 ..f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ..k DOC m DOC=PLIST
 ..s ts="" f  s ts=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts)) q:ts=""  d
 ...s cnt="" f  s cnt=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)) q:cnt=""  d
 ....s rid=rid+1,^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)=rid
 ....s RECORD=$$txt(lab,2)_" "_$$txt(epis,8)_"     "_$$txt(VISIT(3),20)_" "_$$txt(VISIT(4),20)_" "
 ....i TYPE="R" d
 .....s RECORD=RECORD_$$txt($$date(VISIT(6),3),6)_" "
 ..... ; process results record
 .....s x1="" f j="A","B","S","E","O","R" s x2=$g(^TEPI(epis,1,ts,cnt,"CCR",j)) i $l(x2) s x1=x1_$$txt(j_x2,2)
 .....s RECORD=RECORD_x1
 ....i TYPE="D" d
 .....s RECORD=RECORD_$$txt($$date(VISIT(6),1),8)_" "
 .....s RECORD=RECORD_$$txt(VISIT(32),30)_" "
 .....s RECORD=RECORD_$$txt(VISIT(34),15)_" "
 .....s RECORD=RECORD_$$txt(VISIT(36),25)_" "_$$txt(VISIT(20),8)_" "
 .....s RECORD=RECORD_$$txt(DOC(18),8)_" "_$$txt(DOC(3),20)_" "
 .....s RECORD=RECORD_$$txt($$date(VISIT(14),2),6)_" "_$$txt(VISIT(8),11)
 ....w RECORD,!
 .c file
 q file
txt(xx1,xx2,xx3) n (xx1,xx2,xx3) s xx1=$tr($g(xx1),""""),xx2=$g(xx2),xx3=$g(xx3)
 i $tr(xx1," ")="",xx3 s xx1="?"
 i xx2 s result=$e(xx1_$j("",xx2),1,xx2)
 i 'xx2 s result=xx1
 q result
date(date,format) n (date,format) s xx=$$extdate2^SSUTIL4($g(date))
 i format=1 q $tr(xx,"/")
 i format=2 s xx=$e($p(xx,"/",3),3,4)_$p(xx,"/",2)_$p(xx,"/",1)
 i format=3 s xx=$p(xx,"/",1)_$p(xx,"/",2)_$e($p(xx,"/",3),3,4)
 q xx
]]></Routine>
</Export>
