<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF120" type="INT" languagemode="0" generated="1"><![CDATA[
MIF120(mi) ; ML 11/6/99 ; SERONO Baker 9020
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4)
 s lf=$c(10),cr=$c(13),nak=$c(21),record=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s (sample,epis,surname,result,date,time,QC)=""
 f  r *x:20 q:x=-1  d
 .i $c(x)=enq d trace^MIF000(mi,"ENQ","H<--M"),ACK q
 .i $c(x)=etx d RES q
 .s record=record_$c(x)
 q
RES ; extract and file results
 s record=$tr(record,$c(13,10),",")
 s record=$p(record,",",$l(record,"SYSTEM"),99)
 d trace^MIF000(mi,record,"H<--M"),ACK
 s time=$p(record,",",3),time=$$inttime^SSUTIL4($e(time,9,13))
 s date=$p(record,",",4),date=$$intdate^SSUTIL4($e(date,9,16))
 s epis=$p($p(record,",",5),":",2),epis=$tr(epis," ","") q:epis=""
 s res=$p($p($p(record,",",7,99),eot),"------"),res=$e(res,1,$l(res)-1)
 f i=1:1:$l(res,",") d
 .s resx=$p(res,",",i) q:resx=""
 .s x1=$tr($p(resx,":",1)," "),resflg=$p(resx,":",2)
 .f j=1:1 q:$e(resflg)'=" "  s resflg=$e(resflg,2,99)
 .s x2=$p(resflg," "),x3=$p(resflg," ",2)
 .s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,record,result,date,time,QC)=""
 q
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
]]></Routine>
</Export>
