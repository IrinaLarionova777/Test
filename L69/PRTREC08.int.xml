<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTREC08" type="INT" languagemode="0" generated="1"><![CDATA[
PRTREC08 ; ML 03/03/00 ; RECEIPTS - Sydney Skin Pathology
 q
 ; Parameters :  1. Receipt Number
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) S PLIST(12)=$g(bs)_"-"_PAR1
 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
 ; create temporary printing file
BODY s (CreditDate,epis)="" i '$$select^LVBRCPT(PAR1) d
 .f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 .k REC m REC=PLIST
 .i '$$select^LVBCRBL(REC(8)) d
 ..f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ..k crLINE m crLINE=PLIST
 .. ; get single episodes
 ..i $l(crLINE(3)),crLINE(7) i '$l(epis) s epis=crLINE(3)
 .. ; get episode from multiple episodes
 ..i $$open^LVBCRBLB(REC(8))
 ..i '$$fetch^LVBCRBLB(REC(8)) s epis=$p(PLIST(2),$c(1))
 ..i $$close^LVBCRBLB()
 ; update receipt
 i REC(3)="" d
 .s REC(3)=+$h,REC(4)=$p($h,",",2)
 .k PLIST m PLIST=REC i $$update^LVBRCPT(PAR1) 
 i $l(epis),'$$select^LVBEPVIS(epis,"Y") d
 .s ^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW")=crLINE(7)
 .s credit="" i $l(PLIST(87)) s epis=PLIST(87)
 .s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 .s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ..s rowid=deb_"||"_date_"||"_ep
 ..i $$open^LVBDEBVT(rowid)
 ..f  q:$$fetch^LVBDEBVT(rowid)  d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k TRAN m TRAN=PLIST
 ...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) s credit=credit-PLIST(14)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) s credit=credit-PLIST(16),CreditDate=TRAN(4)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) s credit=credit-PLIST(9),CreditDate=TRAN(4)
 ..i $$close^LVBDEBVT()
 .i credit d
 ..s ^TMP($zn,$j,"REPORT",epis,"CREDIT-TOTAL")=credit
 ..s ^TMP($zn,$j,"REPORT",epis,"CREDIT-DATE")=CreditDate
 Q
PRINT ; print report
 d QDR
 s max=20
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3))
 .s compabn=$$INVOICE($p(VISIT(57),$c(1)))
 .s seq="" i $l($p(VISIT(57),"||",1)),$l($p(VISIT(57),"||",2)) s seq=$p(^TMCB($p(VISIT(57),"||",1),$p(VISIT(57),"||",2),epis),"\",2)
 .d ITEM,ADDRESS
 .d ADD^PRTREP00(REPID,"PRT","#")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s0b5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h500V"_compabn)
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h620V")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"*c750h250v15g2P")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s1b5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a800h775VRECEIPT")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h50V")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4300h700V"_$p(VISIT(57),"||",1)_" "_$p(VISIT(57),"||",2)_" "_seq)
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4300h950V"_epis)
 .d ADD^PRTREP00(REPID,"PRT","~BOLD")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h1200V"_name),ADD^PRTREP00(REPID,"PRT","~NORMAL"),LN
 .f i=1:1:4 i $l(ADDRESS(i)) d ADD^PRTREP00(REPID,"PRT","?15 "_ADDRESS(i)),LN
 .s xx=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-DATE")) i 'xx s xx=+$h
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4300h1200V"_$$DateTrans(xx))
 .s tot=$g(^TMP($zn,$j,"REPORT",epis,"TOT")),credit=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-TOTAL"))
 .s current=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW"))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4300h1450V$ "_$j(current,7,2))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4300h1775V$ "_$j(tot-credit,7,2))
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 s ^TMP($zn,$j,"REPORT",epis,"TOT")=amount
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
 ; get company ABN
INVOICE(batch) s COMP(10)="",batch=$g(batch) i $l(batch) d
 .k INVOICE i '$$select^LVBINVB(batch) m INVOICE=PLIST
 .s COMP(10)=$$seldata^LVBCTCO(INVOICE(6),10)
 q COMP(10)
LN ; add line to report
 d ADD^PRTREP00(REPID,"PRT","!")
 q
QDR ; set header
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"E")
 s QDR="SSP-REC",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
 q
DateTrans(date) ; change date to dd Month Name yyyy
 s date=$$extdate2^SSUTIL4(date) s day=$p(date,"/",1),mth=$p(date,"/",2),year=$p(date,"/",3)
 s date=day_" "_$s(mth="01":"January",mth="02":"February",mth="03":"March",mth="04":"April",mth="05":"May",mth="06":"June",mth="07":"July",mth="08":"August",mth="09":"September",mth="10":"October",mth="11":"November",1:"December")_" "_year
 q date
]]></Routine>
</Export>
