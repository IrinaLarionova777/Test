<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBDEB09" type="INT" languagemode="0"><![CDATA[
LVBDEB09 ; IG 12/07/98 ; MRN control for Quinn's  ; Compiled June 8, 2001 13:54:59 for Cache for Windows
 ;
 ; supported calls:
 ;
 ; $$check^LVBDEB09(deb,step)
 ;
 ; check debtor number
 ; return code = ReturnCode_$c(1)_Flag1_$c(1)_Flag2
 ; Return Code - 10 - exist in TDEB              - MRN - last visit
 ;               20 - exist in THOS              - MRN - multiple THOS data
 ;               30 - exist in Labtrak as HRN    -     - multiple Labtrak MRN's and last visits
 ;               50 - does not exist but valid   - MRN - single THOS data
 ;              100 - invalid MRN
 ; Flag1       - Search DB : H - LVBHOSP
 ;                           L - LVBEPVIS
 ; Flag2       - Accept Hospital DB MRN as LabTrak MRN
check(deb,step) n (PLIST,deb) k PLIST,^TMP($zn,$j)
 ; existing LabTrak MRN                          - ok=10_$c(1)_$c(1)_"N"
 i '$$select^LVBDEB(deb) s ok=10_$c(1)_$c(1)_"N",last=PLIST(4) k PLIST s PLIST(1)=deb,PLIST(2)=last g end
 ; request for new MRN                           - ok=50_$c(1)_"L"_$c(1)_"N"
 i deb="." s ok=50_$c(1)_"L"_$c(1)_"N" i '$$new^LVBDEB("") g end
 ; request for new MRN with URprefix             - ok=50_$c(1)_"L"_$c(1)_"N"
 i deb?1U1"." s ok=50_$c(1)_"L"_$c(1)_"N" d  i ok,'$$new^LVBDEB($e(deb)) g end
 .i $$select^LVBCTUR($e(deb),"Y") s ok=0 q
 .i PLIST(7)="Y" s ok=0 q
 ; existing LabTrak-Medicare              - ok=30_$c(1)_$c(1)_"N"
 i deb?1"M"1P9N s ok=30_$c(1)_$c(1)_"N" d  i ok g end
 .k ^TMP($zn,$j) s deb=$e(deb,3,99)
 .s db="" f  s db=$o(^TIND(1,db)) q:db=""  i $e(db,1,$l(deb))=deb d
 ..s epis="" f  s epis=$o(^TIND(1,db,epis)) q:epis=""  d
 ...s mrn=$$mrn^LVBEPVIS(epis) i $l(mrn),'$d(mrn(mrn)) s mrn(mrn)="" d
 ....i '$$select^LVBDEB(mrn) s cnt=$o(^TMP($zn,$j,""),-1)+1,^TMP($zn,$j,cnt)=mrn_$c(1)_PLIST(4)
 .i '$d(^TMP($zn,$j)) s ok=0 q
 .k PLIST m PLIST=^TMP($zn,$j)
 ; invalid MRN
 s ok=100 k PLIST
end k ^TMP($zn,$j) s PLIST=$o(PLIST(""),-1)
 q ok
]]></Routine>
</Export>
