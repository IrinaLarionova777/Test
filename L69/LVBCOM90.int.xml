<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBCOM90" type="INT" languagemode="0" generated="1"><![CDATA[
LVBCOM90 ; IG 29/4/98 ; Common routines
 ;
 ; supported calls:
 ;
 ; conv1^LVBCOM90
 ; conv2^LVBCOM90
 ; conv3^LVBCOM90
 ; conv4^LVBCOM90
 ; -------------------- 990420 -------
 ; conv6^LVBCOM90
 ; conv7^LVBCOM90
 ; conv8^LVBCOM90
 ; conv9^LVBCOM90
 ; conv10^LVBCOM90
 ; conv11^LVBCOM90
 ; conv12^LVBCOM90
 ;
 ; order : 6,7,8,9,7,10,11,12
 ; -------------------- 6.2 -------
 ; conv13^LVBCOM90
 ; conv14^LVBCOM90
 ; conv15^LVBCOM90
 ; conv16^LVBCOM90
 ; conv17^LVBCOM90
 ; conv18^LVBCOM90
 ; conv19^LVBCOM90
 ; order : 13,14,15,16,17,18,19
start d conv6,conv7,conv8,conv9,conv7,conv10,conv11,conv12
 w !!!
 d conv13,conv14,conv15,conv16,conv17,conv18,conv19
 q
 ; convert ^TEPI from V2
conv0 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 . ; setup level 0 for Patient History,Comments
 .i $d(^TEPI(ep,2)) s ^TEPI(ep,2,0)=$o(^TEPI(ep,2,""),-1)
 .i $d(^TEPI(ep,3)) s ^TEPI(ep,3,0)=$o(^TEPI(ep,3,""),-1)
 .k ^issTEPI m ^issTEPI(ep,1)=^TEPI(ep,1)
 .s ts="" f  s ts=$o(^issTEPI(ep,1,ts)) q:ts=""  d
 ..i $g(^(ts))="" q
 ..s test=$p(ts,".") k ^TEPI(ep,1,ts)
 ..s cnt=$o(^TEPI(ep,1,test,""),-1)+1 m ^TEPI(ep,1,test,cnt)=^issTEPI(ep,1,ts)
 q
 ; run in USER namespace on full TEPI
FIXconv0 ;w ! d conv0
 ;w ! s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 ;.w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 ;.s ts="" f  s ts=$o(^TEPI(ep,1,ts)) q:ts=""  d
 ;..i $o(^TEPI(ep,1,ts,""),-1)=1 k ^TEPI(ep,1,ts)
 ;w ! d conv1 w ! d conv105 w ! d conv106 w ! d conv108
 ;w ! s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 ;.w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 ;.s ts="" f  s ts=$o(^TEPI(ep,1,ts)) q:ts=""  m ^tepi(ep,1,ts)=^TEPI(ep,1,ts)
 s ep="" f Xcnt=1:1 s ep=$o(^tepi(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .s ts="" f  s ts=$o(^tepi(ep,1,ts)) q:ts=""  d
 ..k ^TEPI(ep,1,ts)
 ..m ^TEPI(ep,1,ts)=^tepi(ep,1,ts)
 q
 ; create invoice batches
conv100 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .s x1="" f  s x1=$o(^TEPI(ep,6,x1)) q:x1=""  i $p(^(x1),"\",1)="IN" d
 ..s date=$p(^(x1),"\",2),pc=$p($p(^(x1),"\",4)," ",2),batch=$p($p(^(x1),"\",4)," ",3)
 ..s amount=$p($p(^(x1),"\",4)," ",1)
 ..s ^TMCB(pc,batch)=date_"\\\xx\"_(+$h)
 ..s ^TMCB(pc,batch,ep)=amount
 s pc="" f  s pc=$o(^TMCB(pc)) q:pc=""  d
 .s batch="" f  s batch=$o(^TMCB(pc,batch)) q:batch=""  d
 ..s ep=$o(^TMCB(pc,batch,"")),$p(^TMCB(pc,batch),"\",3)=$$epcomp^LVBVIS1(ep)
 q
 ; create ^TBAT,^TJOU
conv101 k ^TBAT,^TJOU s ^CF("JOU-CNT")=""
 s mrn="" f Xcnt=1:1 s mrn=$o(^TDEB(mrn)) q:mrn=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .s date="" f  s date=$o(^TDEB(mrn,date)) q:date=""  d
 ..s x1="" f  s x1=$o(^TDEB(mrn,date,1,x1)) q:x1=""  d
 ... ; journal
 ...i x1'["-" d
 ....i '$d(^TDEB(mrn,date,1,x1,1,1)) q
 ....s JCode=$tr($p(^TDEB(mrn,date,1,x1,1,1),"\",1)," ")
 ....s JDate=date
 ....s JEpisode=$o(^TDEB(mrn,date,1,x1,0,"")) i '$l(JEpisode) w !,"Journal ",mrn," ",date q
 ....l +^CF("JOU-CNT") s ^CF("JOU-CNT")=$g(^CF("JOU-CNT"))+1 s JOrder="J"_$g(^CF("JOU-CNT")) l -^CF("JOU-CNT")
 ....s JAmount=$tr($p(^TDEB(mrn,date,1,x1,1,1),"\",2)," ")
 ....i $p($g(^TTAB("JO",JCode)),"\",2)="C" s JAmount=-JAmount
 ....s JComment=$p(^TDEB(mrn,date,1,x1,1,1),"\",4)
 ....s $p(^TJOU(JCode,JDate,JEpisode,JOrder),"\",3)=JAmount
 ....s $p(^TJOU(JCode,JDate,JEpisode,JOrder),"\",5)=JComment
 ... ; credit
 ...i x1["-" d
 ....s CRBatch=$p(x1,"-",1),CRLine=$p(x1,"-",2),CRDate=date
 ....s $p(^TBAT(CRBatch),"\",1)="P"
 ....s $p(^TBAT(CRBatch),"\",4)=CRDate
 ....s $p(^TBAT(CRBatch),"\",10)=CRDate
 ....s ^TBAT(CRBatch,CRLine)=""
 ....s x2="" f  s x2=$o(^TDEB(mrn,date,1,x1,1,x2)) q:x2=""  d
 .....s CROrder=x2
 .....s CRType=$p(^TDEB(mrn,date,1,x1,1,x2),"\",1)
 .....s CRAmount=-$p(^TDEB(mrn,date,1,x1,1,x2),"\",2)
 .....s CRBank=$p(^TDEB(mrn,date,1,x1,1,x2),"\",3)
 .....s CRCardNumber=$p(^TDEB(mrn,date,1,x1,1,x2),"\",4)
 .....s CRAuthority=$p(^TDEB(mrn,date,1,x1,1,x2),"\",5)
 .....s CRExpiry=$p(^TDEB(mrn,date,1,x1,1,x2),"\",6)
 .....s $p(^TBAT(CRBatch,CRLine,0,CROrder),"\",1)=CRType
 .....s $p(^TBAT(CRBatch,CRLine,0,CROrder),"\",2)=CRAmount
 .....s $p(^TBAT(CRBatch,CRLine,0,CROrder),"\",4)=CRCardNumber
 .....s $p(^TBAT(CRBatch,CRLine,0,CROrder),"\",5)=CRAuthority
 .....s $p(^TBAT(CRBatch,CRLine,0,CROrder),"\",6)=CRExpiry
 .....s $p(^TBAT(CRBatch,CRLine,0,CROrder),"\",7)=CRBank
 ....s x2="" f  s x2=$o(^TDEB(mrn,date,1,x1,0,x2)) q:x2=""  d
 .....s CREpisode=x2,CLDate=^(x2) d
 ......i $d(^TDEB(mrn,CLDate,0,x2,x1)) s CRAmount=-$p(^(x1),"\",1) q
 ......s (x,CRAmount)="" w !,"Credit ",mrn," ",x2
 ......f  s x=$o(^TEPI(x2,6,x),-1) q:x=""  i $p(^(x),"\",1)="IN" s CRAmount=+$p(^(x),"\",4) q
 .....s $p(^TBAT(CRBatch,CRLine,1,CREpisode),"\",1)=CRAmount
 q
 ; check credit batches
conv102 k ^TMP($zn,$j)
 ; first run
 s batch="" f  s batch=$o(^TBAT(batch)) q:batch=""  d
 .s $p(^TBAT(batch),"\",3)=$o(^TBAT(batch,""),-1)
 .s (Am,line)="" f  s line=$o(^TBAT(batch,line)) q:line=""  d
 ..s (Am1,ep)="" f  s ep=$o(^TBAT(batch,line,1,ep)) q:ep=""  s Am1=Am1+^(ep)
 ..s (Am2,xx)="" f  s xx=$o(^TBAT(batch,line,0,xx)) q:xx=""  s Am2=Am2+$p(^(xx),"\",2)
 ..i Am1-Am2 d
 ...w !,batch," ",line," ",Am1,"-",Am2
 ...s ^TMP($zn,$j,batch,line)=""
 ..s $p(^TBAT(batch,line),"\",2)=Am1,Am=Am+Am1
 .s $p(^TBAT(batch),"\",2)=Am
 ; check bad batches
 s batch="" f  s batch=$o(^TMP($zn,$j,batch)) q:batch=""  d
 .s line="" f  s line=$o(^TMP($zn,$j,batch,line)) q:line=""  d
 ..s ep="" f  s ep=$o(^TBAT(batch,line,1,ep)) q:ep=""  d
 ...s x="" f  s x=$o(^TEPI(ep,6,x),-1) q:x=""  i $p(^(x),"\",1)="IN" d  q
 ....s x1=$p($p(^(x),"\",4)," ",2),x2=$p($p(^(x),"\",4)," ",3)
 ....s ^TMP($zn,$j,batch,line,x1,x2)=""
 ..s x1="" f  s x1=$o(^TMP($zn,$j,batch,line,x1)) q:x1=""  d
 ...s x2="" f  s x2=$o(^TMP($zn,$j,batch,line,x1,x2)) q:x2=""  d
 ....s ep="" f  s ep=$o(^TMCB(x1,x2,ep)) q:ep=""  s s=^(ep) d
 .....i '$d(^TBAT(batch,line,1,ep)) s ^TBAT(batch,line,1,ep)=s
 ; second run
 s batch="" f  s batch=$o(^TBAT(batch)) q:batch=""  d
 .s $p(^TBAT(batch),"\",3)=$o(^TBAT(batch,""),-1)
 .s (Am,line)="" f  s line=$o(^TBAT(batch,line)) q:line=""  d
 ..s (Am1,ep)="" f  s ep=$o(^TBAT(batch,line,1,ep)) q:ep=""  s Am1=Am1+^(ep)
 ..s (Am2,xx)="" f  s xx=$o(^TBAT(batch,line,0,xx)) q:xx=""  s Am2=Am2+$p(^(xx),"\",2)
 ..i Am1-Am2 d
 ...w !,batch," ",line," ",Am1,"-",Am2
 ...s $p(^TBAT(batch),"\",1)="U"
 ...s $p(^TBAT(batch),"\",10)=""
 ..s $p(^TBAT(batch,line),"\",2)=Am1,Am=Am+Am1
 .s $p(^TBAT(batch),"\",2)=Am
 k ^TMP($zn,$j)
 q
conv103 ; convert TRBL from V2.2
 s dt="" f  s dt=$o(^TRBL(dt)) q:dt=""  w !,dt d
 .s ep="" f  s ep=$o(^TRBL(dt,ep)) q:ep=""  d
 ..s xx=$tr($p(^(ep),"\",3)," ")
 ..s ev="" f  s ev=$o(^TEPI(ep,6,ev)) q:ev=""  i $p(^(ev),"\")="IN",$tr($p($p(^(ev),"\",4)," ",2,3)," ")=xx s xx=$p($p(^(ev),"\",4)," ",2,3) q
 ..i xx'[" " w !,dt," ",ep q
 ..s line=$p(xx," ",1)_"\"_$p(^TEPI(ep),"\",7)_"\"_$$epcomp^LVBVIS1(ep)_"\Y\"_$p(xx," ",2)
 ..s ^TRBL(dt,ep)=line
 q
conv104 ; create TMCB from TRBL for V2.2
 s dt="" f  s dt=$o(^TRBL(dt)) q:dt=""  w !,dt d
 .s ep="" f  s ep=$o(^TRBL(dt,ep)) q:ep=""  d
 ..s xx1=$p(^(ep),"\",1),xx2=$p(^(ep),"\",5),xx3=0 i $l(xx1),$l(xx2),'$d(^TMCB(xx1,xx2,ep)) d
 ...s ev="" f  s ev=$o(^TEPI(ep,6,ev)) q:ev=""  i $p(^(ev),"\")="IN",$p($p(^(ev),"\",4)," ",2,3)=(xx1_" "_xx2) s xx3=$p($p(^(ev),"\",4)," ")
 ...i 'xx3 w !,dt," ",ep," 1" q
 ...i xx3,'$d(^TMCB(xx1,xx2)) w !,dt," ",ep," 2" q
 ...s ^TMCB(xx1,xx2,ep)=xx3
 q
 ; convert Standard comments from V2.2
 ; must run after conv1
conv105 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .s ts="" f  s ts=$o(^TEPI(ep,1,ts)) q:ts=""  d
 ..s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  d
 ...s ti="" f  s ti=$o(^TEPI(ep,1,ts,cnt,"DATA",ti)) q:ti=""  d
 ....s x2=^(ti) i $l(x2),$l(x2)<50,$d(^TTAB("TC",ti,2,x2)) s ^TEPI(ep,1,ts,cnt,"DATA",ti)="{"_x2_"}"
 ....s x1=0 f  s x1=$o(^TEPI(ep,1,ts,cnt,"DATA",ti,"REM",x1)) q:x1=""  d
 .....s x2=^(x1) i $l(x2),$l(x2)<50,$d(^TTAB("TC",ti,2,x2)) s ^TEPI(ep,1,ts,cnt,"DATA",ti,"REM",x1)="{"_x2_"}"
 q
 ; convert users from V2.2
conv106 k
 s USER("ISSAAK")="issaak"
 s USER("SALLY")="issaak"
 s USER("SUE")="issaak"
 s USER("DEMO")="demo"
 s oldUser="old"
 s ep="" f cnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(cnt#10000=0:"#",cnt#1000=0:".",1:"")
 .s x=$p(^(ep),"\",30) i $l(x) d
 ..i $d(USER(x)) s $p(^TEPI(ep),"\",30)=USER(x)
 ..i '$d(USER(x)) s $p(^TEPI(ep),"\",30)=oldUser
 .s ts="" f  s ts=$o(^TEPI(ep,1,ts)) q:ts=""  d
 ..s tscnt="" f  s tscnt=$o(^TEPI(ep,1,ts,tscnt)) q:tscnt=""  d
 ...f j=3,6,7,36 s x=$p(^TEPI(ep,1,ts,tscnt),"\",j) i $l(x) d
 ....i $d(USER(x)) s $p(^TEPI(ep,1,ts,tscnt),"\",j)=USER(x)
 ....i '$d(USER(x)) s $p(^TEPI(ep,1,ts,tscnt),"\",j)=oldUser
 q
 ; convert invoices from V2.2
conv107 s ep="" f cnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(cnt#10000=0:"#",cnt#1000=0:".",1:"")
 .s inv=$p($g(^TEPI(ep,7)),"\",4) i $l(inv) d
 ..s ok=0
 ..f j=1:1:$l(inv)-1 s x1=$e(inv,1,j),x2=$e(inv,j+1,$l(inv)) i $d(^TMCB(x1,x2,ep)) s ok=1,$p(^TEPI(ep,7),"\",4)=x1_"||"_x2
 ..i 'ok w !,ep
 q
 ; convert Antibiotics from V2.2
conv108 s ep="" f cnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(cnt#10000=0:"#",cnt#1000=0:".",1:"")
 .s ts="" f  s ts=$o(^TEPI(ep,1,ts)) q:ts=""  d
 ..s tscnt="" f  s tscnt=$o(^TEPI(ep,1,ts,tscnt)) q:tscnt=""  d
 ...s ti="" f  s ti=$o(^TEPI(ep,1,ts,tscnt,"DATA",ti)) q:ti=""  i $d(^TEPI(ep,1,ts,tscnt,"DATA",ti,"ANT")) d
 ....s ant="" f  s ant=$o(^TEPI(ep,1,ts,tscnt,"DATA",ti,"ANT",ant)) q:ant=""  s $p(^(ant),"\",2)="Y"
 q
 ; convert ^TEPI
conv1 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .k ^issTEPI m ^issTEPI(ep)=^TEPI(ep)
 .s ts="" f  s ts=$o(^issTEPI(ep,1,ts)) q:ts=""  d
 ..s cnt="" f  s cnt=$o(^issTEPI(ep,1,ts,cnt)) q:cnt=""  d
 ...k ^TMP($zn,$j) s ok=1,test=^issTEPI(ep,1,ts,cnt)
 ...s ti="" f  s ti=$o(^issTEPI(ep,1,ts,cnt,ti)) q:ti=""  d  q:'ok
 ....i (ti="SNOMED")!(ti="CCR")!(ti="DATA") s ok=0 q
 ....s ^TMP($zn,$j,"DATA",ti)=$g(^issTEPI(ep,1,ts,cnt,ti))
 ....s data="REM" i $e($p($g(^TTAB("TC",ti)),"\",3))="V" s data="ANT"
 ....s x="" f  s x=$o(^issTEPI(ep,1,ts,cnt,ti,x)) q:x=""  d  q:'ok
 .....i (x="REM")!(x="ANT") s ok=0 q
 .....s ^TMP($zn,$j,"DATA",ti,data,x)=^issTEPI(ep,1,ts,cnt,ti,x)
 .....i data="REM" s ^TMP($zn,$j,"DATA",ti,data,0)=$o(^TMP($zn,$j,"DATA",ti,data,""),-1)
 ...i $d(^issTEPI(ep,"1R",ts,cnt,"S")) m ^TMP($zn,$j,"SNOMED")=^issTEPI(ep,"1R",ts,cnt,"S") k ^TMP($zn,$j,"SNOMED",0)
 ...i $d(^issTEPI(ep,"1CC",ts,cnt)) m ^TMP($zn,$j,"CCR")=^issTEPI(ep,"1CC",ts,cnt)
 ...i 'ok w !,ep," ",ts," ",cnt
 ...i ok d
 ....k ^TEPI(ep,1,ts,cnt),^TEPI(ep,"1R",ts,cnt),^TEPI(ep,"1CC",ts,cnt)
 ....m ^TEPI(ep,1,ts,cnt)=^TMP($zn,$j)
 ....s ^TEPI(ep,1,ts,cnt)=test
 q
 ; convert ^TEPI events
conv2 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .k ^issTEPI m ^issTEPI(ep,6)=^TEPI(ep,6) k ^TEPI(ep,6)
 .s x1="" f x2=1:1 s x1=$o(^issTEPI(ep,6,x1)) q:x1=""  s xx=^(x1) d
 ..i ($p(xx,"\",2)="Final ")!($p(xx,"\",2)="") q
 ..s ^TEPI(ep,6,x2)=$p(xx,"\",1)_"\"_$p(x1,":",1)_"\"_($p(x1,":",2)*60)_"\"_$p(xx,"\",2)
 q
 ; create ^TEPI WS indexes
conv3 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .s ts="" f  s ts=$o(^TEPI(ep,1,ts)) q:ts=""  d
 ..s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  d
 ...s ws="" f  s ws=$o(^TTAB("TS",ts,6,ws)) q:ws=""  d
 ....s wsno="" f  s wsno=$o(^TWKS(ws,2,wsno)) q:wsno=""  i $d(^TWKS(ws,2,wsno,ep)) q
 ....i $l(wsno) k PLIST s PLIST(2)=ts,PLIST(3)=cnt i $$insert^LVBWKSRT(ws_"||"_wsno_"||"_ep)
 s ws="" f  s ws=$o(^TWKS(ws)) q:ws=""  d
 .s wsno="" f  s wsno=$o(^TWKS(ws,2,wsno)) q:wsno=""  d
 ..s ep="" f  s ep=$o(^TWKS(ws,2,wsno,ep)) q:ep=""  d
 ...i $d(^TWKS(ws,2,wsno,ep))<10 d  q
 ....i $$delete^LVBWKSRE(ws_"||"_wsno_"||"_ep)
 ...i $$select^LVBWKSRE(ws_"||"_wsno_"||"_ep)
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...i $$update^LVBWKSRE(ws_"||"_wsno_"||"_ep)
 q
conv4 k ^TMP($zn,$j) s x1="" f  s x1=$o(^TMCB(x1)) q:x1=""  w !,x1 d
 .s x2="" f  s x2=$o(^TMCB(x1,x2)) q:x2=""  d
 ..s date=$p(^TMCB(x1,x2),"\",1)
 ..s ep="" f  s ep=$o(^TMCB(x1,x2,ep)) q:ep=""  d
 ...s ^TMP($zn,$j,ep)=$g(^TMP($zn,$j,ep))+1
 ...s ^TMP($zn,$j,ep,date,x1,x2)=""
 ...s ^TEPI(ep,"INV",date,x1,x2)=""
 ;s ep="" f cnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  w $s(cnt#10000=0:"#",cnt#1000=0:".",1:"") d
 ;.i $g(^TMP($zn,$j,ep))>1 s date=$o(^TMP($zn,$j,ep,"")) i $l(date) d
 ;..s x1=$o(^TMP($zn,$j,ep,date,"")) i $l(x1) d
 ;...s x2=$o(^TMP($zn,$j,ep,date,x1,"")) i $l(x2) d
 ;....s $p(^TEPI(ep,7),"\",6)=x1_"||"_x2
 ;.i $g(^TMP($zn,$j,ep))'>1 s $p(^TEPI(ep,7),"\",6)=""
 k ^TMP($zn,$j)
 q
 ; create new journal file
conv6 k ^TMP($zn,$j)
 s x1="" f  s x1=$o(^TJOU(x1)) q:x1=""  m ^TMP($zn,$j)=^TJOU(x1)
 k ^TJOU m ^TJOU=^TMP($zn,$j)
 k ^TMP($zn,$j)
 Q
 ; setup rebill events
conv7 k ^TMP($zn,$j)
 s date="" f  s date=$o(^TRBL(date)) q:date=""  d
 .s epis="" f  s epis=$o(^TRBL(date,epis)) q:epis=""  i $p(^(epis),"\",4)="Y" s ^TMP($zn,$j,epis,1,date,"RB")=""
 s epis="" f  s epis=$o(^TMP($zn,$j,epis)) q:epis=""  d
 .s date="" f  s date=$o(^TEPI(epis,"INV",date)) q:date=""  d
 ..s x2="" f  s x2=$o(^TEPI(epis,"INV",date,x2)) q:x2=""  d
 ...s x3="" f  s x3=$o(^TEPI(epis,"INV",date,x2,x3)) q:x3=""  s ^TMP($zn,$j,epis,1,date,"IN",x2_"||"_x3)=""
 .s (error,invoice)=""
 .s date="" f  s date=$o(^TMP($zn,$j,epis,1,date)) q:date=""  d  q:error
 ..f type="RB","IN","RB","IN" d  q:error
 ...i type="RB" d  q
 ....i $d(^TMP($zn,$j,epis,1,date,type)),$l(invoice) d
 .....s ^TMP($zn,$j,epis,0,date,"RB")=invoice
 .....k ^TMP($zn,$j,epis,1,date,"RB") s invoice=""
 ...i type="IN" d  q
 ....i $d(^TMP($zn,$j,epis,1,date,"IN")),$l(invoice) s error=0,invoice="" w !,error," ",epis
 ....i $d(^TMP($zn,$j,epis,1,date,"IN")),'$l(invoice) d  q
 .....s invoice1=$o(^TMP($zn,$j,epis,1,date,"IN",""))
 .....s invoice2=$o(^TMP($zn,$j,epis,1,date,"IN",""),-1)
 .....i $p(invoice1,"||",1)=$p(invoice2,"||",1) s invoice=invoice1 k ^TMP($zn,$j,epis,1,date,"IN",invoice1) q
 .....s (in,x)="" f  s x=$o(^TEPI(epis,6,x),-1) q:x=""  i $p(^(x),"\",1)="IN",$p(^(x),"\",2)=date s in=$p($p(^(x),"\",4)," ",2,3) q
 .....i $l(in) s in=$p(in," ",1)_"||"_$p(in," ",2)
 .....i in=invoice1 s invoice=invoice2 k ^TMP($zn,$j,epis,1,date,"IN",invoice2) q
 .....i in=invoice2 s invoice=invoice1 k ^TMP($zn,$j,epis,1,date,"IN",invoice1) q
 .....s error=2 w !,error," ",epis
 .s date="" f  s date=$o(^TMP($zn,$j,epis,1,date)) q:date=""  d
 ..i $d(^TMP($zn,$j,epis,1,date,"RB")) s ^TMP($zn,$j,epis,0,date,"RB")=""
 .s date="" f  s date=$o(^TMP($zn,$j,epis,0,date)) q:date=""  d
 ..s inv=^(date,"RB") i '$l(inv),$p(^TRBL(date,epis),"\",5)="" q
 ..i $p(^TRBL(date,epis),"\")'=$p(inv,"||") w !,date," ",epis," ",1," ",inv," ",^(epis) d  q
 ...i $l(inv) s $p(^TRBL(date,epis),"\",1)=$p(inv,"||",1),$p(^TRBL(date,epis),"\",5)=$p(inv,"||",2)
 ..i $p(^TRBL(date,epis),"\",5)'=$p(inv,"||",2) w !,date," ",epis," ",2," ",inv," ",^(epis) d  q
 ...i $l(inv) s $p(^TRBL(date,epis),"\",1)=$p(inv,"||",1),$p(^TRBL(date,epis),"\",5)=$p(inv,"||",2)
 .s x="" f  s x=$o(^TEPI(epis,6,x)) q:x=""  i $p(^(x),"\",1)="RB" k ^(x)
 .s date="" f  s date=$o(^TMP($zn,$j,epis,0,date)) q:date=""  d
 ..s inv=^(date,"RB") i $l(inv) s inv=$p(inv,"||",1)_" "_$p(inv,"||",2)
 ..s (x,ok)="" f  s x=$o(^TEPI(epis,6,x)) q:x=""  d  q:ok
 ...i $l(inv),$p($p(^(x),"\",4)," ",2,3)=inv d
 ....s z=x f  s z=$o(^TEPI(epis,6,z)) q:z=""  i $p(^(z),"\",2)'<date q
 ....s z=$o(^TEPI(epis,6,z),-1)\1
 ....s ^TEPI(epis,6,z+.1)="RB\"_date_"\\"_inv,ok=1
 ...i '$l(inv),$p(^(x),"\",2)=date d
 ....s z=x f  s z=$o(^TEPI(epis,6,z)) q:z=""  i $p(^(z),"\",2)'<date q
 ....s z=$o(^TEPI(epis,6,z),-1)\1
 ....s ^TEPI(epis,6,z+.1)="RB\"_date_"\\"_inv,ok=1
 .k xx m xx=^TEPI(epis,6) k ^TEPI(epis,6)
 .s x="" f cnt=1:1 s x=$o(xx(x)) q:x=""  s ^TEPI(epis,6,cnt)=xx(x)
 k ^TMP($zn,$j)
 q
 ; check invoice events
conv8 s pc="" f  s pc=$o(^TMCB(pc)) q:pc=""  d
 .w !,pc
 .s bt="" f  s bt=$o(^TMCB(pc,bt)) q:bt=""  s date=$p(^(bt),"\",1) d
 ..w " ",bt
 ..s ep="" f  s ep=$o(^TMCB(pc,bt,ep)) q:ep=""  s am=$p(^(ep),"\",1) d
 ...s (ok,z,x)="" f  s x=$o(^TEPI(ep,6,x)) q:x=""  d  q:ok
 ....i x["." k ^(x) q
 ....i $p(^(x),"\",2)<date q
 ....i $p(^(x),"\",2)=date,$p(^(x),"\",1)'="IN" q
 ....i $p(^(x),"\",2)=date,$p(^(x),"\",1)="IN",$p($p(^(x),"\",4)," ",2,3)=(pc_" "_bt) d  q
 .....s xx=$p(^(x),"\",4),$p(xx," ",1)=am,$p(^(x),"\",4)=xx s ok=100
 ....i $p(^(x),"\",2)=date,$p(^(x),"\",1)="IN",$p($p(^(x),"\",4)," ",2)=pc,$p($p(^(x),"\",4)," ",3)>bt s ok=1,z=x q
 ....i $p(^(x),"\",2)>date s ok=1,z=x q
 ...i ok'=100 d
 ....s z=$o(^TEPI(ep,6,z),-1),^TEPI(ep,6,z+.1)="IN\"_date_"\\"_am_" "_pc_" "_bt
 ....k xx m xx=^TEPI(ep,6) k ^TEPI(ep,6)
 ....s x="" f cnt=1:1 s x=$o(xx(x)) q:x=""  s ^TEPI(ep,6,cnt)=xx(x)
 q
 ; delete double invoices
conv9 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .s (z,invoice)=""
 .s x="" f  s x=$o(^TEPI(ep,6,x)) q:x=""  s ok=0 d
 ..i $p(^TEPI(ep,6,x),"\",1)="RB" s invoice="" q
 ..i $p(^TEPI(ep,6,x),"\",1)="IN",$l(invoice) d  i ok q
 ...i $p($p(^(x),"\",4)," ",2)=$p($p(invoice," ",2),"||",1),$p($p(^(x),"\",4)," ",3)<$p($p(invoice," ",2),"||",2) s ok=1 k ^(x) q
 ...s invoice2=$p(^(x),"\",2)_" "_$p($p(^(x),"\",4)," ",2)_"||"_$p($p(^(x),"\",4)," ",3)_" "_x
 ...i $p(invoice," ",1)=$p(invoice2," ",1),$p($p(invoice," ",2),"||",1)=$p($p(invoice2," ",2),"||",1),$p($p(invoice," ",2),"||",2)<$p($p(invoice2," ",2),"||",2) d
 ....w !,ep," -1- ",invoice," -2- ",invoice2 s ok=1
 ....s z1=$p(invoice," ",1),z2=$p($p(invoice," ",2),"||",1),z3=$p($p(invoice," ",2),"||",2),z=$p(invoice," ",3)
 ....k ^TEPI(ep,6,z),^TEPI(ep,"INV",z1,z2,z3),^TMCB(z2,z3,ep)
 ...i '$l(z) d
 ....s za1=$p(invoice," ",1),za2=$p($p(invoice," ",2),"||",1),za3=$p($p(invoice," ",2),"||",2)
 ....s zb1=$p(invoice2," ",1),zb2=$p($p(invoice2," ",2),"||",1),zb3=$p($p(invoice2," ",2),"||",2)
 ....s ^TRBL(za1,ep)=za2_"\"_zb2_"\"_$$epcomp^LVBVIS1(ep)_"\Y\"_za3
 ....w !,"***",ep," -1- ",invoice," -2- ",invoice2
 ...s invoice=invoice2
 ..i $p(^TEPI(ep,6,x),"\",1)="IN" s invoice=$p(^(x),"\",2)_" "_$p($p(^(x),"\",4)," ",2)_"||"_$p($p(^(x),"\",4)," ",3)_" "_x
 .i $l(z) d
 ..k xx m xx=^TEPI(ep,6) k ^TEPI(ep,6)
 ..s x="" f cnt=1:1 s x=$o(xx(x)) q:x=""  s ^TEPI(ep,6,cnt)=xx(x)
 Q
 ; check current invoices
conv10 s ep="" f Xcnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(Xcnt#10000=0:"#",Xcnt#1000=0:".",1:"")
 .s (stop,x)="" f  s x=$o(^TEPI(ep,6,x),-1) q:x=""  s xx=^(x) d  i stop q
 ..i $p(xx,"\",1)="IN" s stop=1 d  q
 ...i $p(^TEPI(ep,7),"\",4)'="" d  q
 ....i $p(^TEPI(ep,7),"\",4)=($p($p(xx,"\",4)," ",2)_"||"_$p($p(xx,"\",4)," ",3)) q
 ....w !,3," ",ep," ",$p(xx,"\",2)," ",$p($p(xx,"\",4)," ",2)_"||"_$p($p(xx,"\",4)," ",3)
 ....s $p(^TEPI(ep,7),"\",3)=$p(xx,"\",2)
 ....s $p(^TEPI(ep,7),"\",4)=$p($p(xx,"\",4)," ",2)_"||"_$p($p(xx,"\",4)," ",3)
 ....s deb=$p(^TEPI(ep),"\",18),doc=+$p(^(ep),"\",10)
 ....s rowid=deb_"||"_doc_"||"_ep
 ....i '$$select^LVBDEBVI(rowid),$$update^LVBDEBVI(rowid)
 ...i $p(^TEPI(ep,7),"\",4)="" d  q
 ....w !,2," ",ep," ",$p(xx,"\",2)," ",$p($p(xx,"\",4)," ",2)_"||"_$p($p(xx,"\",4)," ",3)
 ....s $p(^TEPI(ep,7),"\",3)=$p(xx,"\",2)
 ....s $p(^TEPI(ep,7),"\",4)=$p($p(xx,"\",4)," ",2)_"||"_$p($p(xx,"\",4)," ",3)
 ....s deb=$p(^TEPI(ep),"\",18),doc=+$p(^(ep),"\",10)
 ....s rowid=deb_"||"_doc_"||"_ep
 ....i '$$select^LVBDEBVI(rowid),$$update^LVBDEBVI(rowid)
 ..i $p(xx,"\",1)="RB" s stop=1 d  q
 ...i $p(^TEPI(ep,7),"\",4)="" q
 ...w !,1," ",ep
 ...s $p(^TEPI(ep,7),"\",3)=""
 ...s $p(^TEPI(ep,7),"\",4)=""
 ...s deb=$p(^TEPI(ep),"\",18),doc=+$p(^(ep),"\",10)
 ...s rowid=deb_"||"_doc_"||"_ep
 ...i '$$select^LVBDEBVI(rowid),$$update^LVBDEBVI(rowid)
 q
 ; check rebill fields
conv11 k ^TMP($zn,$j)
 s date="" f  s date=$o(^TRBL(date)) q:date=""  d
 .s epis="" f  s epis=$o(^TRBL(date,epis)) q:epis=""  s xx=^(epis) i $p(xx,"\",4)="Y" d
 .. ; check company
 ..i $p(xx,"\",3)="" d
 ...s comp=$$epcomp^LVBVIS1(epis)
 ...i $p(xx,"\",3)'=comp w !,"1 ",epis s $p(^TRBL(date,epis),"\",3)=comp
 ..s pc=$p(xx,"\",1),batch=$p(xx,"\",5)
 ..i $l(pc),$l(batch) d
 ...i $d(^TMP($zn,$j,epis,pc,batch)) w !,"2 ",epis f j=1,5 s $p(^TRBL(date,epis),"\",j)=""
 ...s ^TMP($zn,$j,epis,pc,batch)=""
 k ^TMP($zn,$j)
 q
 ; convert Refference ranges 
conv12 s tc="" f  s tc=$o(^TTAB("TC",tc)) q:tc=""  d
 .f type=0,"0-IN","0-OUT" d
 ..s sex="" f  s sex=$o(^TTAB("TC",tc,type,sex)) q:sex=""  d
 ...s age="" f  s age=$o(^TTAB("TC",tc,type,sex,age)) q:age=""  d
 ....i age'=+age w !,tc," ",type," ",sex," ",age d
 .....s ^TTAB("TC",tc,type,sex,+age)=^TTAB("TC",tc,type,sex,age)
 .....k ^TTAB("TC",tc,type,sex,age)
 q
 ; convert Refference ranges 
conv13 s date=57344
 s tc="" f  s tc=$o(^TTAB("TC",tc)) q:tc=""  d
 .f type=0,"0-IN","0-OUT" m:'$d(^TTAB("TC",tc,4)) ^TTAB("TC",tc,4,date,type)=^TTAB("TC",tc,type) k ^TTAB("TC",tc,type)
 q
 ; convert TDAY
conv14 k ^x m ^x=^TDAY k ^TDAY m ^TDAY(1)=^x k ^x
 s ep="" f cnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(cnt#10000=0:"#",cnt#1000=0:".",1:"")
 .i '$d(^TEPI(ep,7)) s ^TEPI(ep,7)=""
 .s date=$p(^TEPI(ep,7),"\",3) i $l(date) s ^TDAY(2,date,ep)=""
 q
 ; convert TBIL
conv15 s co="" f  s co=$o(^TBIL(0,co)) q:co=""  d
 .s ep="" f  s ep=$o(^TBIL(0,co,ep)) q:ep=""  S ^TBIL(9,ep)=^(ep)
 k ^TBIL(0)
 q
 ; convert Standard comments
conv16 s ti="" f  s ti=$o(^TTAB("TC",ti)) q:ti=""  w ti," " d
 .s sc="" f  s sc=$o(^TTAB("TC",ti,2,sc)) q:sc=""  i '$$select^LVBCTTCT(ti_"||"_sc),$$update^LVBCTTCT(ti_"||"_sc)
 q
 ; convert Generic reports
conv17 s xx="" f  s xx=$o(^SS("REPORT-GENERIC",xx)) q:xx=""  s $p(^TTAB("REPORT-GENERIC",xx),"\",4)="LT0"
 m ^TTAB("CTPG")=^SS("CTPG")
 s ^TTAB("CTPG","LT0")="LabTrak Specific routines"
 s ^TTAB("CTPG","LT1")="Crystal reports"
 s xx="" f  s xx=$o(^SS("CTRP",xx)) q:xx=""  d
 .f j=1,2,3,6 s @("x"_j)=$p(^(xx),"^",j)
 .s ^SS("REPORT-GENERIC",xx)=x1_"^"_x3_"^"_x6
 .s ^TTAB("REPORT-GENERIC",xx)=x3_"\"_x6_"\\"_x2
 s xx="" f  s xx=$o(^TTAB("CTRP",xx)) q:xx=""  i $d(^TTAB("REPORT-GENERIC",xx)) d
 .m ^TTAB("REPORT-GENERIC",xx)=^TTAB("CTRP",xx)
 .k ^TTAB("REPORT-GENERIC",xx,"PAR",0)
 .s x="" f  s x=$o(^TTAB("REPORT-GENERIC",xx,"PAR",x)) q:x=""  s ^(x)=$tr(^(x),"^","\")
 k ^TTAB("CTRP"),^SS("CTRP"),^SS("CTPG")
 q
 ; create TIND for VA/DA
conv18 s ep="" f cnt=1:1 s ep=$o(^TEPI(ep)) q:ep=""  d
 .w $s(cnt#10000=0:"#",cnt#1000=0:".",1:"")
 .s date=$p(^(ep),"\",10),mrn=$p(^(ep),"\",18),va=$p(^(ep),"\",24)
 .i $l(va) s ^TIND("VA",va,ep)=""
 .i $l(date),$l(mrn) s da=$p($g(^TDEB(mrn,date,0,ep)),"\",5) i $l(da) s ^TIND("DA",da,ep)=""
 q
 ; create TBATi for CQ/CC
conv19 k ^TBATi s x1="" f  s x1=$o(^TBAT(x1)) q:x1=""  d
 .s x2="" f  s x2=$o(^TBAT(x1,x2)) q:x2=""  d
 ..s x3="" f  s x3=$o(^TBAT(x1,x2,0,x3)) q:x3=""  d
 ...s xx1=$p(^(x3),"\",1),xx4=$$ALPHAUP^SSUTIL4($p(^(x3),"\",4))
 ...i $l(xx1),$l(xx4) s ^TBATi(xx1,xx4,x1_"||"_x2_"||"_x3)=""
 q
 ; setup HIC index and TBATi
conv21 s x1="" f  s x1=$o(^TMCB(x1)) q:x1=""  d
 .s x2="" f  s x2=$o(^TMCB(x1,x2)) q:x2=""  i $p(^(x2),"\",4)="" s ^TMCBi("HIC",x1_"||"_x2)=""
 s x="" f  s x=$o(^TBAT(x)) q:x=""  s st=$p(^(x),"\",1) i $l(st) s ^TBATi("STATUS",st,x)=""
 q
conv22 ; check prev invoices
 s x1="" f  s x1=$o(^TMCB(x1)) q:x1=""  w !,x1 d
 .s x2="" f  s x2=$o(^TMCB(x1,x2)) q:x2=""  d
 ..s x3="" f  s x3=$o(^TMCB(x1,x2,x3)) q:x3=""  d
 ...i $p(^TEPI(x3,7),"\",4)=(x1_"||"_x2) q
 ...w x3," "
 ...i '$$select^LVBINVBP(x1_"||"_x2_"||"_x3),$$update^LVBINVBP(x1_"||"_x2_"||"_x3)
]]></Routine>
</Export>
