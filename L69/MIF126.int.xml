<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF126" type="INT" languagemode="0" generated="1"><![CDATA[
MIF126(mi) ; ML 24/07/2000 ; Hb Gold
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s ack=$c(6),lf=$c(10),cr=$c(13),nak=$c(21)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000(,lf) q:'$l(record)
 s record=$p(record,cr) d trace^MIF000(mi,record,"H<--M")
 s chsum=$p(record,"|",$l(record,"|"))
 i $$CHSUM($p(record,"|",1,$l(record,"|")-1)_"|")'=chsum w nak,*-3 d trace^MIF000(mi,"NAK","H-->M") q
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 i $p(record,"|")="END" q
 s (sample,epis,surname,result,res,date,time,QC)=""
 ; summary data - format 1 : Date|Time|Episode|Well|Res1|Res2|Checksum
 ;                format 2 : Date|Time|Episode|Well|Res1|Res2|Res3|Res4|Checksum
 ;                format 3 : Episode|Well|Res1|Res2|Checksum
 s CODE=$s($l(record,"|")=7:"CODE1",$l(record,"|")=9:"CODE2",1:"CODE3")
 i CODE="CODE1" s epis=$tr($p(record,"|",3)," "),res=$p(record,"|",5,6),code=$p($t(CODE1),";",2)
 i CODE="CODE2" s epis=$tr($p(record,"|",3)," "),res=$p(record,"|",5,8),code=$p($t(CODE2),";",2)
 i CODE="CODE3" s epis=$tr($p(record,"|",1)," "),res=$p(record,"|",3,4),code=$p($t(CODE3),";",2)
 i epis="" q
 f i=1:1:$l(code,",") d
 .s x1=$p(code,",",i),x2=$tr($p(res,"|",i)," ")
 .s result=result_x1_$c(par10)_x2_$c(par11)
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
CHSUM(x) ; calculate check sum
 s sum=0 f j=1:1:$l(x) s sum=sum+$a(x,j)
 s sum=$zb(sum+($zb(sum,192,1)/64),63,1)
 q $c(sum+32)
CODE1 ;A1c,F
CODE2 ;F,A0,A2,E
CODE3 ;UNK-1,UNK-2
]]></Routine>
</Export>
