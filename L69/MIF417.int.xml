<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF417" type="INT" languagemode="0" generated="1"><![CDATA[
MIF417(mi) ; ML 11/8/2000 ; ASTM protocol  -  BAYER ADVIA Centaur (Bi-Directional)
  s mi=$g(mi) i '$l(mi) q
  k ^TMIF(mi,10) s $zt="err^MIF000"
  i $$select^LVBMIMP(mi) q
  f j=1:1:PLIST s @("par"_j)=PLIST(j)
  s stx=$c(2),etx=$c(3),etb=$c(23),ack=$c(6),enq=$c(5),eot=$c(4)
  s lf=$c(10),cr=$c(13),(result,epis)=""
  i $$start^MIF000() q
  f  d Main i $$stop^MIF000() q
  c par4
  q
Main  r *R:10 e  d  q
  .d:" L "[(" "_par9_" ") loadlist^MIF000(mi)
  i $c(R)=enq d
  .d trace^MIF000(mi,"ENQ","H<--M")
  .d ACK
  .s (stop,RESULT)="" f  r *R:10 q:$c(R)=eot  d  i stop q
  ..i $c(R)'=stx q
  ..s record=$$read^MIF000("",etb,,2) i '$l(record) d
  ...s record=$$read^MIF000("",etx,,2) i $l(record) s stop=1
  ..s RESULT=RESULT_$e(record,2,$l(record))
  ..d trace^MIF000(mi,$tr(record,$c(13)," "),"H<--M "_$p($h,",",2)_" ")
  ..d ACK
  .u par4
  .f jj=1:1:$l(RESULT,cr) s record=$p(RESULT,cr,jj) d
  ..s record=" "_record
  .. ; query record
  ..i $e(record)="Q" d  q
  ...s epis=$p($p(record,"|",3),"^",2)
  ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="" q
  ...s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
  ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix q
  .. ; patient record
  ..i $e(record)="P" d  q
  ...d Last s (sample,epis,surname,result,date,time,QC)=""
  ...s surname=$tr($tr($p(record,"|",6),""),"^"," ")
  .. ; order record
  ..i $e(record)="O" d  q
  ...s epis=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
  ...i QC="Q" s QC="Y"
  .. ; result record
  ..i $e(record)="R" d  q
  ...i $p($p(record,"|",3),"^",8)'="DOSE" q
  ...s x1=$tr($p($p(record,"|",3),"^",4)," "),x2=$tr($p(record,"|",4)," "),x3=$tr($p(record,"|",7)," ")
  ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
  ... ; date and time
  ...i $l($tr($p(record,"|",13)," ")) d
  ....s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
  ....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
  .. ; last record
  ..i $e(record)="L" d Last q
  .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
  .d ORDER
  q
Last  ; file result if exists
  i epis?1.99AN,$l(result),QC="" d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
  s (sample,epis,surname,result,date,time,QC)=""
  q
ORDER  ; create list of orders if exists
  n sample,epis,surname,result,date,time,QC
  s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  d
  .d scanone^MIF000(mi,epis)
  .d PATDET(epis)
  .s mode="QUERY" d SET(epis,mode)
  .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
  .d Send
  .k ^TMP("MIF-SINGLE",$j,mi,epis),^TMP($zn,$j,"ENQ",epis)
  q
SAVE(mi) ; create loadlist records
  s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
  s sample=^TMP("MIF000",$j,"LL","SAMPLE")
  d PATDET(epis)
  s mode="LOAD" d SET(epis,mode)
  s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
  q 0
PATDET(epis,zero) ; set patient details record
  i '$$select^LVBEPVIS(epis,"Y") d  q
  .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
  .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
  .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"|||"_sex_"|||||"_$g(PLIST(15))
  s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||^||||||||"
  q
SET(epis,mode) ; set record to be sent to instrument
  s tc="",tcx=""
  ; process a single episode scan
  i mode="QUERY",$d(^TMP("MIF-SINGLE",$j,mi,epis)) d
  .f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d
  ..q:tcx=$e(tc,1,$l(tc))  s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_$e(tc,1,$l(tc))
  ; process an all episode scan
  i mode="SCAN",$d(^TMP("MIF-SCAN",$j,mi,epis)) d
  .f  s tc=$o(^TMP("MIF-SCAN",$j,mi,epis,tc)) q:tc=""  d
  ..q:tcx=$e(tc,1,$l(tc))  s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_$e(tc,1,$l(tc))
  ; process a load list episode scan
  i mode="LOAD" d
  .f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  d
  ..q:tcx=$e(tc,1,$l(tc))  s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_$e(tc,1,$l(tc))
  i $l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_"||"_tcx_"|||||||N||||||||||||||"_$s(mode="LOAD":"O",1:"Q")
  i '$l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="Q|1|^"_epis_"||ALL||||||||X"
  q
Send  ; send list of orders if exists
  w enq,*-3 f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
  i $c(R)=enq q
  i $c(R)'=ack w eot,*-3 q
  q:$$SEND("1H|\^&|||LABTRAK|||||||P|1")
  s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
  i x="" k ^TMIF(mi,10)
  w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
  q
SEND(str) ; send string to instrument
  s str=str_cr_etx,chsum=$$CHSUM(str)
  w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
  f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
  i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
  i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
  d trace^MIF000(mi,R,"H<--M")
  q 2
CHSUM(x) ; calculate check sum
  n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
  s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
  q z
ACK  ; send 'ack' to instrument
  w ack,*-3
  d trace^MIF000(mi,"ACK","H-->M")
  q
]]></Routine>
</Export>
