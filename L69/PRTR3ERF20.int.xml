<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTR3ERF20" type="INT" languagemode="0"><![CDATA[
PRTR3ERF20 ; IG 07/10/98 ; Rule 3 exempt request forms (followups) - Abbott's Pathology
 Q
 ; Parameters :  1.Collection Date from
 ;               2.Collection Date to
 ;               3.Collection centre
 ;               4.Episode
 ;               5.Additional requests
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($zn,$j)
 D INIT,BODY,PRINT
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR3 I $$update^LVBPRPR(REPID)
 K ^TMP($zn,$j),^TMP("PRTREP00",$j)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 K xSORT D H^PRTREP00(PAR10,$zn)
 S X=xLINE(2),MAX=$L(X),$P(SPACE," ",MAX)="",$P(LINE,"-",MAX)=""
 Q
 ; create temporary printing file
BODY s %routine=$p($g(^TTAB("REPORT-GENERIC","R3ER")),"\",1)     i $l(%routine) d
 .x "d BODY^"_%routine
 .m ^TMP($zn,$j,"REPORT")=^TMP(%routine,$j,"REPORT") k ^TMP(%routine,$j,"REPORT")
 Q
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s lcc="" f  s lcc=$o(^TMP($zn,$j,"REPORT",0,lcc)) q:lcc=""  d
 ..k LCC i '$$select^LVBCTCC(lcc,"Y") m LCC=PLIST
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",0,lcc,epis)) q:epis=""  d
 ...k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 ...k DOC i '$$select^LVBCTDR(VISIT(15)) m DOC=PLIST
 ...s (date,max,comm,list,test)="" f  s test=$o(^TMP($zn,$j,"REPORT",0,lcc,epis,test)) q:test=""  d
 ....s xd=$p(^(test),"\",1) i date<$p(^(test),"\",1) s date=$p(^(test),"\",1)
 ....i max<$p(^(test),"\",2) s max=$p(^(test),"\",2)
 ....i '$l(comm) s comm=$p(^(test),"\",3)
 ....s x1=$$seldata^LVBCTTS(test,4)
 ....s list=list_$s($l(list):" , ",1:"")_$p(x1,$c(1))
 ....s $p(^TEPIi("RULE-3",$p(VISIT(20),$c(1)),test,xd,1),"\",2)="Y"
 ...f order=1:1:$s(max>15:15,1:max) d
 ....s ln=0
 ....f j=1:1:5 d LN
 ....d ADDLINE^PRTREP00(REPID,"PRT","?3  "_$p(VISIT(7),$c(1),2)) ;title
 ....d ADDLINE^PRTREP00(REPID,"PRT","?9  "_VISIT(3)) ;surname
 ....d ADDLINE^PRTREP00(REPID,"PRT","?30 "_VISIT(4)) ;given name
 ....d ADDLINE^PRTREP00(REPID,"PRT","?58 "_$p(VISIT(5),$c(1))) ;sex
 ....d ADDLINE^PRTREP00(REPID,"PRT","?64 "_$p($$extdate2^SSUTIL4(VISIT(6)),"/",1))
 ....d ADDLINE^PRTREP00(REPID,"PRT","?68 "_$p($$extdate2^SSUTIL4(VISIT(6)),"/",2))
 ....d ADDLINE^PRTREP00(REPID,"PRT","?72 "_$p($$extdate2^SSUTIL4(VISIT(6)),"/",3))
 ....d LN,LN
 ....d ADDLINE^PRTREP00(REPID,"PRT","?3  "_VISIT(32)_" "_VISIT(33))
 ....D ADDLINE^PRTREP00(REPID,"PRT","?52 "_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51))),LN
 ....d ADDLINE^PRTREP00(REPID,"PRT","?3  "_VISIT(34)_" "_VISIT(35)_" "_VISIT(36))
 ....D ADDLINE^PRTREP00(REPID,"PRT","?52 "_$e(DOC(9),1,24)),LN
 ....i $l(DOC(10)) d
 .....D ADDLINE^PRTREP00(REPID,"PRT","?52 "_$e(DOC(10),1,24)),LN
 .....d ADDLINE^PRTREP00(REPID,"PRT","?3  "_$p(VISIT(20),$c(1)))
 .....i $l(VISIT(8)) d ADDLINE^PRTREP00(REPID,"PRT","?20 Medicare : "_VISIT(8))
 .....i '$l(VISIT(8)),$l(VISIT(89)) d ADDLINE^PRTREP00(REPID,"PRT","?20 DVA : "_VISIT(89))
 ....D ADDLINE^PRTREP00(REPID,"PRT","?52 "_DOC(11)_" "_DOC(12)_" "_DOC(13)),LN
 ....i '$l(DOC(10)) d
 .....d ADDLINE^PRTREP00(REPID,"PRT","?3  "_$p(VISIT(20),$c(1)))
 .....i $l(VISIT(8)) d ADDLINE^PRTREP00(REPID,"PRT","?20Medicare : "_VISIT(8))
 .....i '$l(VISIT(8)),$l(VISIT(89)) d ADDLINE^PRTREP00(REPID,"PRT","?20 DVA : "_VISIT(89))
 ....D ADDLINE^PRTREP00(REPID,"PRT","?52 "_"Ph:   "_$s($l(DOC(5)):DOC(5),$l(DOC(6)):DOC(6),$l(DOC(7)):DOC(7),1:"")),LN
 ....D ADDLINE^PRTREP00(REPID,"PRT","?52 "_"Fax:  "_DOC(8)),LN
 ....D ADDLINE^PRTREP00(REPID,"PRT","?52 "_"Prov: "_DOC(18)_" ["_DOC(2)_"]"),LN
 ....f  q:ln=16  d LN
 ....i $$open^LVBVISDC(epis)
 ....f  q:$$fetch^LVBVISDC(epis)  d
 .....f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .....s copyto=$s($l(PLIST(3)):PLIST(3)_" - "_PLIST(4),1:PLIST(4)) ; copy to doctor string
 .....d ADDLINE^PRTREP00(REPID,"PRT","?52 "_$e(copyto,1,24)),LN ; print the string
 ....i $$close^LVBVISDC()
 ....f  q:ln=22  d LN
 ....d ADDLINE^PRTREP00(REPID,"PRT","?52 "_list),LN,LN
 ....f j=1:1:$l(VISIT(53),"|") d  q:j=2
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_$p(VISIT(53),"|",j)),LN
 ....f  q:ln=26  d LN
 ....d ADDLINE^PRTREP00(REPID,"PRT","?40 "_"RULE 3 EXEMPT"),LN
 ....i PAR5="",order>1 d
 .....d ADDLINE^PRTREP00(REPID,"PRT","?40 "_"FOLLOW UP No:"_order_$s(order=max:"   LAST FOLLOW UP",1:"")),LN
 ....f  q:ln=29  d LN
 ....d ADDLINE^PRTREP00(REPID,"PRT","?40 "_"Signature not required"),LN
 ....f  q:ln=33  d LN ; print lines until line=33
 q
LN d ADDLINE^PRTREP00(REPID,"PRT","!") s ln=ln+1
 q
]]></Routine>
</Export>
