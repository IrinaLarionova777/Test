<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFDC0104" type="INT" languagemode="0" generated="1"><![CDATA[
MIFDC0104(mi) ; ML 21/3/01 ; Daybook Cassette Writer - RA LAMB Cassette MicroWriter : Territory Health Services
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main h 30
 s epis="" f  s epis=$o(^THIRi("BLOCK-PRINT",epis)) q:epis=""  d
 .f j=1,3,4,17 s VISIT(j)=$$seldata^LVBEPVIS(epis,j)
 .i '$l(VISIT(1)) q
 .s urgent="" i $$seldata^LVBCTPR(VISIT(17),4)="Y" s urgent="Y"
 .l +^THIRi("BLOCK-PRINT",epis):5 e  q
 .s accession="" f  s accession=$o(^THIRi("BLOCK-PRINT",epis,accession)) q:accession=""  d
 ..q:$$select^LVBHIVAC(epis_"||"_accession)  k ACC m ACC=PLIST k PLIST
 ..s AccNum=$p(ACC(8),$c(1))_$p(ACC(14),$c(1))_$p(ACC(9),$c(1))_"-"_$p(ACC(10),$c(1))
 ..s lab=$p(ACC(8),$c(1)),ok="" i '$$select^LVBCTDBL(lab,"Y")," T P "[(" "_PLIST(10)_" ") s ok=1
 ..i ok s block="" f  s block=$o(^THIRi("BLOCK-PRINT",epis,accession,block)) q:block=""  d
 ...q:$$select^LVBHIVAB(epis_"||"_accession_"||"_block)  k BLOCK m BLOCK=PLIST k PLIST
 ...i '$l(urgent) d
 ....s vts="" f  s vts=$o(^THIR(epis,accession,"TEST",vts)) q:vts=""  d  q:$l(urgent)
 .....s x=$$seldata^LVBVISTS(vts,13) i $$seldata^LVBCTPR(x,4)="Y" s urgent="Y"
 ...s hopper=$$Hopper(urgent) i hopper d
 .... ; change to multicolour
 ....w "$#KC",$c(13),*-3
 .... ; font size 3
 ....w "$#3",*-3
 .... ; switch to correct hopper
 ....w "#H"_hopper,*-3
 ....w AccNum_"."_block,*-3
 .... ; new line
 ....w "#N",*-3
 .... ; font selection
 ....w "#2",*-3
 ....w $e(VISIT(3),1,10)_$s($l(VISIT(3))>10:"*",1:"")_"/"_$e(VISIT(4)),*-3
 .... ; eject cassette
 ....w $c(13),*-3 h 5
 ...m PLIST=BLOCK s PLIST(5)="Y" i $$update^LVBHIVAB(epis_"||"_accession_"||"_block)
 .l -^THIRi("BLOCK-PRINT",epis)
 q
 ; hopper colours - 1 : white  - default
 ;                  2 : green  - 
 ;                  3 : blue   - 
 ;                  4 : yellow - 
 ;                  5 : pink   - 
 ;                  6 : orange - urgent episodes
Hopper(urgent) s hopper=""
 i '$l(hopper),urgent="Y" s hopper=6
 i '$l(hopper),'$$select^LVBCTSPE($p(ACC(3),$c(1))) s hopper=$p(PLIST(3),"/",2)
 s:'$l(hopper) hopper=1
 q $s(hopper:7-hopper,1:"")
]]></Routine>
</Export>
