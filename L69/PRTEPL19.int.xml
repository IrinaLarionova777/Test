<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPL19" type="INT" languagemode="0" generated="1"><![CDATA[
PRTEPL19 ; IG 21/1/99 ; episode patient label - MEDLAB
 q
 ; Parameters :  1. Episode number
 ;               2. Label code
 ;               3. Quantity
 ;               4. Computer Name
 ;               5. Worksheet
 ;               6. Worksheet No.
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($ZN,$J)
 D INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR4_"-"_PAR1,PLIST(9)="M" i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
BODY ; create temporary printing file
 ; label and quantity entered
 i $l(PAR1),$l(PAR2),$l(PAR3) s ^TMP($zn,$j,"REPORT",PAR1,PAR2)=PAR3 q
 ; print labels for a patient
 i $l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)
 ..f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBWKSRT()
 .i '$l(PAR5) d
 ..i $$open^LVBVISTS(PAR1)
 ..f  q:$$fetch^LVBVISTS(PAR1)  s ts=$p(PLIST(3),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBVISTS()
 i '$l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRE(PAR5_"||"_PAR6)
 ..f  q:$$fetch^LVBWKSRE(PAR5_"||"_PAR6)  s epis=$p(PLIST(2),$c(1)) d
 ...i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)
 ...f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,epis,ts)=$g(^TMP($zn,$j,1,epis,ts))+1
 ...i $$close^LVBWKSRT()
 ..i $$close^LVBWKSRE()
 s epis="" f  s epis=$o(^TMP($zn,$j,1,epis)) q:epis=""  d
 .s ts="" f  s ts=$o(^TMP($zn,$j,1,epis,ts)) q:ts=""  d
 ..s (label,max)=""
 ..s label=$$seldata^LVBCTTS(ts,23),max=$$seldata^LVBCTLB(label,4)
 ..i $l(label),max d
 ...i $l(PAR2),PAR2'=label q
 ...s ^TMP($zn,$j,"REPORT",epis,label,ts)=^TMP($zn,$j,1,epis,ts)
 ...s (cnt,ts1)="" f  s ts1=$o(^TMP($zn,$j,"REPORT",epis,label,ts1)) q:ts1=""  s cnt=cnt+^(ts1)
 ...s xx=cnt\max s:cnt#max xx=xx+1 s ^TMP($zn,$j,"REPORT",epis,label)=xx
 q
PRINT 
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  i '$$select^LVBEPVIS(epis) d
 .k VISIT m VISIT=PLIST
 .i $l($p($g(PLIST(7)),$c(1),2)) s name=$p($g(PLIST(7)),$c(1),2)_" "_$g(PLIST(4))_" "_$g(PLIST(3))
 .e  s name=$g(PLIST(4))_" "_$g(PLIST(3))
 .s label="" f  s label=$o(^TMP($zn,$j,"REPORT",epis,label)) q:label=""  d
 ..s cnt=^(label)
 ..s (ts,list)="" f  s ts=$o(^TMP($zn,$j,"REPORT",epis,label,ts)) q:ts=""  d
 ...i '$$selskip^LVBCTTS(ts) s list=list_$s($l(list):" ",1:"")_PLIST(4)
 ..f j=1:1:cnt d
 ...d ADD^SSPRINT(REPID,"PRT","{F,1,A,R,M,180,250,""LABEL""|")
 ...d ADD^SSPRINT(REPID,"PRT","C,180,70,0,17,1,1,B,L,0,0,"""_"Medlab Australia"_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,136,20,0,15,1,1,B,L,0,0,"""_"Lab Number"_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,132,205,0,1,1,1,B,L,0,0,"""_": "_epis_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,100,20,0,1,1,1,B,L,0,0,"""_name_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,72,20,0,15,1,1,B,L,0,0,"""_"Date of Birth"_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,68,205,0,1,1,1,B,L,0,0,"""_": "_$$extdate2^SSUTIL4(VISIT(6))_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,40,20,0,15,1,1,B,L,0,0,"""_"Collection date"_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,36,205,0,1,1,1,B,L,0,0,"""_": "_$$extdate2^SSUTIL4(VISIT(12))_""",0|")
 ...d ADD^SSPRINT(REPID,"PRT","C,8,20,0,15,1,1,B,L,0,0,"""_label_" : "_list_""",0|}")
 ...d ADD^SSPRINT(REPID,"PRT","{B,1,N,1}")
 q
]]></Routine>
</Export>
