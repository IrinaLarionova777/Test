<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR021" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR021 ; ML 12/12/2002 ; Doctor's Report HEADER - Histopath/Dermatopath
 q
 ; supported calls
 ;
 ; client specific parameters
INIT s MaxLine=45
 q
 ; print header
Header(page,par1,par2) n (page,par1,par2,VISIT,DOC,HOSP,REPID)
 d ADD^PRTREP00(REPID,"PRT","#")
 ; find location
 s location="" i VISIT(22)'="" s location=$p(VISIT(22),$c(1),2)
 s epis=$p(VISIT(2),$c(1))
 ; received date
 s recdate="" s:$p(VISIT(61),$c(1))'="" recdate=$$extdate2^SSUTIL4($p(VISIT(61),$c(1)))
 ; format patient and reporting doctor address
 d Address(VISIT(32),VISIT(33),VISIT(34),VISIT(35),VISIT(36),.pAddress)
 i $d(DOC) d Address(DOC(9),DOC(10),DOC(11),DOC(12),DOC(13),.dAddress)
 i $d(HOSP) d Address(HOSP(4),HOSP(5),HOSP(6),HOSP(7),HOSP(8),.hAddress)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b16602T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4200h600V"_epis)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b16602T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h900VPatient")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4300h900VReceived: "_recdate)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b16602T")
 i $d(DOC) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h1100V"_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3)))
 i $d(HOSP) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h1100V"_HOSP(3))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h1100V"_$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),$p(VISIT(4),$c(1)),$p(VISIT(3),$c(1))))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b16602T")
 i $d(dAddress) d
 .f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h"_(1100+(j*130))_"V"_dAddress(j))
 i $d(hAddress) d
 .f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h"_(1100+(j*130))_"V"_hAddress(j))
 i $d(pAddress) d
 .f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h"_(1100+(j*130))_"V"_pAddress(j))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h"_(1100+((j+1)*130))_"VDOB: "_$s($l($p(VISIT(6),$c(1))):$$extdate2^SSUTIL4($p(VISIT(6),$c(1))),1:"Unknown"))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a400h1800V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
 ; print footer
Footer(page,par1,par2) q
 ; print dept header
PrintDepHeader 
 i PrintDepHeader d  s PrintDepHeader=0
 .n (dep,page,PageLine,tsLM)
 .s print=$$seldata^LVBCTDEP(dep,7),name=$$seldata^LVBCTDEP(dep,3)
 .i print="Y" d
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1
 ..s ^TMP("REPORT",$j,page,"TEXT",x)="~BOLD"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+1)=$c(13)
 ..s ^TMP("REPORT",$j,page,"TEXT",x+2)=$j("",12)_name
 ..s ^TMP("REPORT",$j,page,"TEXT",x+3)="~NORMAL"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+4)="!"
 ..s PageLine=PageLine+1
 q
 ; format address
Address(x1,x2,x3,x4,x5,Address) n (x1,x2,x3,x4,x5,Address) k Address s cnt=0 f j=1:1:3 s Address(j)=""
 i $l(x1) s cnt=cnt+1,Address(cnt)=x1
 i $l(x2) s cnt=cnt+1,Address(cnt)=x2
 s cnt=cnt+1
 i $l(x3) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x3
 i $l(x4) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x4
 i $l(x5) d
 .i cnt>1,'$l(Address(cnt)) s cnt=cnt-1
 .s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x5
 q
 ; Extra fields : 1 - courier code
 ;                2 - courier code description
 ;                3 - copy to doctors
ExtraFields(vtsRID,episRID) n (episRID,vtsRID,HOSP,DOC,EPVIS,PLIST) s vtsRID=$g(vtsRID),epis=$p(vtsRID,"||") k PLIST
 i '$$select^LVBCTCR($p(DOC(16),$c(1))) m COUR=PLIST k PLIST
 ; courier code
 s EXTRA(1)="EXTRA_1"_$c(1)_$p($g(COUR(2)),$c(1))
 ; courier code description
 s EXTRA(2)="EXTRA_2"_$c(1)_$p($g(COUR(3)),$c(1))
 ; copy-to doctors
 s copyto=""
 i $$open^LVBVISDC(epis)
 f  q:$$fetch^LVBVISDC(epis)  d
 . ; do not display Non-Printable CR
 .i $p(PLIST(10),$c(1),3)="Y" q
 .i $l($p($g(PLIST(3)),$c(1))),$p($g(DOC(2)),$c(1))=$p($g(PLIST(3)),$c(1)) q
 .i $l($p($g(PLIST(3)),$c(1))),$p($g(HOSP(2)),$c(1))=$p($g(PLIST(3)),$c(1)) q
 .s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 i $$close^LVBVISDC()
 i $l(EPVIS(22)) s copyto=copyto_$s($l(copyto):", ",1:"")_$p(EPVIS(22),$c(1),2)
 s EXTRA(3)="EXTRA_3"_$c(1)_$s($l(copyto):"Copies to: ",1:"")_copyto
 k PLIST m PLIST=EXTRA
 s PLIST=$o(PLIST(""),-1)
 q
]]></Routine>
</Export>
