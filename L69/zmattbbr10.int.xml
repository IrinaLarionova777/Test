<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="zmattbbr10" type="INT" languagemode="0"><![CDATA[
PNTBBR10 ; Blood Unit Inventory and status display  ; Compiled December 2, 1999 13:42:23 for Cache for Windows
 q
print(ROWID,%IN,MSG) 
 n (ROWID,%IN,MSG)
 k ^PR(+ROWID,"PRT")
 S %="\"
 S site=$P(MSG,%,1),datefrom=$P(MSG,%,2),dateto=$p(MSG,%,3),days=$p(MSG,%,4),product=$p(MSG,%,5)
 s user=%IN
 S $P(LINE,"-",100)="",page=0
 d tab
 d initial
 s site1="" f  s site1=$o(^TMP("MVB",$j,site1)) q:site1=""  d
 .s prod="" f  s prod=$o(^TMP("MVB",$j,site1,prod)) q:prod=""  d
 ..s len=20,str2="",bgrp="" f  s bgrp=$o(^TMP("MVB",$j,site1,prod,1,bgrp)) q:bgrp=""  s str2=str2_$p($g(^CT("BG",bgrp)),"^")_",",len=len+8
 ..s str2=str2_"Total",len=len+10
 ..d head1
 ..s ind="" f  s ind=$o(statcde(ind)) q:ind=""  d  ;
 ...s str1="",tot=0,bgrp="" f  s bgrp=$o(^TMP("MVB",$j,site1,prod,1,bgrp)) q:bgrp=""  s num=+$g(^TMP("MVB",$j,site1,prod,1,bgrp,ind)),tot=tot+num,str1=str1_$j(num,2,0)_","
 ...s str1=str1_$j(tot,2,0) ;_","
 ...s desc=$p($g(^CT("ST",ind)),"^",2) s:desc="" desc=ind
 ...s str=desc_","_str1
 ...D ADD^SSPRINT(ROWID,"PRT","!")
 ...D ADD^SSPRINT(ROWID,"PRT","?7  | "_$$tab1(str))
 ...D ADD^SSPRINT(ROWID,"PRT","?"_$e(2+$l(tab1)_"   ",1,3)_" |")
 .. ; s ind="" f  s ind=$o(^TMP("MVB",$j,site1,prod,2,ind)) q:'ind  d
 ...; s str1="",tot=0,bgrp="" f  s bgrp=$o(^TMP("MVB",$j,site1,prod,1,bgrp)) q:bgrp=""  s num=+$g(^TMP("MVB",$j,site1,prod,1,bgrp,ind)),tot=tot+num,str1=str1_$j(num,2,0)_","
 ...; s str1=str1_$j(tot,2,0) ;_","
 ...; s str=$p($g(^CT("TRANS",ind)),"^",2)_","_str1 
 ...; D ADD^SSPRINT(ROWID,"PRT","!")
 ...; D ADD^SSPRINT(ROWID,"PRT","?7  | "_$$tab1(str))
 ...; D ADD^SSPRINT(ROWID,"PRT","?"_$e(7+$l(tab1)_"   ",1,3)_" |")
 k ^TMP("MVB",$j)
 Q
 ;
 ;name of field,length of field
tab ;Product,20;
 k tab n a s $p(space," ",70)="",a=$p($t(tab),";",2,99),tab1="",tab2=""
 f jj=1:1:$l(a,";") d
 .s tab=jj
 .s a1=$p(a,";",jj) q:a1=""
 .s a2=$p(a1,","),a3=$p(a1,",",2)
 .s tab1=tab1_$e(a2_space,1,a3),tab(jj)=a3
 .s tab2=tab2_a2_","
 ;b
 q
 ;
tab1(str) 
 s $p(space," ",70)=""
 s str1="" f j=1:1:$l(str,",") s str1=str1_$e($p(str,",",j)_space,1,$s($g(tab(j)):tab(j),1:8))
 q str1
head1 ;Print Page Header
 s page=page+1
 D ADD^SSPRINT(ROWID,"PRT","#")
 D ADD^SSPRINT(ROWID,"PRT","!")
 d head0
 D ADD^SSPRINT(ROWID,"PRT","?2  Blood Unit Inventory and status display  Page "_page)
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  From "_$$extdate^SSUTIL4(datefrom))
 D ADD^SSPRINT(ROWID,"PRT","?22  To "_$$extdate^SSUTIL4(dateto))
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  Site "_site1_" "_$p($g(^TTAB("USLOC",site1)),"\"))
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  Product "_$p($g(^CT("PR",prod)),"^",2))
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  Days to expire "_+days)
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  "_$e(LINE,1,len))
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  |"_$$tab1("Blood Group,"_str2))
 D ADD^SSPRINT(ROWID,"PRT","?"_$e(5+$l(tab1)_"   ",1,3)_"  |")
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  "_$e(LINE,1,len))
 D ADD^SSPRINT(ROWID,"PRT","!")
 q
head0 ;Print Common Page Header
 d head0^PNTBBR01
 q
 D ADD^SSPRINT(ROWID,"PRT","!")
 D ADD^SSPRINT(ROWID,"PRT","?2  Territory Health Services")
 D ADD^SSPRINT(ROWID,"PRT","?50 "_$$extdate^SSUTIL4(+$h)_" "_$$exttime^SSUTIL4($p($h,",",2)))
 D ADD^SSPRINT(ROWID,"PRT","!")
 q
initial ;initial sorting
 k ^TMP("MVB",$j),statcde s statcde=""
 ;get packs currently in stock
 s st="" f  s st=$o(^BB("PK",0,"Stat",st)) q:st=""  d
 .s expdate=$h-.1 f  s expdate=$o(^BB("PK",0,"Stat",st,expdate)) q:expdate=""  d
 ..s pack="" f  s pack=$o(^BB("PK",0,"Stat",st,expdate,pack)) q:pack=""  d
 ...q:'$d(^BB("PK",pack))  s s=^(pack),loc=$p(s,"^",10),site1=$p($g(^CT("LO",+loc)),"^",3),bgrp=$p(s,"^",8),prod=$p(s,"^",6) q:'prod
 ...i product'="",product-prod q
 ...i site'="",site'=site1 q
 ...s:site1="" site1=" "
 ...s s=$g(^TMP("MVB",$j,site1,prod,1,+bgrp,st))+1,^TMP("MVB",$j,site1,prod,1,+bgrp,st)=s
 ...s statcde(st)=""
 ...s ^TMP("MVB",$j,site1,prod,2,"STOCK")=""
 ... ; i expdate'>($h+days) d
 ....; s s=$g(^TMP("MVB",$j,site1,prod,1,+bgrp,"EXPIRED"))+1,^TMP("MVB",$j,site1,prod,1,+bgrp,"EXPIRED")=s
 ....; s ^TMP("MVB",$j,site1,prod,2,"EXPIRED")=""
 q
]]></Routine>
</Export>
