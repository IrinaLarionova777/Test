<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBHL7OUT23" type="INT" languagemode="0"><![CDATA[
LVBHL7OUT23 ; ML 13/9/2000 ; HL7 RESULT COMPILER - Labtrak to Vital : Goulburn Valley Health  ; Compiled February 13, 2006 13:38:41 for Cache for Windows
 q
 ; loop thru and find unsent episodes
create 
 n
 i '$$select^MVBCFLAB(1) m SYSDET=PLIST k PLIST
 s:'$g(SYSDET(18)) SYSDET(18)="*"
 s:'$g(SYSDET(19)) SYSDET(19)="*"
 ; environment - T = Test
 ;               P = Production/Live
 s environ=$p($g(SYSDET(45)),$c(1)) i environ="L" s environ="P"
 i " P T "[(" "_environ_" ") s Pcount=0 d
 . ; check changes to test status
 .s reptype="" f  s reptype=$o(^TEDI("OUT","HL7",reptype)) q:reptype=""  d  q:Pcount=50
 ..s client="" f  s client=$o(^TEDI("OUT","HL7",reptype,client)) q:client=""  d  q:Pcount=50
 ...s hospdoc="" f  s hospdoc=$o(^TEDI("OUT","HL7",reptype,client,hospdoc)) q:hospdoc=""  d  q:Pcount=50
 .... ; ignore uncoded doctors
 ....i hospdoc["NC-" q
 ....s epis="" f  s epis=$o(^TEDI("OUT","HL7",reptype,client,hospdoc,epis)) q:epis=""  s Pcount=Pcount+1 d  q:Pcount=50
 .....d DETpat
 .....s vtsRID="" f  s vtsRID=$o(^TEDI("OUT","HL7",reptype,client,hospdoc,epis,46,vtsRID)) q:vtsRID=""  d
 ......s seq="" f  s seq=$o(^TEDI("OUT","HL7",reptype,client,hospdoc,epis,46,vtsRID,seq)) q:seq=""  s status=$p(^(seq),"\",3) d
 .......d DETts
 .......d BUILD(status)
 .......k ^TEDI("OUT","HL7",reptype,client,hospdoc,epis,46,vtsRID,seq)
 ..... ; check for episode detail changes and resend all test details
 .....q:'$d(^TEDI("OUT","HL7",reptype,client,hospdoc,epis,42))
 .....d DETpat
 .....s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ......s nnx="" f  s nnx=$o(^TEPI(epis,1,ts,nnx)) q:nnx=""  s status=$p(^(nnx),"\",31) d
 .......s status=$s(status="A":"Auth",1:"Add"),vtsRID=epis_"||"_ts_"||"_nnx
 .......d DETts
 .......d BUILD(status)
 .....k ^TEDI("OUT","HL7",reptype,client,hospdoc,epis,42)
 q
 ; extract details - patient
DETpat s (mrn,carepis,caremrn,name,dob,sex,add,homeph)=""
 s (drcode,drcumul,drprov,drname)=""
 i '$$select^LVBEPVIS(epis,"Y") m VISIT=PLIST k PLIST d
 .s title=$p(VISIT(7),$c(1)),colldate=$p(VISIT(12),$c(1)),colltime=$p(VISIT(13),$c(1)),loc=$p(VISIT(66),$c(1))
 .s mrn=$p(VISIT(20),$c(1)),carepis=$p(VISIT(65),",",1),caremrn=$p(VISIT(65),",",2)
 .i caremrn="",$d(^THOS("H","PATIENT",mrn)) s caremrn=mrn
 .s ccisorder=$p(VISIT(67),$c(1))
 .s name=$p(VISIT(3),$c(1))_"^"_$p(VISIT(4),$c(1))_"^^^"_title
 .i VISIT(6)'=-1,(VISIT(6)'="") s dob=$tr($zd($p(VISIT(6),$c(1)),3),"-","")
 .s sex=$p(VISIT(5),$c(1))
 .s add=$p(VISIT(32),$c(1))_"^"_$p(VISIT(33),$c(1))_"^"_$p(VISIT(34),$c(1))_"^^"_$p(VISIT(36),$c(1))
 .s homeph=$p(VISIT(43),$c(1))
 . ; extract details - doctor
 .s drcode=$p(VISIT(15),$c(1))
 .i '$$select^LVBCTDR(drcode,"Y") m DOC=PLIST k PLIST d
 ..s drname=$p(DOC(26),$c(1))_" "_$p(DOC(3),$c(1))_" "_$p(DOC(4),$c(1))
 ..s drprov=$p(DOC(18),$c(1)),drcumul=$p(DOC(34),$c(1))
 q
 ; extract details - test set
DETts s ts=$p(vtsRID,"||",2),nnx=$p(vtsRID,"||",3)
 i '$$select^LVBCTTS(ts,"Y") m TS=PLIST k PLIST d
 .s cumul=$p(TS(7),$c(1))
 .s:cumul'="D" format=$s(cumul="F":"C",cumul="Y":"C",1:"")
 .s:cumul="D" format=$s(drcumul'="N":"C",1:"")
 .s tsname=$p(TS(3),$c(1)),tssyn=$p(TS(4),$c(1)),report=$p(TS(21),$c(1)),secur=$p(TS(22),$c(1))
 . ; test set report page for doctor
 .k REPORT i '$$select^LVBCTRG(report,"Y") m REPORT=PLIST
 .i (report=" ")!'$d(REPORT) s REPORT(4)=0,REPORT(5)="Y"
 q
 ; PendAuth : Add    = Pending/Entered Test
 ;          : Auth   = Authorised Test
 ;          : DeAuth = Deauthorised test
 ;          : Del    = Deleted Test (Pending/Resulted)
 ;          : Update = Updated
 ;          : Print  = Retransmit
 ;
BUILD(PendAuth) ; build packet strings
 l +^TMPclean k ^TMP($zn,$j) k PLIST,visres s uniseq=$$new()
 s currdate=$tr($zd(+$h,3),"-",""),currtime=$tr($$exttime^SSUTIL4($p($h,",",2)),":","")
 s ^TMP($zn,$j,"MSH")="MSH|^~\&|LABTRAK||"_$s(ccisorder="":"PATH_RESULTS",1:"JADE_COMMUNITY")_"||"_currdate_currtime_"||ORU^R01|"_uniseq_"|"_environ_"|2.3|||AL|NE"
 s ^TMP($zn,$j,"PID")="PID||"_caremrn_"^^^^^"_loc_"|"_mrn_"|"_caremrn_"|"_name_"||"_dob_"|"_sex_"|||"_add_"||"_homeph_"|"
 i ccisorder="" s ^TMP($zn,$j,"PV1")="PV1||*|",$p(^TMP($zn,$j,"PV1"),"|",20)=carepis
 s colltimex=$tr($$exttime^SSUTIL4(colltime),":","") s:colltimex="-1" colltimex="0000"
 s colldatex=$tr($zd(colldate,3),"-","")
 ; build pending/deleted/deauthorised test packet
 i '((PendAuth="Auth")!(PendAuth="Update")!(PendAuth="Print")) d  g BUILDend
 . ; Pending = P, Deleted = X, Deauthorised = D
 .s status=$s(PendAuth="Add":"P",PendAuth="Del":"X",1:"D")
 .s (obrseq,^TMP($zn,$j,"OBR"))=$g(^TMP($zn,$j,"OBR"))+1
 .s ^TMP($zn,$j,"OBR","A",obrseq)="OBR|"_obrseq_"|"_$s(ccisorder'="":ccisorder,1:"")_"|"_$tr(vtsRID,"||"," ")_"^PLS|"_tssyn_"^"_tsname_"|||"_colldatex_colltimex_"|||||"_secur_"||||"_drprov_"^"_drcode_"^"_drname_"||||ATOMIC|||||"_status
 ; check if updated test has to be sent away
 i PendAuth="Update" s stop=1 d  i stop k ^TMP($zn,$j) g BUILDend
 .s vq=""
 .i $$open^LVBVISTQ(vtsRID)
 .f  q:$$fetch^LVBVISTQ(vtsRID)  i PLIST(2)="VQ",$p(PLIST(9),$c(1))="" s vq=$p(PLIST(10),$c(1))
 .i $$close^LVBVISTQ()
 .i $l(vq),'$$select^LVBCTVQ(vq),PLIST(5)="Y" s stop=0
 k VISTS q:$$select^LVBVISTS(vtsRID)  m VISTS=PLIST k PLIST
 ; check confidentiality
 s conf="" i (VISIT(84)="Y")!(VISTS(46)="Y") s conf=1
 ; build authorised result packet
 k visres i 'conf q:$$results^LVBVISTS(vtsRID,,,,"Y")  m visres=PLIST k PLIST
 i conf s PLIST=1,PLIST(1)=vtsRID_"||T"_$c(1)_"T"_$c(1)_"Confidential result. Please call pathology on <>"  m visres=PLIST k PLIST
 ; Updated = P, Authorised = A
 s status=$s($$status^LVBVIS1(vtsRID)="A":"A",1:"U")
 s (obrseq,^TMP($zn,$j,"OBR"))=$g(^TMP($zn,$j,"OBR"))+1
 s ^TMP($zn,$j,"OBR","A",obrseq)="OBR|"_obrseq_"|"_$s(ccisorder'="":ccisorder,1:"")_"|"_$tr(vtsRID,"||"," ")_"^PLS|"_tssyn_"^"_tsname_"|||"_colldatex_colltimex_"|||||"_secur_"||||"_drprov_"^"_drcode_"^"_drname_"||||ATOMIC|||||"_status
 s i="" f  s i=$o(visres(i)) q:i=""  s tc=$p(visres(i),$c(1),2) i '$$select^LVBCTTC(tc) d
 .k CTTC m CTTC=PLIST k PLIST
 .s res=$p(visres(i),$c(1),3),units=CTTC(4),normal=$p(visres(i),$c(1),5)
 .s CTTC(14)=$s(CTTC(14)'="":CTTC(14),1:CTTC(3))
 .i $e(CTTC(5),1)="N" d
 ..s range=$$ranges^LVBVIS1(tc,VISIT(26),$p(VISIT(5),$c(1)),,"0",$s($l(VISTS(7)):VISTS(7),1:+$h),VISIT(98),VISIT(64))
 ..s range1=$p(range,$c(1)),range2=$p(range,$c(1),2),range=range1_"-"_range2 s:range="-" range=""
 ..s (obxaseq,^TMP($zn,$j,"OBX","A",obrseq))=$g(^TMP($zn,$j,"OBX","A",obrseq))+1
 ..s ^TMP($zn,$j,"OBX","A",obrseq,obxaseq)="OBX|"_obxaseq_"|NM|"_CTTC(14)_"^"_CTTC(3)_"^L||"_res_"|"_units_"|"_range_"|"_$s(normal="L":SYSDET(18),normal="H":SYSDET(19),1:"")
 .i $e(CTTC(5),1)="S" i '$$select^LVBCTTCT(tc_"||"_res) d
 ..s res=$tr(XX,"|","~")
 ..s (obxaseq,^TMP($zn,$j,"OBX","A",obrseq))=$g(^TMP($zn,$j,"OBX","A",obrseq))+1
 ..s ^TMP($zn,$j,"OBX","A",obrseq,obxaseq)="OBX|"_obxaseq_"|TX|"_CTTC(14)_"^"_CTTC(3)_"^L||"_res_"|||"
 .i $p(visres(i),$c(1),4)'="" d
 ..s res=$p(visres(i),$c(1),4),res=$tr(res,"|","~")
 ..s (obxaseq,^TMP($zn,$j,"OBX","A",obrseq))=$g(^TMP($zn,$j,"OBX","A",obrseq))+1
 ..s ^TMP($zn,$j,"OBX","A",obrseq,obxaseq)="OBX|"_obxaseq_"|TX|"_CTTC(14)_"^"_CTTC(3)_"^L||"_res_"|||"
 s (obrseq,^TMP($zn,$j,"OBR"))=$g(^TMP($zn,$j,"OBR"))+1
 s ^TMP($zn,$j,"OBR","I",obrseq)="OBR|"_obrseq_"|"_$s(ccisorder'="":ccisorder,1:"")_"|"_$tr(vtsRID,"||"," ")_"^PLS|"_tssyn_"^"_tsname_"|||"_colldatex_colltimex_"|||||"_secur_"||||"_drprov_"^"_drcode_"^"_drname_"||||IMAGE|||||A"
 ; always compile non cumulative formatted result
 k SingRes,CumRes,AllRes,CompRes
 i '$$SINGLE^PRTDR00(vtsRID,"E","") d
 .d adjust(.PLIST) i PLIST f j=1:1:PLIST s PLIST(j+.5)="!"
 .i PLIST m SingRes=PLIST k PLIST
 ; compile cumulative format
 i format="C" d
 .k ^TMP($zn,$j,"EpisodeList")
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=$g(VISIT(j))
 .s visit=$tr(visit,"""","'")
 .d CumList^PRTDR00($p(vtsRID,"||",2),$p(vtsRID,"||",1),visit,$s(REPORT(4)=1:"Y",REPORT(4)=4:"Y",1:""))
 .m ^TMP($zn,$j,"EpisodeList")=PLIST k PLIST
 .s (col,EpisodeList)="" f  s col=$o(^TMP($zn,$j,"EpisodeList",col)) q:col=""  s EpisodeList=$s($l(EpisodeList):EpisodeList_",",1:"")_^(col)
 .k ^TMP($zn,$j,"EpisodeList")
 .i '$$SINGLE^PRTDR00(vtsRID,"E","C",EpisodeList) d
 ..d adjust(.PLIST) i PLIST f j=1:1:PLIST s PLIST(j+.5)="!"
 ..d adjust(.HEADER) i HEADER f j=1:1:HEADER s HEADER(j+.5)="!"
 ..i PLIST m CumRes=PLIST k PLIST
 s ^TMP($zn,$j,"OBX","I",obrseq)=$g(^TMP($zn,$j,"OBX","I",obrseq))+1,obxiseq=1
 s ^TMP($zn,$j,"OBX","I",obrseq,obxiseq)="OBX|"_obxiseq_"|FT|||"
 ; merge single result and cumulative results together
 s cnt="" f  s cnt=$o(SingRes(cnt)) q:cnt=""  s AllRes(cnt)=SingRes(cnt)
 i $d(SingRes) f i=1:1:2 s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)="!"
 s cnt="" f  s cnt=$o(HEADER(cnt)) q:cnt=""  s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)=HEADER(cnt)
 s cnt="" f  s cnt=$o(CumRes(cnt)) q:cnt=""  s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)=CumRes(cnt)
 ; compile for sending
 s (cnt,count,text,textx)="",string="~",i="" f  s i=$o(AllRes(i)) q:i=""  d
 .i $e(AllRes(i))="~" q
 .s text=AllRes(i) i text=$c(13) q
 .i ($e(text)="!")!($e(text)="#") d  q
 ..i textx'="" s cnt=cnt+1,CompRes(cnt)=textx
 ..s cnt=cnt+1,CompRes(cnt)="!",textx="",textx=$e($tr(text,"~","^"),2,99)
 .s text=$tr(text,"~","^")
 .s len=$l(text) f x=1:1 q:$e(text,x)'=" "
 .f j=x:1:len s $e(textx,j)=$e(text,j)
 s string="~",i="" f  s i=$o(CompRes(i)) q:i=""  d
 .i CompRes(i)="!" s string=string_"~" q
 .s string=string_CompRes(i)
 s string=$e(string,1,$l(string)-1)
 s str="" f i=1:1:$l(string,"^") s str=str_$p(string,"^",i)_"\S\"
 s string=$p(str,"\S\",1,$l(str,"\S\")-1)
 s ^TMP($zn,$j,"OBX","I",obrseq,obxiseq)=^TMP($zn,$j,"OBX","I",obrseq,obxiseq)_string
 s x1=$p(vtsRID,"||",1)
 s x2=$p(vtsRID,"||",2)
 s x3=$p(vtsRID,"||",3)
 s $p(^TEPI(x1,1,x2,x3),"\",35)=uniseq
 k PLIST,visres,TC,TS,^TMP("PRT",$j)
 ; get packets in order of transmission
BUILDend s system=$s(ccisorder'="":"CCIS",1:"JADE")
 l +^TEDI("OUT-DATA","HL7",system,epis,vtsRID,uniseq)
 i $d(^TMP($zn,$j)) d
 .f type="MSH","PID","PV1" s str=$g(^TMP($zn,$j,type)) d:$l(str) SET(str,system)
 .f xx="A","I" d
 ..s j="" f  s j=$o(^TMP($zn,$j,"OBR",xx,j)) q:j=""  s str=^TMP($zn,$j,"OBR",xx,j) d:$l(str) SET(str,system) d
 ...s k="" f  s k=$o(^TMP($zn,$j,"OBX",xx,j,k)) q:k=""  s str=^TMP($zn,$j,"OBX",xx,j,k) d:$l(str) SET(str,system)
 l -^TEDI("OUT-DATA","HL7",system,epis,vtsRID,uniseq)
 k ^TMP($zn,$j) l -^TMPclean
 q
 ; adjust into one line
adjust(ARRAY) k array s x2=1
 s x1="" f  s x1=$o(ARRAY(x1)) q:x1=""  d
 .i ARRAY(x1)=$c(13) q
 .i ARRAY(x1)="!" s x2=x2+1 q
 .i $e(ARRAY(x1))="~" q
 .i $e(ARRAY(x1))="?" d  q
 ..s tab=+$e(ARRAY(x1),2,4),ARRAY(x1)=$e(ARRAY(x1),5,$l(ARRAY(x1)))
 ..i tab>$l($g(array(x2))) s array(x2)=$e($g(array(x2))_$j("",tab),1,tab)
 ..i $l(ARRAY(x1)) s array(x2)=$g(array(x2))_ARRAY(x1)
 .i $l(ARRAY(x1)) s array(x2)=$g(array(x2))_ARRAY(x1)
 k ARRAY m ARRAY=array s ARRAY=$o(ARRAY(""),-1)
 f j=1:1:ARRAY i '$d(ARRAY(j)) s ARRAY(j)=""
 q
 ; build transmission global
SET(line,system) s line=$g(line)
 s x=$o(^TEDI("OUT-DATA","HL7",system,epis,vtsRID,uniseq,""),-1)+1
 s ^TEDI("OUT-DATA","HL7",system,epis,vtsRID,uniseq,x)=line
 q
 ; create new sequence number for MSH record
new() s num=$$next("^CF(""HOSP-SEQ"")")
 q num
next(glo) n a
 l +@glo
 s a=$g(@glo)+1,@glo=a
 l -@glo
 q a
]]></Routine>
</Export>
