<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="SSAUDIT" type="INT" languagemode="0" generated="1"><![CDATA[
SSAUDIT ; Audit trail for all Code Tables
 q
 ;
 ; supported calls:
 ;
 ; $$insert^SSAUDIT(table)
 ; $$update^SSAUDIT(table)
 ; $$delete^SSAUDIT(table)
 ; $$header^SSAUDIT(table)
 ;
insert(table) d pre("N") n (table,%d) s table=$g(table)
 k PLIST s Master=$p(^AUDIT(table),"\") i '$l(Master) s Master=table
 s PLIST(9)="I",PLIST(10)=table
 m data=%d d check(table,.data)
 s (PLIST(4),j)="" f  s j=$o(data(j)) q:j=""  s $p(PLIST(4),$c(27),j)=$p($g(data(j)),$c(1))
 i $$insert^LVBAUTR(Master)
 q 0
update(table) d pre("Y") n (table,%e,%d) s table=$g(table)
 k PLIST s Master=$p(^AUDIT(table),"\") i '$l(Master) s Master=table
 s PLIST(9)="U",PLIST(10)=table
 k data m data=%e d check(table,.data)
 s (PLIST(3),j)="" f  s j=$o(data(j)) q:j=""  s $p(PLIST(3),$c(27),j)=$p($g(data(j)),$c(1))
 k data m data=%d d check(table,.data)
 s (PLIST(4),j)="" f  s j=$o(data(j)) q:j=""  s $p(PLIST(4),$c(27),j)=$p($g(data(j)),$c(1))
 i PLIST(3)'=PLIST(4),$$insert^LVBAUTR(Master)
 q 0
delete(table) n (table,%e) s table=$g(table)
 k PLIST s Master=$p(^AUDIT(table),"\") i '$l(Master) s Master=table
 s PLIST(9)="D",PLIST(10)=table,PLIST(8)=$g(^TMP("Comments",$j))
 m data=%e d check(table,.%d)
 s (PLIST(3),j)="" f  s j=$o(data(j)) q:j=""  s $p(PLIST(3),$c(27),j)=$p($g(data(j)),$c(1))
 i $$insert^LVBAUTR(Master)
 q 0
header(table) n (table,PLIST) s table=$g(table) k PLIST
 i '$$fieldList^SSUTIL4(table) d
 .k xPLIST m xPLIST=PLIST k PLIST
 .f j=1:1:xPLIST s col=$p(xPLIST(j),$c(2),3) i col s PLIST(col)=$p(xPLIST(j),$c(2),2)
 s PLIST=$o(PLIST(""),-1)
 q 0
 ; check all changes
check(table,data) n (table,data) s table=$g(table)
 s tableName=$p($$tableName^SSUTIL4(table),$c(1))
 s j="" f  s j=$o(data(j)) q:j=""  d
 .s xj=$p($$fieldName^SSUTIL4(tableName,j),$c(1),2)
 .i $l(xj),$l($g(^oddDEF(tableName,"a",xj,44))) k data(j) ; skip calculated fields
 q
pre(xUpdate) d PreSQL^SSUTIL4()
 q
]]></Routine>
</Export>
