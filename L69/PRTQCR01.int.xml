<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTQCR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTQCR01	; IG 07/10/98 ; QC report
	Q
	; Parameters :  1.Date from
	;               2.Date to
	;               3.Material
	;               4.Test item
	;               5.File type : 1 - Rendox
	;                             2 - BioRad
	;
CREATE(REPID,PAR,USER,%msql)	; Create report
	N (REPID,PAR,USER,%msql) K ^TMP($zn,$j)
	K xHEADER D INIT
	s line=1,xHEADER(line)="QC Report"
	s line=2,xHEADER(line)=""
	s line=3,xHEADER(line)="for material : "_QCM(3)_"   ("_QCM(2)_")"
	i $l(PAR4) s line=line+1,xHEADER(line)="for item : "_CTTC(3)_" ("_CTTC(2)_")"
	i PAR5 d
	.s dir=$$seldata^MVBCFSM(1,48)
	.f j="\","/" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
	.s file=dir_PAR3_"_"_$tr($$extdate2^SSUTIL4(PAR1),"/")_".csv"
	.s line=line+1,xHEADER(line)="created file : "_file
	D HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT i PAR5 d FILE
CREND	I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR3 I $$update^LVBPRPR(REPID)
	K ^TMP($zn,$j),^TMP("PRTREP00",$j)
	Q
	; initialize parameters
INIT	F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
	K xSORT D H^PRTREP00(PAR10,$zn)
	S X=xLINE(2),MAX=$L(X),$P(SPACE," ",MAX)="",$P(LINE,"-",MAX)=""
	k QCM i '$$select^LVBQCM(PAR3) m QCM=PLIST
	k CTTC i '$$select^LVBCTTC(PAR4) m CTTC=PLIST
	Q
	; create temporary printing file
BODY	s lot="" f  s lot=$o(^TTQC(PAR3,"LOT",lot)) q:lot=""  d
	.s type="" f  s type=$o(^TTQC(PAR3,"LOT",lot,"TYPE",type)) q:type=""  d
	..s wm="" f  s wm=$o(^TTQC(PAR3,"LOT",lot,"TYPE",type,wm)) q:wm=""  d
	...s ti="" f  s ti=$o(^TTQC(PAR3,"LOT",lot,"TYPE",type,wm,"TEST",ti)) q:ti=""  d
	....i $l(PAR4),ti'=PAR4 q
	....s level="" f  s level=$o(^TTQC(PAR3,"LOT",lot,"TYPE",type,wm,"TEST",ti,"DATA",level)) q:level=""  d
	.....s date=PAR1-1 f  s date=$o(^TTQC(PAR3,"LOT",lot,"TYPE",type,wm,"TEST",ti,"DATA",level,date)) q:date=""  q:date>PAR2  d
	......s run="" f  s run=$o(^TTQC(PAR3,"LOT",lot,"TYPE",type,wm,"TEST",ti,"DATA",level,date,run)) q:run=""  d
	.......m ^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run,level)=^TTQC(PAR3,"LOT",lot,"TYPE",type,wm,"TEST",ti,"DATA",level,date,run)
	Q
PRINT	i $d(^TMP($zn,$j,"REPORT")) d
	.s lot="" f  s lot=$o(^TMP($zn,$j,"REPORT",lot)) q:lot=""  d
	..s type="" f  s type=$o(^TMP($zn,$j,"REPORT",lot,type)) q:type=""  d
	...s wm="" f  s wm=$o(^TMP($zn,$j,"REPORT",lot,type,wm)) q:wm=""  d
	....s ti="" f  s ti=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti)) q:ti=""  d
	.....s date="" f  s date=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date)) q:date=""  d
	......s run="" f  s run=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run)) q:run=""  d
	.......k xxx
	.......s level="" f  s level=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run,level)) q:level=""  d
	........s xxx(level+10)=$p(^(level),"\",1)
	........s xxx(level+20)=$p(^(level),"\",2)
	........s xxx(level+30)=$p(^(level),"\",3)
	........s xxx(level+40)=$p(^(level),"\",5)
	........s xxx(level+50)=$p(^(level),"\",6)
	.......s xxx(1)=$p($g(^TTQC(PAR3,"LOT",lot)),"\",1)
	.......s xxx(2)=ti_$c(1)_$$seldata^LVBCTTC(ti,3)
	.......s xxx(3)=$$extdate2^SSUTIL4(date)
	.......s xxx(4)=run
	.......d PRINT^PRTREP00(,.N) k xxx
	......D ADDLINE^PRTREP00(REPID,"PRT","!")
	......D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
	......D ADDLINE^PRTREP00(REPID,"PRT","!")
	.....D ADDLINE^PRTREP00(REPID,"PRT","!")
	.....D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
	.....D ADDLINE^PRTREP00(REPID,"PRT","!")
	..D ADDLINE^PRTREP00(REPID,"PRT","!")
	..D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
	..D ADDLINE^PRTREP00(REPID,"PRT","!")
	s report="QCR"
	s EndLine=$$seldata^LVBCTCLR(report,9)
	i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
	q
FILE	i $d(^TMP($zn,$j,"REPORT")) d
	.i PAR5=1 d
	..o file:("NSW") u file s del=","
	..w "LIMS_ID,PERFORMED_BY,DATE,ANALYSIS_CODE,SERUM1_RESULT,SERUM2_RESULT,SERUM3_RESULT",!
	..s lot="" f  s lot=$o(^TMP($zn,$j,"REPORT",lot)) q:lot=""  d
	...s type="" f  s type=$o(^TMP($zn,$j,"REPORT",lot,type)) q:type=""  d
	....s wm="" f  s wm=$o(^TMP($zn,$j,"REPORT",lot,type,wm)) q:wm=""  d
	.....s ti="" f  s ti=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti)) q:ti=""  d
	......s date="" f  s date=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date)) q:date=""  d
	.......s run="" f  s run=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run)) q:run=""  d
	........s user="" f level=1:1:3 d
	.........s res(level)=$p($g(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run,level)),"\",1)
	.........i '$l(user) s user=$p($g(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run,level)),"\",3)
	........w ti,del,user,del,$$extdate2^SSUTIL4(date),del,del,res(1),del,res(2),del,res(3),!
	..c file
	.i PAR5=2 d
	..o file:("NSW") u file s del=","
	..s lot="" f  s lot=$o(^TMP($zn,$j,"REPORT",lot)) q:lot=""  d
	...s lotName=$p($g(^TTQC(PAR3,"LOT",lot)),"\",1)
	...s type="" f  s type=$o(^TMP($zn,$j,"REPORT",lot,type)) q:type=""  d
	....s wm="" f  s wm=$o(^TMP($zn,$j,"REPORT",lot,type,wm)) q:wm=""  d
	.....s wmName=$$seldata^LVBMIMP(wm,3)
	.....s ti="" f  s ti=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti)) q:ti=""  d
	......s tiName=$$seldata^LVBCTTC(ti,14) i '$l(tiName) s tiName=$e($$seldata^LVBCTTC(ti,3),1,20) i '$l(tiName) s tiName=ti
	......s date="" f  s date=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date)) q:date=""  d
	.......s xdt=$p($$extdate2^SSUTIL4(date),"/",3)_$e(100+$p($$extdate2^SSUTIL4(date),"/",1),2,3)_$e(100+$p($$extdate2^SSUTIL4(date),"/",2),2,3)
	.......s run="" f  s run=$o(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run)) q:run=""  d
	........f level=1:1:3 d
	.........s res=$p($g(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run,level)),"\",1) i $l(res) d
	..........s time=$p($g(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run,level)),"\",4)
	..........s user=$p($g(^TMP($zn,$j,"REPORT",lot,type,wm,ti,date,run,level)),"\",3)
	..........w wmName,del,lotName,del,level,del,tiName,del,xdt,del,$tr($$exttime^SSUTIL4(time),":"),del,res,!
	..c file
	q
]]></Routine>
</Export>
