<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDRSUM22" type="INT" languagemode="0"><![CDATA[
PRTDRSUM22 ; ML 16/3/98 ; DAILY RESULTS SUMMARY - Trak
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Site
 ;               4. Department
 ;               5. Group of Tests
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 d ADDLINE^PRTREP00(REPID,"HDR","~BOLD")
 d ADDLINE^PRTREP00(REPID,"HDR","?5  "_"UNSW W&CH ENDOCRINE LABORATORY")
 s text="Daily Results Summary" s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 d ADDLINE^PRTREP00(REPID,"HDR","?5  "_"EXT: 26641")
 s text="Site : " i PAR3'="" d
 .i PAR3="*" s text=text_"All Sites"
 .i PAR3'="" s text=text_site
 .s tab=$$TAB(text)
 .d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 .f i=1:1:2 d HDLN
 s text="Department : " i PAR4'="" d
 .i PAR4="*" s text=text_"All Departments"
 .i PAR4'="",$l(PAR4)>0 s text=text_dept
 .s tab=$$TAB(text)
 .d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 .f i=1:1:2 d HDLN
 s text="Test Set Group : " i PAR5'="" d
 .s text=text_tsgrp
 .s tab=$$TAB(text)
 .d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 .f i=1:1:2 d HDLN
 s text="From "_$$extdate2^SSUTIL4(PAR1)_" to "_$$extdate2^SSUTIL4(PAR2) s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 d HDLN
 D ADDLINE^PRTREP00(REPID,"HDR","?90 "_"Page : ")
 D ADDLINE^PRTREP00(REPID,"HDR","~PAGE")
 d HDLN
 s prt="" f i="Name:1","MRN:30","DOB:45","Sex:57","Location:62" d PRT(i)
 d ADDLINE^PRTREP00(REPID,"HDR",prt),HDLN
 d ADDLINE^PRTREP00(REPID,"HDR","?1  "_und),HDLN
 d ADDLINE^PRTREP00(REPID,"HDR","~NORMAL")
 d BODY,PRINT
 d ADDLINE^PRTREP00(REPID,"PRT","~FONTSIZE=10")
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
TAB(text) ; calculate header position
 s textlen=130-$l(text),tab=textlen\2_"   "
 q $e(tab,1,3)
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s (site,dept,tsgrp)=""
 s:PAR3="" PAR3="*"
 i PAR3'="*",'$$select^LVBCTUSL(PAR3,"Y") s site=$p(PLIST(3),$c(1)) k PLIST
 i PAR4'="*",'$$select^LVBCTDEP(PAR4,"Y") s dept=$p(PLIST(3),$c(1)) k PLIST
 i PAR5'="",'$$select^LVBCTTWG(PAR5,"Y") s tsgrp=$p(PLIST(3),$c(1)) k PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)="",$p(und,"=",130)="",$p(und1,"-",130)=""
 q
BODY ; create temporary printing file
 k ts i $l(PAR5),PAR5'="*" q:$$getall^LVBCTTWT(PAR5)  m grpts=PLIST k PLIST d
 .f i=1:1:grpts s ts=$p(grpts(i),$c(2),3) i $l(ts) s ts(ts)=""
 s date=PAR1-1 f  s date=$o(^TEPIi("AUTHORISED",date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TEPIi("AUTHORISED",date,epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis,"Y") d
 ...k EPVIS m EPVIS=PLIST
 ...i $l(PAR3),PAR3'="*",$p(EPVIS(66),$c(1))'=PAR3 q
 ...s ts="" f  s ts=$o(^TEPIi("AUTHORISED",date,epis,ts)) q:ts=""  d
 ....i $l(PAR4),PAR4'="*",$e(ts)'=PAR4 q
 ....i $l(PAR5),PAR5'="*",'$d(ts(ts)) q
 ....s nn="" f  s nn=$o(^TEPIi("AUTHORISED",date,epis,ts,nn)) q:nn=""  d
 .....s:'$l(EPVIS(3)) EPVIS(3)=" "
 .....s:'$l(EPVIS(20)) EPVIS(20)=" "
 .....s ^TMP($zn,$j,EPVIS(3),EPVIS(20),epis,ts,nn)=""
 q
PRINT ; print report
 d LN
 s surn="" f  s surn=$o(^TMP($zn,$j,surn)) q:surn=""  d
 .s mrn="" f  s mrn=$o(^TMP($zn,$j,surn,mrn)) q:mrn=""  d
 ..s epis="" f start=0:1 s epis=$o(^TMP($zn,$j,surn,mrn,epis)) q:epis=""  d
 ...s spec="" i '$$getall^LVBVISSP(epis) f j=1:1:PLIST s $p(spec,",",j)=$p(PLIST(j),$c(2),4)
 ...i '$$select^LVBEPVIS(epis) k EPVIS m EPVIS=PLIST
 ... ; print first line for MRN
 ...i 'start d
 ....s prt=$e($$NameFormat^LVBCOM03("PT","",EPVIS(4),EPVIS(3)),1,28)
 ....f i=$p(EPVIS(20),$c(1))_":29",$$extdate2^SSUTIL4(EPVIS(6))_":40",$p(EPVIS(5),$c(1))_":56",$p(EPVIS(22),$c(1),2)_":61" d PRT(i)
 ....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 ... ; print first line for Episode
 ...s prt=$e(epis_$j("",10),1,10)_"  Col.Date "_$$extdate2^SSUTIL4(EPVIS(12))
 ...d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN,LN
 ...s ts="" f  s ts=$o(^TMP($zn,$j,surn,mrn,epis,ts)) q:ts=""  d
 ....s nn="" f  s nn=$o(^TMP($zn,$j,surn,mrn,epis,ts,nn)) q:nn=""  d
 .....s vtsRID=epis_"||"_ts_"||"_nn q:$$results^LVBVISTS(vtsRID,,,,"Y")  m visres=PLIST k PLIST
 .....s prt=$j("",12) f i=1:1:visres s resstr=$p(visres(i),$c(2)),tc=$p(resstr,$c(1),2),res=$p(resstr,$c(1),3) i $l(res) d
 ......q:$$select^LVBCTTC(tc,"Y")  s tcnm=$s($l(PLIST(14)):PLIST(14),1:PLIST(3)),type=$e($p(PLIST(5),$c(1))),unit=$p(PLIST(4),$c(1))
 ......i '((type="N")!(type="M")) q
 ......s tstprt=prt_" "_$e(tcnm_$j(" ",15),1,15)_" "_$e(res_$j(" ",10),1,10)_$s($l(unit):" "_unit,1:"")
 ......i $l(tstprt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 ......s prt=prt_" "_tcnm_" "_res_$s($l(unit):" "_unit,1:"")
 .....i $l($tr(prt," ")) d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 ..d ADDLINE^PRTREP00(REPID,"PRT",und1),LN
 q
PRT(det) ; create line to print
 s text=$p(det,":",1),tab=$p(det,":",2),prt=prt_$j(" ",tab-$l(prt))_text
 q
HDLN ; add line to header
 d ADDLINE^PRTREP00(REPID,"HDR","!")
 q
LN ; add line to report
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 q
]]></Routine>
</Export>
