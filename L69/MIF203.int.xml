<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF203" type="INT" languagemode="0" generated="1"><![CDATA[
MIF203(mi) ; ML 24/6/98 ; DADE Sysmex R3000
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000(stx,etx) q:'$l(record)
 d trace^MIF000(mi,record,"H<--M")
 d ACK
 s (sample,epis,surname,result,date,time,QC)=""
 i $e(record,3)="U" d A1
 i $e(record,3)="C" d A2
 i epis'="",result'="" d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
A1 ; process results record
 s epis=$e(record,14,26),epis=$tr(epis," ")
 i $l(epis)>7 s epis=$e(epis,$l(epis)-6,$l(epis))
 s res=$e(record,28,57),resx=""
 f j=1:5:26 i $e(res,j)'="*" d
 .s code=$p($t(C00)," ",j\5+3),flag=$e(res,j+4) i 'flag s flag=""
 .s resx=$tr($e(res,j,j+3)," ")
 .s result=result_code_$c(par10)_resx_$c(par10)_flag_$c(par11)
 q
A2 ; process QC record
 s epis=$e(record,4),epis=$tr(epis," "),QC=1
 s res=$e(record,15,46),resx=""
 f j=1:4:29 i $e(res,j)'="*" d
 .s code=$p($t(C01)," ",j\4+3),resx=$tr($e(res,j,j+3)," ")
 .s result=result_code_$c(par10)_resx_$c(par11)
 q
C00 ; RET% RET# LFR MFR HFR RBC
C01 ; RET% RET# RBC# RBC-X RBC-Y RET-X DW/X DW/Y
 q
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
]]></Routine>
</Export>
