<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTTRU01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTTRU01 ; ML 28/10/98 ; TRANSFUSED UNITS
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Site
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Transfused Units"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s(PAR3="":"All Sites",1:$p(^TTAB("USLOC",PAR3),"^"))
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^BB("PK",0,"Date",date)) q:date=""  q:date>PAR2  d
 .s trans="" f  s trans=$o(^BB("PK",0,"Date",date,trans)) q:trans=""  d
 ..; search only for transfused packs
 ..q:" T "'[(" "_$p($g(^CT("TRANS",trans)),"^")_" ")
 ..s pack="" f  s pack=$o(^BB("PK",0,"Date",date,trans,pack)) q:pack=""  d
 ...q:'$d(^BB("PK",pack))  s s=^(pack),loc=$p(s,"^",10),site=$p($g(^CT("LO",+loc)),"^",3)
 ...i PAR3'="",PAR3'=site q
 ...s:site="" site=" "
 ...s chl="" f  s chl=$o(^BB("PK",0,"Date",date,trans,pack,chl)) q:chl=""  d
 ....q:$$select^BVBPACK1(pack)
 ....s bag=PLIST(2) i '$l(bag) s bag=" "
 ....s prod=$p($g(^CT("PR",+PLIST(8))),"^") i '$l(prod) s prod=" "
 ....s group=$p($g(^CT("BG",+PLIST(10))),"^",1) i '$l(group) s group=" "
 ....s chlx=chl-1 q:$$select^BVBPKTRN(pack_"||"_chlx)
 ....s epis=PLIST(11) q:$$select^LVBEPVIS(epis,"Y")
 ....s ^TMP($zn,$j,"REPORT",site,prod,group,bag)=pack
 ....s ^TMP($zn,$j,"REPORT",site,prod,group,bag,epis)=date
 q
PRINT ; print report
 s site="" f  s site=$o(^TMP($zn,$j,"REPORT",site)) q:site=""  d
 .i '$l(PAR3) d
 ..d LN
 ..d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ..d ADDLINE^PRTREP00(REPID,"PRT","~ITALIC")
 ..d ADDLINE^PRTREP00(REPID,"PRT","?5  "_site)
 ..i $l(site) d ADDLINE^PRTREP00(REPID,"PRT","?15 "_$p(^TTAB("USLOC",site),"^"))
 ..d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ..d LN
 .s prod="" f  s prod=$o(^TMP($zn,$j,"REPORT",site,prod)) q:prod=""  d
 ..k xxx s xxx(3)=prod
 ..s group="" f  s group=$o(^TMP($zn,$j,"REPORT",site,prod,group)) q:group=""  d
 ...s bag="" f  s bag=$o(^TMP($zn,$j,"REPORT",site,prod,group,bag)) q:bag=""  d
 ....s pack=^(bag) i '$$select^BVBPACK1(pack) d
 .....s xxx(1)=PLIST(2)
 .....s xxx(2)=group
 .....s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",site,prod,group,bag,epis)) q:epis=""  d
 ......s xxx(4)=$$extdate2^SSUTIL4(^(epis))
 ......i '$$select^LVBEPVIS(epis) d
 .......s xxx(5)=$p(PLIST(65),",",2)
 .......d PRINT^PRTREP00(.PLIST,.N) k xxx
 ..d LN
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 .d LN
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d LN
 q
LN ; add line to report
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 q
]]></Routine>
</Export>
