<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF441" type="INT" languagemode="0" generated="1"><![CDATA[
MIF441(mi) ; ML 29/6/98 ; ASTM protocol  -  DataInnovations (Bi-Directional) ; QC - Yes
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),etb=$c(23),ack=$c(6),enq=$c(5),eot=$c(4),lf=$c(10),cr=$c(13),nak=$c(21)
 s (result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  d  q
 .d:" BU "[(" "_par9_" ") SCAN
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK
 .k ^TMP($zn,$j,"records") s recordOLD=""
 .f  r *R:10 q:$c(R)=eot  q:R=-1  d
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",lf) q:'$l(record)
 ..s record=$e(record,1,$l(record)-1)
 ..d trace^MIF000(mi,$tr(record,cr_etx_etb," "),"H<--M")
 .. ; check record validity
 ..s et=$s(record[etx:etx,1:etb),chsum=$p(record,et,2),chrecord=$p(record,et,1)_et
 ..i $$CHSUM(chrecord)'=chsum d  q
 ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 ..d ACK
 ..s record=recordOLD_$p($e(record,2,$l(record)),et,1) i et=etb s recordOLD=$p(record,cr,$l(record,cr)),record=$p(record,cr,1,$l(record,cr)-1)
 ..f j=1:1:$l(record,cr) s x=$p(record,cr,j) i $l(x) s cnt=$o(^TMP($zn,$j,"records",""),-1)+1,^TMP($zn,$j,"records",cnt)=x
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 .s cnt="" f  s cnt=$o(^TMP($zn,$j,"records",cnt)) q:cnt=""  d
 ..s record=^(cnt)
 .. ; patient record
 ..i $e(record)="P" d  q
 ...d Last
 ...k comment,result s (sample,epis,surname,result,date,time,QC,cRecord)=""
 ...s surname=$tr($tr($p(record,"|",6)," "),"^"," "),surname=$p(surname,cr,1)
 .. ; order record
 ..i $e(record)="O" d  q
 ...s epis=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
 ...s cup=$p($p(record,"|",4),"^",3)
 ... ; remove punctuation from QC id
 ...i QC="Q" s epis=$tr(epis,"-,.<>?/':;{[}]+-=)(*&^%$#@!|\ ",""),QC=cup_"\"_epis
 .. ; 'C' Error mess record
 ..i $e(record)="C" d
 ...s x3=$p($p(record,"|",4),"^",1)
 ...s result=result_$s(cRecord=1:",",1:"")_x3,cRecord=1
 ...s x1="COM"_$p(record,"|",2),x2=$p($p(record,"|",4),"^",1) i " -771 -772 -2054 "[(" "_x2_" ") s comment(x1)=x2
 .. ; result record
 ..i $e(record)="R" s cRecord=0 d  q
 ...s x1=$tr($p($p(record,"|",3),"^",4)," "),x2=$tr($p(record,"|",4)," "),x3=$tr($p(record,"|",7),"\ ",",")
 ...s inst=$tr($p(record,"|",14)," ")
 ...i $l(x1),$l(x2) d
 ....s:$d(result(inst)) result(inst)=result(inst)_$c(par11)
 ....s result(inst)=$g(result(inst))_x1_$c(par10)_x2_$c(par10)_x3_$c(par10)
 ... ; date and time
 ...i $l($tr($p(record,"|",13)," ")) d
 ....s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
 ....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
 .. ; last record
 ..i $e(record)="L" d Last q
 .k ^TMP($zn,$j,"records")
 q
Last ; file result if exist
 s x1="" f  s x1=$o(comment(x1)) q:x1=""  d
 .s x2=comment(x1) s:$l(result) result=result_$c(par11) s result=result_x1_$c(par10)_x2_$c(par10)
 s inst="" f  s inst=$o(result(inst)) q:inst=""  d
 .s result=result(inst)
 .i epis?1.99AN,$l(result) d file^MIF000(inst,sample,epis,surname,result,date,time,QC)
 k comment,result s (sample,epis,surname,result,date,time,QC)=""
 q
 ; check all episodes for tests to be performed
SCAN d scanall^MIF000(mi) q:'$d(^TMP("MIF-SCAN",$j,mi))
 s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
 .i $d(^TMP("MIF-SCAN",$j,mi,epis)) d
 ..s tc="" s tc=$o(^TMP("MIF-SCAN",$j,mi,epis,tc)) i $l(tc) d
 ...d CHANNEL(epis)
 ...s spectype="" f  s spectype=$o(CHANNEL(spectype)) q:spectype=""  d
 ....d PATDET(epis)
 ....d SET(epis,spectype)
 ....s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
 ....d Send
 ...k CHANNEL
 .k ^TMP("MIF-SCAN",$j,mi,epis)
 q
 ; separate channels into specimen type
CHANNEL(epis) s epis=$g(epis) k CHANNEL
 ; process an all episode scan
 i $d(^TMP("MIF-SCAN",$j,mi,epis)) d
 .s tc="" f  s tc=$o(^TMP("MIF-SCAN",$j,mi,epis,tc)) q:tc=""  d
 ..i '$d(^TMIF(mi,20,epis,"TI-done",$p(tc,"_"))) s spectype=$s($p(tc,"_",2)'="":$p(tc,"_",2),1:" "),CHANNEL(spectype,$p(tc,"_"))="",^TMIF(mi,20,epis,"TI-done",$p(tc,"_"))=""
 q
 ; set patient details record
PATDET(epis) s epis=$g(epis)
 i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U"),mrn=$g(PLIST(20))
 .s name="" i $l($g(PLIST(3)))!$l($g(PLIST(4))) s name=$g(PLIST(3))_"^"_$g(PLIST(4))
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_mrn_"|||"_name_"|||"_sex_"|||||"_$g(PLIST(15))
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1"
 q
 ; set record to be sent to instrument
SET(epis,spectype) s epis=$g(epis),spectype=$g(spectype)
 ; compose based on specimen
 s (tc,tcx)="" f  s tc=$o(CHANNEL(spectype,tc)) q:tc=""  s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_tc
 i $l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_"||"_tcx_"|||||||N||||"_$s(spectype=" ":"",1:spectype)_"||||||||||O"
 q
Send ; send list of orders if exists
 w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M")
 f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=nak:"NAK",1:R),"H<--M")
 i $c(R)=enq q
 i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 s Dx=$$extdate2^SSUTIL4(+$h),Dx=$p(Dx,"/",3)_$p(Dx,"/",2)_$p(Dx,"/",1)
 s Tx=$e($p($h,",",2)\3600+100,2,3)_":"_$e($p($h,",",2)\60#60+100,2,3)_":"_$e($p($h,",",2)#60+100,2,3)
 q:$$SEND("1H|\^&|||LABTRAK|||||||P|1|"_Dx_Tx)
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 ; ---- new changes ----
 i x="" k ^TMIF(mi,10)
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
NEXTTRAY(tray) 
 q tray
]]></Routine>
</Export>
