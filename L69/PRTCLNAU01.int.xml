<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCLNAU01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTCLNAU01 ; ML 10/04/2003 ; HistoPath Diagnostic report
 q
 ; Parameters :  1.Date from
 ;               2.Date to
 ;               3.Doctor
 ;               4.Report type : 1 - Diagnosis
 ;                               2 - Correlation
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 f i=1:1:10 s xHEADER(i)=""
 s xHEADER(11)=$s(PAR4=1:"Diagnostic Report",1:"Diagnosis Correlation Report")
 s xHEADER(12)=""
 i $l(PAR3) d
 .s xHEADER(13)="Doctor: "_$s($l(PAR3):$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3)),1:"All Doctors")
 .s xHEADER(14)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2,,,,"Y"),BODY,PRINT
CREND k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 i PAR4=2 s x=$p(PAR10,",",4),$p(x,$c(2),3)="% Correlation",$p(PAR10,",",4)=x
 k xSORT d H^PRTREP00(PAR10,$zn)
 k DOC i $l(PAR3),'$$select^LVBCTDR(PAR3,"Y") m DOC=PLIST
 S X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 s ^TMP($zn,$j,"TESTS",1)="H0006\H0008"
 s ^TMP($zn,$j,"TESTS",2)="H0038\H0041"
 s ^TMP($zn,$j,"TESTS",3)="H0039\H0042"
 s ^TMP($zn,$j,"TESTS",4)="H0040\H0043"
 s ^TMP($zn,$j,"TESTS",5)="H0075\H0079"
 s ^TMP($zn,$j,"TESTS",6)="H0076\H0080"
 s ^TMP($zn,$j,"TESTS",7)="H0077\H0081"
 s ^TMP($zn,$j,"TESTS",8)="H0078\H0082"
 q
 ; create temporary printing file
BODY s date=PAR1 f  d:$l(date)  s date=$o(^TDAY(1,date)) q:date=""  i $l(PAR2),date>PAR2 q
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s doc=$$seldata^LVBEPVIS(epis,15)
 ..i $l(PAR3),doc'=PAR3 q
 ..s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....f j1=1:1 q:'$d(^TMP($zn,$j,"TESTS",j1))  d
 .....f j2=1,2 s @("x"_j2)=$p(^TMP($zn,$j,"TESTS",j1),"\",j2)
 .....s a1=$p($g(^TEPI(epis,1,ts,tscnt,"DATA",x1)),"\")
 .....s a2=$p($g(^TEPI(epis,1,ts,tscnt,"DATA",x2)),"\")
 .....i $l(a1),$l(a2) d
 ......i PAR4=1 d
 .......s $p(^TMP($zn,$j,"REPORT",doc,a1),"\",1)=$p($g(^TMP($zn,$j,"REPORT",doc,a1)),"\",1)+1
 .......s $p(^TMP($zn,$j,"REPORT",doc),"\",1)=$p($g(^TMP($zn,$j,"REPORT",doc)),"\",1)+1
 ......i PAR4=2 d
 .......s $p(^TMP($zn,$j,"REPORT",doc,a1),"\",1)=$p($g(^TMP($zn,$j,"REPORT",doc,a1)),"\",1)+1
 .......i a1=a2 d
 ........s $p(^TMP($zn,$j,"REPORT",doc),"\",1)=$p($g(^TMP($zn,$j,"REPORT",doc)),"\",1)+1
 ........s $p(^TMP($zn,$j,"REPORT",doc,a1),"\",2)=$p(^TMP($zn,$j,"REPORT",doc,a1),"\",2)+1
 q
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s x1="" f  s x1=$o(^TMP($zn,$j,"REPORT",x1)) q:x1=""  d
 ..s xxx(1)=x1
 ..s x2="" f cnt=0:1 s x2=$o(^TMP($zn,$j,"REPORT",x1,x2)) q:x2=""  d
 ...s xxx(2)=x2,xxx(3)=$s(PAR4=1:$p(^(x2),"\",1),1:$p(^(x2),"\",2)) s:xxx(3)="" xxx(3)=0
 ...i PAR4=2 s xxx(4)=$p(^(x2),"\",2)*100/$p(^(x2),"\",1)
 ...i PAR4=1 s xxx(4)=$p(^(x2),"\",1)*100/^TMP($zn,$j,"REPORT",x1)
 ...d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..s xxx(1)="Total",xxx(3)=$p($g(^TMP($zn,$j,"REPORT",x1)),"\",1)
 ..d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",$s($o(^TMP($zn,$j,"REPORT",x1))="":xLINE(1),1:xLINE(4)))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="CLNAU01"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" d PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
