<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LHOSPINT13" type="INT" languagemode="0" generated="1"><![CDATA[
LHOSPINT13 ; IG 12/07/98 ; Hospital Interface - Launceston
 Q
RESULT(HOSP) N (HOSP) k ^TMP($zn,$j)
 i $$select^LVBCTRL(HOSP) q
 s dir=PLIST(7),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.*"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""results.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""results.txt""")
 s $zt="Error1^"_$zn
 s FILE="results.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  u xFILE r MESS s seq=$o(^TMP($zn,$j,xxFILE,"IN",""),-1)+1,^TMP($zn,$j,xxFILE,"IN",seq)=MESS
 s $zt=""
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s cnt=0
 .s seq="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=^(seq) d
 ..i $p(xx,"|")="OBR" s cnt=cnt+1,^TMP($zn,$j,xFile,"IN-1",cnt,0)=xx
 ..i $p(xx,"|")="OBX",$d(^TMP($zn,$j,xFile,"IN-1",cnt)) s x=$o(^TMP($zn,$j,xFile,"IN-1",cnt,""),-1)+1,^TMP($zn,$j,xFile,"IN-1",cnt,x)=xx
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .k ^TMPimport(xFile)
 .s seq1="" f  s seq1=$o(^TMP($zn,$j,xFile,"IN-1",seq1)) q:seq1=""  d
 ..s (epis,ts,tscnt,vts)="",done="bad"
 ..s seq2="" f  s seq2=$o(^TMP($zn,$j,xFile,"IN-1",seq1,seq2)) q:seq2=""  s xx=^(seq2) d
 ...i $p(xx,"|")="OBR" d
 ....s epis=$p($p(xx,"|",3),"^") i $l(epis) s ts=$$loinc($p($p($p(xx,"|",4),"^"),"-",3)) i $l(ts) s tscnt=$o(^TEPI(epis,1,ts,""))
 ....i $l(epis),$l(ts),$l(tscnt) s vts=epis_"||"_ts_"||"_tscnt
 ....i '$l($$seldata^LVBVISTS(vts,1)) s vts=""
 .... ; only file results in RTF test set
 ....i $$seldata^LVBCTTS(ts,31)'="Y" s vts=""
 ...i $l(vts),$p(xx,"|")="OBX",$p(xx,"|",3)="FT" s result=$p(xx,"|",6) d
 ....f j1=1:1:$l(result,"\.br\") d
 .....s xx=$p(result,"\.br\",j1)
 .....f j2="S" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(16)_$p(xx,x2,2,$l(xx,x2))
 .....f j2="F" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(17)_$p(xx,x2,2,$l(xx,x2))
 .....f j2="T" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"&"_$p(xx,x2,2,$l(xx,x2))
 .....f j2="R" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"~"_$p(xx,x2,2,$l(xx,x2))
 .....f j2="H","N","E" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$p(xx,x2,2,$l(xx,x2))
 .....s result(j1)=xx
 ....s ^TMPimport(xFile,"index",vts)=seq1
 ....m ^TMP($zn,$j,xFile,"OUT",vts,"T")=result
 ....k result s done="process"
 ..m ^TMPimport(xFile,done,seq1)=^TMP($zn,$j,xFile,"IN-1",seq1)
 ..k ^TMP($zn,$j,xFile,"IN-1",seq1)
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s vts="" f  s vts=$o(^TMP($zn,$j,xFile,"OUT",vts)) q:vts=""  d
 ..s (text,cnt)="" f  s cnt=$o(^TMP($zn,$j,xFile,"OUT",vts,"T",cnt)) q:cnt=""  s text=text_$s($l(text):"|",1:"")_^(cnt)
 ..s epis=$p(vts,"||",1),ts=$p(vts,"||",2),tscnt=$p(vts,"||",3)
 ..i $$vtsLock^SSLOCK("IM",epis,vts,"trak")=-1 d
 ...i $$status^LVBVIS1(vts)="A" d  q
 ....k xx m xx=^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
 ....k ^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
 ....s max=0
 ....f j=1:1 q:'$d(xx(j))  i max<$l(xx(j)) s max=$l(xx(j))
 ....f j=1:1:$l(text,"|") i max<$l($p(text,"|",j)) s max=$l($p(text,"|",j))
 ....s line="",$p(line,"-",max+1)=""
 ....s text=$j("",max-22\2)_"--- Amended Result ---"_"||"_text_"||"_line
 ....f j=1:1:$l(text,"|") s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1)+1,^TEPI(epis,1,ts,tscnt,"DATA","T","REM",x)=$p(text,"|",j)
 ....f j=1:1 q:'$d(xx(j))  s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1)+1,^TEPI(epis,1,ts,tscnt,"DATA","T","REM",x)=xx(j)
 ....s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1),^TEPI(epis,1,ts,tscnt,"DATA","T","REM",0)=x
 ...i $$status^LVBVIS1(vts)'="A" d  q
 ....k PLIST
 ....s PLIST(1)="DATA"_$c(1)_"T"_$c(1)_text
 ....s PLIST=$o(PLIST(""),-1)
 ....i $$TScheck^LVBVIS7(vts,"A","","B")
 ....s (VQ,ok)="" f j=1:1:PLIST d  i ok="N" q
 .....i $e($p(PLIST(j),$c(1)))="E" s ok="N" q
 .....i $e($p(PLIST(j),$c(1)),1,2)="VQ" s VQ=$p(PLIST(j),$c(1),2)
 ....k PLIST s PLIST(1)="T\"_text
 ....s PLIST=$o(PLIST(""),-1)
 ....s err=$$save^LVBVIS4(vts,"A","trak","N")
 ....s ^TMPimport(xFile,"index",vts)=$g(^TMPimport(xFile,"index",vts))_" err"
 ....i 'err d
 .....i '$$select^LVBVISTS(vts,"Y") s PLIST(29)=$$seldata^LVBEPVIS(epis,66) i $$update^LVBVISTS(vts)
 .....k PLIST
 .....s PLIST=$o(PLIST(""),-1)
 .....i $$TScheck^LVBVIS7(vts,"A","","A")
 .....f j=1:1:PLIST d
 ......s val=$p($p(PLIST(j),$c(1),2),$c(2))
 ......i $e($p(PLIST(j),$c(1)),1,2)="VQ",$$VQ^LVBCOM13(vts,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="TA",$$TAR^LVBCOM13($p(vts,"||"),,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SA",$$SA^LVBCOM13($p(vts,"||"),val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SF",$$SF^LVBCOM13(vts,$p(PLIST(j),$c(1),6),val)
 ......i $e($p(PLIST(j),$c(1)),1,3)="ACT",$$ACT^LVBCOM13($p(vts,"||"),$p(val,"-",1),$p(val,"-",2))
 ..i $$vtsLockClr^SSLOCK("IM",epis,vts)
 s (param,xFile)="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  s param=param_$s($l(param):",",1:"")_xFile
 i $l(param),$$START^PRTREP00("","",HOSP_"\"_param,"RLI")
 k ^TMP($zn,$j)
 q
 ; Error log
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
 ; Error log
Error2 i $ze'["ENDOFFILE" d BACK^%ETN q
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
loinc(loinc) n (loinc)
 s x="" f  s x=$o(^TTAB("TS",x)) q:x=""  i $$seldata^LVBCTTS(x,34)=loinc q
 q x
 ; check hospital files for updates
START(HOSP) N (HOSP) k ^TMP($zn,$j)
 i $$select^LVBHOS(HOSP) Q
 s file=PLIST(5),$zt="ERROR^"_$zn
 l +^THOS(HOSP):10 e  q
 ; retrieve data
 i HOSP="P" d
 .s file="e:\cachedata\livedata\ppatext1.csv"
 .o file::0 e  l -^THOS(HOSP) q
 .f  u file r MESSAGE q:'$l(MESSAGE)  d
 ..k x f j=1:1:9 s x(j)=$$space($p(MESSAGE,",",j))
 .. ; check UR prefix
 ..s ur1="" i x(1)[":" s ur1=$p(x(1),":",1)
 ..s ur2=x(1) i x(1)[":" s ur2=$p(x(1),":",2)
 ..i "  GP BP AC ADS AE AHC AN BRA BTS CRH FI FPA SHP RH RHH KI "'[(" "_ur1_" ") q
 ..s x(10)="P"_ur2,x(1)=ur1_ur2
 .. ; check sex
 ..i $l(x(5)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCS(HOSP_"||"_x(5)) s X=PLIST(3)
 ...s x(5)=X
 .. ; check date fields
 ..f j=4 i $l(x(j)) s x(j)=$$intdate^SSUTIL4($p(x(j),"-",2)_"/"_$p(x(j),"-",1)_"/"_$p(x(j),"-",3))
 .. ; check if admission already exists
 ..s exist="N" i '$$select^LVBHOSP(HOSP_"||"_x(10)_"||"_x(1)) s exist="Y"
 .. ; file data
 ..k PLIST
 ..s PLIST(2)=x(10)
 ..s PLIST(3)=x(1)
 ..s PLIST(4)=x(3)
 ..s PLIST(5)=x(2)
 ..s PLIST(7)=x(4)
 ..s PLIST(8)=x(5)
 ..s PLIST(9)=x(6)
 ..s PLIST(10)=x(7)
 ..s PLIST(11)=x(8)
 ..s PLIST(12)=x(9)
 ..s PLIST=$o(PLIST(""),-1)
 .. ; update patient information
 ..i exist="Y" d  q
 ...i $$update^LVBHOSP(HOSP_"||"_x(10)_"||"_x(1))
 .. ; insert new patient information
 ..i exist="N" d  q
 ...i $$insert^LVBHOSP(HOSP)
 .c file
 i HOSP="H" d
 .s file="e:\cachedata\livedata\pathpats.csv"
 .o file::0 e  l -^THOS(HOSP) q
 .f xx=1:1 u file r MESSAGE q:'$l(MESSAGE)  d
 ..k x f j=1:1:15 s x(j)=$$space($p(MESSAGE,",",j))
 .. ; check UR prefix
 ..i x(1)[":" q
 ..s x(13)=$e(1000000+x(1),2,99),x(1)="H"_x(13)
 .. ; check sex
 ..i $l(x(5)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCS(HOSP_"||"_x(5)) s X=PLIST(3)
 ...s x(5)=X
 .. ; check date fields
 ..f j=4 i $l(x(j)) s x(j)=$$intdate^SSUTIL4($p(x(j),"/",2)_"/"_$p(x(j),"/",1)_"/"_$p(x(j),"/",3))
 .. ; check if admission already exists
 ..s exist="N" i '$$select^LVBHOSP(HOSP_"||"_x(1)_"||"_x(13)) s exist="Y"
 .. ; file data
 ..k PLIST
 ..s PLIST(2)=x(1)
 ..s PLIST(3)=x(13)
 ..s PLIST(4)=x(3)
 ..s PLIST(5)=x(2)
 ..s PLIST(6)=x(10)
 ..s PLIST(7)=x(4)
 ..s PLIST(8)=x(5)
 ..s PLIST(11)=x(11)
 ..s PLIST(12)=x(12)
 ..s PLIST(33)=x(15)
 ..s PLIST=$o(PLIST(""),-1)
 .. ; update patient information
 ..i exist="Y" d  q
 ...i '$$update^LVBHOSP(HOSP_"||"_x(1)_"||"_x(13)) d tepi(HOSP_"||"_x(1)_"||"_x(13))
 .. ; insert new patient information
 ..i exist="N" d  q
 ...i '$$insert^LVBHOSP(HOSP) d tepi(HOSP_"||"_x(1)_"||"_x(13))
 .c file
end ; update hospital level information
 l -^THOS(HOSP)
 i '$$select^LVBHOS(HOSP) d
 .s PLIST(3)=+$h,PLIST(4)=$P($h,",",2)
 .i $$update^LVBHOS(HOSP)
 q
 ; delete end spaces
space(x) s x=$tr(x,"""")
 f  q:'$l(x)  q:$e(x,$l(x))'=" "  s x=$e(x,1,$l(x)-1)
 f  q:'$l(x)  q:$e(x)'=" "  s x=$e(x,2,$l(x))
 q x
ERROR i $ze'["ENDOFFILE" D BACK^%ETN
 c file l -THOS(HOSP) s $zt=""
 g end
tepi(hROWID) n (hROWID) s hROWID=$g(hROWID)
 i '$$select^LVBHOSP(hROWID) d
 .k HOSP m HOSP=PLIST
 .s (deb,hospMRN)=$p(hROWID,"||",2)
 .s epis="" f  s epis=$o(^TDEBi(deb,"HOSPITAL",hospMRN,epis)) q:epis=""  q:$e(epis)="X"
 .i $l(epis) d  d change q
 ..i '$$select^LVBEPVIS(epis,"Y") d set s err=$$save^LVBEPVIS(epis) i err d log("ERROR in TEPI for episode "_epis_" : Return "_err)
 .i '$l(epis) d  q
 ..i '$$new^LVBEPVIS("X") s epis=PLIST(1) k PLIST d set s err=$$save^LVBEPVIS(epis) i err d log("ERROR in TEPI for episode "_epis_" : Return "_err)
 q
change s name=$g(HOSP(4))
 i $l(name),$e(name,1,2)'="B/" d
 .s date="" f  s date=$o(^TDEB(deb,date)) q:date=""  d
 ..s epis="" f  s epis=$o(^TDEB(deb,date,0,epis)) q:epis=""  d
 ...i $e($$seldata^LVBEPVIS(epis,4),1,2)="B/",'$$select^LVBEPVIS(epis,"Y") s PLIST(4)=name,err=$$save^LVBEPVIS(epis) i err d log("ERROR in TEPI for episode "_epis_" : Return "_err)
 q
set s PLIST=$g(PLIST) i 'PLIST s PLIST=100
 f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 s PLIST(2)=epis
 i $l($g(HOSP(5))) s PLIST(3)=HOSP(5)
 i $l($g(HOSP(4))) s PLIST(4)=HOSP(4)
 i $l($g(HOSP(8))) s PLIST(5)=HOSP(8)
 i $l($g(HOSP(7))) s PLIST(6)=HOSP(7)
 i $l($g(HOSP(6))) s PLIST(7)=HOSP(6)
 i $l($g(HOSP(24))) s PLIST(8)=HOSP(24)
 i $l($g(HOSP(38))) s PLIST(9)=HOSP(38)
 s PLIST(12)=$$intdate^SSUTIL4("01/01/1999")
 i $l($g(HOSP(17))) s PLIST(15)=HOSP(17)
 s PLIST(20)=$g(HOSP(2))
 i $l($g(HOSP(15))) s PLIST(22)=HOSP(15)
 i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(32)=$g(HOSP(9))
 i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(33)=$g(HOSP(10))
 i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(34)=$g(HOSP(11))
 i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(35)=$g(HOSP(31))
 i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(36)=$g(HOSP(12))
 i $l($g(HOSP(13))) s PLIST(43)=HOSP(13)
 i $l($g(HOSP(32))) s PLIST(44)=HOSP(32)
 i $l($g(HOSP(21))) s PLIST(47)=HOSP(21)
 i $l($g(HOSP(34))) s PLIST(51)=HOSP(34)
 i $l($g(HOSP(33))) s PLIST(60)=HOSP(33)
 s PLIST(65)=$g(HOSP(3))_","_$g(HOSP(2))
 i $l($g(HOSP(28))) s PLIST(66)=HOSP(28)
 i $l($g(HOSP(35))) s PLIST(73)=HOSP(35)
 s PLIST(86)=0
 i $l($g(HOSP(37))) s PLIST(88)=HOSP(37)
 i $l($g(HOSP(36))) s PLIST(89)=HOSP(36)
 ; add All Extra info
 i $l($g(HOSP(23))) f j=3:1:$l(HOSP(23),"|") i $l($p(HOSP(23),"|",j)) s PLIST(j)=$p(HOSP(23),"|",j)
 q
log(txt) n (txt,HOSP)
 s d=$p($h,","),n=$o(^THOS(HOSP,"log",d,""),-1)+1
 s ^THOS(HOSP,"log",d,n)=$zt($p($h,",",2))_" "_txt
 q
]]></Routine>
</Export>
