<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDLS01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDLS01 ; IG 19/08/99 ; DayBook Laboratory statistics
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. DayBook Laboratory
 ;               4. DayBook Site
 ;               5. Report
 ;               6. Print 0 values
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="DayBook Laboratory analysis"
 s line=2,xHEADER(line)=""
 i $l(PAR3) d
 .s line=3,xHEADER(line)="for Laboratory : "_CTDBL(2)_" - "_CTDBL(3)
 .s line=4,xHEADER(line)=""
 .i $l(PAR4) d
 ..s line=3,xHEADER(line)="for Site : "_CTDBF(2)_" - "_CTDBF(3)
 ..s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xxx,xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 k CTDBL,CTDBF
 i $l(PAR3) d
 .i '$$select^LVBCTDBL(PAR3,"Y") m CTDBL=PLIST
 .i $l(PAR4),'$$select^LVBCTDBF(PAR3_"||"_PAR4,"Y") m CTDBF=PLIST
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s x1="" f  s x1=$o(^TTAB("LA-REPORT",PAR5,"GROUP",x1)) q:x1=""  d
 ...s x2="" f  s x2=$o(^TTAB("LA-REPORT",PAR5,"GROUP",x1,x2)) q:x2=""  d
 ....s x3="" f  s x3=$o(^TTAB("LA-REPORT",PAR5,"GROUP",x1,x2,x3)) q:x3=""  s xx=^(x3) d
 .....f j=4,5 s @("xx"_j)=$p(xx,"\",j)
 .....s an=PAR3_PAR4 f  s an=$o(^THIR(epis,an)) q:an=""  q:$e(an,1,$l(PAR3_PAR4))'=(PAR3_PAR4)  d
 ......s block="" f  s block=$o(^THIR(epis,an,"BLOCK",block)) q:block=""  d
 .......s order="" f  s order=$o(^THIR(epis,an,"BLOCK",block,order)) q:order=""  d
 ........f j=1,3 s @("aa"_j)=$p(^(order),"\",j)
 ........i $l(xx4),xx4'=aa1 q
 ........i $l(xx5),xx5'=aa3 q
 ........s val="" d
 .........i '$l(val),$l(aa3) s rowid=PAR3_"||"_aa3 s val=$$seldata^LVBCTDBS(rowid,4)
 .........i '$l(val),$l(aa1) s rowid=PAR3_"||"_aa1 s val=$$seldata^LVBCTDBP(rowid,4)
 ........d SetTotal(x1,x2,val)
 q
 ; set totals
SetTotal(x1,x2,val) n (x1,x2,val)
 s $p(^TMP($zn,$j,"REPORT",0),"\",2)=$p($g(^TMP($zn,$j,"REPORT",0)),"\",2)+1
 s $p(^TMP($zn,$j,"REPORT",0,x1),"\",2)=$p($g(^TMP($zn,$j,"REPORT",0,x1)),"\",2)+1
 s $p(^TMP($zn,$j,"REPORT",0,x1,x2),"\",2)=$p($g(^TMP($zn,$j,"REPORT",0,x1,x2)),"\",2)+1
 s $p(^TMP($zn,$j,"REPORT",0),"\",3)=$p($g(^TMP($zn,$j,"REPORT",0)),"\",3)+val
 s $p(^TMP($zn,$j,"REPORT",0,x1),"\",3)=$p($g(^TMP($zn,$j,"REPORT",0,x1)),"\",3)+val
 s $p(^TMP($zn,$j,"REPORT",0,x1,x2),"\",3)=$p($g(^TMP($zn,$j,"REPORT",0,x1,x2)),"\",3)+val
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .i PAR6="Y" d
 ..s group="" f  s group=$o(^TTAB("LA-REPORT",PAR5,"GROUP",group)) q:group=""  d
 ...s item="" f  s item=$o(^TTAB("LA-REPORT",PAR5,"GROUP",group,item)) q:item=""  d
 ....s ^TMP($zn,$j,"REPORT",0,group,item)=$g(^TMP($zn,$j,"REPORT",0,group,item))
 .s group="" f  s group=$o(^TMP($zn,$j,"REPORT",0,group)) q:group=""  d
 ..f j=2,3 s xxx(j)=+$p(^(group),"\",j) i PAR6'="Y",'xxx(j) s xxx(j)=""
 ..i '$$select^LVBCTLRG(PAR5_"||"_group) s xxx(1)=PLIST(3)
 ..d PRINT^PRTREP00(,.N) k xxx
 ..s item="" f  s item=$o(^TMP($zn,$j,"REPORT",0,group,item)) q:item=""  d
 ...f j=2,3 s xxx(j)=+$p(^(item),"\",j) i PAR6'="Y",'xxx(j) s xxx(j)=""
 ...i '$$select^LVBCTLRA(PAR5_"||"_group_"||"_item) s xxx(1)=PLIST(3)
 ...d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s xxx(1)="Total Laboratory"
 .f j=2,3 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT",0)),"\",j) i PAR6'="Y",'xxx(j) s xxx(j)=""
 .d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="DLS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
