<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTUNW01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTUNW01 ; IG 07/10/98 ; UNVIEWED RESULTS REPORT
 Q
 ; Parameters :  1.Patient Location
 ;               2.Usergroup
 ;               3.Date From
 ;               4.Date To
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($zn,$j)
 K xHEADER D INIT
 s (xx1,xx2)="" i '$$select^LVBINVB(PAR1_"||"_PAR2) s xx1=$p(PLIST(10),$c(1),2),xx2=PLIST(5)
 s line=1,xHEADER(line)="UNVIEWED RESULTS "
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Patient Location : "_PLDesc
 s line=4,xHEADER(line)="Not viewed by usergroup : "_UGDesc
 D HEADER^PRTREP00(.xHEADER,PAR3,PAR4),BODY,PRINT
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR1_"-"_PAR2 I $$update^LVBPRPR(REPID)
 K ^TMP($zn,$j),^TMP("PRTREP00",$j)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 K xSORT D H^PRTREP00(PAR10,$zn)
 S X=xLINE(2),MAX=$L(X),$P(SPACE," ",MAX)="",$P(LINE,"-",MAX)=""
 s PLDesc=""     i '$$select^LVBCTHOS(PAR1,,USER) s PLDesc=PLIST(3)
 s UGDesc=""     i '$$select^MVBSSGRP(PAR2) s UGDesc=PLIST(3)
 Q
 ; create temporary printing file
BODY s day=PAR3-1 f  s day=$o(^TDAY(1,day)) q:day=""  q:day>PAR4  d
 .s epis="" f  s epis=$o(^TDAY(1,day,0,epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 ..i $l(PAR1),$p(PLIST(22),$c(1))'=PAR1 q
 ..i $g(PAR5)="Y",$g(PLIST(22))="" q
 ..s pl=$p($g(PLIST(22)),$c(1))
 ..i '$l(pl) s pl=" "
 ..i '$$open^LVBVISTS(epis,,USER) d
 ...f  q:$$fetch^LVBVISTS(epis,,USER)  d
 ....i PLIST(33)="A" s ts=$p(PLIST(3),$c(1),2) i $l(ts) d
 .....s tsc="" i '$$select^LVBCTTS(ts) s tsc=PLIST(1) s vts=epis_"||"_tsc_"||"_$o(^TEPI(epis,1,tsc,"")) 
 .....s pri=$$priority^LVBVISTS(vts) i '$l(pri) s pri=" " 
 .....s ^TMP($zn,$j,"REPORT",pl,epis,ts)=pri
 ...i $$close^LVBVISTS()
 ..i $$open^LVBVISEV(epis)
 ..f  q:$$fetch^LVBVISEV(epis)  d
 ...i " VW WV WR "[" "_PLIST(3)_" " d
 ....s user=$p($p(PLIST(4),"reviewed by ",2)," ")
 ....i '$l(user) s user=$p($p(PLIST(4),"web by ",2)," ")
 ....s ts=$p($p(PLIST(4),"<",2),">")
 ....i ts="" s ts=$p(PLIST(4)," ",3) i ts="" q
 ....s date=PLIST(5)
 ....s time=PLIST(6)
 ....i '$$selrow^MVBSSUSR(user) d
 .....s grp=$p($g(PLIST(4)),$c(1))
 .....i $l(PAR2),grp'=PAR2,$d(^TMP($zn,$j,"REPORT",pl,epis,ts)) s ^TMP($zn,$j,"REPORT",pl,epis,ts,date,time,user)=grp q
 .....i $l(ts) k ^TMP($zn,$j,"REPORT",pl,epis,ts)
 ..i $$close^LVBVISEV()
 Q
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s pl="" f  s pl=$o(^TMP($zn,$j,"REPORT",pl)) q:pl=""  d
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",pl,epis)) q:epis=""  d
 ...k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 ...s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",pl,epis,ts)) q:ts=""  d
 ....k xxx s xxx(1)=ts,printed=0 
 ....s pri=^TMP($zn,$j,"REPORT",pl,epis,ts) s priDesc="" i '$$select^LVBCTPR(pri) s priDesc=PLIST(3) s xxx(2)=pri_$c(1)_priDesc
 ....s date="" f  s date=$o(^TMP($zn,$j,"REPORT",pl,epis,ts,date)) q:date=""  d
 .....s xxx(5)=$$extdate2^SSUTIL4(date)
 .....s time="" f  s time=$o(^TMP($zn,$j,"REPORT",pl,epis,ts,date,time)) q:time=""  d
 ......s xxx(6)=$$exttime^SSUTIL4(time)
 ......s user="" f  s user=$o(^TMP($zn,$j,"REPORT",pl,epis,ts,date,time,user)) q:user=""  d
 .......s userName="" i '$$selrow^MVBSSUSR(user) s userName=PLIST(3)
 .......s xxx(3)=user_$c(1)_userName
 .......s ug=$p(^TMP($zn,$j,"REPORT",pl,epis,ts,date,time,user),$c(1))
 .......s ugDesc="" i '$$select^MVBSSGRP(ug,"Y") s ugDesc=PLIST(3)
 .......s xxx(4)=ug_$c(1)_ugDesc
 .......d PRINT^PRTREP00(.VISIT,.N) s printed=1
 ....i 'printed d PRINT^PRTREP00(.VISIT,.N) k xxx,VISIT
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="UNW"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
