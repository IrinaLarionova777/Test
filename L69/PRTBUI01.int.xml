<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBUI01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBUI01 ; ML 08/11/2000 ; Blood Unit Inventory
 q
 ; Parameters :  1. Site
 ;               2. Product
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Blood Unit Inventory and Status Display"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s(PAR1="":"All Sites",1:$p($g(^TTAB("USLOC",PAR1)),"\"))
 s line=4,xHEADER(line)=""
 s line=5,xHEADER(line)="Product : "_$s(PAR2="":"All Products",1:ProdName)
 s line=6,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 s site=PAR1
 s ProdName="" i PAR2'="",'$$select^LVBBBBP(PAR2) s ProdName=PLIST(3) k PLIST
H1 ; extract status descriptions
 s status="" f j=1:1 s status=$o(^TTAB("BB-ST",status)) q:status=""  s desc=$p(^(status),"\"),descx="" d
 .i $l(desc)>15 f x=1:1 i $l($p(desc," ",1,x))>15 s descx=$p(desc," ",x,$l(desc," ")),desc=$p(desc," ",1,x-1) q
 .s Caption(j)=desc_$c(2)_descx_$c(2)_status_$c(2)_$s($l(desc)>$l(descx):$l(desc)+2,1:$l(descx)+2),Caption=j
 s xLINE(2)="|  Site   | Product | Blood | Total ||" f j1=1:1:Caption s desc=Caption(j1) d
 .s xLINE(2)=xLINE(2)_$$PART^PRTREP00($p(Caption(j1),$c(2)),$p(Caption(j1),$c(2),4))_"|"
 s xLINE(3)="|         |         | Group |       ||" f j1=1:1:Caption s desc=Caption(j1) d
 .s xLINE(3)=xLINE(3)_$$PART^PRTREP00($p(Caption(j1),$c(2),2),$p(Caption(j1),$c(2),4))_"|"
 s width=$l(xLINE(2))-2,xLINE(4)="|" f j1=1:1:width s xLINE(4)=xLINE(4)_"-"
 s xLINE(4)=$e(xLINE(4),1,$l(xLINE(4)))_"|"
 s length=$l(xLINE(2)),$p(xLINE(1),"-",length)=""
 s xLINE(1)=" "_$e(xLINE(1),1,$l(xLINE(1))-1)
 s cnt=1,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 s cnt=2,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 s cnt=3,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 s cnt=4,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 s cnt=5,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 f j1=1:1:Caption f j2=1:1:$l($p(Caption(j1),$c(2),2),$c(28)) d
 .s cnt=cnt+1,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 .s N(cnt,"Status")=$p(Caption(j1),$c(2),3)
 s X=xLINE(2)
 s N(1,"Z")=1 f j=2:1 q:'$F(X,"|",N(j-1,"Z")+1)  s N(j,"Z")=$f(X,"|",N(j-1,"Z")+1)-1
 f j=1:1 q:'$d(N(j+1,"Z"))  s N(j,"L")=N(j+1,"Z")-N(j,"Z")-1
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s status="" f  s status=$o(^TBBPi("STATUS",status)) q:status=""  d
 .s inout="" f  s inout=$o(^TBBPi("STATUS",status,inout)) q:inout=""  d
 ..s PackID="" f  s PackID=$o(^TBBPi("STATUS",status,inout,PackID)) q:PackID=""  d
 ...s SecondID="" f  s SecondID=$o(^TBBPi("STATUS",status,inout,PackID,SecondID)) q:SecondID=""  d
 ....q:$$select^LVBBBP(PackID_"||"_SecondID)  m PackDet=PLIST k PLIST
 ....s product=$p(PackDet(8),$c(1)),BloodGrp=$p(PackDet(9),$c(1)),site=$p(PackDet(11),$c(1),3)
 ....i site="" s site=" "
 ....i PAR1'="",PAR1'=site q
 ....i product="" s product=" "
 ....i PAR2'="",PAR2'=product q
 ....s ^TMP($zn,$j,site,product,"Status",BloodGrp,status)=$g(^TMP($zn,$j,site,product,"Status",BloodGrp,status))+1
 ....s ^TMP($zn,$j,site,"Status-Total",status)=$g(^TMP($zn,$j,site,"Status-Total",status))+1
 ....s ^TMP($zn,$j,site,"Total")=$g(^TMP($zn,$j,site,"Total"))+1
 q
PRINT ; print report
 s site="" f  s site=$o(^TMP($zn,$j,site)) q:site=""  d
 .s xxx(1)=site,product="" f  s product=$o(^TMP($zn,$j,site,product)) q:product=""  d
 ..s xxx(2)=product,BloodGrp="" f  s BloodGrp=$o(^TMP($zn,$j,site,product,"Status",BloodGrp)) q:BloodGrp=""  d
 ...s xxx(3)=BloodGrp,(xxx(4),status)="" f  s status=$o(^TMP($zn,$j,site,product,"Status",BloodGrp,status)) q:status=""  d
 ....s xxx(4)=xxx(4)+^TMP($zn,$j,site,product,"Status",BloodGrp,status)
 ....s cnt=5 f  s cnt=$o(N(cnt)) q:cnt=""  i $g(N(cnt,"Status"))=status s xxx(cnt)=^TMP($zn,$j,site,product,"Status",BloodGrp,status) q
 ...d PRINT^PRTREP00(,.N) k xxx f i=1:1:30 s xxx(i)=""
 .s xxx(2)="",xxx(3)="Total:",xxx(4)=^TMP($zn,$j,site,"Total")
 .s status="" f  s status=$o(^TMP($zn,$j,site,"Status-Total",status)) q:status=""  d
 ..s cnt=5 f  s cnt=$o(N(cnt)) q:cnt=""  i $g(N(cnt,"Status"))=status s xxx(cnt)=^TMP($zn,$j,site,"Status-Total",status) q
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 .d PRINT^PRTREP00(,.N) k xxx f i=1:1:30 s xxx(i)=""
 .i $o(^TMP($zn,$j,site))'="" d
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="BUI"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
