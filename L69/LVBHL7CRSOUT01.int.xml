<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBHL7CRSOUT01" type="INT" languagemode="0" generated="1"><![CDATA[
LVBHL7CRSOUT01 ; ML 29/11/01 ; Create HL7 Format File - CRS - TRAK
 ;
 ; supported calls:
 ;
 ; create^LVBHL7CRSOUT01
 ;
create s type="HL7CRS" d start(type)
 q
 ; type : HL7CRS
 ;        TEP
start(type) n (type) s type=$g(type)
 i $$select^MVBCFSM(1) q 100
 s labname=PLIST(9)
 k CFLAB i '$$select^MVBCFLAB(1) m CFLAB=PLIST
 l +^TMPclean k ^TMP($zn,$j)
 l +^TEDI("OUT",type)
 s dh="" f  s dh=$o(^TEDI("OUT",type,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TEDI("OUT",type,dh,client)) q:client=""  d
 ..s doc="" f  s doc=$o(^TEDI("OUT",type,dh,client,doc)) q:doc=""  d
 ...s epis="" f  s epis=$o(^TEDI("OUT",type,dh,client,doc,epis)) q:epis=""  d
 ....s table="" f  s table=$o(^TEDI("OUT",type,dh,client,doc,epis,table)) q:table=""  d
 .....s vtsRID="" f  s vtsRID=$o(^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID)) q:vtsRID=""  d
 ......m ^TMP($zn,$j,dh,client,doc,epis,table,vtsRID)=^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID)
 ......k ^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID)
 l -^TEDI("OUT",type)
 ; check confidentiality
 s dh="" f  s dh=$o(^TMP($zn,$j,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TMP($zn,$j,dh,client)) q:client=""  d
 ..s doc="" f  s doc=$o(^TMP($zn,$j,dh,client,doc)) q:doc=""  d
 ...s epis="" f  s epis=$o(^TMP($zn,$j,dh,client,doc,epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 ....s vtsRID="" f  s vtsRID=$o(^TMP($zn,$j,dh,client,doc,epis,46,vtsRID)) q:vtsRID=""  d
 .....s conf=$$conf^LVBVISTS(vtsRID)
 .....i " Y P "[(" "_conf_" "),CFLAB(131)'="Y" k ^TMP($zn,$j,dh,client,doc,epis,46,vtsRID)
 ; check Visit Action
 s table=46,dh="" f  s dh=$o(^TMP($zn,$j,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TMP($zn,$j,dh,client)) q:client=""  d
 ..s doc="" f  s doc=$o(^TMP($zn,$j,dh,client,doc)) q:doc=""  d
 ...s ediType=$s(dh="DR":$$seldata^LVBCTDR(doc,35),dh="HOSP":$$seldata^LVBCTHOS(doc,22),1:"")
 ...i ediType="" s ediType=CFLAB(54)
 ...s epis="" f  s epis=$o(^TMP($zn,$j,dh,client,doc,epis)) q:epis=""  s stop="" d
 ....i 'stop,$$seldata^LVBCTVA($$seldata^LVBEPVIS(epis,25),5)="Y" s stop=1
 ....i 'stop d
 .....i $$open^LVBVISVA(epis)
 .....s stop="" f  q:$$fetch^LVBVISVA(epis)  i $p(PLIST(2),$c(1),4)="Y" s stop=1 q
 .....i $$close^LVBVISVA()
 ....i 'stop,ediType="F" d
 .....s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d  q:stop
 ......s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d  q:stop
 .......i '$$printable^LVBCTTS(ts,epis_"||"_ts_"||"_tscnt) q
 .......i $$status^LVBVIS1(epis_"||"_ts_"||"_tscnt)'="A" s stop=1
 ....i stop d
 .....s vtsRID="" f  s vtsRID=$o(^TMP($zn,$j,dh,client,doc,epis,table,vtsRID)) q:vtsRID=""  d
 ......s seq="" f  s seq=$o(^TMP($zn,$j,dh,client,doc,epis,table,vtsRID,seq)) q:seq=""  d
 .......m ^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID,seq)=^TMP($zn,$j,dh,client,doc,epis,table,vtsRID,seq)
 .......k ^TMP($zn,$j,dh,client,doc,epis,table,vtsRID,seq)
 s dh="" f  s dh=$o(^TMP($zn,$j,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TMP($zn,$j,dh,client)) q:client=""  d
 ..i $d(^TMP($zn,$j,dh,client))<10 k ^TMP($zn,$j,dh,client) q
 ..i type="HL7CRS" d
 ...s fileno=$o(^TMP($zn,$j,"OUT-DATA",type,client,""),-1)+1
 ...k ^TMP($zn,$j,"OUT-DATA",type,client,fileno)
 ..s doc="" f  s doc=$o(^TMP($zn,$j,dh,client,doc)) q:doc=""  d
 ...k toDOC,HOSP
 ...i dh="DR" d
 ....i doc'["NC-",'$$select^LVBCTDR(doc,"Y") m toDOC=PLIST q
 ....i doc["NC-" s (toDOC(2),toDOC(3))=$p(doc,"-",2),(toDOC(4),toDOC(18),toDOC(26))=""
 ...i dh="HOSP",'$$select^LVBCTHOS(doc,"Y") m HOSP=PLIST
 ...s epis="" f  s epis=$o(^TMP($zn,$j,dh,client,doc,epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 ....s vtsRID="" f  s vtsRID=$o(^TMP($zn,$j,dh,client,doc,epis,46,vtsRID)) q:vtsRID=""  d
 .....i '$$printable^LVBCTTS($p(vtsRID,"||",2),vtsRID) k ^TMP($zn,$j,dh,client,doc,epis,46,vtsRID) q
 .....s seq="" f  s seq=$o(^TMP($zn,$j,dh,client,doc,epis,46,vtsRID,seq)) q:seq=""  d
 ......s status=$p(^(seq),"\",3),test=$p(vtsRID,"||",2)
 ......; do not include unauthorised tests
 ......i '((status="Auth")!(status="Print")) q
 ...... ; do not include deleted tests still in queue
 ......i $$select^LVBVISTS(vtsRID) q
 ......i type="TEP" d
 .......s fileno=$o(^TMP($zn,$j,"OUT-DATA",type,client,""),-1)+1
 .......k ^TMP($zn,$j,"OUT-DATA",type,client,fileno)
 ......d SingleTest(vtsRID,status)
 ......s ^TMP($zn,$j,"OUT-DATA",type,client,fileno,"EPIS",epis,test)=doc
 ......i type="TEP",$d(^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ")) d
 .......s xfile=$g(^TEDI("OUT",type,dh,client))+1,^TEDI("OUT",type,dh,client)=xfile
 .......l +^TEDI("OUT-DATA",type,client,xfile)
 .......m ^TEDI("OUT-DATA",type,client,xfile)=^TMP($zn,$j,"OUT-DATA",type,client,fileno)
 .......l -^TEDI("OUT-DATA",type,client,xfile)
 .......k ^TMP($zn,$j,"OUT-DATA",type,client,fileno)
 ..i type="HL7CRS",$d(^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ")) d
 ...s xfile=$g(^TEDI("OUT",type,dh,client))+1,^TEDI("OUT",type,dh,client)=xfile
 ...l +^TEDI("OUT-DATA",type,client,xfile)
 ...m ^TEDI("OUT-DATA",type,client,xfile)=^TMP($zn,$j,"OUT-DATA",type,client,fileno)
 ...l -^TEDI("OUT-DATA",type,client,xfile)
 ...k ^TMP($zn,$j,"OUT-DATA",type,client,fileno)
 k ^TMP($zn,$j) l -^TMPclean
 q
 ; status : Auth   = Authorised Test
 ;        : Print  = Retransmit
 ;
SingleTest(vtsRID,status) ; build packet strings
 s uniseq=$$new(),environ="L" k OBR,OBX ; live system
 k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 k VISTS i '$$select^LVBVISTS(vtsRID) m VISTS=PLIST
 k CTTS i '$$select^LVBCTTS(test,"Y") m CTTS=PLIST
 k DOC i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") m DOC=PLIST
 ; extract copy-to doctors
 s (copyto,copycnt)=""
 s x="" f  s x=$o(^TEPI(epis,5,x)) q:x=""  d  q:copycnt=5
 .s copy=$p(^TEPI(epis,5,x),"\",1) i $l(copy),'$$select^LVBCTDR(copy,"Y") d  q
 ..s copyto=copyto_$s($l(copyto):"~",1:"")_PLIST(18)_"^"_PLIST(3)_"^"_PLIST(4)_"^^^"_PLIST(26)
 ..s copycnt=copycnt+1
 .s copy=$p(^TEPI(epis,5,x),"\",15) i $l(copy),'$$select^LVBCTHOS(copy,"Y") d  q
 ..s copyto=copyto_$s($l(copyto):"~",1:"")_"^"_PLIST(3)_"^^^^"
 ..s copycnt=copycnt+1
 .s copy=$p(^TEPI(epis,5,x),"\",2) i $l(copy) d
 ..s copyto=copyto_$s($l(copyto):"~",1:"")_"^"_copy_"^^^^"
 ..s copycnt=copycnt+1
 s msh(3)=labname
 s msh(6)=$$dt(+$h,$p($h,",",2))
 s msh(9)=uniseq
 s msh(10)=environ
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="MSH|^~\&|LABTRAK|"_msh(3)_"|PATH_RESULTS||"_msh(6)_"||ORU^R01|"_msh(9)_"|"_msh(10)_"|2.3|||AL|NE"
 s pid(2)=$p(VISIT(20),$c(1))_"^^^^MRLT"
 s pid(3)=$p(VISIT(20),$c(1))
 i dh="DR" s pid(4)=$p(VISIT(16),$c(1)) i pid(4)="" s pid(4)=$p(VISIT(20),$c(1))
 i dh="HOSP" s pid(4)=$p(VISIT(65),",",2) i pid(4)="" s pid(4)=$p(VISIT(20),$c(1))
 s pid(5)=$p(VISIT(3),$c(1))_"^"_$p(VISIT(4),$c(1))_"^^^"_$p(VISIT(7),$c(1),2)
 s pid(7)=$$dt(VISIT(6))
 s pid(8)=$p(VISIT(5),$c(1))
 s pid(11)=$p(VISIT(32),$c(1))_"^"_$p(VISIT(33),$c(1))_"^"_$p(VISIT(34),$c(1))_"^^"_$p(VISIT(36),$c(1))
 s pid(13)=$p(VISIT(43),$c(1))
 s pid(14)=$p(VISIT(44),$c(1))
 s pid(19)=$p(VISIT(8),$c(1))
 s pid(27)=$p(VISIT(80),$c(1))
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="PID||"_pid(2)_"|"_pid(3)_"|"_pid(4)_"|"_pid(5)_"||"_pid(7)_"|"_pid(8)_"|||"_pid(11)_"||"_pid(13)_"|"_pid(14)_"|||||"_pid(19)_"||||||||"_pid(27)
 s pv1(2)="*"
 s pv1(8)=DOC(18)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)
 i dh="DR" s pv1(9)=toDOC(18)_"^"_toDOC(3)_"^"_toDOC(4)_"^"_toDOC(49)_"^"_toDOC(50)_"^"_toDOC(26)
 i dh="HOSP" s pv1(9)=HOSP(2)_"^"_HOSP(3)
 s pv1(19)=$p(VISIT(65),",",1)
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="PV1||"_pv1(2)_"||||||"_pv1(8)_"|"_pv1(9)_"||||||||||"_pv1(19)
 s orc(1)="OK"
 s orc(2)=$p(vtsRID,"||")
 s orc(4)=$p(vtsRID,"||")
 s orc(5)="IP"
 s orc(9)=$$dt(+$h,$p($h,",",2))
 s orc(12)=DOC(18)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)
 s seq=$o(^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",seq)="ORC|"_orc(1)_"|"_orc(2)_"||"_orc(4)_"|"_orc(5)_"||||"_orc(9)_"|||"_orc(12)
 ; check confidentiality
 s conf=$$conf^LVBVISTS(vtsRID)
 ; build authorised result packet
 i " Y P "[(" "_conf_" ") k PLIST s PLIST=1,PLIST(1)=vtsRID_"||T"_$c(1)_"T"_$c(1)_"Confidential result. Please call pathology." m visres=PLIST
 e  k visres i '$$results^LVBVISTS(vtsRID) m visres=PLIST
 k FORMAT i '$$getit^LVBCTTSF(,vtsRID,"Y") m FORMAT=PLIST
 ; remove nonreportable items
 s i="" f  s i=$o(visres(i)) q:i=""  i $p(visres(i),$c(1),22)="Y" k visres(i)
 s i="" f  s i=$o(visres(i)) q:i=""  s tc=$p(visres(i),$c(1),2) d
 .s j="" f  s j=$o(FORMAT(j)) q:j=""  i tc=$p(FORMAT(j),$c(2),10) d  q
 ..i $p(FORMAT(j),$c(2),15)'="Y" k visres(i)
 k PLIST s OBR=$g(OBR)+1,OBX=0
 s obr(1)=OBR
 s obr(3)=$p(vtsRID,"||")
 s obr(4)=CTTS(4)_"^"_CTTS(3)_"^L" i $l(CTTS(34)) s obr(4)=CTTS(34)_"^"_$s($l(CTTS(36)):CTTS(36),1:CTTS(35))_"^"_CFLAB(122)
 s obr(7)=$$dt(VISIT(12),+VISIT(13))
 s obr(12)=CTTS(22)
 s obr(13)=$tr(VISIT(53),"^~\&|","     ")
 s obr(16)=DOC(18)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)
 s obr(20)=copyto
 s obr(22)=$$dt(+$h,$p($h,",",2))
 s obr(25)="F"
 s obr(27)="^^^"_$$dt(VISIT(14),+VISIT(111))
 s obr(28)=copyto
 s seq=$o(^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",seq)="OBR|"_obr(1)_"||"_obr(3)_"|"_obr(4)_"|||"_obr(7)_"|||||"_obr(12)_"|"_obr(13)_"|||"_obr(16)_"||||"_obr(20)_"||"_obr(22)_"|||"_obr(25)_"||"_obr(27)_"|"_obr(28)
 ; always compile non cumulative formatted result
 k SingRes,CumRes,AllRes,CompRes
 s report="" i '$$SINGLE^PRTDR00(vtsRID,"E-HL7CRS","") d
 .d adjust(.PLIST) i PLIST d
 ..f j=1:1:PLIST s PLIST(j+.5)="!"
 ..m SingRes=PLIST k PLIST s report="Y"
 ; compile cumulative format
 s format=""
 i $p(CTTS(7),$c(1))'="D" s format=$s($p(CTTS(7),$c(1))="F":"C",CTTS(7)="Y":"C",1:"")
 i $p(CTTS(7),$c(1))="D" s format=$s((dh="DR")&($g(toDOC(34))'="N"):"C",(dh="HOSP")&($g(HOSP(15))'="N"):"C",1:"")
 i format="C" d
 .k ^TMP($zn,$j,"EpisodeList")
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=$g(VISIT(j))
 .s visit=$tr(visit,"""","'")
 .d CumList^PRTDR00($p(vtsRID,"||",2),$p(vtsRID,"||",1),visit,$s(dh="DR":$g(toDOC(34)),dh="HOSP":$g(HOSP(15)),1:""))
 .m ^TMP($zn,$j,"EpisodeList")=PLIST k PLIST
 .s (col,EpisodeList)="" f  s col=$o(^TMP($zn,$j,"EpisodeList",col)) q:col=""  s EpisodeList=$s($l(EpisodeList):EpisodeList_",",1:"")_^(col)
 .k ^TMP($zn,$j,"EpisodeList")
 .i '$$SINGLE^PRTDR00(vtsRID,"E-HL7CRS","C",EpisodeList) d
 ..d adjust(.PLIST) i PLIST f j=1:1:PLIST s PLIST(j+.5)="!",report="Y"
 ..d adjust(.HEADER) i HEADER f j=1:1:HEADER s HEADER(j+.5)="!",report="Y"
 ..i PLIST m CumRes=PLIST k PLIST
 i report'="Y",'$$SINGLE^PRTDR00(vtsRID,"E-HL7CRS","") d
 .d adjust(.PLIST) i PLIST d
 ..f j=1:1:PLIST s PLIST(j+.5)="!"
 ..m SingRes=PLIST k PLIST
 ; merge single result and cumulative results together
 s cnt="" f  s cnt=$o(SingRes(cnt)) q:cnt=""  s AllRes(cnt)=SingRes(cnt)
 i $d(SingRes) f j=1:1:2 s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)="!"
 s cnt="" f  s cnt=$o(HEADER(cnt)) q:cnt=""  s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)=HEADER(cnt)
 s cnt="" f  s cnt=$o(CumRes(cnt)) q:cnt=""  s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)=CumRes(cnt)
 ; compile for sending
 s (cnt,count,text,textx)="",string="~",i="" f  s i=$o(AllRes(i)) q:i=""  d
 .i $e(AllRes(i))="~" q
 .s text=AllRes(i) i text=$c(13) q
 .i ($e(text)="!")!($e(text)="#") d  q
 ..i textx'="" s cnt=cnt+1,CompRes(cnt)=textx
 ..s cnt=cnt+1,CompRes(cnt)="!",textx="",textx=$e($tr(text,"~","^"),2,$l(text))
 .s text=$tr(text,"~","^")
 .s len=$l(text) f x=1:1 q:$e(text,x)'=" "
 .f j=x:1:len s $e(textx,j)=$e(text,j)
 s string="~" f j=1:1 q:'$d(CompRes(j))  d
 .i CompRes(j)="!" s string=string_"\.br\" q
 .s string=string_CompRes(j)
 s str="" f j=1:1:$l(string,"^") s str=str_$p(string,"^",j)_"\S\"
 s string=$p(str,"\S\",1,$l(str,"\S\")-1)
 s OBX=OBX+1
 s obx(1)=OBX
 s obx(2)="FT"
 s obx(3)="REPORT^^LN"
 s obx(5)=string
 s seq=$o(^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",seq)="OBX|"_obx(1)_"|"_obx(2)_"|"_obx(3)_"||"_obx(5)_"|||"
 s i="" f  s i=$o(visres(i)) q:i=""  d
 .s tc=$p(visres(i),$c(1),2)
 .s res=$p(visres(i),$c(1),3)
 .s comm=$p(visres(i),$c(1),4)
 .s flag=$p(visres(i),$c(1),5)
 .k CTTC i '$$select^LVBCTTC(tc,"Y") m CTTC=PLIST k PLIST
 .s xx1="" i $e(CTTC(5),1)="N" d
 ..s xx=$$ranges^LVBVIS1(tc,VISIT(26),$p(VISIT(5),$c(1)),,"0",$s($l(VISTS(7)):VISTS(7),1:+$h),VISIT(98),VISIT(22),,VISIT(64)_$c(1)_$p(VISIT(60),$c(1))_$c(1)_$p(VISIT(167),$c(1)))
 ..s x1=$p(xx,$c(1)),x2=$p(xx,$c(1),2),xx1=x1_"-"_x2 s:xx1="-" xx1=""
 .i $e(CTTC(5),1)="S",'$$select^LVBCTTCT(tc_"||"_res) s res=PLIST(3) d
 ..f  q:res'["|"  s res=$p(res,"|")_" "_$p(res,"|",2,$l(res,"|"))
 ..i $e(res)'=" " s res=" "_res
 .i $l(res) d
 ..s seq=$o(^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",""),-1)+1,OBX=OBX+1
 ..s obx(1)=OBX
 ..s obx(2)="NM"
 ..s obx(3)=CTTC(14)_"^"_CTTC(3)_"^L" i $l(CTTC(36)) s obx(3)=CTTC(36)_"^"_$s($l(CTTC(38)):CTTC(38),1:CTTC(37))_"^"_CFLAB(123)
 ..s obx(4)=$tr(CTTC(4),$c(16),"^")
 ..s obx(5)=xx1
 ..s obx(6)=$tr(flag,"LH","-+")
 ..s ^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",seq)="OBX|"_obx(1)_"|"_obx(2)_"|"_obx(3)_"|"_obx(4)_"|"_obx(5)_"|"_obx(6)_"||"
 .i $l(comm) d
 ..k PLIST
 ..s res=comm f ii=1:1:$l(res,"|") s PLIST(ii)=ii_$c(1)_tc_$c(1)_$p(res,"|",ii)
 ..s PLIST=$o(PLIST(""),-1)
 ..i '$$COMMENTS^LVBCOM01("",$p(vtsRID,"||",2))
 ..s res="" f ii=1:1:PLIST s res=res_$s($l(res):"\.br\",1:"")_PLIST(ii)
 ..f  q:res'["|"  s res=$p(res,"|")_"\.br\"_$p(PLIST(3),"|",2,$l(res,"|"))
 ..i $e(res,1,5)'="\.br\" s res="\.br\"_res
 ..s seq=$o(^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",""),-1)+1,OBX=OBX+1
 ..s obx(1)=OBX
 ..s obx(2)="TX"
 ..s obx(3)=CTTC(14)_"^"_CTTC(3)_"^L" i $l(CTTC(36)) s obx(3)=CTTC(36)_"^"_$s($l(CTTC(38)):CTTC(38),1:CTTC(37))_"^"_CFLAB(123)
 ..s obx(5)=$tr(res,$c(18),"!")
 ..s ^TMP($zn,$j,"OUT-DATA",type,client,fileno,"SEQ",seq)="OBX|"_obx(1)_"|"_obx(2)_"|"_obx(3)_"||"_obx(5)_"|||"
SingleEnd q
dt(date,time) n (date,time) s date=$g(date),time=$g(time),result=""
 i date?5N s result=$tr($zd(date,3),"-","") i $l(time),time?1.5N s result=result_$tr($zt(time,2),":")
 q result
 ; create new sequence number for MSH record
new() l +^CF(dh_"-SEQ")
 s num=$g(^CF(dh_"-SEQ"))+1,^CF(dh_"-SEQ")=num
 l -^CF(dh_"-SEQ")
 q num
 ; adjust into one line
adjust(ARRAY) k array s x2=1,boldon=0
 s x1="" f  s x1=$o(ARRAY(x1)) q:x1=""  d
 . ; s ARRAY(x1)=$s(ARRAY(x1)="~RES-ABNORMAL":"",ARRAY(x1)="~BOLD":"",ARRAY(x1)="~NORMAL":"",1:ARRAY(x1))
 .i ARRAY(x1)=$c(13) q
 .i ARRAY(x1)="!" s x2=x2+1 q
 . ; check for 'hl7' bold
 .i $e(ARRAY(x1))="~" d  q
 ..i " ~RES-ABNORMAL ~BOLD "[(" "_ARRAY(x1)_" "),'boldon s boldon=1 s array(x2)=$g(array(x2))_"\H\" q
 ..i " ~NORMAL "[(" "_ARRAY(x1)_" "),boldon s boldon=0 s array(x2)=$g(array(x2))_"\N\" q
 .i $e(ARRAY(x1))="?" d  q
 .. ; if 'hl7' bold exists in line add 3 chars for each one to keep correct positioning
 ..s add=0 i $g(array(x2))'="" d
 ...s bold=$l(array(x2),"\H\")-1,add=add+(3*bold)
 ...s bold=$l(array(x2),"\N\")-1,add=add+(3*bold)
 ..s tab=+$e(ARRAY(x1),2,4),tab=tab+add,xx=$e(ARRAY(x1),5,$l(ARRAY(x1)))
 ..i tab>$l($g(array(x2))) s array(x2)=$e($g(array(x2))_$j("",tab),1,tab)
 ..i $l(xx) s array(x2)=$g(array(x2))_xx
 .s array(x2)=$g(array(x2))_ARRAY(x1)
 k ARRAY m ARRAY=array s ARRAY=$o(ARRAY(""),-1)
 f j=1:1:ARRAY i '$d(ARRAY(j)) s ARRAY(j)=""
 q
]]></Routine>
</Export>
