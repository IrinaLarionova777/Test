<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCLNVITROESP01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTCLNVITROESP01        ;Rutina de exportación de datos para Vitro
 q
 ; Feb 22, 2006 by Mc_Fly - TrakHealth BCN
 ;
 ; Parameters :  1. From Date
 ;               2. To Date
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Exportación de datos a Vitro"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s fdat=+PAR1,tdat=+PAR2
 i '$d(fdat) s fdat=+$h
 i '$d(tdat) s tdat=+$h
 i $$select^MVBCFSM(1)
 s file=PLIST(35)_"\Vitro\Urgencias\VITRO"_$tr($zdt($h,8)," :")_".txt" 
 o file:"WNS":20 e  d add("No se puede abrir la ruta "_file)
 u file
 s (mat,lotord,mach,tc,level,lote,run)="",swline=0,tot=0
 f  s mat=$o(^TTQC(mat))  q:mat=""  d
 . i $e(mat,1,2)'="LU" q
 . f  s lotord=$o(^TTQC(mat,"LOT",lotord))  q:lotord=""  d
 .. s lotnum=$p(^TTQC(mat,"LOT",lotord),"\",1)
 .. f  s mach=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach))  q:mach=""  d
 ... f  s tc=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc))  q:tc=""  d
 .... f  s level=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level))  q:level=""  d 
 ..... s dat=fdat-1
 ..... f  s dat=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat)) q:dat=""  q:dat>tdat  d
 ...... f  s run=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat,run))  q:run=""  d 
 ....... i '$d(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat,run)) q
 ....... s reg=^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat,run)
 ....... i swline w !
 ....... w lotnum_"|"_level_"|"_mach_"|"_tc_"|"_$zd(dat)_"|"_$zt($p(reg,"\",4))_"|"_$p(reg,"\"),"|"
 ....... s swline=1,tot=tot+1
 c file
 d add("Se ha creado el fichero "_file)
 d add("Se han traspasado "_tot_" controles")
 d add("Copiando en fichero al ordenador remoto")
 s cmd="copy /Y  """_file_""" \\gasomet06\VITRO\"
 s old=$zu(68,40,1)
 o cmd:"Q":10 e  d add("No se ha podido copiar al ordenador remoto") q
 u cmd f  u cmd r line q:$zeof=-1  d add(line)
 c cmd
 q
add(txt) 
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",txt)
 q
]]></Routine>
</Export>
