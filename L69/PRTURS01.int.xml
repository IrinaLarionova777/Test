<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTURS01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTURS01 ; ML 23/10/2000 ; UNITS REVERTED TO STOCK
 q
 ; Parameters :  1. Site
 ;               2. Date From
 ;               3. Date To
 ;               4. Reverted Transaction Codes
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Units Reverted to Stock"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s(PAR1="":"All Sites",1:$p(^TTAB("USLOC",PAR1),"^"))
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR2,PAR3),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 f i=1:1:$l(PAR4,",") q:i=""  i '$$select^LVBBBTR($p(PAR4,",",i)) s ^TMP($zn,$j,"TR-Code",$p(PAR4,",",i))=PLIST(3)
 s date=PAR3+1 f  s date=$o(^TBBPi("TR-Date",date),-1) q:date=""  q:date<PAR2  d
 .s trans="" f  s trans=$o(^TBBPi("TR-Date",date,trans)) q:trans=""  d
 ..q:","_PAR4_","'[(","_trans_",")
 ..s PackID="" f  s PackID=$o(^TBBPi("TR-Date",date,trans,PackID)) q:PackID=""  d
 ...s SecondID="" f  s SecondID=$o(^TBBPi("TR-Date",date,trans,PackID,SecondID)) q:SecondID=""  d
 ....q:$d(^TMP($zn,$j,"PackID",PackID,SecondID))
 ....s TransCnt="" f  s TransCnt=$o(^TBBPi("TR-Date",date,trans,PackID,SecondID,TransCnt),-1) q:TransCnt=""  i '$$select^LVBBBPT(PackID_"||"_SecondID_"||"_TransCnt),PLIST(13)'="Y" q
 ....i TransCnt d
 .....s LastTran="" f  s LastTran=$o(^TBBP(PackID,SecondID,"TRAN",LastTran),-1) q:LastTran=""  i '$$select^LVBBBPT(PackID_"||"_SecondID_"||"_LastTran),PLIST(13)'="Y" q
 .....i LastTran=TransCnt d
 ......q:$$select^LVBBBPT(PackID_"||"_SecondID_"||"_TransCnt)  s status=$p(PLIST(10),$c(1),2)
 ......q:$$select^LVBBBP(PackID_"||"_SecondID)  m PackDet=PLIST k PLIST
 ......s location=$p(PackDet(11),$c(1),2),site=$p(PackDet(11),$c(1),3),product=$p(PackDet(8),$c(1)),BloodGrp=$p(PackDet(9),$c(1)),DateColl=$p(PackDet(4),$c(1)),DateExp=$p(PackDet(13),$c(1))
 ......i site="" s site=" "
 ......i PAR1'="",PAR1'=site q
 ......s ^TMP($zn,$j,"PackID",PackID,SecondID)="",^TMP($zn,$j,"PackDet",site,PackID,SecondID)=product_"\"_BloodGrp_"\"_DateColl_"\"_DateExp_"\"_^TMP($zn,$j,"TR-Code",trans)
 q
PRINT ; print report
 s site="" f  s site=$o(^TMP($zn,$j,"PackDet",site)) q:site=""  d
 .s xxx(1)=site,PackID="" f  s PackID=$o(^TMP($zn,$j,"PackDet",site,PackID)) q:PackID=""  d
 ..s SecondID="" f  s SecondID=$o(^TMP($zn,$j,"PackDet",site,PackID,SecondID)) q:SecondID=""  d
 ...s PackDet=^(SecondID),xxx(2)=PackID_" "_SecondID
 ...s xxx(3)=$p(PackDet,"\",1),xxx(4)=$p(PackDet,"\",2),xxx(5)=$p(PackDet,"\",3),xxx(6)=$p(PackDet,"\",4),xxx(7)=$p(PackDet,"\",5)
 ...s xxx(5)=$$extdate2^SSUTIL4(xxx(5)),xxx(6)=$$extdate2^SSUTIL4(xxx(6))
 ...d PRINT^PRTREP00(,.N)
 ...k xxx f i=1:1:10 s xxx(i)=""
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="URS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
