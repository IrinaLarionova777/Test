<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF439" type="INT" languagemode="0" generated="1"><![CDATA[
MIF439(mi) ; IG 4/6/98 ; ASTM protocol  -  Dade - BEP2000
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),lf=$c(10),cr=$c(13),(result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  d  q
 .i " L "[(" "_par9_" ") d loadlist^MIF000(mi) i $d(^TMIF(mi,10)) d Send
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK
 .s stop=0 f  r *R:10 q:'$t  d  q:stop
 ..i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") s stop=1 q
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",lf) q:'$l(record)
 ..s record=$e(record,1,$l(record)-1)
 ..d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
 .. ; check record validity
 ..s chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
 ..i $$CHSUM(chrecord)'=chsum d  q
 ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 ..d ACK
 .. ; query record
 ..i $e(record,2)="Q" d  q
 ...n (record) f j=1:1:$l($p(record,"|",3),"\") s epis=$tr($p($p($p(record,"|",3),"\",j),"^",2)," ") d
 ....i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="" q
 ....s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
 ....i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix q
 .. ; header record
 ..i $e(record,2)="H" d Last q
 .. ; patient record
 ..i $e(record,2)="P" d Last s surname=$tr($p($p(record,"|",6),"^",1)," ") q
 .. ; order record
 ..i $e(record,2)="O" d  q
 ...s epis=$tr($p($p(record,"|",3),"^",1)," ")
 .. ; result record
 ..i $e(record,2)="R" d  q
 ...s x1=$tr($p($p(record,"|",3),"^",4)," "),x2=$tr($p(record,"|",4)," ")
 ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par11)
 ... ; date and time
 ...i $l($tr($p(record,"|",13)," ")) d
 ....s date=$e($tr($p(record,"|",13)," "),1,8)
 ....s date=$e(date,5,6)_"/"_$e(date,7,8)_"/"_$e(date,1,4)
 ....s date=$$intdate^SSUTIL4(date)
 ....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
 .. ; last record
 ..i $e(record,2)="L" d Last q
 .d ORDER
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)=""
 q
ORDER ; create list of orders if exists
 s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  s suffix=$p(^(epis),"|") d
 .k ^TMP("MIF-SINGLE",$j,mi)
 .d scanone^MIF000(mi,epis,,,suffix)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="H|\^&|||LABTRAK||||||||1"
 .d PATDET(epis)
 .s (tc,tsstr)="" f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis_suffix,tc)) q:tc=""  s tsstr=tsstr_$s($l(tsstr):"\",1:"")_"^^^"_tc
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_suffix_"||"_tsstr_"|||||||N||||||||||||||Q"
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
 .k ^TMP("MIF-SINGLE",$j,mi)
 .k ^TMP($zn,$j,"ENQ",epis)
 .d Send
 q
SAVE(mi) n (mi) ; create loadlist records
 s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s suffix=^TMP("MIF000",$j,"LL","SUFFIX"),suffix=$p(suffix,$c(1),1)
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="H|\^&|||LABTRAK||||||||1"
 d PATDET(epis)
 s (tc,tsstr)="" f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  s tsstr=tsstr_$s($l(tsstr):"\",1:"")_"^^^"_tcx
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_suffix_"||"_tsstr_"|||||||N||||||||||||||O"
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
 q 0
PATDET(epis) ; set patient details record
 k PLIST s sex="U" i '$$select^LVBEPVIS(epis,"Y") d
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"|||"_sex_"|||||"_$g(PLIST(15))
 q
Send ; send list of orders if exists
 w enq,*-3 f j=1:1:10 r *R:1 i ($c(R)=ack)!($c(R)=enq) q
 i $c(R)=ack d
 .s x="" f FRAME=1:1 s x=$o(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 .i x="" k ^TMIF(mi,10)
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 q
]]></Routine>
</Export>
