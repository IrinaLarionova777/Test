<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR317" type="INT" languagemode="0"><![CDATA[
PRTDR317 ; ML 24/10/00 ; Doctors Reports printing (cumulative) - Russell, de Wit, Tiley
 ;
 ; supported calls :
 ;
 ; print^PRTDR317(vtsRID,type,EpisodeList,DH)
 ;
 ; type          : P - print
 ;                 V - view from Enquiry
 ;                   - view from Entry
print(vtsRID,type,EpisodeList,DH,group,cOrder) n (vtsRID,type,EpisodeList,DH,group,cOrder,tsLM,tsWD,GroupName) 
 s vtsRID=$g(vtsRID),type=$g(type),EpisodeList=$g(EpisodeList),DH=$g(DH),group=$g(group),cOrder=$g(cOrder)
 s epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2),max=4,ColWidth=12,ColLeft=22,FullYear="N",tsLM=$g(tsLM),tsWD=$g(tsWD),ExcludeList=""
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-COMMENTS")
 i '$$select^LVBEPVIS(epis) d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s range1="(",range2=")",(flaglow,flaghigh,Reportable)="" i '$$select^MVBCFLAB(1) d
 .i $l(PLIST(18)) s flaglow=PLIST(18)
 .i $l(PLIST(19)) s flaghigh=PLIST(19)
 .i $l(PLIST(20)) s range1=PLIST(20)
 .i $l(PLIST(21)) s range2=PLIST(21)
 .i $l(PLIST(14)) s max=PLIST(14)
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 .i PLIST(37)="Y" s ColWidth=11,FullYear="Y"
 .i $l(PLIST(68)) s Reportable=PLIST(68)
 k FORMAT,TS
 i '$$getit^LVBCTTSF(,vtsRID,"Y") d
 .f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS($p(vtsRID,"||",2),ti)=""
 i $l(group),group'=" " d
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ...i '$$getit^LVBCTTSF(,epis_"||"_ts_"||"_tscnt,"Y") d
 ....f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)=""
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..i '$$getit^LVBCTTSF(ts,,"Y") d
 ...f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)=""
 .s ti="" f cnt=1:1 s ti=$o(FORMAT(ti)) q:ti=""  d
 ..i $g(^TTAB("GR",group,"TI",ti)) s $p(FORMAT(ti),$c(2),27)=^(ti)
 ..e  s $p(FORMAT(ti),$c(2),27)=cnt
 ; create list of episodes and results
 k EPTS i $l(EpisodeList) d
 .f col=1:1:$l(EpisodeList,",") s ep=$p(EpisodeList,",",col) i $l(ep) d
 ..s date=$$seldata^LVBEPVIS(ep,12),time=$$seldata^LVBEPVIS(ep,13)
 ..s time=$tr(time," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 k EPTS i '$l(EpisodeList) d
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=VISIT(j)
 .s TestList=test i $l(group),group'=" " s ts="" f  s ts=$o(TS(ts)) q:ts=""  s TestList=TestList_","_ts
 .d CumList^PRTDR00(TestList,epis,visit,cOrder)
 .f col=1:1:PLIST d
 ..s ep=PLIST(col)
 ..s date=$$seldata^LVBEPVIS(ep,12),time=$$seldata^LVBEPVIS(ep,13)
 ..s time=$tr(time," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 i '$d(^TMP($zn,$j,"CUM-TC")) d  q 100_$c(1)_EpisodeList
 .k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS")
 ; check for units and ranges
 s (UnitRange,units,LowRange,HighRange)=""
 s tc="" f  s tc=$o(FORMAT(tc)) q:tc=""  d
 .s units=$p(FORMAT(tc),$c(2),17)
 .i $e($p(FORMAT(tc),$c(2),22))?1N s LowRange=$j($p(FORMAT(tc),$c(2),22),0,$e($p(FORMAT(tc),$c(2),18),2,4))
 .i $e($p(FORMAT(tc),$c(2),23))?1N s HighRange=$j($p(FORMAT(tc),$c(2),23),0,$e($p(FORMAT(tc),$c(2),18),2,4))
 .i units'="",LowRange'="",HighRange'="",UnitRange="" s UnitRange=1
 ; print cumulative header
 s maxcol=$o(^TMP($zn,$j,"CUM",""),-1) s:maxcol>max maxcol=max
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),"        ","Y")
 d LN^PRTDR00("Y")
 ; print date
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) s BoldOn="" d
 .s xx=$p(^TMP($zn,$j,"CUM",col),$c(1),2)
 .i xx s xx=$$extdate2^SSUTIL4(xx) i FullYear="N" s $p(xx,"/",3)=$e($p(xx,"/",3),3,4)
 .i cOrder'="L",col=1 s BoldOn="Y"
 .i cOrder="L",col=$o(^TMP($zn,$j,"CUM",""),-1) s BoldOn="Y"
 .i BoldOn="Y" d ADD^PRTDR00(,"~BOLD","Y")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .i BoldOn="Y" d ADD^PRTDR00(,"~NORMAL","Y")
 d LN^PRTDR00("Y")
 ; print time
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) s BoldOn="" d
 .s ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .s xx=$$seldata^LVBEPVIS(ep,13)
 .s xx=$tr(xx," "),xx=$s($l(xx):$$exttime^SSUTIL4(xx),1:"N/S")
 .i cOrder'="L",col=1 s BoldOn="Y"
 .i cOrder="L",col=$o(^TMP($zn,$j,"CUM",""),-1) s BoldOn="Y"
 .i BoldOn="Y" d ADD^PRTDR00(,"~BOLD","Y")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .i BoldOn="Y" d ADD^PRTDR00(,"~NORMAL","Y")
 d LN^PRTDR00("Y")
 ; print test set header only for Cum report page
 i $l(EpisodeList) d
 .i '$l(group)!(group=" "),$l(TEST(17)) d
 ..d LN^PRTDR00()
 ..d ADD^PRTDR00(,"~BOLD")
 ..d ADD^PRTDR00(tsLM,$s(TEST(17)="*":TEST(3),1:TEST(17)))
 ..d ADD^PRTDR00(,"~NORMAL")
 .i $l(group),group'=" ",$l(GroupName) d  s GroupName=""
 ..d LN^PRTDR00()
 ..d ADD^PRTDR00(,"~BOLD")
 ..d ADD^PRTDR00(tsLM,GroupName)
 ..d ADD^PRTDR00(,"~NORMAL")
 .d LN^PRTDR00()
 ; print results
 s order="" f  s order=$o(^TMP($zn,$j,"CUM-TC-ORDER",order)) q:order=""  d
 .s tc=^(order)
 .s tcName=$s($l($tr($p(FORMAT(tc),$c(2),24),":")):$tr($p(FORMAT(tc),$c(2),24),":"),1:$tr($p(FORMAT(tc),$c(2),25),":"))
 .d ADD^PRTDR00(tsLM,$e(tcName_$j("",tsWD),1,ColLeft-3)_"  ")
 .f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 ..s result=$p($g(^TMP($zn,$j,"CUM",col,tc)),$c(1),1),flag=$p($g(^TMP($zn,$j,"CUM",col,tc)),$c(1),2)
 ..i result="" d
 ...i $d(^TMP($zn,$j,"CUM-EXIST",col,tc)) d  q
 ....i $p(FORMAT(tc),$c(2),8)="M" s result="TF"
 ....e  s result=""
 ...s result="-"
 .. ; print extra info
 ..i flag="L" d
 ...i "MN"[$e($p(FORMAT(tc),$c(2),18)),$l(flaglow) s result=result_" "_flaglow
 ...d ADD^PRTDR00(,"~BOLD")
 ..i flag="H" d
 ...i "MN"[$e($p(FORMAT(tc),$c(2),18)),$l(flaghigh) s result=result_" "_flaghigh
 ...d ADD^PRTDR00(,"~BOLD")
 ..d ADD^PRTDR00(,$c(13))
 ..d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),result)
 ..i (flag="L")!(flag="H") d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .s units=$p(FORMAT(tc),$c(2),17)
 .d ADD^PRTDR00(tsLM+ColLeft+(col*ColWidth),units)
 .i $l($p(FORMAT(tc),$c(2),22)),$l($p(FORMAT(tc),$c(2),23)) d
 ..i $e($p(FORMAT(tc),$c(2),22))?1N s $p(FORMAT(tc),$c(2),22)=$j($p(FORMAT(tc),$c(2),22),0,$e($p(FORMAT(tc),$c(2),18),2,4))
 ..i $e($p(FORMAT(tc),$c(2),23))?1N s $p(FORMAT(tc),$c(2),23)=$j($p(FORMAT(tc),$c(2),23),0,$e($p(FORMAT(tc),$c(2),18),2,4))
 ..d ADD^PRTDR00(tsLM+ColLeft+((col+1)*ColWidth),range1_$p(FORMAT(tc),$c(2),22)_"-"_$p(FORMAT(tc),$c(2),23)_range2)
 .i $l($p(FORMAT(tc),$c(2),22)),'$l($p(FORMAT(tc),$c(2),23)) d
 ..i $e($p(FORMAT(tc),$c(2),22))?1N s $p(FORMAT(tc),$c(2),22)=$j($j($p(FORMAT(tc),$c(2),22),0,$e($p(FORMAT(tc),$c(2),18),2,4)),$p(FORMAT(tc),$c(2),19)+$l(range1))
 ..d ADD^PRTDR00(tsLM+ColLeft+((col+1)*ColWidth),range1_$p(FORMAT(tc),$c(2),22)_range2)
 .d LN^PRTDR00()
 i $l(EpisodeList),$d(^TMP($zn,$j,"CUM-COMMENTS")) d
 .s col="" f  s col=$o(^TMP($zn,$j,"CUM-COMMENTS",col)) q:col=""  d
 ..d LN^PRTDR00()
 ..f j=1:1 q:'$d(^TMP($zn,$j,"CUM-COMMENTS",col,j))  d
 ...f j1=1:1:$l(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^") d
 ....s xx=$p(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^",j1) i type="P" s xx=$tr(xx,$c(16,17,18),"^\|")
 ....d ADD^PRTDR00(tsLM,xx)
 ...d LN^PRTDR00()
 .d LN^PRTDR00()
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS"),^TMP($zn,$j,"CUM-SET")
 q 0
 ; collect Test Items information
TestItems(col,rowid) n (col,rowid,ExcludeList,type,FORMAT,vtsRID,tsLM,tsWD,ColWidth,Reportable) s col=$g(col),rowid=$g(rowid)
 i '$$select^LVBVISTS(rowid,"Y") d
 .m ^TMP($zn,$j,"CUM-EXIST",col)=TS($p(rowid,"||",2))
 .s status=$$status^LVBVIS1(rowid)
 .s Result=0 i status="A" s Result=1
 .e  i vtsRID=rowid s Result=1
 .e  i $l(type),status="E",Reportable["ENTERED" s Result=1
 .e  i $l(type),status="P",Reportable["PATHOLOGIST" s Result=1
 .i '$$results^LVBVISTS(rowid) f j=1:1:PLIST d
 ..s tc=$p(PLIST(j),$c(1),2),(result,flag)=""
 ..i Result s result=$p(PLIST(j),$c(1),3),flag=$p(PLIST(j),$c(1),5)
 ..i '$d(FORMAT(tc)) q
 .. ; not included in cumulative
 ..i $p(FORMAT(tc),$c(2),20)'="Y" q
 ..i $l(tc),$l(result) d
 ... ; non reportable test item
 ...i $l(type),$p(FORMAT(tc),$c(2),15)'="Y" q
 ...i $e($p(FORMAT(tc),$c(2),18))="N" d IN
 ...i $e($p(FORMAT(tc),$c(2),18))="M" d IM
 ...i $e($p(FORMAT(tc),$c(2),18))="D" d ID^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="T" d IT^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="Y" d IY^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="S" s cnt=tc,resultx=result d IS^PRTDR00 d
 ....i $o(result(""),-1)=1,$l(result(1))'>ColWidth s x=result(1) k result s result=x q
 ....m rescom(tc,resultx)=result s result=resultx
 ...s ^TMP($zn,$j,"CUM",col,tc)=result_$c(1)_flag
 ...i '$d(^TMP($zn,$j,"CUM-TC",tc)) d
 ....s order=$p(FORMAT(tc),$c(2),27) i '$l(order) s order=$p(FORMAT(tc),$c(2),2)
 ....s ^TMP($zn,$j,"CUM-TC-ORDER",order)=tc,^TMP($zn,$j,"CUM-TC",tc)=""
 . ; don't print comments if not most recent episode
 .i $p(rowid,"||",1)'=$p(vtsRID,"||",1) k rescom q
 . ; compile test item standard comments
 .i Result,$d(rescom) d
 ..s ^TMP($zn,$j,"CUM-COMMENTS",col,1)="Std Comm : "
 ..s tc="",b=1 f  s tc=$o(rescom(tc)) q:tc=""  d
 ...s res="" f  s res=$o(rescom(tc,res)) q:res=""  d
 ....s ^TMP($zn,$j,"CUM-COMMENTS",col,1)=^TMP($zn,$j,"CUM-COMMENTS",col,1)_res_$s($o(rescom(tc,res))'="":", ",1:"")
 ....s x="" f  s x=$o(rescom(tc,res,x)) q:x=""  s b=b+1,^TMP($zn,$j,"CUM-COMMENTS",col,b)=rescom(tc,res,x)
 . ; compile test set comments
 .i Result k PLIST i '$$COMMENTS^LVBCOM01(rowid,,type,"N",,"N","Y",.ExcludeList) i PLIST d
 ..f j1=1:1:PLIST s x1=$o(^TMP($zn,$j,"CUM-COMMENTS",col,""),-1)+1,^TMP($zn,$j,"CUM-COMMENTS",col,x1)=PLIST(j1)
 q
IN ; Numeric
IM ; Numeric *1000
 i $l(result),$d(^TTAB("RC",result)) q
 s Flag="" f  q:'$l(result)  q:"<>+-"'[$e(result)  s Flag=Flag_$e(result),result=$e(result,2,$l(result))
 s result=Flag_$j(result,0,$e($p(FORMAT(tc),$c(2),18),2,9))
 q
]]></Routine>
</Export>
