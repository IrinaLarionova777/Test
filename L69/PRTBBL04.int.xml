<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBL04" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBL04 ; ML 15/5/2000 ; BloodBank Labels - Territory Health Services
 q
 ; Parameters :  1. vtsRID
 ;               2. list of packRID
 ;               3. User
 ;               4. R - Blood Group Check
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j),^TMP("PRTDR00",$j),^TMP("REPORT",$j)
 k xHEADER d INIT,BODY,PRINT
 k ^TMP($zn,$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 i $l(PAR3) d
 .i '$$selrow^MVBSSUSR(PAR3,"Y") s PAR3=PAR3_$c(1)_PLIST(3)
 q
BODY ; create report
 i $l(PAR1) d
 .s epis=$p(PAR1,"||",1)
 .k EPVIS i '$$select^LVBEPVIS(epis) m EPVIS=PLIST
 .k DOC i '$$select^LVBCTDR($p(EPVIS(15),$c(1))) m DOC=PLIST
 .k VISTS i '$$select^LVBVISTS(PAR1) m VISTS=PLIST
 .k BBBG1 i '$$select^LVBBBBG($p($g(EPVIS(69)),$c(1))) m BBBG1=PLIST
 .i $$open^LVBVISTE(PAR1)
 .f  q:$$fetch^LVBVISTE(PAR1)  k VISTE m VISTE=PLIST d
 ..i $p(VISTE(4),$c(1))="I" q
 ..i $l(PAR2),(","_PAR2_",")'[(","_$p(PLIST(2),$c(1))_"||"_$p(PLIST(3),$c(1))_",") q
 ..s order=VISTE(13) i '$l(order) s order=" "
 ..s ^TMP($zn,$j,"REPORT",order,VISTE(1))=""
 .i $$close^LVBVISTE()
 q
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s x1="" f  s x1=$o(^TMP($zn,$j,"REPORT",x1)) q:x1=""  d
 ..s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",x1,x2)) q:x2=""  d
 ...i '$$select^LVBVISTE(x2)  k VISTE m VISTE=PLIST d
 ....s rowid=$p(PLIST(2),$c(1))_"||"_$p(PLIST(3),$c(1))
 ....i '$$select^LVBBBP(rowid) k PACK m PACK=PLIST d
 .....k BBBG2 i '$$select^LVBBBBG($p($g(PACK(9)),$c(1))) m BBBG2=PLIST
 .....d LABEL($p(VISTE(5),$c(1)))
 .....k PLIST m PLIST=PACK s PLIST(22)=+$h,PLIST(23)=$p(EPVIS(20),$c(1)) i $$update^LVBBBP(rowid)
 q
 ; print labels with bar-code
LABEL(tran) s tran=$g(tran)
 s lf=$c(10)
 s LABnum=1
 d ADD^SSPRINT(REPID,"PRT","FK""*"""_lf)
 d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_LABnum_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_LABnum_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","Q584,24"_lf)
 d ADD^SSPRINT(REPID,"PRT","R8,8"_lf)
 d ADD^SSPRINT(REPID,"PRT","A72,48,0,4,1,1,N,""* "_$p(PACK(8),$c(1),2)_" *"""_lf)
 d ADD^SSPRINT(REPID,"PRT","B520,48,0,K,2,5,40,N,""a"_$p(PACK(8),$c(1),3)_"b"""_lf)
 ; message for cryoprecipitate and platelets only
 i ($p(PACK(8),$c(1))="CP")!($p(PACK(8),$c(1))="PL") d ADD^SSPRINT(REPID,"PRT","A72,80,0,4,1,1,N,""*** DO NOT REFRIGERATE ***"""_lf)
 i tran="GS" d ADD^SSPRINT(REPID,"PRT","A72,100,0,4,1,1,N,""*** GROUP SPECIFIC ISSUE - NOT XMATCHED ***"""_lf)
 i tran="CL" d ADD^SSPRINT(REPID,"PRT","A72,100,0,4,1,1,N,""*** DIRECT ISSUE - NOT XMATCHED ***"""_lf)
 d ADD^SSPRINT(REPID,"PRT","B414,128,0,K,3,7,40,N,""a"_$p(PACK(2),$c(1))_"a"""_lf)
 d ADD^SSPRINT(REPID,"PRT","A72,144,0,4,1,1,N,""DONOR No : "_$p(PACK(2),$c(1))_""""_lf)
 i $d(BBBG2(3)) d ADD^SSPRINT(REPID,"PRT","A72,176,0,4,1,1,N,""OF GROUP :  "_BBBG2(3)_""""_lf)
 i " GS CL "'[(" "_tran_" ") d ADD^SSPRINT(REPID,"PRT","A72,240,0,4,1,1,N,"""_"APPEARS COMPATIBLE WITH"_" : """_lf)
 i " GS CL "[(" "_tran_" ") d ADD^SSPRINT(REPID,"PRT","A72,240,0,4,1,1,N,"""_"ISSUED TO"_" : """_lf)
 d ADD^SSPRINT(REPID,"PRT","A80,304,0,4,1,1,N,""NAME :  "_$$NameFormat^LVBCOM03("PT",$p(EPVIS(7),$c(1),2),EPVIS(4),EPVIS(3),EPVIS(99),EPVIS(100),EPVIS(101))_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","A80,336,0,4,1,1,N,""HOSPITAL NUMBER :  "_$p(EPVIS(20),$c(1))_""""_lf)
 i $d(BBBG1(3)) d ADD^SSPRINT(REPID,"PRT","A80,368,0,4,1,1,N,""GROUP :  "_BBBG1(3)_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","A80,400,0,4,1,1,N,""DOCTOR :  "_DOC(3)_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","A560,400,0,4,1,1,N,""WARD : "_$e(EPVIS(22),1,3)_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","A80,432,0,4,1,1,N,""EPISODE NUMBER :  "_epis_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","B560,420,0,9,2,5,40,N,"""_epis_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","A72,464,0,4,1,1,N,""SIGNED :"""_lf)
 d ADD^SSPRINT(REPID,"PRT","A536,464,0,4,1,1,N,""DATE : "_$$extdate2^SSUTIL4(+$h)_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","A24,528,0,4,1,1,N,""DO NOT REMOVE THIS LABEL FROM THIS PACK"""_lf)
 d ADD^SSPRINT(REPID,"PRT","FE"_lf)
 d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_LABnum_""""_lf)
 d ADD^SSPRINT(REPID,"PRT","P1"_lf)
 q
]]></Routine>
</Export>
