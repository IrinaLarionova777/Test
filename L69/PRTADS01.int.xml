<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTADS01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTADS01 ; ML 12/10/98 ; AGED DEBTORS LIST - SUMMARY
 q
 ; Parameters :  1. Company
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j) k ^TMPage m ^TMPage=^TAGE
 k xHEADER d INIT
 s line=1,xHEADER(line)="Debtor Listing - Summary"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Company : "_comp
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s comp=$p(^TTAB("CO",PAR1),"\")_"("_PAR1_")"
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s epis="" f  s epis=$o(^TMPage(epis)) q:epis=""  d
 .s sum=$p(^(epis),"\",1),date=$p(^(epis),"\",2),payc=""
 .s diff=$h-date\30 s:diff>6 diff=6 s:date="" diff=8
 .s comp=$$epcomp^LVBVIS1(epis) i $l(PAR1),PAR1'=comp q
 .s payc=$$seldata^LVBEPVIS(epis,9)
 .i '$l(payc) s payc=" "
 .s ^TMP($zn,$j,"REPORT",payc)=$g(^TMP($zn,$j,"REPORT",payc))+sum
 .s ^TMP($zn,$j,"REPORT",payc,diff)=$g(^TMP($zn,$j,"REPORT",payc,diff))+sum
 .s ^TMP($zn,$j,"COL",diff)=$g(^TMP($zn,$j,"COL",diff))+sum
 .i diff=8 s ^TMP($zn,$j,"COL",diff,epis)=""
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s payc="" f  s payc=$o(^TMP($zn,$j,"REPORT",payc)) q:payc=""  d
 ..k xxx s xxx(1)=payc_$c(1)_$p($g(^TTAB("BI",payc)),"\")
 ..f diff=0:1:6,8 d
 ...s xxx(9)=$g(xxx(9))+$g(^TMP($zn,$j,"REPORT",payc,diff))
 ...s xxx(diff+2)=$g(^TMP($zn,$j,"REPORT",payc,diff))
 ..d PRINT^PRTREP00(,.N) k xxx
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .s xxx(1)=$c(1)_"Total"
 .s diff="" f  s diff=$o(^TMP($zn,$j,"COL",diff)) q:diff=""  i ^(diff) d
 ..s xxx(diff+2)=^TMP($zn,$j,"COL",diff)
 ..s xxx(9)=$g(xxx(9))+^TMP($zn,$j,"COL",diff)
 .d PRINT^PRTREP00(,.N) s total=xxx(9) k xxx
 .s xxx(1)=$c(1)_"% of Total"
 .s diff="" f  s diff=$o(^TMP($zn,$j,"COL",diff)) q:diff=""  i ^(diff) d
 ..s xxx(diff+2)=+$j(^TMP($zn,$j,"COL",diff)/total*100,0,2)
 .d PRINT^PRTREP00(,.N) k xxx
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .i $d(^TMP($zn,$j,"COL",8))\10=1 d
 ..s x="" f  s x=$o(N(x)) q:x=""  s N(x,6)=""
 ..s epis="",xxx(1)=$c(1)_"UnAllocated ep" f  d  q:epis=""
 ...f j=2:1:10 s epis=$o(^TMP($zn,$j,"COL",8,epis)) q:epis=""  s xxx(j)=epis
 ...d PRINT^PRTREP00(,.N) k xxx
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="ADS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
