<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LHOSPINTHBO02" type="INT" languagemode="0" generated="1"><![CDATA[
LHOSPINTHBO02 ; IG 12/07/98 ; Hospital Interface - ME - SQUH
 Q
RESULT(HOSP) N (HOSP) k ^TMP($zn,$j)
 i $$select^LVBCTRL(HOSP) q
 s dir=PLIST(7),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.*"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""results.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""results.txt""")
 s $zt="Error1^"_$zn
 s FILE="results.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  u xFILE r MESS s seq=$o(^TMP($zn,$j,xxFILE,"IN",""),-1)+1,^TMP($zn,$j,xxFILE,"IN",seq)=MESS
 s $zt=""
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s seq="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=$tr(^(seq),$c(13)) d
 ..s xx=$$translate^LVBCOM01(xx,">",">"_$c(13))
 ..s xx=$$translate^LVBCOM01(xx,"<",$c(13)_"<")
 ..f j=1:1:$l(xx,$c(13)) i $l($p(xx,$c(13),j)) s x=$o(^TMP($zn,$j,xFile,"IN-0",""),-1)+1,^TMP($zn,$j,xFile,"IN-0",x)=$p(xx,$c(13),j)
 .s (stop,cnt)=0
 .s seq="" f  s seq=$o(^TMP($zn,$j,xFile,"IN-0",seq)) q:seq=""  s xx=$tr(^(seq),$c(13)) d
 ..i xx="<PathologyReport>" s cnt=cnt+1,stop=0
 ..i 'stop,cnt s x=$o(^TMP($zn,$j,xFile,"IN-1",cnt,""),-1)+1,^TMP($zn,$j,xFile,"IN-1",cnt,x)=xx
 ..i xx="</PathologyReport>" s stop=1
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s seq1="" f  s seq1=$o(^TMP($zn,$j,xFile,"IN-1",seq1)) q:seq1=""  d
 ..s cnt=0,test="N"
 ..s seq2="" f  s seq2=$o(^TMP($zn,$j,xFile,"IN-1",seq1,seq2)) q:seq2=""  s xx=^(seq2) d
 ...i xx["<ClinicalResult>" s cnt=cnt+1,test="Y"
 ...i xx["<ClinicalResult " s cnt=cnt+1,test="Y"
 ...i test="Y" s ^TMP($zn,$j,xFile,"IN-2",seq1,cnt,seq2)=xx
 ...i test="N" s ^TMP($zn,$j,xFile,"IN-2",seq1,0,seq2)=xx
 ...i xx["</ClinicalResult" s test="N"
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .k ^TMPimport(xFile)
 .s seq1="" f  s seq1=$o(^TMP($zn,$j,xFile,"IN-2",seq1)) q:seq1=""  d
 ..s epis="",(stop,cnt)=0
 ..s seq3="" f  s seq3=$o(^TMP($zn,$j,xFile,"IN-2",seq1,0,seq3)) q:seq3=""  s xx=^(seq3) d  q:stop
 ...i xx["<LabNo>" s cnt=cnt+1 q
 ...i cnt,'stop,'$l(epis) s epis=xx
 ...i xx["</LabNo>" s stop=1
 ..i $l(epis) d
 ...s seq2="" f  s seq2=$o(^TMP($zn,$j,xFile,"IN-2",seq1,seq2)) q:seq2=""  i seq2 d
 ....s (ts,tscnt,vts)="",done="bad",(stop,cnt)=0
 ....s seq3="" f  s seq3=$o(^TMP($zn,$j,xFile,"IN-2",seq1,seq2,seq3)) q:seq3=""  s xx=^(seq3) d  q:stop
 .....i '$l(ts),xx["<LabTestCode>" s cnt=cnt+1 q
 .....i cnt,'stop,'$l(ts) s ts=xx,ts=$$loinc(ts) i $l(ts) s tscnt=$o(^TEPI(epis,1,ts,""))
 .....i xx["</LabTestCode>" s stop=1
 ....i $l(ts),$l(tscnt) s vts=epis_"||"_ts_"||"_tscnt
 ....i '$l($$seldata^LVBVISTS(vts,1)) s vts=""
 .... ; only file results in RTF test set
 ....i $$seldata^LVBCTTS(ts,31)'="Y" s vts=""
 ....i $l(vts) d
 .....k result
 .....s (report,seq3)="" f  s seq3=$o(^TMP($zn,$j,xFile,"IN-2",seq1,seq2,seq3)) q:seq3=""  s xx=^(seq3) d
 ......i xx="<Comments>" s report="Y" q
 ......i xx="</Comments>" s report="" q
 ......i report="Y" d
 .......i xx["<![CDATA[" s xx=$p(xx,"<![CDATA[",2)
 .......i xx["]]]]><![CDATA[>" s xx=$p(xx,"]]]]><![CDATA[>",1)
 .......s j1=$o(result(""),-1)+1,result(j1)=xx,done="process"
 .....s ^TMPimport(xFile,"index",vts)=seq1_" "_seq2
 .....m ^TMP($zn,$j,xFile,"OUT",vts,"T")=result
 .....k result
 ....m ^TMPimport(xFile,done,seq1)=^TMP($zn,$j,xFile,"IN-2",seq1,0)
 ....m ^TMPimport(xFile,done,seq1)=^TMP($zn,$j,xFile,"IN-2",seq1,seq2)
 ....k ^TMP($zn,$j,xFile,"IN-1",seq1)
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s vts="" f  s vts=$o(^TMP($zn,$j,xFile,"OUT",vts)) q:vts=""  d
 ..s (text,cnt)="" f  s cnt=$o(^TMP($zn,$j,xFile,"OUT",vts,"T",cnt)) q:cnt=""  s text=text_$s($l(text):"|",1:"")_^(cnt)
 ..s epis=$p(vts,"||",1),ts=$p(vts,"||",2),tscnt=$p(vts,"||",3)
 ..i $$vtsLock^SSLOCK("IM",epis,vts,"trak")=-1 d
 ...i $$status^LVBVIS1(vts)="A" d  q
 ....k xx m xx=^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
 ....k ^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
 ....s max=0
 ....f j=1:1 q:'$d(xx(j))  i max<$l(xx(j)) s max=$l(xx(j))
 ....f j=1:1:$l(text,"|") i max<$l($p(text,"|",j)) s max=$l($p(text,"|",j))
 ....s line="",$p(line,"-",max+1)=""
 ....s text=$j("",max-22\2)_"--- Amended Result ---"_"||"_text_"||"_line
 ....f j=1:1:$l(text,"|") s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1)+1,^TEPI(epis,1,ts,tscnt,"DATA","T","REM",x)=$p(text,"|",j)
 ....f j=1:1 q:'$d(xx(j))  s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1)+1,^TEPI(epis,1,ts,tscnt,"DATA","T","REM",x)=xx(j)
 ....s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1),^TEPI(epis,1,ts,tscnt,"DATA","T","REM",0)=x
 ...i $$status^LVBVIS1(vts)'="A" d  q
 ....k PLIST
 ....s PLIST(1)="DATA"_$c(1)_"T"_$c(1)_text
 ....s PLIST=$o(PLIST(""),-1)
 ....i $$TScheck^LVBVIS7(vts,"A","","B")
 ....s (VQ,ok)="" f j=1:1:PLIST d  i ok="N" q
 .....i $e($p(PLIST(j),$c(1)))="E" s ok="N" q
 .....i $e($p(PLIST(j),$c(1)),1,2)="VQ" s VQ=$p(PLIST(j),$c(1),2)
 ....k PLIST s PLIST(1)="T\"_text
 ....s PLIST=$o(PLIST(""),-1)
 ....s err=$$save^LVBVIS4(vts,"A","trak","N")
 ....s ^TMPimport(xFile,"index",vts)=$g(^TMPimport(xFile,"index",vts))_" err"
 ....i 'err d
 .....i '$$select^LVBVISTS(vts,"Y") s PLIST(29)=$$seldata^LVBEPVIS(epis,66) i $$update^LVBVISTS(vts)
 .....k PLIST
 .....s PLIST=$o(PLIST(""),-1)
 .....i $$TScheck^LVBVIS7(vts,"A","","A")
 .....f j=1:1:PLIST d
 ......s val=$p($p(PLIST(j),$c(1),2),$c(2))
 ......i $e($p(PLIST(j),$c(1)),1,2)="VQ",$$VQ^LVBCOM13(vts,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="TA",$$TAR^LVBCOM13($p(vts,"||"),,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SA",$$SA^LVBCOM13($p(vts,"||"),val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SF",$$SF^LVBCOM13(vts,$p(PLIST(j),$c(1),6),val)
 ......i $e($p(PLIST(j),$c(1)),1,3)="ACT",$$ACT^LVBCOM13($p(vts,"||"),$p(val,"-",1),$p(val,"-",2))
 ..i $$vtsLockClr^SSLOCK("IM",epis,vts)
 s (param,xFile)="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  s param=param_$s($l(param):",",1:"")_xFile
 i $l(param),$$START^PRTREP00("","",HOSP_"\"_param,"RLI")
 k ^TMP($zn,$j)
 q
 ; Error log
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
 ; Error log
Error2 i $ze'["ENDOFFILE" d BACK^%ETN s Error=1
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
loinc(loinc) n (loinc)
 s x="" f  s x=$o(^TTAB("TS",x)) q:x=""  i $$seldata^LVBCTTS(x,34)=loinc q
 q x
]]></Routine>
</Export>
