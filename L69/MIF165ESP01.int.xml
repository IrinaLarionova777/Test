<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF165ESP01" type="INT" languagemode="0" generated="1"><![CDATA[
MIF165ESP01(mi) ; Aution MAX AX-4280 ARKRAY ; QC - Yes
 ; This protocol is not the documentation protocol
 ; It is defined at Aution Software
 ; <STX>reg<ETX>
 ;  Oct 26, 2005 by Mc_Fly: QC Added
 ; ----------------------------------
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),soh=$c(1),etb=$c(17),lf=$c(10)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main ;
 f  r *c:10 q:c=-1  q:c=2  d trace^MIF000(mi,"Esperando STX. Despreciado "_c,"H<--M")
 i c=-1 d trace^MIF000(mi,"Salida por timeout","H<--M") q
 d trace^MIF000(mi,"Recibido STX","H<--M")
 s record=""
 f  r *c:10 q:c=-1  q:c=3  s record=record_$c(c)
 i c=-1 d trace^MIF000(mi,"Salida por timeout. Despreciado "_record,"H<--M") q
 d trace^MIF000(mi,"Recibido ETX","H<--M")
 d trace^MIF000(mi,record,"H<--M")
 s (sample,epis,surname,result,date,time,QC)=""
 s epis=$p(record,",",6)
 i $f(" 0001 0002 "," "_epis_" ") s QC="\"_epis
 f i=10:1:$l(record,",") d
 .s val=$p(record,",",i)
 .i val="" q
 .s cod=+$e(val,1)
 .i 'cod q
 .s ch=$p("B0925,B0930,B0935,B0936,B0940,B0941,B0945,B0950,B0951",",",cod)
 .s result=result_ch_$s(result[(ch_$c(par10)):"D",1:"")_$c(par10)_val_$c(par11)
 s record=$p(record,",",7),record=$e(record,18,$l(record))
 f  s rec=$e(record,1,17) q:$l(rec)<16  s record=$e(record,18,$l(record)) d
 . s cod=$tr($e(rec,1,3)," ",""),ress=$tr($e(rec,5,6)," ",""),resn=$tr($e(rec,7,11)," ","")
 . s res=$s(resn="":ress,1:+resn)
 . s result=result_cod_$c(par10)_res_$c(par11)
 i $l(epis),$l(result) d
 . d trace^MIF000(mi,"Guardado "_epis,"H<--M")
 . d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z     
]]></Routine>
</Export>
