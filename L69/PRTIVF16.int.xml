<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTIVF16" type="INT" languagemode="0" generated="1"><![CDATA[
PRTIVF16 ; ML 03/05/2000 ; IVF RESULTS REPORT - INDEPENDENT DIAGNOSTIC SERVICES
  q
  ; Parameters :  1. Dr Code
  ;               2. Date From
  ;               3. Date To
  ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID)
 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialise parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q:$$select^LVBCTDR(PAR1,"Y")  m DOC=PLIST k PLIST
 ; set dr name
 s Title=$g(DOC(26)),Name=$g(DOC(4)),Surname=$g(DOC(3))
 s DrName=$$NameFormat^LVBCOM03("DR",Title,$e(Name),Surname,DOC(49),DOC(50),DOC(51))
 ; set dr address
 i $d(DOC) d
 .k DrAdd s cnt=0 f j=1:1:3 s DrAdd(j)=""
 .i $l(DOC(9)) s cnt=cnt+1,DrAdd(cnt)=DOC(9)
 .i $l(DOC(10)) s cnt=cnt+1,DrAdd(cnt)=DOC(10)
 .s cnt=cnt+1
 .i $l(DOC(11)) s DrAdd(cnt)=DrAdd(cnt)_$s($l(DrAdd(cnt)):", ",1:"")_DOC(11)
 .i $l(DOC(12)) s DrAdd(cnt)=DrAdd(cnt)_$s($l(DrAdd(cnt)):", ",1:"")_DOC(12)
 .i $l(DOC(13)) d
 ..i cnt>1,'$l(DrAdd(cnt)) s cnt=cnt-1
 ..s DrAdd(cnt)=DrAdd(cnt)_$s($l(DrAdd(cnt)):", ",1:"")_DOC(13)
 q
 ; create temporary printing file
BODY s episcnt="",date=PAR2-1 f  s date=$o(^TEPIi("AUTHORISED",date)) q:date=""  q:date>PAR3  d
 .s epis="" f  s epis=$o(^TEPIi("AUTHORISED",date,epis)) q:epis=""  d
 ..q:'$d(^TEPIi("AUTHORISED",date,epis,"N216"))
 ..q:$$select^LVBEPVIS(epis,"Y")  q:PLIST(15)'=PAR1
 ..s num="" f  s num=$o(^TEPIi("AUTHORISED",date,epis,"N216",num)) q:num=""  d
 ...f i="N0190:E2","N0210:PROG","N0200:LH","N0260:bHCG" s ^TMP($zn,$j,date,epis,num,$p(i,":",2))=$g(^TEPI(epis,1,"N216",num,"DATA",$p(i,":")))
 ...s episcnt=$g(episcnt)+1
 s ^TMP($zn,$j)=episcnt
 q
 ; print report
PRINT d QDR
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"(3@"),ADD^PRTREP00(REPID,"HDR",$c(27)_"(s0p12h0s0b5T")
 d ADD^PRTREP00(REPID,"HDR","~BOLD"),ADD^PRTREP00(REPID,"HDR",$c(27)_"&a1000h850V"_DrName)
 d ADD^PRTREP00(REPID,"HDR","~NORMAL"),ADD^PRTREP00(REPID,"HDR","!")
 f i=1:1:3 d ADD^PRTREP00(REPID,"HDR",$c(27)_"&a1000h"_(i*110+850)_"V"_DrAdd(i))
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"&a4400h850V"_$$extdate2^SSUTIL4(PAR2))
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"&a4400h1000V"_$$extdate2^SSUTIL4(PAR3))
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"&a0h1450V"),ADD^PRTREP00(REPID,"HDR","!")
 s episcnt=^TMP($zn,$j),epcnt=""
 s (shade,linecnt,page)="",cnt=^TMP($zn,$j),cnt=cnt/50,pagetot=$p(cnt,".")+$s(cnt[".":1,1:0)
 s date="" f  s date=$o(^TMP($zn,$j,date)) q:date=""  d
 .s epis="" f  s epis=$o(^TMP($zn,$j,date,epis)) q:epis=""  d
 ..q:$$select^LVBEPVIS(epis,"Y")
 ..i PLIST(6)'="" s dob=$$extdate2^SSUTIL4(PLIST(6)),PLIST(6)=$p(dob,"/",1,2)_"/"_$e($p(dob,"/",3),3,4)
 ..s num="" f  s num=$o(^TMP($zn,$j,date,epis,num)) q:num=""  d
 ...s epcnt=epcnt+1
 ...d ADD^PRTREP00(REPID,"PRT","?3  "_epis)
 ...d ADD^PRTREP00(REPID,"PRT","?13 "_PLIST(3)_","_PLIST(4))
 ...d ADD^PRTREP00(REPID,"PRT","?44 "_PLIST(6))
 ...f i="E2:53","PROG:62","LH:70","bHCG:79" d
 ....s tab=$p(i,":",2)_$j(" ",3-$l($p(i,":",2)))
 ....s res=$g(^TMP($zn,$j,date,epis,num,$p(i,":",1))) s:res="" res="-"
 ....d ADD^PRTREP00(REPID,"PRT","?"_tab_res)
 ...s shade=$s(shade="":1,1:"") i shade="1",(epcnt'=episcnt) d Shade
 ...d ADD^PRTREP00(REPID,"PRT","!")
 ...s linecnt=$g(linecnt)+1 i linecnt=50 d  s (shade,linecnt)=""
 ....s page=page+1 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4400h1150V"_page_" of "_pagetot)
 ....d ADD^PRTREP00(REPID,"PRT","#")
 s page=page+1 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4400h1150V"_page_" of "_pagetot)
 q
 ; set header
QDR s QDR="IDS-IVF",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
 q
 ; shade every other line
Shade d ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?2  ")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"*c5100h120v15g2P")
 q
]]></Routine>
</Export>
