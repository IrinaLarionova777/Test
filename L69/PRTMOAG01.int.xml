<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTMOAG01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTMOAG01 ; ML 22/12/98 ; MICRO ORGANISMS PER AGE GROUP
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Organism
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 d ADDLINE^PRTREP00(REPID,"HDR","~ORIENTATION=1")
 d ADDLINE^PRTREP00(REPID,"HDR","~BOLD")
 d ADDLINE^PRTREP00(REPID,"HDR","~FONT-COURIER")
 d ADDLINE^PRTREP00(REPID,"HDR","~FONTSIZE=7")
 s text="Micro Organisms per Age Group" s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s text="Organism : "_$s(bug'="":bug,1:"All") s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s text="From "_$$extdate2^SSUTIL4(PAR1)_" to "_$$extdate2^SSUTIL4(PAR2) s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s prt="Organism",age="" f  s age=$o(^TMP($zn,$j,"AGE",age)) q:age=""  s i=^(age) d PRT(i)
 d ADDLINE^PRTREP00(REPID,"HDR","?1  "_prt)
 d HDLN
 d ADDLINE^PRTREP00(REPID,"HDR","?1  "_und)
 d HDLN
 d ADDLINE^PRTREP00(REPID,"HDR","~NORMAL")
 d BODY,PRINT
 d ADDLINE^PRTREP00(REPID,"PRT","~FONTSIZE=10")
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
TAB(text) ; calculate header position
 s textlen=180-$l(text),tab=textlen/2_"   "
 q $e(tab,1,3)
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s bug=PAR3 s:bug="" bug="All"
 s PAR3=","_PAR3_","
 s $p(und,"-",189)=""
 f i="1:<1 mnth:10","2:1-6 mnths:19","3:7-12 mnths:30","4:1 year:42","5:2-5 yrs:50","6:6-10 yrs:59","7:11-15 yrs:69","8:16-20 yrs:80" s ^TMP($zn,$j,"AGE",$p(i,":",1))=$p(i,":",2,3)
 f i="9:21-30 yrs:91","10:31-40 yrs:102","11:41-50 yrs:113","12:51-60 yrs:124","13:61-70 yrs:135","14:71-80 yrs:146","15:81-90 yrs:157","16:91-100 yrs:168","17:100+ yrs:180" s ^TMP($zn,$j,"AGE",$p(i,":",1))=$p(i,":",2,3)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s tc="" f  s tc=$o(^TTAB("TC",tc)) q:tc=""  d
 .q:$$select^LVBCTTC(tc,"Y")  s type=$e($p(PLIST(5),$c(1)),1)
 .s:type="V" ^TMP($zn,$j,"TC",tc)=""
 s date=PAR1-1
 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..q:$$select^LVBEPVIS(epis,"Y")  k visdet m visdet=PLIST k PLIST
 ..s episage=visdet(26) q:episage=""
 ..q:$$getall^LVBVISTS(epis,,,,,,,,"ALL")  k vists m vists=PLIST k PLIST
 ..f i=1:1:vists s tsstr=$p(vists(i),$c(2)),ts=$p(tsstr,"||",2),nn=$p(tsstr,"||",3) d
 ...s z=$g(^TEPI(epis,1,ts,nn)),dateauth=$p(z,"\",4) q:dateauth=""
 ...s visRWD=epis_"||"_ts_"||"_nn  q:$$results^LVBVISTS(visRWD,,,,"Y")  k visres m visres=PLIST k PLIST
 ...f j=1:1:visres s resstr=$p(visres(j),$c(2)),tc=$p(resstr,$c(1),2),bug=$p(resstr,$c(1),3) d
 ....q:'$d(^TMP($zn,$j,"TC",tc))  q:bug=""
 ....i PAR3'=",,",PAR3'[(","_bug_",") q
 ....q:$$select^LVBCTBUG(bug,"Y")
 ....i $e(episage)="." d
 .....s days=episage*1000
 .....i days<31 s age=1 q
 .....i days<182 s age=2 q
 .....s age=3 q
 ....i episage<2,age="" s age=4
 ....i episage<6,age="" s age=5
 ....i episage<11,age="" s age=6
 ....i episage<16,age="" s age=7
 ....i episage<21,age="" s age=8
 ....i episage<31,age="" s age=9
 ....i episage<41,age="" s age=10
 ....i episage<51,age="" s age=11
 ....i episage<61,age="" s age=12
 ....i episage<71,age="" s age=13
 ....i episage<81,age="" s age=14
 ....i episage<91,age="" s age=15
 ....i episage<101,age="" s age=16
 ....i episage>100,age="" s age=17
 ....s ^TMP($zn,$j,"BUG",bug,age)=$g(^TMP($zn,$j,"BUG",bug,age))+1
 q
PRINT ; print report
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s bug="" f  s bug=$o(^TMP($zn,$j,"BUG",bug)) q:bug=""  d
 .s (col,prt)="",prt=bug
 .s age="" f  s age=$o(^TMP($zn,$j,"BUG",bug,age)) q:age=""  s text=^(age) d
 ..s tab=$p(^TMP($zn,$j,"AGE",age),":",2),i=text_":"_tab d PRT(i)
 ..s age(age)=$g(age(age))+1
 .d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 d LN,ADDLINE^PRTREP00(REPID,"PRT","?1  "_und)
 s prt="Total"
 s age="" f  s age=$o(age(age)) q:age=""  d
 .s tab=$p(^TMP($zn,$j,"AGE",age),":",2),i=age(age)_":"_tab d PRT(i)
 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt)
 q
PRT(det) ; create line to print
 s text=$p(det,":",1),tab=$p(det,":",2)
 s prt=prt_$j(" ",tab-$l(prt))_text
 q
HDLN ; add line to header
 d ADDLINE^PRTREP00(REPID,"HDR","!")
 q
LN ; add line to report
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 q
]]></Routine>
</Export>
