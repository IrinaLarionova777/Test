<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTQCE01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTQCE01	; BJ 12/11/2007 ; QC Exceptions List
	q
	; Parameters :  1.Date from
	;               2.Date to
	;               3.Instrument
	;               4.Material
	;
CREATE(REPID,PAR,USER,%msql)	; Create report
	n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
	k xHEADER d INIT
	s line=1,xHEADER(line)="QC Exceptions Report"
	s line=2,xHEADER(line)=""
	s line=3,xHEADER(line)="for instrument : "_$g(MACH(3))_"   ("_PAR3_")"
	d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
CREND	i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR3 i $$update^LVBPRPR(REPID)
	k ^TMP($zn,$j),^TMP("PRTREP00",$j)
	q
	; initialize parameters
INIT	f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
	k xSORT d H^PRTREP00(PAR10,$zn)
	s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
	k MACH i '$$select^LVBMIMP(PAR3) m MACH=PLIST
	q
	; create temporary printing file
BODY	s mat="" f  s mat=$o(^TTQC(mat)) q:mat=""  d
	.i $l(PAR4),mat'=PAR4 q
	.s lot="" f  s lot=$o(^TTQC(mat,"LOT",lot)) q:lot=""  d
	..q:'$d(^TTQC(mat,"LOT",lot,"TYPE","M",PAR3))
	..s ti="" f  s ti=$o(^TTQC(mat,"LOT",lot,"TYPE","M",PAR3,"TEST",ti)) q:ti=""  d
	...s level="" f  s level=$o(^TTQC(mat,"LOT",lot,"TYPE","M",PAR3,"TEST",ti,"DATA",level)) q:level=""  d
	....s date=PAR1-1 f  s date=$o(^TTQC(mat,"LOT",lot,"TYPE","M",PAR3,"TEST",ti,"DATA",level,date)) q:date=""  q:date>PAR2  d
	.....s run="" f  s run=$o(^TTQC(mat,"LOT",lot,"TYPE","M",PAR3,"TEST",ti,"DATA",level,date,run)) q:run=""  d
	......s detail=$g(^TTQC(mat,"LOT",lot,"TYPE","M",PAR3,"TEST",ti,"DATA",level,date,run))
	......s value=$p(detail,"\"),exclude=$p(detail,"\",2),user=$p(detail,"\",3),time=$p(detail,"\",4),rule=$p(detail,"\",5)
	......i $l(rule) d
	.......s ^TMP($zn,$j,"REPORT",mat,lot,ti,date,run)=time_$c(1)_user
	....... ; extract rule
	.......i '$$select^LVBCTQCR(rule,"Y") s rule=$g(PLIST(3))
	.......s ^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,level)=value_$c(1)_"["_rule_"]"_$s(exclude="Y":" (EX)",1:"")
	.......m ^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,level,"REM")=^TTQC(mat,"LOT",lot,"TYPE","M",PAR3,"TEST",ti,"DATA",level,date,run,"REM")
	q
PRINT	i $d(^TMP($zn,$j,"REPORT")) d
	.s mat="" f  s mat=$o(^TMP($zn,$j,"REPORT",mat)) q:mat=""  d
	..s lot="" f  s lot=$o(^TMP($zn,$j,"REPORT",mat,lot)) q:lot=""  d
	...s ti="" f  s ti=$o(^TMP($zn,$j,"REPORT",mat,lot,ti)) q:ti=""  d
	....s date="" f  s date=$o(^TMP($zn,$j,"REPORT",mat,lot,ti,date)) q:date=""  d
	.....s run="" f  s run=$o(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run)) q:run=""  d
	......f cnt=1:1:3 d
	.......k xxx
	.......; print values
	.......i cnt=1 d
	........s xxx(1)=$$extdate2^SSUTIL4(date)
	........s xxx(2)=$p($g(^TTQC(mat)),"\",1)
	........s xxx(3)=$p($g(^TTQC(mat,"LOT",lot)),"\",1)
	........s desc="" i '$$select^LVBCTTC(ti) s desc=$s($l(PLIST(14)):PLIST(14),$l(PLIST(3)):PLIST(3),1:PLIST(2))
	........s xxx(4)=ti_$c(1)_desc
	........s xxx(5)=run
	........s xxx(6)=$$exttime^SSUTIL4($p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run)),$c(1),1))
	........s xxx(7)=$p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run)),$c(1),2)
	........s xxx(8)=$p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,1)),$c(1),1)
	........s xxx(9)=$p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,2)),$c(1),1)
	........s xxx(10)=$p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,3)),$c(1),1)
	....... ; print rules
	.......i cnt=2 d
	........s xxx(8)=$p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,1)),$c(1),2)
	........s xxx(9)=$p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,2)),$c(1),2)
	........s xxx(10)=$p($g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,3)),$c(1),2)
	....... ; print comments
	.......i cnt=3 d
	........s x1=$g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,1,"REM",0)) s max=x1
	........s x2=$g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,2,"REM",0)) i x2>max s max=x2
	........s x3=$g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,3,"REM",0)) i x3>max s max=x3
	........f j=1:1:max d
	.........s xxx(8)=$g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,1,"REM",j))
	.........s xxx(9)=$g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,2,"REM",j))
	.........s xxx(10)=$g(^TMP($zn,$j,"REPORT",mat,lot,ti,date,run,3,"REM",j))
	.........d PRINT^PRTREP00(,.N) k xxx
	.......i cnt'=3 d PRINT^PRTREP00(,.N) k xxx
	.d ADDLINE^PRTREP00(REPID,"PRT","!")
	.d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
	.d ADDLINE^PRTREP00(REPID,"PRT","!")
	s report="QCE",EndLine=$$seldata^LVBCTCLR(report,9)
	i EndLine="Y" d PRINTEND^PRTREP00(xLINE(1))
	q
]]></Routine>
</Export>
