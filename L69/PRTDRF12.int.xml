<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDRF12" type="INT" languagemode="0"><![CDATA[
PRTDRF12 ;IG 27/05/98 ;Doctors Request Forms for NSW Pathology
 Q
 ; Parameters :  1.Pad Code
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($ZN,$J)
 D INIT
 I '$$select^LVBPAD(PAR1) D
 .I PLIST(5)=1 D HEADER1
 D PRINT
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR1 I $$update^LVBPRPR(REPID)
 K ^TMP($ZN,$J),^TMP("PRTREP00",$J)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
HEADER1 i $$open^LVBPADDR(PAR1)
 i '$$fetch^LVBPADDR(PAR1) s doc=$p(PLIST(2),$c(1)) i $l(doc) d
 .s (ADDRESS1,ADDRESS2,ADDRESS3)=""
 .i $$select^LVBCTDR(doc,"Y") q
 .k DOC m DOC=PLIST
 .s NAME=$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51))
 .D ADDLINE^PRTREP00(REPID,"HDR",$c(27)_"@"_$c(27)_"C39"_$c(27)_"g")
 .f j=1:1:4 D ADDLINE^PRTREP00(REPID,"HDR","!")
 .s cnt=0 f j=1:1:3 s add(j)=""
 .i $l(DOC(9)) s cnt=cnt+1,add(cnt)=DOC(9)
 .i $l(DOC(10)) s cnt=cnt+1,add(cnt)=DOC(10)
 .s cnt=cnt+1,add(cnt)=$p(DOC(11),$c(1))_" "_$p(DOC(12),$c(1))_" "_$p(DOC(13),$c(1))
 .d ADDLINE^PRTREP00(REPID,"HDR","?26 "_DOC(2))
 .d ADDLINE^PRTREP00(REPID,"HDR","?38 "_add(1))
 .d ADDLINE^PRTREP00(REPID,"HDR","!")
 .d ADDLINE^PRTREP00(REPID,"HDR","?12 "_NAME)
 .d ADDLINE^PRTREP00(REPID,"HDR","?38 "_add(2))
 .d ADDLINE^PRTREP00(REPID,"HDR","!")
 .d ADDLINE^PRTREP00(REPID,"HDR","?12 Provider No."_DOC(18))
 .d ADDLINE^PRTREP00(REPID,"HDR","?38 "_add(3))
 .d ADDLINE^PRTREP00(REPID,"HDR","!")
 .d ADDLINE^PRTREP00(REPID,"HDR","?12 Ph. "_$s($l(DOC(5)):DOC(5),$l(DOC(6)):DOC(6),1:DOC(7)))
 i $$close^LVBPADDR()
 q
PRINT i '$$select^LVBPAD(PAR1) d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .s QUANTITY=PLIST(4),PLIST(3)=+$h i $$update^LVBPAD(PLIST(1))
 .f CNT=1:1:QUANTITY d ADDLINE^PRTREP00(REPID,"PRT","#")
 q
]]></Routine>
</Export>
