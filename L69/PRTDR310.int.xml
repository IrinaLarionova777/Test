<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR310" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR310 ;IG 27/05/98 ;Doctors Reports printing (cumulative) - Moaven+partners
 ;
 ; supported calls :
 ;
 ; $$print^PRTDR310(vtsRID,type,EpisodeList,DH)
 ;
 ; type          : P - print
 ;                 V - view from Enquiry
 ;                   - view from Entry
print(vtsRID,type,EpisodeList,DH,group,cOrder) n (vtsRID,type,EpisodeList,DH,group,cOrder,dh1,dh2,tsLM,tsWD,GroupName) 
 s vtsRID=$g(vtsRID),type=$g(type),EpisodeList=$g(EpisodeList),DH=$g(DH),group=$g(group),cOrder=$g(cOrder),dh1=$g(dh1),dh2=$g(dh2)
 s epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2),max=6,ColWidth=9,ColLeft=17,FullYear="N",tsLM=$g(tsLM),tsWD=$g(tsWD)
 ; check for margin changes
 s margin="",%routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 i $l(%routine) s LineRoutine="Margin^"_%routine i $l($t(@LineRoutine)) x "s margin=$$Margin^"_%routine_"()"
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS")
 i '$$select^LVBEPVIS(epis,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s range1="(",range2=")",(flaglow,flaghigh,Alternate,Reportable)="" i '$$select^MVBCFLAB(1) d
 .i $l(PLIST(18)) s flaglow=PLIST(18)
 .i $l(PLIST(19)) s flaghigh=PLIST(19)
 .i $l(PLIST(20)) s range1=PLIST(20)
 .i $l(PLIST(21)) s range2=PLIST(21)
 .i $l(PLIST(14)) s max=PLIST(14)
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 .i PLIST(37)="Y" s ColWidth=11,FullYear="Y"
 .i $l(PLIST(68)) s Reportable=PLIST(68)
 .i $l(PLIST(69)) s Alternate=PLIST(69)
 k FORMAT,TS
 s rowid=test_"||"_dh1_"||"_dh2,layout=""
 s layout=$$seldata^LVBCTTSP(rowid,4)
 s layout=$p(layout,$c(1))
 i dh1="D" s lan=$$seldata^LVBCTDR(dh2,46)
 s lan=$p($g(lan),$c(1))
 i '$$getit^LVBCTTSF(,vtsRID,"Y",,,layout,,lan) d
 .f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS($p(vtsRID,"||",2),ti)=""
 i $l(group),group'=" " d
 .i '$$select^LVBCTTWG(group) m CTTWG=PLIST
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ...i '$$getit^LVBCTTSF(,epis_"||"_ts_"||"_tscnt,"Y") d
 ....f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)=""
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..i '$$getit^LVBCTTSF(ts,,"Y") d
 ...f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)=""
 .s ti="" f cnt=1:1 s ti=$o(FORMAT(ti)) q:ti=""  d
 ..i $g(^TTAB("GR",group,"TI",ti)) s $p(FORMAT(ti),$c(2),27)=^(ti)
 ..e  s $p(FORMAT(ti),$c(2),27)=cnt
 ; create list of episodes and results
 k EPTS i $l(EpisodeList) d
 .f col=1:1:$l(EpisodeList,",") s ep=$p(EpisodeList,",",col) i $l(ep) d
 ..s date=$$seldata^LVBEPVIS(ep,12)
 ..s time=$$seldata^LVBEPVIS(ep,13)
 ..s time=$tr(time," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 k EPTS i '$l(EpisodeList) d
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=VISIT(j)
 .s TestList=test i $l(group),group'=" " s ts="" f  s ts=$o(TS(ts)) q:ts=""  s TestList=TestList_","_ts
 .d CumList^PRTDR00(TestList,epis,visit,cOrder)
 .f col=1:1:PLIST d
 ..s ep=PLIST(col)
 ..s date=$$seldata^LVBEPVIS(ep,12)
 ..s time=$$seldata^LVBEPVIS(ep,13)
 ..s time=$tr(time," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 i '$d(^TMP($zn,$j,"CUM-TC")) d  q 100_$c(1)_EpisodeList
 .k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS")
 ; print cumulative header
 d ADD^PRTDR00(,"~BOLD","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM+margin,$e("Date Collected"_$j("",tsWD),1,ColLeft-3)_" :","Y")
 s maxcol=$o(^TMP($zn,$j,"CUM",""),-1) s:maxcol>max maxcol=max
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s xx=$p(^TMP($zn,$j,"CUM",col),$c(1),2)
 .i xx s xx=$$extdate2^SSUTIL4(xx) i FullYear="N" s $p(xx,"/",3)=$e($p(xx,"/",3),3,4)
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,xx,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM+margin,$e("Episode"_$j("",tsWD),1,ColLeft-3)_" :","Y")
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s xx=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,xx,"Y")
 d ADD^PRTDR00(tsLM+ColLeft+(col*ColWidth),"Units","Y")
 d ADD^PRTDR00(tsLM+ColLeft+((col+1)*ColWidth),"Range","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 ; print test set header only for Cum report page
 i $l(EpisodeList) d
 .d ADD^PRTDR00(,"~RES-TSHEADER"),ADD^PRTDR00(,$c(13))
 .i ('$l(group)!(group=" ")),$l(TEST(17)) d
 ..s tsName=""
 ..i '$l(tsName),$l(lan) d
 ...s rowid=TEST(2)_"||"_lan
 ...s tsName=$$seldata^LVBCTTSZ(rowid,3)
 ...s tsName=$p(tsName,$c(1))
 ..i '$l(tsName) s tsName=$s(TEST(17)="*":TEST(3),1:TEST(17))
 ..d LN^PRTDR00(),ADD^PRTDR00(tsLM+margin,tsName),LN^PRTDR00()
 .i $l(group),group'=" ",$l(GroupName) d LN^PRTDR00(),ADD^PRTDR00(tsLM+margin,GroupName),LN^PRTDR00() s GroupName=""
 .d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 ; print start of the group
 i $l($g(CTTWG(11))) d
 .d LN^PRTDR00()
 .f j=1:1:$l(CTTWG(11),"|") d ADD^PRTDR00(tsLM+margin,$p(CTTWG(11),"|",j)),LN^PRTDR00()
 ; print results
 s order="" f  s order=$o(^TMP($zn,$j,"CUM-TC-ORDER",order)) q:order=""  d
 .s ti=^(order)
 .i type="",$p(FORMAT(ti),$c(2),15)'="Y" d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13))
 .i order#1=0 d ADD^PRTDR00(tsLM+margin,$e($tr($p(FORMAT(ti),$c(2),3),":")_$j("",tsWD),1,ColLeft-3)_" :")
 .f col=(1+(order#1)):1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 ..s result=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),1),flag=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),2)
 ..i result="" d
 ...i $d(^TMP($zn,$j,"CUM-EXIST",col,ti)) d  q
 ....i $p(FORMAT(ti),$c(2),8)="M" s result="TF"
 ....e  s result=""
 ...s result="-"
 .. ; print extra info
 ..i flag="L" d
 ...i "MN"[$e($p(FORMAT(ti),$c(2),18)),$l(flaglow) s result=result_" "_flaglow
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ...d ADD^PRTDR00(,"~BOLD")
 ..i flag="H" d
 ...i "MN"[$e($p(FORMAT(ti),$c(2),18)),$l(flaghigh) s result=result_" "_flaghigh
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ...d ADD^PRTDR00(,"~BOLD")
 ..d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,result)
 ..i (flag="L")!(flag="H") d
 ...i type="",$p(FORMAT(ti),$c(2),15)'="Y" d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13)) q
 ...d ADD^PRTDR00(,"~NORMAL")
 .i type="",$p(FORMAT(ti),$c(2),15)'="Y" d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .s units=$p(FORMAT(ti),$c(2),17)
 .d ADD^PRTDR00(tsLM+ColLeft+(col*ColWidth),units)
 .i $l($p(FORMAT(ti),$c(2),22)),$l($p(FORMAT(ti),$c(2),23)) d
 ..i $e($p(FORMAT(ti),$c(2),22))?1N s $p(FORMAT(ti),$c(2),22)=$j($p(FORMAT(ti),$c(2),22),0,$e($p(FORMAT(ti),$c(2),18),2,4))
 ..i $e($p(FORMAT(ti),$c(2),23))?1N s $p(FORMAT(ti),$c(2),23)=$j($p(FORMAT(ti),$c(2),23),0,$e($p(FORMAT(ti),$c(2),18),2,4))
 ..d ADD^PRTDR00(tsLM+ColLeft+((col+1)*ColWidth),range1_$p(FORMAT(ti),$c(2),22)_"-"_$p(FORMAT(ti),$c(2),23)_range2)
 .i $l($p(FORMAT(ti),$c(2),22)),'$l($p(FORMAT(ti),$c(2),23)) d
 ..i $e($p(FORMAT(ti),$c(2),22))?1N s $p(FORMAT(ti),$c(2),22)=$j($j($p(FORMAT(ti),$c(2),22),0,$e($p(FORMAT(ti),$c(2),18),2,4)),$p(FORMAT(ti),$c(2),19)+$l(range1))
 ..d ADD^PRTDR00(tsLM+ColLeft+((col+1)*ColWidth),range1_$p(FORMAT(ti),$c(2),22)_range2)
 .i type="",$p(FORMAT(ti),$c(2),15)'="Y" d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .d LN^PRTDR00()
 i $d(^TMP($zn,$j,"CUM-COMMENTS")) d
 .s col="" f  s col=$o(^TMP($zn,$j,"CUM-COMMENTS",col)) q:col=""  d
 ..d LN^PRTDR00()
 ..f j=1:1 q:'$d(^TMP($zn,$j,"CUM-COMMENTS",col,j))  d
 ...f j1=1:1:$l(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^") d
 ....s xx=$p(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^",j1) i type="P" s xx=$tr(xx,$c(16,17,18),"^\|")
 ....d ADD^PRTDR00(tsLM,xx)
 ...d LN^PRTDR00()
 .d LN^PRTDR00()
 ; print end of the group
 i $l($g(CTTWG(12))) d
 .d LN^PRTDR00()
 .f j=1:1:$l(CTTWG(12),"|") d ADD^PRTDR00(tsLM+margin,$p(CTTWG(12),"|",j)),LN^PRTDR00()
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS")
 q 0
 ; collect Test Items information
TestItems(col,rowid) n (col,rowid,ExcludeList,type,FORMAT,vtsRID,tsLM,tsWD,ColWidth) s col=$g(col),rowid=$g(rowid)
 i '$$select^LVBVISTS(rowid,"Y") d
 .m ^TMP($zn,$j,"CUM-EXIST",col)=TS($p(rowid,"||",2))
 .s status=$$status^LVBVIS1(rowid)
 .i '$$results^LVBVISTS(rowid) f j=1:1:PLIST d
 ..s tc=$p(PLIST(j),$c(1),2),(result,flag)=""
 ..i (status="A")!(vtsRID=rowid)!(type'="P") s result=$p(PLIST(j),$c(1),3),flag=$p(PLIST(j),$c(1),5)
 ..i '$d(FORMAT(tc)) q
 .. ; not included in cumulative
 ..i $p(FORMAT(tc),$c(2),20)'="Y" q
 ..i $l(tc),$l(result) d
 ... ; non reportable test item
 ...i $l(type),$p(FORMAT(tc),$c(2),15)'="Y" q
 ...i $e($p(FORMAT(tc),$c(2),18))="N" d IN
 ...i $e($p(FORMAT(tc),$c(2),18))="D" d ID^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="T" d IT^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="Y" d IY^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="S" s cnt=tc,resultx=result d IS^PRTDR00 d
 ....i $o(result(""),-1)=1,$l(result(1))'>ColWidth s x=result(1) k result s result=x q
 ....m rescom(tc,resultx)=result s result=resultx
 ...s ^TMP($zn,$j,"CUM",col,tc)=result_$c(1)_flag
 ...i '$d(^TMP($zn,$j,"CUM-TC",tc)) d
 ....s order=$p(FORMAT(tc),$c(2),27) i '$l(order) s order=$p(FORMAT(tc),$c(2),2)
 ....s ^TMP($zn,$j,"CUM-TC-ORDER",order)=tc,^TMP($zn,$j,"CUM-TC",tc)=""
 ; don't print comments if not most recent episode
 i $p(rowid,"||",1)'=$p(vtsRID,"||",1) k rescom q
 ; compile test item standard comments
 i $d(rescom) d
 .s ^TMP($zn,$j,"CUM-COMMENTS",col,1)="Std Comm : "
 .s tc="",b=1 f  s tc=$o(rescom(tc)) q:tc=""  d
 ..s res="" f  s res=$o(rescom(tc,res)) q:res=""  d
 ...s ^TMP($zn,$j,"CUM-COMMENTS",col,1)=^TMP($zn,$j,"CUM-COMMENTS",col,1)_res_$s($o(rescom(tc,res))'="":", ",1:"")
 ...s x="" f  s x=$o(rescom(tc,res,x)) q:x=""  s b=b+1,^TMP($zn,$j,"CUM-COMMENTS",col,b)=rescom(tc,res,x)
 ; compile test set comments
 k PLIST i '$$COMMENTS^LVBCOM01(rowid,,type,"N",,"N","Y",.ExcludeList) i PLIST d
 .f j1=1:1:PLIST s x1=$o(^TMP($zn,$j,"CUM-COMMENTS",col,""),-1)+1,^TMP($zn,$j,"CUM-COMMENTS",col,x1)=PLIST(j1)
 q
IN ; Numeric
 i $l(result),$d(^TTAB("RC",result)) q
 s Flag="" f  q:'$l(result)  q:"<>+-"'[$e(result)  s Flag=Flag_$e(result),result=$e(result,2,$l(result))
 s result=Flag_$j(result,0,$e($p(FORMAT(tc),$c(2),18),2,9))
 q
]]></Routine>
</Export>
