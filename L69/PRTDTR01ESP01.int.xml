<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDTR01ESP01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDTR01ESP01 ; IG 19/08/99 ; Deauthorised Tests Report
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Hospital/Ward
 ;               4. Department
 ;               5. User
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Informe de Desvalidados"
 s line=2,xHEADER(line)=""
 i $l(PAR3) d
 .s line=line+1,xHEADER(line)="Hospital/Ward : "_CTHOS(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR4) d
 .s line=line+1,xHEADER(line)="Departamento : "_CTDEP(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR5) d
 .s line=line+1,xHEADER(line)="por Usuario: "_SSUSR(3)
 .s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 K xSORT D H^PRTREP00(PAR10,$zn)
 S X=xLINE(2),MAX=$L(X),$P(SPACE," ",MAX)="",$P(LINE,"-",MAX)=""
 k CTHOS i '$$select^LVBCTHOS(PAR3,"Y") m CTHOS=PLIST
 k CTDEP i '$$select^LVBCTDEP(PAR4,"Y") m CTDEP=PLIST
 k SSUSR i '$$selrow^MVBSSUSR(PAR5,"Y") m SSUSR=PLIST
 Q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TEPIi("DE-AUTHORISED",date)) q:date=""  d
 .i $l(PAR2),date>PAR2 q
 .s epis="" f  s epis=$o(^TEPIi("DE-AUTHORISED",date,epis)) q:epis=""  d
 ..i $l(PAR3),$$seldata^LVBEPVIS(epis,22)'=PAR3 q
 ..s ts="" f  s ts=$o(^TEPIi("DE-AUTHORISED",date,epis,ts)) q:ts=""  d
 ...i $l(PAR4),$$seldata^LVBCTTS(ts,26)'=PAR4 q
 ...s tscnt="" f  s tscnt=$o(^TEPIi("DE-AUTHORISED",date,epis,ts,tscnt)) q:tscnt=""  d
 ....i $l(PAR5),$p(^(tscnt),"\",3)'=PAR5 q
 ....s ^TMP($zn,$j,"REPORT",date,epis,ts,tscnt)=^TEPIi("DE-AUTHORISED",date,epis,ts,tscnt)
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s date="" f  s date=$o(^TMP($zn,$j,"REPORT",date)) q:date=""  d
 ..s xxx(1)=$$extdate2^SSUTIL4(date)
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",date,epis)) q:epis=""  d
 ...k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 ...s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",date,epis,ts)) q:ts=""  d
 ....s xxx(5)=$$seldata^LVBCTTS(ts,3)
 ....s tscnt="" f  s tscnt=$o(^TMP($zn,$j,"REPORT",date,epis,ts,tscnt)) q:tscnt=""  d
 .....f j=1:1:3 s @("x"_j)=$p(^(tscnt),"\",j)
 .....i '$$selrow^MVBSSUSR(x3) s xxx(2)=PLIST(3)
 .....s xxx(3)=$$extdate2^SSUTIL4(x1)
 .....i '$$selrow^MVBSSUSR(x2) s xxx(4)=PLIST(3)
 .....d PRINT^PRTREP00(.VISIT,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="DTR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
