<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV23" type="INT" languagemode="0" generated="1"><![CDATA[
PRTINV23	; ML 13/04/2004 ; PRIVATE INVOICES - Goulburn Valley Health
	q
	; Parameters :  1.Payment Code
	;               2.Batch Number
	;               3.Episode Number
	;               4.Sequence Number
	;               5.Reminder
	;
CREATE(REPID,PAR,USER,%msql)	; Create report
	n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
	d INIT,BODY,PRINT
CREND	i 'PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)=PAR1_"-"_PAR2 i $$update^LVBPRPR(REPID)
	i PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)="REM-"_PAR5 i $$update^LVBPRPR(REPID)
	k ^TMP($zn,$j),^TMP("PRTREP00",$j)
	q
	; initialize parameters
INIT	f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
	q
	; create temporary printing file
BODY	
	; print single patient
	i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
	.m VISIT=PLIST k PLIST s epis=PAR3
	.s pc=$p(VISIT(57),"||",1),batch=$p(VISIT(57),"||",2)
	.k INVOICE i '$$select^LVBINVB(pc_"||"_batch) m INVOICE=PLIST
	.k COMP i '$$select^LVBCTCO(INVOICE(6)) m COMP=PLIST
	.s seq=$p($g(^TMCB(pc,batch,epis)),"\",2)
	.s ^TMP($zn,$j,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq_"\"_$p(VISIT(9),$c(1))_"\"_COMP(10)
	.s ^TMP($zn,$j,"EPIS",epis,"COMP")=$g(INVOICE(10))_"\"_$g(INVOICE(5))_"\"_$g(COMP(10))_"\"_$g(COMP(12))
	.d ITEM
	; print batch
	i $l(PAR1),$l(PAR2) d
	.k INVOICE i '$$select^LVBINVB(PAR1_"||"_PAR2) m INVOICE=PLIST
	.k COMP i '$$select^LVBCTCO(INVOICE(6)) m COMP=PLIST
	.s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
	..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
	..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
	..i $l(PAR4),PAR4'=seq q
	..i '$$select^LVBEPVIS(epis,"Y") d  q
	...m VISIT=PLIST k PLIST
	...s ^TMP($zn,$j,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq_"\"_$p(VISIT(9),$c(1))_"\"_COMP(10)
	...s ^TMP($zn,$j,"EPIS",epis,"COMP")=$g(INVOICE(10))_"\"_$g(INVOICE(5))_"\"_$g(COMP(10))_"\"_$g(COMP(12))
	...d ITEM
	; check credits
	s (credit,epis)="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
	.s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
	.s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
	..s rowid=deb_"||"_date_"||"_ep
	..i $$open^LVBDEBVT(rowid)
	..f  q:$$fetch^LVBDEBVT(rowid)  d
	...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
	...k TRAN m TRAN=PLIST
	...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) d
	....s credit=credit-PLIST(14)
	...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) d
	....s credit=credit-PLIST(16)
	...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) d
	....s credit=credit-PLIST(9)
	..i $$close^LVBDEBVT()
	.i credit s ^TMP($zn,$j,"EPIS",epis,"CREDIT")=credit,credit=""
	; set events
	s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
	.s invoice=$p($p(^(epis),"\",2),"||")_"-"_$p($p(^(epis),"\",2),"||",2)_"-"_$p($p(^(epis),"\",2),"||",3)
	.i '$l(PAR5) d events^LVBCOM01(epis,"ACC","Original invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
	.i 'PAR5 d events^LVBCOM01(epis,"ACC","Duplicate invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
	.d events^LVBCOM01(epis,"ACC","Reminder "_PAR5_$s($l(USER):" printed by "_USER,1:""))
	q
	; print report
PRINT	d QDR
	s max=20,(line,nextpg)=""
	s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis))  q:epis=""  d
	.k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
	.k PAYC i '$$select^LVBCTPC(VISIT(9)) m PAYC=PLIST k PLIST
	.s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3))
	.s (inDescr,lcc)="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y") s inDescr=PLIST(5) i PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1)),"Y") s lcc=$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))
	.f  q:$e(name)'=" "  s name=$e(name,2,99)
	.s (line,nextpg,item)=""
	.s payable=$g(^TMP($zn,$j,"EPIS",epis,"TOT"))-$g(^TMP($zn,$j,"EPIS",epis,"CREDIT"))
	.d ADDRESS,COMP,PHEAD s start=1
	.d ADD^PRTREP00(REPID,"PRT","?2  "_$$extdate2^SSUTIL4(VISIT(12)))
	.d ADD^PRTREP00(REPID,"PRT","?14 "_epis)
	.d ADD^PRTREP00(REPID,"PRT","?26 "_lcc)
	.s GSTTot=""
	.s item="" f  s item=$o(^TMP($zn,$j,"EPIS",epis,"ITEM",item)) q:item=""  d
	..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2)
	..i item=VISIT(54) s desc=inDescr
	..i $$ALPHAUP^SSUTIL4(desc)["GST" s GSTTot=GSTTot+amt q
	.. ; check item desc length
	..s x="" s xdesc=desc k desc s desc=xdesc
	..i $l(desc)<30 s desc(1)=desc
	..i $l(desc)>29 d
	...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>29 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>34
	..d ADD^PRTREP00(REPID,"PRT","?39 "_item_$s(PAYC(37)["inpatient":"*",1:""))
	..d ADD^PRTREP00(REPID,"PRT","?48 "_desc(1))
	..d ADD^PRTREP00(REPID,"PRT","?81 "_$j(amt,7,2)),LN s line=line+1
	..i '$d(desc(2)) d NewPage
	..f i=2:1 q:'$d(desc(i))  d
	...d ADD^PRTREP00(REPID,"PRT","?46 "_desc(i)),LN s line=line+1
	...d NewPage
	.f i=1:1:17-line d LN
	.d LN i $l(PAYC(10)) d
	..d ADD^PRTREP00(REPID,"PRT","?3  ")
	..d ADD^PRTREP00(REPID,"PRT","~BOLD"),ADD^PRTREP00(REPID,"PRT","~ITALIC")
	..d ADD^PRTREP00(REPID,"PRT",PAYC(10))
	..d ADD^PRTREP00(REPID,"PRT","~NORMAL")
	..d ADD^PRTREP00(REPID,"PRT",$c(13))
	.s tot=$g(^TMP($zn,$j,"EPIS",epis,"TOT")),credit=$g(^TMP($zn,$j,"EPIS",epis,"CREDIT"))
	.i credit d ADD^PRTREP00(REPID,"PRT","?81 "_$j(credit,7,2)_" CR")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4770V"_$c(13)),ADD^PRTREP00(REPID,"PRT","?81 "_$j(GSTTot,7,2))
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4950V"_$c(13)),ADD^PRTREP00(REPID,"PRT","?81 "_$j(tot-credit,7,2))
	.s episdet=$g(^TMP($zn,$j,"EPIS",epis)),batch=$p(episdet,"\",2),payc=$p(episdet,"\",3)
	. ; print reminder message
	.i PAR5=1!(PAR5=2) d
	..d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0s2b4148T")
	..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a200h4500VTHIS ACCOUNT IS "_$s(PAR5=1:30,1:60)_" DAYS OVERDUE. PROMPT PAYMENT WOULD BE APPRECIATED")
	. ; print tear-off slip
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p15v0s0b4148T")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h5300V"_pathname)
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4148T")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h5400VProvider No : "_pathprov)
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h5660V"_$$DateTrans(+$h))
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h5780V"_name)
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h5900V"_$p(VISIT(20),$c(1)))
	.d ADD^PRTREP00(REPID,"PRT","~BOLD")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4900h6750V"_$j(payable,0,2))
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h7100V"_name),ADD^PRTREP00(REPID,"PRT","~NORMAL"),LN
	.f i=1:1:4 i $l(ADDRESS(i)) d ADD^PRTREP00(REPID,"PRT","?15 "_ADDRESS(i)) d:i<4 LN
	.d ADD^PRTREP00(REPID,"PRT","~BOLD")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4000h7100V"_$p(batch,"||")_"-"_$p(batch,"||",2)_"-"_$p(batch,"||",3))
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4000h7350V"_payc)
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4000h7600V"_epis)
	.d ADD^PRTREP00(REPID,"PRT","~NORMAL")
	q
	; print patient details
PHEAD	d ADD^PRTREP00(REPID,"PRT","#")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h0V")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p15v0s0b4148T")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h-100V"_pathname)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4148T")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h0VProvider No : "_pathprov)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4148T")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h130V"_comphone)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3200h260V"_compabn)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p16v0s3b4148T")
	s (InvMsg,InvPos)="" d
	.i '$l(PAR5) s InvMsg="TAX INVOICE",InvPos="400h550V" q
	.i 'PAR5 s InvMsg="TAX INVOICE~COPY",InvPos="400h500V~680h650V" q
	.i PAR5=1!(PAR5=2) s InvMsg="TAX INVOICE~REMINDER",InvPos="400h500V~500h650V" q
	.s InvMsg="TAX INVOICE~FINAL NOTICE",InvPos="400h500V~370h650V"
	f i=1:1:$l(InvMsg,"~") d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$p(InvPos,"~",i)_$p(InvMsg,"~",i))
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
    s episdet=$g(^TMP($zn,$j,"EPIS",epis)),batch=$p(episdet,"\",2),payc=$p(episdet,"\",3)
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h500V"_payc)
    d ADD^PRTREP00(REPID,"PRT","~BOLD")
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h669V"_$p(batch,"||")_"-"_$p(batch,"||",2)_"-"_$p(batch,"||",3))
    d ADD^PRTREP00(REPID,"PRT","~NORMAL")
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h838V"_$$DateTrans(+$h))
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h1100V"),LN
    i '$l(VISIT(42)) d
    .d ADD^PRTREP00(REPID,"PRT","~BOLD")
    .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h1200V"_name),ADD^PRTREP00(REPID,"PRT","~NORMAL"),LN
    f i=1:1:4 i $l(ADDRESS(i)) d ADD^PRTREP00(REPID,"PRT","?5  "_ADDRESS(i)) d LN
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h950V"_name)
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h1100V"_$p(VISIT(20),$c(1)))
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h1250V"_$p(VISIT(8),$c(1))_" "_$p(VISIT(108),$c(1)))
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h1400V"_$p(VISIT(106),$c(1)))
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h1550V"_$p(VISIT(107),$c(1)))
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h1700V"_$p(VISIT(131),$c(1)))
    d ADD^PRTREP00(REPID,"PRT","~BOLD")
    d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4500h1850V"_$$DateTrans(+$h+14))
    d ADD^PRTREP00(REPID,"PRT","~NORMAL")
	d DOC
	s date=$$extdate2^SSUTIL4(VISIT(12)),date=$p(date,"/",1,2)_"/"_$e($p(date,"/",3),3,4)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2280h2100V"_date)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3050h2100V"_drdet)
	f i=1:1:3 d LN
	q
	; extract billing items for episode
ITEM	s (item,amt,amount,det)=""
	f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
	.s det=^(item),amount=amount+$p(det,"\")
	.s ^TMP($zn,$j,"EPIS",epis,"ITEM",item)=det
	s ^TMP($zn,$j,"EPIS",epis,"TOT")=amount
	q
	; extract company details
COMP	s company=$g(^TMP($zn,$j,"EPIS",epis,"COMP"))
	s pathname=$p($p(company,"\"),$c(1),2),pathprov=$p(company,"\",2),compabn=$p(company,"\",3),comphone=$p(company,"\",4)
	q
	; extract referring doctor details
DOC	s (drdet,drprov)=""
	q:$$select^LVBCTDR(VISIT(15))  m DOC=PLIST k PLIST
	s drdet=$$NameFormat^LVBCOM03("DR",DOC(26),$e(DOC(4)),DOC(3))_" Provider No. "_DOC(18)
	q
	; if billing address different to actual address use billing address
ADDRESS	k ADDRESS s ADDRESS(1)=VISIT(42)
	f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
	f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
	i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
	s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
	f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
	q
	; check if new page is required
NewPage	i line>20,($o(^TMP($zn,$j,"EPIS",epis,"ITEM",item))'=""!($o(^TMP($zn,$j,"EPIS",epis))'="")) d
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4800h4950VCont. Over..")
	.s nextpg=1 d PHEAD s (line,nextpg)=""
	q
	; change date to dd Month Name yyyy
DateTrans(date)	s date=$$extdate2^SSUTIL4(date) s day=$p(date,"/",1),mth=$p(date,"/",2),year=$p(date,"/",3)
	s date=day_" "_$s(mth="01":"January",mth="02":"February",mth="03":"March",mth="04":"April",mth="05":"May",mth="06":"June",mth="07":"July",mth="08":"August",mth="09":"September",mth="10":"October",mth="11":"November",1:"December")_" "_year
	q date
	; add line to report
LN	d ADD^PRTREP00(REPID,"PRT","!")
	q
	; set header
QDR	d ADD^PRTREP00(REPID,"HDR",$c(27)_"E")
	s QDR="GVH-INV",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
	.i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
	..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
	..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
	.d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
	q
]]></Routine>
</Export>
