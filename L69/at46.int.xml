<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="at46" type="INT" languagemode="0" generated="1"><![CDATA[
at46 ; table EP_VisitTestSet
 q
 ; VISTS_DateOfSentSTM
CT078(vts) s vts=$g(vts) n (vts) s epis=$p(vts,"||"),result=""
 s x="" f  s x=$o(^TEPI(epis,9,x)) q:x=""  s xx=$g(^TEPI(epis,9,x)) d
 .i " S "[(" "_$p(xx,"\",9)_" "),$d(^TEPI(epis,9,x,"VTS",vts)) d
 ..i $l(result),result<$p(xx,"\",6) s result=$p(xx,"\",6)
 ..i result="" s result=$p(xx,"\",6)
 q result
 ; VISTS_TimeOfSentSTM
CT079(vts) s vts=$g(vts) n (vts) s epis=$p(vts,"||"),result=""
 s x="" f  s x=$o(^TEPI(epis,9,x)) q:x=""  s xx=$g(^TEPI(epis,9,x)) d
 .i " S "[(" "_$p(xx,"\",9)_" "),$d(^TEPI(epis,9,x,"VTS",vts)) d
 ..i $l(result),$p(result,",")<$p(xx,"\",6) s result=$p(xx,"\",6)_","_$p(xx,"\",7)
 ..i $l(result),$p(result,",")=$p(xx,"\",6),$p(result,",",2)<$p(xx,"\",7) s result=$p(xx,"\",6)_","_$p(xx,"\",7)
 ..i result="" s result=$p(xx,"\",6)_","_$p(xx,"\",7)
 q $p(result,",",2)
 ; VISTS_DateOfReceivedSTM
CT080(vts) s vts=$g(vts) n (vts) s epis=$p(vts,"||"),result=""
 s x="" f  s x=$o(^TEPI(epis,9,x)) q:x=""  s xx=$g(^TEPI(epis,9,x)) d
 .i " R P "[(" "_$p(xx,"\",9)_" "),$g(^TEPI(epis,9,x,"VTS",vts))="Y" d
 ..i $l(result),result<$p(xx,"\",11) s result=$p(xx,"\",11)
 ..i result="" s result=$p(xx,"\",11)
 q result
 ; VISTS_TimeOfReceivedSTM
CT081(vts) s vts=$g(vts) n (vts) s epis=$p(vts,"||"),result=""
 s x="" f  s x=$o(^TEPI(epis,9,x)) q:x=""  s xx=$g(^TEPI(epis,9,x)) d
 .i " R P "[(" "_$p(xx,"\",9)_" "),$g(^TEPI(epis,9,x,"VTS",vts))="Y" d
 ..i $l(result),$p(result,",")<$p(xx,"\",11) s result=$p(xx,"\",11)_","_$p(xx,"\",12)
 ..i $l(result),$p(result,",")=$p(xx,"\",11),$p(result,",",2)<$p(xx,"\",12) s result=$p(xx,"\",11)_","_$p(xx,"\",12)
 ..i result="" s result=$p(xx,"\",11)_","_$p(xx,"\",12)
 q $p(result,",",2)
 ; VISTS_MovementStatus
CT082(vts) s vts=$g(vts) n (vts) s epis=$p(vts,"||"),(res,res1,res2,res3)=""
 s x="" f  s x=$o(^TEPI(epis,9,x)) q:x=""  s xx=$g(^TEPI(epis,9,x)) d
 .i " W "[(" "_$p(xx,"\",9)_" "),$d(^TEPI(epis,9,x,"VTS",vts)) d
 ..i $l(res3),$p(res3,",")<$p(xx,"\",6) s res3=$p(xx,"\",6)_","_$p(xx,"\",7)
 ..i $l(res3),$p(res3,",")=$p(xx,"\",6),$p(res3,",",2)<$p(xx,"\",7) s res3=$p(xx,"\",6)_","_$p(xx,"\",7)
 ..i res3="" s res3=$p(xx,"\",6)_","_$p(xx,"\",7)
 .i " S "[(" "_$p(xx,"\",9)_" "),$d(^TEPI(epis,9,x,"VTS",vts)) d
 ..i $l(res2),$p(res2,",")<$p(xx,"\",6) s res2=$p(xx,"\",6)_","_$p(xx,"\",7)
 ..i $l(res2),$p(res2,",")=$p(xx,"\",6),$p(res2,",",2)<$p(xx,"\",7) s res2=$p(xx,"\",6)_","_$p(xx,"\",7)
 ..i res2="" s res2=$p(xx,"\",6)_","_$p(xx,"\",7)
 .i " R P "[(" "_$p(xx,"\",9)_" "),$g(^TEPI(epis,9,x,"VTS",vts))="Y" d
 ..i $l(res1),$p(res1,",")<$p(xx,"\",11) s res1=$p(xx,"\",11)_","_$p(xx,"\",12)
 ..i $l(res1),$p(res1,",")=$p(xx,"\",11),$p(res1,",",2)<$p(xx,"\",12) s res1=$p(xx,"\",11)_","_$p(xx,"\",12)
 ..i res1="" s res1=$p(xx,"\",11)_","_$p(xx,"\",12)
 i $l(res1) s res(+$p(res1,",",1),+$p(res1,",",2))="R"
 i $l(res2) s res(+$p(res2,",",1),+$p(res2,",",2))="S"
 i $l(res3) s res(+$p(res3,",",1),+$p(res3,",",2))="W"
 s x1=$o(res(""),-1) i $l(x1) s x2=$o(res(x1,""),-1) i $l(x2) s res=res(x1,x2)
 q res
WARD(epis) s result=""
 i $l(epis) d
 .s xx1=$$seldata^LVBEPVIS(epis,22),xx2=$$seldata^LVBCTHOS(xx1,17)
 .i xx2 s result=xx1
 q result
 ; create/delete episodes in/from TBIL
BILL(epis,rebill,check) n (epis,rebill,check) s epis=$g(epis),rebill=$g(rebill),check=$g(check)
 ; only for the central site
 s xCentral=$p($g(^CF("SM",1,"SITE")),"^",1),xCurrent=$p($g(^CF("SM",1,"SITE")),"^",2)
 i $l(xCurrent),xCentral'=xCurrent q
 s result=$$PriceAble^LVBCOM02(epis)
 s ok=$s('result:0,$p(result,$c(1),1)'=$p(result,$c(1),2):0,1:1)
 i 'ok d  q
 .i $l(check) s check="N" q
 . ; delete from pricing list
 .k ^TBIL(9,epis)
 . ; delete from billing list
 .s x1="" f  s x1=$o(^TBIL(2,x1)) q:x1=""  d
 ..s x2="" f  s x2=$o(^TBIL(2,x1,x2)) q:x2=""  k ^TBIL(2,x1,x2,epis)
 . ; delete from manual billing list
 .k ^TBIL(8,epis)
 i ok d  q
 . ; already on pricing list
 .i $d(^TBIL(9,epis)) s ok=0 s:$l(check) check="YP" q
 . ; already on billing list
 .d  i 'ok q
 ..s x1="" f  s x1=$o(^TBIL(2,x1)) q:x1=""  d  i 'ok q
 ...s x2="" f  s x2=$o(^TBIL(2,x1,x2)) q:x2=""  i $d(^TBIL(2,x1,x2,epis)) s ok=0 s:$l(check) check="YB" q
 . ; already invoiced
 .i $p($g(^TEPI(epis,7)),"\",3)'="" s ok=0 s:$l(check) check="YI" q
 . ; ready to be billed
 .i $l(check) s check="Y"
 .i '$l(check) s $p(^TBIL(9,epis),"\",1)=+$h i rebill="Y" s $p(^TBIL(9,epis),"\",2)="Y"
 q
WKS(epis,ts,cnt,type) n (epis,ts,cnt,type) s epis=$g(epis),ts=$g(ts),cnt=$g(cnt),type=$g(type)
 s xCentral=$p($g(^CF("SM",1,"SITE")),"^",1),xCurrent=$p($g(^CF("SM",1,"SITE")),"^",2)
 s site=$$site^LVBVISTS(epis_"||"_ts_"||"_cnt)
 i $l(epis),$l(ts),$l(cnt) d
 .s ws="" f  s ws=$o(^TTAB("TS",ts,6,ws)) q:ws=""  s ok=$s(type="DELETE":"del",type="POOL":"pool",1:"add") d
 ..s xSite=$$seldata^LVBWKS(ws,30),xManual=$$seldata^LVBWKS(ws,17)
 .. ; check if worksheet is active
 ..i $$seldata^LVBWKS(ws,4)'="Y" q
 .. ; one site only
 ..i (ok="add")!(ok="pool"),$$seldata^LVBWKS(ws,11)="Y",$l(xCurrent),site'=xCurrent s ok="del"
 .. ; specific site
 ..i (ok="add")!(ok="pool"),$l(xSite),xSite'=site s ok="del"
 ..i (ok="add")!(ok="pool"),$l(xSite),$l(xCurrent),xSite'=xCurrent s ok="del"
 .. ; check reason
 ..i (ok="add")!(ok="pool") s reason=$$seldata^LVBVISTS(epis_"||"_ts_"||"_cnt,58) i $l(reason),$$seldata^LVBCTR(reason,5)'="Y" s ok="del"
 ..i (ok="add")!(ok="pool") s reason=$$seldata^LVBVISTS(epis_"||"_ts_"||"_cnt,58) i $l(reason),$$seldata^LVBCTR(reason,5)="Y" s ok="add"
 .. ; check site for manual WS
 ..i ok="add",xManual="Y",$l(xCurrent),$l(site),xCurrent'=site s ok="pool"
 .. ; delete epis/test from the WorkSheet
 ..i (ok="del")!(ok="pool") d
 ... ; delete from manual worksheet
 ...i xManual="Y" s x=$$delete^LVBWKSOE(ws_"||"_epis_"||"_ts_"||"_cnt)
 ... ; delete from automatic worksheet
 ...i ok="del" s wsno="" f  s wsno=$o(^TEPI(epis,"WS",ws,wsno)) q:wsno=""  i $d(^TWKS(ws,2,wsno,"EP",epis,"TEST",ts,cnt)) d
 ....i $$delete^LVBWKSRT(ws_"||"_wsno_"||"_epis_"||"_ts_"||"_cnt)
 ....i $d(^TWKS(ws,2,wsno,"EP",epis))<10,$$delete^LVBWKSRE(ws_"||"_wsno_"||"_epis)
 .. ; add epis/test to the WorkSheet
 ..i ok="add" d
 ... ; add to manual worksheet
 ...i xManual="Y" d  q
 .... ; check if epis/test already assigned to a WorkSheet
 ....s wsno="" f  s wsno=$o(^TEPI(epis,"WS",ws,wsno)) q:wsno=""  i $d(^TWKS(ws,2,wsno,"EP",epis,"TEST",ts,cnt)) s ok="" q
 ....i ok="add" k PLIST s PLIST(2)=epis_"||"_ts_"||"_cnt,x=$$insert^LVBWKSOE(ws)
 ... ; add to automatic worksheet
 ... ; check if epis/test already assigned to a WorkSheet
 ...s wsno="" f  s wsno=$o(^TEPI(epis,"WS",ws,wsno)) q:wsno=""  i $d(^TWKS(ws,2,wsno,"EP",epis,"TEST",ts,cnt)) d
 ....s ok="" i $l($p($g(^TWKS(ws,2,wsno)),"\",5)),$p($g(^TWKS(ws,2,wsno)),"\",5)'=site d  s ok="add"
 .....i $$delete^LVBWKSRT(ws_"||"_wsno_"||"_epis_"||"_ts_"||"_cnt)
 .....i $d(^TWKS(ws,2,wsno,"EP",epis))<10,$$delete^LVBWKSRE(ws_"||"_wsno_"||"_epis)
 ...i ok="add",$$add^LVBWKSRE(ws,,epis_"||"_ts_"||"_cnt)
 q
 ; check if referal test should be add to standard letters list
LETTER(epis,ts,tscnt,type) n (epis,ts,tscnt,type) s epis=$g(epis),ts=$g(ts),tscnt=$g(tscnt),type=$g(type)
 ; only for the current site
 s xCentral=$p($g(^CF("SM",1,"SITE")),"^",1),xCurrent=$p($g(^CF("SM",1,"SITE")),"^",2)
 s ok=$s(type="DELETE":"del",1:"add")
 i ok="add",$l(xCurrent),$$site^LVBVISTS(epis_"||"_ts_"||"_tscnt)'=xCurrent s ok="del"
 i ok="add",$p(^TEPI(epis,1,ts,tscnt),"\",53)'="Y" d
 .i '$$select^LVBCTTS(ts,"Y") d
 ..s $p(^TEPI(epis,1,ts,tscnt),"\",53)="Y"
 ..i $p(PLIST(39),$c(1),1)="Y" d
 ...s let="" f  s let=$o(^TTAB("SL",let)) q:let=""  d
 ....i $d(^TTAB("SL",let,"TEST",ts)) d
 ..... ; insert patient
 .....i $$select^LVBLTPT(let_"||"_epis) d
 ......k PLIST s PLIST(2)=epis,PLIST=$o(PLIST(""),-1) i $$insert^LVBLTPT(let)
 ..... ; insert patient test set
 .....i $$select^LVBLTPTT(let_"||"_epis_"||"_ts_"||"_tscnt) d
 ......k PLIST s PLIST(2)=ts,PLIST(3)=tscnt,PLIST=$o(PLIST(""),-1) i $$insert^LVBLTPTT(let_"||"_epis)
 i ok="del" d
 .i $$seldata^LVBCTTS(ts,39)="Y" d
 ..s $p(^TEPI(epis,1,ts,tscnt),"\",53)="N"
 ..s let="" f  s let=$o(^TTAB("SL",let)) q:let=""  d
 ...i $d(^TTAB("SL",let,"TEST",ts)),$d(^TTLT(let,epis,ts,tscnt)) d
 ....i '$$delete^LVBLTPTT(let_"||"_epis_"||"_ts_"||"_tscnt),$d(^TTLT(let,epis))<10,'$$delete^LVBLTPT(let_"||"_epis)
 q
 ; set/delete TTRQ
TTRQ(epis,ts,tscnt,mach,type) n (epis,ts,tscnt,mach,type) s epis=$g(epis),ts=$g(ts),tscnt=$g(tscnt),mach=$g(mach),type=$g(type)
 ; only for the current site
 s xCentral=$p($g(^CF("SM",1,"SITE")),"^",1),xCurrent=$p($g(^CF("SM",1,"SITE")),"^",2)
 s ok=$s(type="DELETE":"del",1:"add")
 s xTsSite=$$site^LVBVISTS(epis_"||"_ts_"||"_tscnt)
 i $l(xCurrent),xTsSite'=xCurrent s ok="del"
 i ok="del" d
 .k ^TTRQ(ts,epis,tscnt)
 .k ^TTRQi(epis,ts,tscnt)
 .s mi="" f  s mi=$o(^TMIF(mi)) q:mi=""  i $d(^TMIF(mi,0,ts)) d
 ..i $l(mach),mach'=mi q
 ..s xMiSite=$p(^TMIF(mi),"\",18)
 ..i $l(xCurrent),xMiSite'=xCurrent q
 ..i '$l(xCurrent),$l(xMiSite),xMiSite'=xTsSite q
 ..i '$d(^TMIF(mi,20,epis)),$d(^TMPmif(mi,20,epis)) m ^TMIF(mi,20,epis)=^TMPmif(mi,20,epis) k ^TMPmif(mi,20,epis)
 ..i $d(^TMIF(mi,20,epis,"TS",ts,tscnt)) d
 ...k ^TMIF(mi,20,epis,"TS",ts,tscnt)
 ...s $p(^TMIF(mi,20,epis),"\",1)=$$zts()
 i ok="add" d
 .i $$getall1^LVBCTTC(ts,,,,"Y")
 .i '$l(mach) d
 ..s cnt="" f  s cnt=$o(PLIST(cnt)) q:cnt=""  d
 ...s tc=$p(PLIST(cnt),$c(2),1) i '$l(tc) q
 ...i $d(^TEPI(epis,1,ts,tscnt,"DATA",tc)),$p($g(^TEPI(epis,1,ts,tscnt,"DATA",tc)),"\",10)'="Y",$d(^TTRQ(ts,epis,tscnt,tc)) d
 ....k ^TTRQ(ts,epis,tscnt,tc),^TTRQi(epis,ts,tscnt,tc)
 ....i $d(^TTRQ(ts,epis,tscnt))\10=0 k ^TTRQ(ts,epis,tscnt),^TTRQi(epis,ts,tscnt)
 ...i $d(^TEPI(epis,1,ts,tscnt,"DATA",tc)),$p($g(^TEPI(epis,1,ts,tscnt,"DATA",tc)),"\",10)="Y",'$d(^TTRQ(ts,epis,tscnt,tc)) d
 ....s (^TTRQ(ts,epis,tscnt,tc),^TTRQi(epis,ts,tscnt,tc))=""
 ....s (^TTRQ(ts,epis,tscnt),^TTRQi(epis,ts,tscnt))=$h
 ...i '$d(^TEPI(epis,1,ts,tscnt,"DATA",tc)),'$d(^TTRQ(ts,epis,tscnt,tc)) d
 ....s (^TTRQ(ts,epis,tscnt,tc),^TTRQi(epis,ts,tscnt,tc))=""
 ....s (^TTRQ(ts,epis,tscnt),^TTRQi(epis,ts,tscnt))=$h
 .s mi="" f  s mi=$o(^TMIF(mi)) q:mi=""  i $d(^TMIF(mi,0,ts)) d
 ..i $l(mach),mach'=mi q
 ..s xMiSite=$p(^TMIF(mi),"\",18)
 ..i $l(xCurrent),xMiSite'=xCurrent q
 ..i '$l(xCurrent),$l(xMiSite),xMiSite'=xTsSite q
 ..i '$d(^TMIF(mi,20,epis)),$d(^TMPmif(mi,20,epis)) m ^TMIF(mi,20,epis)=^TMPmif(mi,20,epis) k ^TMPmif(mi,20,epis)
 ..s cnt="" f  s cnt=$o(PLIST(cnt)) q:cnt=""  d
 ...s tc=$p(PLIST(cnt),$c(2),1) i '$l(tc) q
 ...i $d(^TEPI(epis,1,ts,tscnt,"DATA",tc)),$p($g(^TEPI(epis,1,ts,tscnt,"DATA",tc)),"\",10)'="Y",$d(^TMIF(mi,20,epis,"TS",ts,tscnt,tc)) d
 ....k ^TMIF(mi,20,epis,"TS",ts,tscnt,tc)
 ....s $p(^TMIF(mi,20,epis),"\",1)=$$zts()
 ...i $d(^TEPI(epis,1,ts,tscnt,"DATA",tc)),$p($g(^TEPI(epis,1,ts,tscnt,"DATA",tc)),"\",10)="Y",'$d(^TMIF(mi,20,epis,"TS",ts,tscnt,tc)) d
 ....s ^TMIF(mi,20,epis,"TS",ts,tscnt,tc)=""
 ....s $p(^TMIF(mi,20,epis),"\",1)=$$zts()
 ...i '$d(^TEPI(epis,1,ts,tscnt,"DATA",tc)),'$d(^TMIF(mi,20,epis,"TS",ts,tscnt,tc)) d
 ....s ^TMIF(mi,20,epis,"TS",ts,tscnt,tc)=""
 ....s $p(^TMIF(mi,20,epis),"\",1)=$$zts()
 q
zts() q $h_"."_$e($p($zts,".",2),1,3)
 ; post insert trigger
POFILINS i $$check^LVBCOM02(%d(1),46,"Y")
 n (%d) s x="" f  s x=$o(%d(x)) q:x=""  s %d(x)=$p(%d(x),$c(1))
 ; Only if Patient Status 0 - Fully Entered
 ;                        1 - PreEntered
 ;                        9 - BB Units
 i $l(%d(0))," 0 1 9 "[(" "_$p(^TEPI(%d(0)),"\",44)_" ") d
 . ; set/delete TTRQ
 .d TTRQ(%d(0),%d(3),%d(2),,$s($p($g(%d(33)),$c(1))="A":"DELETE",1:""))
 . ; add to standard letters
 .d LETTER(%d(0),%d(3),%d(2))
 . ; insert WS
 .d WKS(%d(0),%d(3),%d(2))
 .i '$p(^TEPI(%d(0)),"\",44)
 .i  i $p($g(^TEPI(%d(0),1,%d(3),%d(2))),"\",34)'="Y"
 .i  i $p($g(^TTAB("TS",%d(3))),"\",13)'="N"
 .i  d BILL(%d(0))
 s exclude=$$seldata^LVBCTTS(%d(3),57) i exclude'="Y" d OUT^LVBEDI00(%d(0),46,%d(1),1,%d(1),"Add")
 i '$d(^TMOVE("TEMP","46I",%d(0),%d(1))) d  d SetAll^LVBCOM50(%d(0),"46I",.%d,%d(1))
 .s epis=%d(0) n (epis)
 .i '$d(^TMOVE("TEMP","42I",epis)),'$$select^LVBEPVIS(epis,"Y") d
 ..f j=11,13,62 i $g(PLIST(j)) s PLIST(j)=PLIST(j)\60
 ..d SetAll^LVBCOM50(epis,"42I",.PLIST)
 ; check if need to close DFT
 s mrn=$$mrn^LVBEPVIS($p(%d(1),"||"))
 i $l(mrn),$l($g(%d(42))),$l($g(%d(44))),$l($g(%d(34))) d
 .s max=$$seldata^LVBCTDF(%d(42),4)
 .s full=1 f x=1:1:max i '$d(^TEPIi("DFT",mrn,%d(42),%d(44),%d(34),"SEQ",x)) s full=0 q
 .i full s $p(^TEPIi("DFT",mrn,%d(42),%d(44),%d(34)),"\",1)="Y"
 i $l($p($g(^CF("SM",1)),"^",28)),$l($t(@("POFILINS^"_$zn_$p(^CF("SM",1),"^",28)))) d @("POFILINS^"_$zn_$p(^CF("SM",1),"^",28))
 q
checkFax(type,client,test) n (type,client,test) s ok=1
 s type=$g(type),client=$g(client),test=$g(test)
 i type="DR" d
 .i '$l($p(^TTAB("DR",client),"\",6)) s ok=0 q
 .i $p(^TTAB("DR",client,0),"\",9)="Y" s ok=0 q
 .i $d(^TTAB("DR",client,2,test)) s ok=0 q
 i type="RH" d
 .i '$l($p(^TTAB("RH",client),"\",11)) s ok=0 q
 .i $p(^TTAB("RH",client),"\",18)="Y" s ok=0 q
 .i $d(^TTAB("RH",client,2,test)) s ok=0 q
 q ok
PREFILUPD n (%e,%old)
 l +^DBLock("Printing",$p(%e(1),"||"))
 i $$select^LVBVISTS(%e(1),"Y")
 k %old m %old=PLIST
 q
 ; post update trigger
POFILUPD i $$check^LVBCOM02(%d(1),46)
 n (%d,%old)
 ; Only if Patient Status 0 - Fully Entered
 ;                        1 - PreEntered
 ;                        9 - BB Units
 i $l(%d(0))," 0 1 9 "[(" "_$p(^TEPI(%d(0)),"\",44)_" ") d
 .d WKS(%d(0),%d(3),%d(2),$s($p($g(%d(33)),$c(1))="A":"POOL",1:""))
 . ; add to standard letters
 .d LETTER(%d(0),%d(3),%d(2))
 .i '$p(^TEPI(%d(0)),"\",44) d
 ..i $p($g(^TEPI(%d(0),1,%d(3),%d(2))),"\",34)'="Y"
 ..i  i $p($g(^TTAB("TS",%d(3))),"\",13)'="N"
 ..i  i $p($g(%d(33)),$c(1))="A",$p($g(%old(33)),$c(1))'="A" d BILL(%d(0))
 i '$d(^TMOVE("TEMP","46U",%d(0),%d(1))) d
 .s change=0
 .i 'change,$o(%d(""),-1)'=$o(%old(""),-1) s change=1
 .i 'change f j1=1:1:$o(%d(""),-1) d  q:change
 ..i $g(%d(j1))'=$g(%old(j1)) s change=1 q
 ..f j2=1:1:$o(%d(j1,""),-1) i $g(%d(j1,j2))'=$g(%old(j1,j2)) s change=1 q
 .i change d  d SetAll^LVBCOM50(%d(0),"46U",.%d,%d(1))
 ..s epis=%d(0) n (epis)
 ..i '$d(^TMOVE("TEMP","42I",epis)),'$$select^LVBEPVIS(epis,"Y") d
 ...f j=11,13,62 i $g(PLIST(j)) s PLIST(j)=PLIST(j)\60
 ...d SetAll^LVBCOM50(epis,"42I",.PLIST)
 i $p($g(%d(33)),$c(1))="A",$p($g(%old(33)),$c(1))'="A",$$clean^LVBVISMV(%d(0),%d(1))
 ; Only if Patient Status 0 - Fully Entered
 ;                        1 - PreEntered
 ;                        9 - BB Units
 i $l(%d(0))," 0 1 9 "[(" "_$p(^TEPI(%d(0)),"\",44)_" ") d
 . ; update patient/pack BG
 .i $p($g(%d(74)),$c(1))="Y" d updateBG(%d(0),%d(3),%d(2),"D") q
 .i $p($g(%d(33)),$c(1))'="A" d updateBG(%d(0),%d(3),%d(2),"D")
 .i $p($g(%d(33)),$c(1))="A",$p($g(%d(67)),$c(1))="Y" d updateBG(%d(0),%d(3),%d(2),"D")
 .i $p($g(%d(33)),$c(1))="A",$p($g(%d(67)),$c(1))'="Y" d updateBG(%d(0),%d(3),%d(2))
 . ; set/delete TTRQ
 .d TTRQ(%d(0),%d(3),%d(2),,$s($p($g(%d(33)),$c(1))="A":"DELETE",1:""))
 ; only for the current site
 s xCentral=$p($g(^CF("SM",1,"SITE")),"^",1),xCurrent=$p($g(^CF("SM",1,"SITE")),"^",2)
 i $l(xCurrent),$$site^LVBVISTS(%d(1))'=xCurrent g POFILUPDend
 ; Only if Patient Status 0 - Fully Entered
 ;                        9 - BB Units
 i $l(%d(0))," 0 9 "[(" "_$p(^TEPI(%d(0)),"\",44)_" ") d
 .s ffAdd="Y",ffDel="Y" i $$seldata^MVBCFLAB(1,155)'="Y" d
 ..s ffAdd="" i $p($g(%d(33)),$c(1))="A" s ffAdd="Y"
 ..s ffDel="" i $p($g(%d(33)),$c(1))'="A" s ffDel="Y"
 .i $p($g(%d(67)),$c(1))="Y" s ffDel="Y",ffAdd=""
 .i $p($g(%d(74)),$c(1))="Y" s ffDel="Y",ffAdd=""
 . ; if result not authorised delete from BB register 
 .i ffDel="Y" d BBRegistryDel(%d(0),%d(3),%d(2))
 . ; if result authorised create BB register 
 .i ffAdd="Y" d BBRegistryAdd(%d(0),%d(3),%d(2),%d(7),%d(8),%d(9))
 .i '$p(^TEPI(%d(0)),"\",44) d
 ..s ConfCR="",vtsRID=%d(0)_"||"_%d(3)_"||"_%d(2)
 ..s site=$$site^LVBVISTS(vtsRID),Confidential=$$conf^LVBVISTS(vtsRID)
 ..i " Y P "[(" "_Confidential_" ") d
 ...i '$l(ConfCR) s cr="" f  s cr=$o(^TTAB("CR",cr)) q:cr=""  d:$p(^(cr),"\",3)="Y"  i $l(ConfCR) q
 ....i $l(site),$p(^(cr),"\",7)'=site q
 ....s ConfCR=cr
 ...i '$l(ConfCR) s cr="" f  s cr=$o(^TTAB("CR",cr)) q:cr=""  d:$p(^(cr),"\",3)="Y"  i $l(ConfCR) q
 ....s ConfCR=cr
 ..s ts=$g(%d(3)) q:ts=""  s billtype=$p($g(^TTAB("TS",ts)),"\",13)
 ..s copyto=$p(^CF("LAB",1),"^",29),FaxConfidential=$p(^CF("LAB",1),"^",26)
 ..s x=$$doc(%d(0)),cour1=$p(x,"^"),doc=$p(x,"^",2),docname=$p(x,"^",3)
 ..s x=$$hosp(%d(0)),cour2=$p(x,"^"),hosp=$p(x,"^",2),hospname=$p(x,"^",3)
 .. ; find if report should be faxed
 ..s (fax1,fax2)="" d
 ... ; check fax via patient - fax
 ...i (fax1=""),$p(^TEPI(%d(0)),"\",43)="Y" d
 ....i $l(doc),$$checkFax("DR",doc,%d(3)) s fax1=$p(^TTAB("DR",doc),"\",6)_"\"_docname
 ...i (fax2=""),$p(^TEPI(%d(0)),"\",62)="Y" d
 ....i $l(hosp),$$checkFax("RH",hosp,%d(3)) s fax2=$p(^TTAB("RH",hosp),"\",11)_"\"_hospname
 ... ; check fax via patient - priority
 ...i (fax1="")!(fax2="") s priority=$p(^TEPI(%d(0)),"\",15) d
 ....n (priority,fax1,fax2,doc,hosp,docname,hospname,%d)
 ....i $$seldata^LVBCTPR(priority,6)="Y" d
 .....i $l(doc),$$checkFax("DR",doc,%d(3)) s fax1=$p(^TTAB("DR",doc),"\",6)_"\"_docname
 .....i $l(hosp),$$checkFax("RH",hosp,%d(3)) s fax2=$p(^TTAB("RH",hosp),"\",11)_"\"_hospname
 ... ; check fax via t/s - priority
 ...i (fax1="")!(fax2="") s priority=$p(%d(13),$c(1)) d
 ....n (priority,fax1,fax2,doc,hosp,docname,hospname,%d)
 ....i $$seldata^LVBCTPR(priority,6)="Y" d
 .....i $l(doc),$$checkFax("DR",doc,%d(3)) s fax1=$p(^TTAB("DR",doc),"\",6)_"\"_docname
 .....i $l(hosp),$$checkFax("RH",hosp,%d(3)) s fax2=$p(^TTAB("RH",hosp),"\",11)_"\"_hospname
 ..i copyto="N" d
 ... ; setup family doctor
 ...s mrn=$$mrn^LVBEPVIS(%d(0)) i $l(mrn) s fd=$p($g(^TDEB(mrn)),"\",5) i $l(fd) d
 ....i fd=doc q
 ....s (stop,x)=0 f  s x=$o(^TEPI(%d(0),5,x)) q:x=""  i $p(^(x),"\")=fd s stop=1 q
 ....i 'stop i $l($p($g(^TTAB("DR",fd,0)),"\",1)) s cour1(0)=$p(^TTAB("DR",fd,0),"\",1),doc(0)=fd
 ... ; create list of copyto doctors
 ...s (x,cnt)=0 f  s x=$o(^TEPI(%d(0),5,x)) q:x=""  d
 ....s xx1=$p(^(x),"\"),xx15=$p(^(x),"\",15),xx3=$tr($p(^(x),"\",2,7),"\","^"),xx2=xx3_"^"_$p(^(x),"\",12)
 ....s cour=$p(^(x),"\",8),fax=$p(^(x),"\",10),fnum=$p(^(x),"\",9)
 ....i $l(xx1) d  q
 .....s cour=$p($g(^TTAB("DR",xx1,0)),"\",1) i $l(cour) d
 ......s fnum=$p($g(^TTAB("DR",xx1)),"\",6) i '$$checkFax("DR",xx1,%d(3)) s (fnum,fax)=""
 ......s cnt=$o(cour1(""),-1)+1,cour1(cnt)=cour,doc(cnt)=xx1_"\"_fax_"\"_fnum_"\"_xx1
 ....i $l(xx15) d  q
 .....s cour=$p($g(^TTAB("RH",xx15)),"\",12) i $l(cour) d
 ......s fnum=$p($g(^TTAB("RH",xx15)),"\",11) i '$$checkFax("RH",xx15,%d(3)) s (fnum,fax)=""
 ......s cnt=$o(cour2(""),-1)+1,cour2(cnt)=cour,hosp(cnt)=xx15_"\"_fax_"\"_fnum
 ....i $l(cour) s cnt=$o(cour1(""),-1)+1,cour1(cnt)=cour,doc(cnt)="xxx-"_doc_"-"_xx2_"\"_fax_"\"_fnum_"\xxx-"_doc_"-"_xx3
 ..s ts=$g(%d(3)) q:ts=""  s tsname=$p($g(^TTAB("TS",ts)),"\",17)
 ..i $p($g(%old(33)),$c(1))="A",$p($g(%d(33)),$c(1))'="A" d
 ... ; delete doctors print-q
 ...i $l(doc),$l(cour1) d
 .... ; delete referal doctors print-q
 ....k ^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour1),"D",doc,%d(0),%d(3),%d(2))
 .... ; delete doctors fax-q
 ....k ^TRPT("FAX",cour1,"D",doc,%d(0),%d(3),%d(2))
 ....i $d(^TRPT("FAX",cour1,"D",doc,%d(0)))\10=0 k ^TRPT("FAX",cour1,"D",doc,%d(0))
 ... ; delete copyto doctors print-q/fax-q
 ...f j=1:1 q:'$d(cour1(j))  d
 ....k ^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour1(j)),"D",$p(doc(j),"\"),%d(0),%d(3),%d(2))
 ....k ^TRPT("FAX",cour1(j),"D",$p(doc(j),"\",4),%d(0),%d(3),%d(2))
 ....i $d(^TRPT("FAX",cour1(j),"D",$p(doc(j),"\",4),%d(0)))=1 k ^TRPT("FAX",cour1(j),"D",$p(doc(j),"\",4),%d(0))
 ...i $l(hosp),$l(cour2) d
 .... ; delete patient location print-q
 ....k ^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour2),"H",hosp,%d(0),%d(3),%d(2))
 .... ; delete patient location fax-q
 ....k ^TRPT("FAX",cour2,"H",hosp,%d(0),%d(3),%d(2))
 ....i $d(^TRPT("FAX",cour2,"H",hosp,%d(0)))\10=0 k ^TRPT("FAX",cour2,"H",hosp,%d(0))
 ... ; delete copyto locations print-q/fax-q
 ...f j=1:1 q:'$d(cour2(j))  d
 ....k ^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour2(j)),"H",$p(hosp(j),"\"),%d(0),%d(3),%d(2))
 ....k ^TRPT("FAX",cour2(j),"H",$p(hosp(j),"\"),%d(0),%d(3),%d(2))
 ....i $d(^TRPT("FAX",cour2(j),"H",$p(hosp(j),"\"),%d(0)))=1 k ^TRPT("FAX",cour2(j),"H",$p(hosp(j),"\"),%d(0))
 .. ; if test set was authorised set EDI triggers
 ..i $p($g(%d(33)),$c(1))="A",$p($g(%old(33)),$c(1))'="A" s exclude=$$seldata^LVBCTTS(%d(3),57) i exclude'="Y" d OUT^LVBEDI00(%d(0),46,%d(1),1,%d(1),"Auth")
 .. ; if test set was Deauthorised set EDI triggers
 ..i $p($g(%d(33)),$c(1))'="A",$p($g(%old(33)),$c(1))="A" s exclude=$$seldata^LVBCTTS(%d(3),57) i exclude'="Y" d OUT^LVBEDI00(%d(0),46,%d(1),1,%d(1),"DeAuth")
 .. ; if test set was Updated set EDI triggers
 ..i $p($g(%d(33)),$c(1))'="A",$p($g(%old(33)),$c(1))'="A" s exclude=$$seldata^LVBCTTS(%d(3),57) i exclude'="Y" d OUT^LVBEDI00(%d(0),46,%d(1),1,%d(1),"Update")
 .. ; update MEDTRAK indexes
 ..i $p($g(%d(33)),$c(1))="A",$p($g(%old(33)),$c(1))'="A" d
 ...s x=$g(^TEPIi("MEDTRAKi",%d(0),%d(3),%d(2))) i $l(x) d
 ....f j=1:1:6 s @("x"_j)=$p(x,"\",j)
 ....i $l(x1),$l(x2),$l(x3),$l(x4),$l(x5),$l(x6),$d(^TEPIi("MEDTRAK",x1,x2,x3,x4,x5,x6,%d(3),%d(2),%d(0))) s ^TEPIi("MEDTRAK",x1,x2,x3,x4,x5,x6,%d(3),%d(2),%d(0))=2
 ..i $p($g(%d(33)),$c(1))'="A",$p($g(%old(33)),$c(1))="A" d
 ...s x=$g(^TEPIi("MEDTRAKi",%d(0),%d(3),%d(2))) i $l(x) d
 ....f j=1:1:6 s @("x"_j)=$p(x,"\",j)
 ....i $l(x1),$l(x2),$l(x3),$l(x4),$l(x5),$l(x6),$d(^TEPIi("MEDTRAK",x1,x2,x3,x4,x5,x6,%d(3),%d(2),%d(0))) s ^TEPIi("MEDTRAK",x1,x2,x3,x4,x5,x6,%d(3),%d(2),%d(0))=4
 .. ; if test set was authorised set index for reports
 ..i $p($g(%d(33)),$c(1))="A",$p($g(%old(33)),$c(1))'="A",tsname'="-" d
 ...s epis=$p(%d(1),"||",1)
 ...s edi=$$seldata^LVBEPVIS(epis,67),mrn=$$seldata^LVBEPVIS(epis,20),dd=$$seldata^LVBEPVIS(epis,165),ReportSuppress=$$seldata^LVBCTVA($$seldata^LVBEPVIS(epis,25),5)
 ...i $p(^CF("LAB",1),"^",105)="Y",$l(dd),'$$DDS^LVBVIS21(,,mrn,,,,epis) k cour2 s cour2=""
 ... ; reports suppressed by visit/action
 ...i $p(ReportSuppress,$c(1))="Y" q
 ...s va="" f  s va=$o(^TEPI(epis,"VA",va)) q:va=""  s ReportSuppress=$$seldata^LVBCTVA(va,5) i $p(ReportSuppress,$c(1))="Y" q
 ...i $p(ReportSuppress,$c(1))="Y" q
 ... ; set-up doctors print-q/fax-q
 ...i $l(doc),$l(cour1) d
 ....i $l(fax1) d
 .....i " Y P "[(" "_Confidential_" "),FaxConfidential'="Y" q
 .....s ^TRPT("FAX",cour1,"D",doc,%d(0),%d(3),%d(2))=""
 .....s ^TRPT("FAX",cour1,"D",doc,%d(0))=fax1
 .... ; non - printable
 ....i '$l(ConfCR),$$cour(cour1)="Y" q
 ....s $p(^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour1),"D",doc,%d(0),%d(3),%d(2)),"\",5)=$g(%d(9))
 ... ; set-up copyto doctors print-q/fax-q
 ...f j=1:1 q:'$d(cour1(j))  d
 ....i $p(doc(j),"\",2)="Y" d
 .....i " Y P "[(" "_Confidential_" "),FaxConfidential'="Y" q
 .....s xx=$p(doc(j),"\"),fax=$p(doc(j),"\",3)
 .....i $p(xx,"-")="xxx" s name=$p($p(xx,"-",3),"^")
 .....e  i $l(xx),$d(^TTAB("DR",xx)) s name=$p(^TTAB("DR",xx,0),"\",11)_" "_$e($p(^TTAB("DR",xx),"\",2))_"."_$p(^TTAB("DR",xx),"\",1)
 .....s ^TRPT("FAX",cour1(j),"D",$p(doc(j),"\",4),%d(0),%d(3),%d(2))=""
 .....s ^TRPT("FAX",cour1(j),"D",$p(doc(j),"\",4),%d(0))=fax_"\"_name
 .... ; non - printable
 ....i '$l(ConfCR),$$cour(cour1(j))="Y" q
 ....s $p(^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour1(j)),"D",$p(doc(j),"\"),%d(0),%d(3),%d(2)),"\",5)=$g(%d(9))
 ... ; set-up patient location print-q/fax-q
 ...i $l(hosp),$l(cour2) d
 ....i $l(fax2) d
 .....i " Y P "[(" "_Confidential_" "),FaxConfidential'="Y" q
 .....s ^TRPT("FAX",cour2,"H",hosp,%d(0),%d(3),%d(2))=""
 .....s ^TRPT("FAX",cour2,"H",hosp,%d(0))=fax2
 .... ; non - printable
 ....i '$l(ConfCR),$$cour(cour2)="Y" q
 ....s $p(^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour2),"H",hosp,%d(0),%d(3),%d(2)),"\",5)=$g(%d(9))
 ... ; set-up copyto locations print-q/fax-q
 ...f j=1:1 q:'$d(cour2(j))  d
 ....i $p(hosp(j),"\",2)="Y" d
 .....i " Y P "[(" "_Confidential_" "),FaxConfidential'="Y" q
 .....s xx=$p(hosp(j),"\"),fax=$p(hosp(j),"\",3)
 .....i $p(xx,"-")="xxx" s name=$p($p(xx,"-",3),"^")
 .....e  i $l(xx),$d(^TTAB("RH",xx)) s name=$p(^TTAB("RH",xx),"\",1)
 .....s ^TRPT("FAX",cour2(j),"H",$p(hosp(j),"\"),%d(0),%d(3),%d(2))=""
 .....s ^TRPT("FAX",cour2(j),"H",$p(hosp(j),"\"),%d(0))=fax_"\"_name
 .... ; non - printable
 ....i '$l(ConfCR),$$cour(cour2(j))="Y" q
 ....s $p(^TRPT("DR",$s($l(ConfCR):ConfCR,1:cour2(j)),"H",$p(hosp(j),"\"),%d(0),%d(3),%d(2)),"\",5)=$g(%d(9))
 . ; clear ALL Queues
 .i $p($g(%d(33)),$c(1))'="A",$p($g(%old(33)),$c(1))="A",$$delall^LVBVISTQ(%d(1))
 . ; if test set was Deauthorised set TEPIi (DeAuthorised) index
 .i $p($g(%d(33)),$c(1))'="A",$p($g(%old(33)),$c(1))="A" s ^TEPIi("DE-AUTHORISED",+$h,%d(0),%d(3),%d(2))=$g(%old(7))_"\"_$g(%old(9))_"\"_$$user^MVBSSUSR($j)
 . ; send test set to phone queue
 .i $p($g(%d(33)),$c(1))="A",$p($g(%old(33)),$c(1))'="A" d
 ..s prio=$$priority^LVBVISTS(%d(0)_"||"_%d(3)_"||"_%d(2)) i $$seldata^LVBCTPR(prio,8)="Y" d
 ...i $$open^LVBCTVQ()
 ...s PhoneQueue="" f  q:$$fetch^LVBCTVQ()  i PLIST(7)="P" s PhoneQueue=PLIST(2) q
 ...i $$close^LVBCTVQ()
 ...i $l(PhoneQueue) d
 ....s x=$o(^TEPIi("QUEUE","RQ",PhoneQueue,%d(0),%d(3),%d(2),""))
 ....i $l(x),'$$select^LVBVISTQ(%d(0)_"||"_%d(3)_"||"_%d(2)_"||RQ||"_PhoneQueue),'$l(PLIST(7)) q
 ....k PLIST
 ....s PLIST(2)="RQ",PLIST(3)=$o(^TEPI(%d(0),1,%d(3),%d(2),"QUEUE","RQ",""),-1)+1
 ....s PLIST(4)=+$h,PLIST(5)=$p($h,",",2),PLIST(6)=$$user^MVBSSUSR(),PLIST(10)=PhoneQueue
 ....i '$$insert^LVBVISTQ(%d(0)_"||"_%d(3)_"||"_%d(2))
POFILUPDend 
 i $l($p($g(^CF("SM",1)),"^",28)),$l($t(@("POFILUPD^"_$zn_$p(^CF("SM",1),"^",28)))) d @("POFILUPD^"_$zn_$p(^CF("SM",1),"^",28))
 l -^DBLock("Printing",%d(0))
 q
 ; pre delete trigger
PREFDEL n (%e,%old,BillingFlag)
 i $$select^LVBVISTS(%e(1),"Y")
 k %old m %old=PLIST
 i $$delete^LVBCOM02(%old(1),46,"3")
 d TTRQ($p(%old(1),"||"),$p(%old(1),"||",2),$p(%old(1),"||",3),,"DELETE")
 d WKS($p(%old(1),"||"),$p(%old(1),"||",2),$p(%old(1),"||",3),"DELETE")
 d LETTER($p(%old(1),"||"),$p(%old(1),"||",2),$p(%old(1),"||",3),"DELETE")
 d updateBG($p(%old(1),"||"),$p(%old(1),"||",2),$p(%old(1),"||",3),"D")
 s BillingFlag="N"
 i $p($g(^TEPI($p(%old(1),"||"),1,$p(%old(1),"||",2),$p(%old(1),"||",3))),"\",34)'="Y"
 i  i $p($g(^TTAB("TS",$p(%old(1),"||",2))),"\",13)'="N"
 i  s BillingFlag="Y"
 q
 ; post delete trigger
POFILDEL n (%old,BillingFlag) i BillingFlag="Y" d BILL($p(%old(1),"||"))
 s exclude=$$seldata^LVBCTTS($p(%old(1),"||",2),57) i exclude'="Y" d OUT^LVBEDI00($p(%old(1),"||"),46,%old(1),1,%old(1),"Del")
 i '$d(^TMOVE("TEMP","46D",$p(%old(1),"||"),%old(1))) d  d SetAll^LVBCOM50($p(%old(1),"||"),"46D",.%old,%old(1))
 .s epis=$p(%old(1),"||") n (epis)
 .i '$d(^TMOVE("TEMP","42I",epis)),'$$select^LVBEPVIS(epis,"Y") d
 ..f j=11,13,62 i $g(PLIST(j)) s PLIST(j)=PLIST(j)\60
 ..d SetAll^LVBCOM50(epis,"42I",.PLIST)
 i $l($p($g(^CF("SM",1)),"^",28)),$l($t(@("POFILDEL^"_$zn_$p(^CF("SM",1),"^",28)))) d @("POFILDEL^"_$zn_$p(^CF("SM",1),"^",28))
 q
cour(cour) n (cour) s cour=$g(cour),result=$$seldata^LVBCTCR(cour,6)
 q $p(result,$c(1))
 ; get courier/doctor
doc(epis) n (epis) s epis=$g(epis),doc=$p($g(^TEPI(epis)),"\",13),(cour,name)=""
 i doc'="" d
 .s name=$p($g(^TTAB("DR",doc,0)),"\",11)_" "_$e($p($g(^TTAB("DR",doc)),"\",2))_"."_$p($g(^TTAB("DR",doc)),"\",1)
 .s cour=$p($g(^TTAB("DR",doc,0)),"\",1)
 q cour_"^"_doc_"^"_name
 ; get courier/patient location
hosp(epis) n (epis) s epis=$g(epis),name=""
 s hosp=$p($g(^TEPI(epis)),"\",20)
 i hosp'="" s cour=$p($g(^TTAB("RH",hosp)),"\",12),name=$p($g(^TTAB("RH",hosp)),"\",1)
 q $g(cour)_"^"_hosp_"^"_name
 ; update BG
updateBG(epis,testset,cnt,stat,deb) n (epis,testset,cnt,stat,deb)
 s epis=$g(epis),testset=$g(testset),cnt=$g(cnt),stat=$g(stat),deb=$g(deb)
 s (res,stop)=""
 i $l(epis),$l(testset),$l(cnt) d
 .i '$l(deb) s deb=$$mrn^LVBEPVIS(epis)
 . ; delete
 .i stat="D" k:$l(deb) ^TDEBi(deb,"BBG",epis,testset,cnt)
 . ; add
 .i stat'="D" d
 ..s ti="" f  s ti=$o(^TEPI(epis,1,testset,cnt,"DATA",ti)) q:ti=""  d  q:$l(res)
 ...i $$seldata^LVBCTTC(ti,5)="B2" s res=$p(^TEPI(epis,1,testset,cnt,"DATA",ti),"\")
 . ; update patient BG
 .i $l(deb) d
 ..i $l(res) s ^TDEBi(deb,"BBG",epis,testset,cnt)=res
 .. ; sort
 ..k ^TMP($zn,$j)
 ..s x1="" f  s x1=$o(^TDEBi(deb,"BBG",x1)) q:x1=""  d
 ...s x2="" f  s x2=$o(^TDEBi(deb,"BBG",x1,x2)) q:x2=""  d
 ....s x3="" f  s x3=$o(^TDEBi(deb,"BBG",x1,x2,x3)) q:x3=""  d
 .....s sort=$p($g(^TEPI(x1,1,x2,x3)),"\",4)_" "_$e($p($g(^TEPI(x1,1,x2,x3)),"\",5)*60+100000,2,6)
 .....s ^TMP($zn,$j,1,sort,x1_"||"_x2_"||"_x3)=^TDEBi(deb,"BBG",x1,x2,x3)
 ..s (x,sort)="" f  s sort=$o(^TMP($zn,$j,1,sort)) q:sort=""  d
 ...s vts="" f  s vts=$o(^TMP($zn,$j,1,sort,vts)) q:vts=""  s x=x+1,^TMP($zn,$j,2,x)=^TMP($zn,$j,1,sort,vts)
 ..s (x1,x2,res)=""
 ..
 ..s seq=$o(^TMP($zn,$j,2,"")) i '$l(seq) d
 ...i '$$select^LVBDEB(deb,"Y") d
 ....f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ....i $l(PLIST(16)) s old=PLIST(16),PLIST(16)="",new=PLIST(16) i old'=new d
 .....i '$$update^LVBDEB(deb) d events^LVBCOM01(epis,"BG","Blood Group Original for MRN <"_deb_"> has been changed from <"_old_"> to <"_new_">")
 ....f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ....i $l(PLIST(9)) s old=PLIST(9),PLIST(9)="",new=PLIST(9) i old'=new d
 .....i '$$update^LVBDEB(deb) d events^LVBCOM01(epis,"BG","Blood Group Confirmed for MRN <"_deb_"> has been changed from <"_old_"> to <"_new_">")
 ..
 ..s seq1=$o(^TMP($zn,$j,2,"")) i $l(seq1) s seq2=$o(^TMP($zn,$j,2,seq1)) i '$l(seq2) d
 ...s res=^TMP($zn,$j,2,seq1) i $l(res),'$$select^LVBDEB(deb,"Y") d
 ....s old=PLIST(16),PLIST(16)=res,new=PLIST(16) i old'=new d
 .....i '$$update^LVBDEB(deb) d events^LVBCOM01(epis,"BG","Blood Group Original for MRN <"_deb_"> has been changed from <"_old_"> to <"_new_">")
 ...i '$$select^LVBDEB(deb,"Y") d
 ....s old=PLIST(9),PLIST(9)="",new=PLIST(9) i old'=new d
 .....i '$$update^LVBDEB(deb) d events^LVBCOM01(epis,"BG","Blood Group Confirmed for MRN <"_deb_"> has been changed from <"_old_"> to <"_new_">")
 ..
 ..s seq1=$o(^TMP($zn,$j,2,"")) i $l(seq1) s seq2=$o(^TMP($zn,$j,2,seq1)) i $l(seq2) d
 ...s seq=$o(^TMP($zn,$j,2,""),-1)
 ...s res=^TMP($zn,$j,2,seq) i $l(res),'$$select^LVBDEB(deb,"Y") d
 ....s old=PLIST(9),PLIST(9)=res,new=PLIST(9) i old'=new d
 .....i '$$update^LVBDEB(deb) d events^LVBCOM01(epis,"BG","Blood Group Confirmed for MRN <"_deb_"> has been changed from <"_old_"> to <"_new_">")
 ..
 ..k ^TMP($zn,$j)
 . ; update donor pack BG
 .i $l(res) d
 ..s pack=$$seldata^LVBEPVIS(epis,96)
 ..i $l(pack),'$$select^LVBBBP(pack,"Y"),'$l(PLIST(9)) s PLIST(9)=res i $$update^LVBBBP(pack)
 . ; update pack BG
 .i $l(res),$p(epis,"_")="BBP" d
 ..s pack=$p(epis,"_",2)_"||"_$p(epis,"_",3)
 ..i '$$select^LVBBBP(pack,"Y"),'$l(PLIST(9)) s PLIST(9)=res i $$update^LVBBBP(pack)
 q
af(donor) s donor=$g(donor)
 ; update existing record
 i $d(Exist(ti)) s x1=Exist(ti) k Exist(ti) d  q
 .i '$$select^LVBANTRG(x1) d
 ..s PLIST(12)=Flag(ti)
 ..i type="ATR" d 
 ...s PLIST(6)=$p($g(^TTAB("BB-GEN",Flag(ti))),"\",1)
 ...s pti=$p($g(^TTAB("BB-Register",type,testset,ti)),"\",2)
 ...s pres=$p($g(^TTAB("BB-Register",type,testset,ti)),"\",3)
 ...s detect="N" i $l(pti),$l(pres),$p($g(^TEPI(epis,1,testset,cnt,"DATA",pti)),"\",1)=pres s detect="Y"
 ...s PLIST(15)=detect,PLIST(16)=donor
 ..i type="AR" s PLIST(6)=$p($g(^TTAB("BB-GEN",Flag(ti))),"\",2)
 ..i type="TRR" s PLIST(6)=$p($g(^TTAB("TC",ti,2,Flag(ti))),"\",6)
 ..i $$update^LVBANTRG(x1)
 s cs=""
 i type="AR",$p($g(^TTAB("TC",ti)),"\",3)="B3" s cs=$p($g(^TTAB("BB-GEN",Flag(ti))),"\",9)
 i type="ATR",$p($g(^TTAB("TC",ti)),"\",3)="B4" s cs=$p($g(^TTAB("BB-GEN",Flag(ti))),"\",9)
 ; insert new record
 k PLIST
 s PLIST(2)=$p($g(^TTAB("BB-Register",type,testset,ti)),"\"),PLIST(3)=epis,PLIST(4)=testset,PLIST(5)=ti,PLIST(12)=Flag(ti)
 i type="ATR" d 
 .s PLIST(6)=$p($g(^TTAB("BB-GEN",Flag(ti))),"\",1)
 .s pti=$p($g(^TTAB("BB-Register",type,testset,ti)),"\",2)
 .s pres=$p($g(^TTAB("BB-Register",type,testset,ti)),"\",3)
 .s detect="N" i $l(pti),$l(pres),$p($g(^TEPI(epis,1,testset,cnt,"DATA",pti)),"\",1)=pres s detect="Y"
 .s PLIST(15)=detect,PLIST(16)=donor
 i type="AR" s PLIST(6)=$p($g(^TTAB("BB-GEN",Flag(ti))),"\",2)
 i type="TRR" s PLIST(6)=$p($g(^TTAB("TC",ti,2,Flag(ti))),"\",6)
 s PLIST(7)=date,PLIST(8)=time*60,PLIST(9)=user,PLIST(10)=epis_"||"_testset_"||"_cnt,PLIST(11)=cs,PLIST(13)=type
 i $$insert^LVBANTRG()
 q
 ; create BB Register
BBRegistryAdd(epis,testset,cnt,date,time,user) n (epis,testset,cnt,date,time,user)
 s patType=$p(^TEPI(epis),"\",44)
 i $l(epis),$l(testset),$l(cnt) d
 . ; update patient BB Register
 .i patType=0 f type="AR","TRR","ATR" d
 ..k Flag,Exist
 ..s x1="" f  s x1=$o(^BBRi(type,epis,x1)) q:x1=""  i $p($g(^BBR(type,x1)),"\",9)=(epis_"||"_testset_"||"_cnt) s Exist($p($g(^BBR(type_"||"_x1)),"\",4))=type_"||"_x1
 ..i '$d(Flag) s ti="" f  s ti=$o(^TEPI(epis,1,testset,cnt,"DATA",ti)) q:ti=""  s res=$p($g(^(ti)),"\",1) i $l(res),$d(^TTAB("BB-Register",type,testset,ti)) s Flag(ti)=res
 ..i '$d(Flag),type="AR" s ti="" f  s ti=$o(^TEPI(epis,1,testset,cnt,"DATA",ti)) q:ti=""  s res=$p($g(^(ti)),"\",1) i $l(res),$p($g(^TTAB("TC",ti)),"\",3)="B3" s Flag(ti)=res
 ..i '$d(Flag),type="ATR" s ti="" f  s ti=$o(^TEPI(epis,1,testset,cnt,"DATA",ti)) q:ti=""  s res=$p($g(^(ti)),"\",1) i $l(res),$p($g(^TTAB("TC",ti)),"\",3)="B4" s Flag(ti)=res
 ..i $d(Flag) s ti="" f  s ti=$o(Flag(ti)) q:ti=""  d af()
 .. ; delete from BB Register
 ..i $d(Exist) s ti="" f  s ti=$o(Exist(ti)) q:ti=""  i $$delete^LVBANTRG(Exist(ti))
 . ; update pack details
 .i patType=9 f type="ATR" d
 ..s packRID=$p($p(epis,"BBP_",2),"_",1)_"||"_$p($p(epis,"BBP_",2),"_",2)
 ..i '$$select^LVBBBP(packRID,"Y") d
 ...k Flag,Exist s donorID=$p(PLIST(44),$c(1))
 ...s x1="" f  s x1=$o(^BBRi(type,epis,x1)) q:x1=""  i $p($g(^BBR(type,x1)),"\",9)=(epis_"||"_testset_"||"_cnt) s Exist($p($g(^BBR(type_"||"_x1)),"\",4))=type_"||"_x1
 ...i '$d(Flag) s ti="" f  s ti=$o(^TEPI(epis,1,testset,cnt,"DATA",ti)) q:ti=""  s res=$p(^(ti),"\",1) i $l(res),$d(^TTAB("BB-Register",type,testset,ti)) s Flag(ti)=res
 ...i '$d(Flag) s ti="" f  s ti=$o(^TEPI(epis,1,testset,cnt,"DATA",ti)) q:ti=""  s res=$p(^(ti),"\",1) i $l(res),$p($g(^TTAB("TC",ti)),"\",3)="B4" s Flag(ti)=res
 ...i $d(Flag) d
 ....s ti="" f  s ti=$o(Flag(ti)) q:ti=""  d af(donorID)
 ....s ti="" f  s ti=$o(Flag(ti)) q:ti=""  d
 .....k PLIST
 .....s PLIST(2)=Flag(ti)
 .....s pti=$p($g(^TTAB("BB-Register",type,testset,ti)),"\",2)
 .....s pres=$p($g(^TTAB("BB-Register",type,testset,ti)),"\",3)
 .....s PLIST(3)="N" i $l(pti),$l(pres),$p($g(^TEPI(epis,1,testset,cnt,"DATA",pti)),"\",1)=pres s PLIST(3)="Y"
 .....i $$insert^LVBBBPC(packRID)
 .... ; delete from BB Register
 ....i $d(Exist) s ti="" f  s ti=$o(Exist(ti)) q:ti=""  i $$delete^LVBANTRG(Exist(ti))
 q
 ; delete from BB Register
BBRegistryDel(epis,testset,cnt) n (epis,testset,cnt)
 i $l(epis),$l(testset),$l(cnt) d
 .f type="AR","TRR","ATR" d
 ..s x1="" f  s x1=$o(^BBRi(type,epis,x1)) q:x1=""  i $p($g(^BBR(type,x1)),"\",9)=(epis_"||"_testset_"||"_cnt),$$delete^LVBANTRG(type_"||"_x1)
 q
]]></Routine>
</Export>
