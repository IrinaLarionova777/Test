<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR401" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR401 ;IG 27/05/98 ;Doctors Reports printing BB - TRAK
 ;
 ; supported calls :
 ;
 ; $$print^PRTDR401(vtsRID,type,DH)
 ;
 ; type  : P - print
 ;         V - view from Enquiry
 ;           - view from Entry
 ; DH    : D - Doctor
 ;         H - Hospital
print(vtsRID,type,DH) n (vtsRID,type,DH,tsLM,tsWD)
 s vtsRID=$g(vtsRID),type=$g(type),DH=$g(DH),tsLM=$g(tsLM),tsWD=$g(tsWD),test=$p(vtsRID,"||",2)
 ; check for margin changes
 s margin="",%routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 i $l(%routine) s LineRoutine="Margin^"_%routine i $l($t(@LineRoutine)) x "s margin=$$Margin^"_%routine_"()"
 ; non-printable
 s (ok,x)="" f  s x=$o(^TTAB("TS",test,"XM",x)) q:x=""  i $p(^(x),"\",3)="Y" s ok=1 q
 i type="P",'ok q
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 i '$$select^MVBCFLAB(1) d
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 k VISTE i '$$getall^LVBVISTE(vtsRID) m VISTE=PLIST
 i 'VISTE q
 i $$open^LVBCTTSO(test)
 f  q:$$fetch^LVBCTTSO(test)  d
 .i (type="P")!(type="V"),$p(PLIST(6),$c(1))'="Y" q
 .s str="" f j=1:1:PLIST s $p(str,$c(28),j)=PLIST(j)
 .s Order($p(PLIST(2),$c(1)))=PLIST(8)_$c(2)_str
 i $$close^LVBCTTSO()
 s seq="" f  s seq=$o(Order(seq)) q:seq=""  s xm=$p(Order(seq),$c(2),1) d
 .s xtype=$$seldata^MVBSSXM(xm,8),side=$$seldata^MVBSSXM(xm,9)
 .s $p(Order(seq),$c(2),1)=xtype_$c(28)_side
 ; create caption
 f j=1:1:5 s Line(j)=""
 s (x1,x2,c1,c2,seq)="" f  s seq=$o(Order(seq)) q:seq=""  d
 .s x=$p(Order(seq),$c(2),2) f j=1:1:$l(x,$c(28)) s x(j)=$p(x,$c(28),j)
 .i $p($p(Order(seq),$c(2)),$c(28),2)="D" s x1=x1+x(4),c1=c1+1
 .i $p($p(Order(seq),$c(2)),$c(28),2)="C" s x2=x2+x(4),c2=c2+1
 s line="",$p(line,"-",x1+x2+c1+c2)="",Line(1)=" "_line_" ",Line(5)="|"_line_"|"
 s Line(3)="|"
 i $l(x1) s line="",$p(line,"-",x1+c1)="",Line(3)=Line(3)_line_"|"
 i $l(x2) s line="",$p(line,"-",x2+c2)="",Line(3)=Line(3)_line_"|"
 i $l(x1) s text="Donation details",line=$j("",x1+c1-$l(text)\2)_text,Line(2)="|"_$e(line_$j("",x1+c1),1,(x1+c1-1))_"|"
 i $l(x2) s text="Compatibility details",line=$j("",x2+c2-$l(text)\2)_text,Line(2)=Line(2)_$e(line_$j("",x2+c2),1,(x2+c2-1))_"|"
 s Line(4)="|" f side="D","C" s seq="" f  s seq=$o(Order(seq)) q:seq=""  d
 .i $p($p(Order(seq),$c(2)),$c(28),2)'=side q
 .s x=$p(Order(seq),$c(2),2) f j=1:1:$l(x,$c(28)) s x(j)=$p(x,$c(28),j)
 .i side="D",$l(x1) s line=$j("",x(4)-$l(x(3))\2)_x(3),Line(4)=Line(4)_$e(line_$j("",x(4)),1,x(4))_"|"
 .i side="C",$l(x2) s line=$j("",x(4)-$l(x(3))\2)_x(3),Line(4)=Line(4)_$e(line_$j("",x(4)),1,x(4))_"|"
 d LN^PRTDR00(),LN^PRTDR00() f j=1:1:5 d ADD^PRTDR00(tsLM+margin,Line(j)),LN^PRTDR00()
 s cnt="" f  s cnt=$o(VISTE(cnt)) q:cnt=""  s data=$p(VISTE(cnt),$c(2),4) d
 .s x1=$p(data,$c(28),2)_"||"_$p(data,$c(28),3) i '$$select^LVBBBP(x1) d
 ..k PACK m PACK=PLIST
 ..s (ph,seq)="",str="|" f  s seq=$o(Order(seq)) q:seq=""  d
 ...s x=$p(Order(seq),$c(2),2) f j=1:1:$l(x,$c(28)) s x(j)=$p(x,$c(28),j)
 ...s tt=$p($p(Order(seq),$c(2)),$c(28))
 ...i tt=0 s str=str_$e($p(data,$c(28),2)_$j("",x(4)),1,x(4))_"|"
 ...i tt=1 s str=str_$e($p(PACK(8),$c(1),2)_$j("",x(4)),1,x(4))_"|"
 ...i tt=2 s str=str_$e($p(PACK(9),$c(1),2)_$j("",x(4)),1,x(4))_"|"
 ...i tt=3 d
 ....i $l(PACK(13)) s str=str_$e($$extdate2^SSUTIL4(PACK(13))_$j("",x(4)),1,x(4))_"|"
 ....i '$l(PACK(13)) s str=str_$e($j("",x(4)),1,x(4))_"|"
 ...i tt=4 d
 ....i $l($p(data,$c(28),8)) s str=str_$e($$extdate2^SSUTIL4($p(data,$c(28),8))_"  "_$$exttime^SSUTIL4($p(data,$c(28),9))_$j("",x(4)),1,x(4))_"|"
 ....i '$l($p(data,$c(28),8)) s str=str_$e($j("",x(4)),1,x(4))_"|"
 ...i tt=5 s ph=ph+1,str=str_$e($p($p(data,$c(28),6),",",ph)_$j("",x(4)),1,x(4))_"|"
 ...i tt=6 s str=str_$e($p(PACK(17),$c(1),2)_$j("",x(4)),1,x(4))_"|"
 ...i tt=7 s str=str_$e($p($p(data,$c(28),5),$c(1),2)_$j("",x(4)),1,x(4))_"|"
 ...i tt=8 s str=str_$e($p($p(data,$c(28),7),$c(1),2)_$j("",x(4)),1,x(4))_"|"
 ...i tt=9 s str=str_$e($s($p(data,$c(28),4)="A":"Acceptable",$p(data,$c(28),4)="C":"Compatible",$p(data,$c(28),4)="N":"Non Compatible",1:"")_$j("",x(4)),1,x(4))_"|"
 ...i tt=10 s str=str_$e($j("",x(4)),1,x(4))_"|"
 ...i tt=13 s str=str_$e($p(PACK(25),$c(1),1)_$j("",x(4)),1,x(4))_"|"
 ..d ADD^PRTDR00(tsLM+margin,str),LN^PRTDR00()
 ..d ADD^PRTDR00(tsLM+margin,Line(5)),LN^PRTDR00()
 d ADD^PRTDR00(tsLM+margin,Line(1)),LN^PRTDR00()
 q
]]></Routine>
</Export>
