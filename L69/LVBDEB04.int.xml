<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBDEB04" type="INT" languagemode="0" generated="1"><![CDATA[
LVBDEB04 ; IG 12/07/98 ; MRN control for Darwin
 ;
 ; supported calls:
 ;
 ; $$check^LVBDEB04(deb,step)
 ;
 ; check debtor number
 ; return code = ReturnCode_$c(1)_Flag1_$c(1)_Flag2
 ; Return Code - 10 - exist in TDEB              - MRN - last visit
 ;               20 - exist in THOS              - MRN - multiple THOS data
 ;               30 - exist in Labtrak as HRN    -     - multiple Labtrak MRN's and last visits
 ;               50 - does not exist but valid   - MRN - single THOS data
 ;              100 - invalid MRN
 ; Flag1       - Search DB : H - LVBHOSP
 ;                           L - LVBEPVIS
 ; Flag2       - Accept Hospital DB MRN as LabTrak MRN
check(deb,step) n (PLIST,deb,step) k PLIST
 i step=3 d  g end
 .s ok=20_$c(1)_"HL"_$c(1)_"Y"
 .k ^TMP($zn,$j)
 .i $l(deb) s x1="" f  s x1=$o(^THOSi("VIS",deb,x1)) q:x1=""  d
 ..s x2="" f  s x2=$o(^THOSi("VIS",deb,x1,x2)) q:x2=""  d
 ...i '$$select^LVBHOSP(x1_"||"_x2_"||"_deb) s cnt=$o(^TMP($zn,$j,""),-1)+1 s:cnt=1 cnt=2 f j=1:1:PLIST s $p(^TMP($zn,$j,cnt),$c(28),j)=PLIST(j)
 .i '$d(^TMP($zn,$j)) s ok=0 q
 .k PLIST m PLIST=^TMP($zn,$j) s PLIST(1)=deb
 ; existing LabTrak-Hospital MRN                 - ok=30_$c(1)_"HL"_$c(1)_"Y"
 i step=4 d  g end
 .s ok=30_$c(1)_"HL"_$c(1)_"Y"
 .k ^TMP($zn,$j)
 .f j=1:1:$l(deb,$c(1)) s xx=$p(deb,$c(1),j) i $l(xx) d
 ..s mrn="" f  s mrn=$o(^TDEBj("HOSPITAL",xx,mrn)) q:mrn=""  d
 ...i '$$select^LVBDEB(mrn) s cnt=$o(^TMP($zn,$j,""),-1)+1 s:cnt=1 cnt=2 s ^TMP($zn,$j,cnt)=mrn_$c(1)_PLIST(4)
 ..i '$d(^TMP($zn,$j)) s ok=0 q
 ..k PLIST s PLIST(1)="{New}" m PLIST=^TMP($zn,$j)
 ; request for new MRN with URprefix             - ok=50_$c(1)_"LH"_$c(1)_"N"
 i deb="T." s ok=50_$c(1)_"LH"_$c(1)_"N" d  i ok k PLIST s PLIST(1)=$e(deb)_"{New}" g end
 .i $$select^LVBCTUR($e(deb),"Y") s ok=0 q
 ; request for new MRN                           - ok=50_$c(1)_"LH"_$c(1)_"N"
 i deb["." s ok=100 g end
 ; existing Hospital MRN                         - ok=20_$c(1)_"HL"_$c(1)_"Y"
 i step<2,deb?1.7UN s ok=20_$c(1)_"HL"_$c(1)_"Y" d  i ok g end
 .k ^TMP($zn,$j)
 .i $$open^LVBHOSP("H",,,,,deb)
 .f jj=0:1:5 q:$$fetch^LVBHOSP("H",,,,,deb)  d
 ..i PLIST(21)>$h q
 ..s cnt=$o(^TMP($zn,$j,""),-1)+1 s:cnt=1 cnt=2 f j=1:1:PLIST s $p(^TMP($zn,$j,cnt),$c(28),j)=PLIST(j)
 .i $$close^LVBHOSP()
 .i '$d(^TMP($zn,$j)) s ok=0 q
 .k PLIST m PLIST=^TMP($zn,$j) s PLIST(1)=deb
 ; existing LabTrak MRN                          - ok=10_$c(1)_$c(1)_"N"
 i '$$select^LVBDEB(deb) s ok=10_$c(1)_$c(1)_"N",last=PLIST(4) k PLIST s PLIST(1)=deb,PLIST(2)=last g end
 ; existing LabTrak-Hospital MRN                 - ok=30_$c(1)_"HL"_$c(1)_"Y"
 i deb?1.7UN s ok=30_$c(1)_"HL"_$c(1)_"Y" d  i ok g end
 .k ^TMP($zn,$j)
 .s mrn="" f  s mrn=$o(^TDEBj("HOSPITAL",deb,mrn)) q:mrn=""  d
 ..i '$$select^LVBDEB(mrn) s cnt=$o(^TMP($zn,$j,""),-1)+1 s:cnt=1 cnt=2 s ^TMP($zn,$j,cnt)=mrn_$c(1)_PLIST(4)
 .i '$d(^TMP($zn,$j)) s ok=0 q
 .k PLIST s PLIST(1)="{New}" m PLIST=^TMP($zn,$j)
 ; request for new MRN with new Hospital MRN     - ok=50_$c(1)_"HL"_$c(1)_"Y"
 ;i deb?1.7UN s ok=50_$c(1)_"HL"_$c(1)_"Y" k PLIST s PLIST(1)="{New}" g end
 ; existing LabTrak-Medicare              - ok=30_$c(1)_$c(1)_"N"
 i deb?1"M"1P10N s ok=30_$c(1)_$c(1)_"N" d  i ok g end
 .k ^TMP($zn,$j) s deb=$e(deb,3,99)
 .s epis="" f  s epis=$o(^TIND(1,deb,epis)) q:epis=""  d
 ..s mrn=$$mrn^LVBEPVIS(epis) i $l(mrn),'$d(mrn(mrn)) s mrn(mrn)="" d
 ...i '$$select^LVBDEB(mrn) s cnt=$o(^TMP($zn,$j,""),-1)+1 s:cnt=1 cnt=2 s ^TMP($zn,$j,cnt)=mrn_$c(1)_PLIST(4)
 .i '$d(^TMP($zn,$j)) s ok=0 q
 .k PLIST m PLIST=^TMP($zn,$j) s PLIST(1)=deb
 ; invalid MRN
 s ok=100 k PLIST
end k ^TMP($zn,$j) s PLIST=$o(PLIST(""),-1)
 q ok
selorder s SQLCODE=50
 q
]]></Routine>
</Export>
