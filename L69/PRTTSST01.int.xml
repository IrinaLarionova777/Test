<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTTSST01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTTSST01 ; IG 07/10/98 ; TEST SET STATISTICS
 q
 ; Parameters :  1. Start Date
 ;               2. End Date
 ;               3. Site
 ;               4. Department
 ;               5. Test Set WorkGroup
 ;               6. Test Set
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Test Set Statistics Report"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s($l(PAR3):SITE(3),1:"All")
 s line=4,xHEADER(line)=""
 i $l(PAR4) d
 .s line=line+1,xHEADER(line)="Department : "_DEP(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR5) d
 .s line=line+1,xHEADER(line)="Test WorkGroup : "_TWG(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR6) d
 .s line=line+1,xHEADER(line)="Test Set : "_TS(3)
 .s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR3_"-"_PAR4 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xsite d H^PRTREP00(PAR10,$zn)
 i $l(PAR3),'$$select^LVBCTUSL(PAR3,"Y") k SITE m SITE=PLIST
 i $l(PAR4),'$$select^LVBCTDEP(PAR4,"Y") k DEP m DEP=PLIST
 i $l(PAR5),'$$select^LVBCTTWG(PAR5,"Y") k TWG m TWG=PLIST
 i $l(PAR6),'$$select^LVBCTTS(PAR6,"Y") k TS m TS=PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 i $l(PAR5) d
 .i $$open^LVBCTTWT(PAR5,"Y")
 .f  q:$$fetch^LVBCTTWT(PAR5,"Y")  s ^TMP($zn,$j,"TWG",PLIST(2))=""
 .i $$close^LVBCTTWT()
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s USite=$$seldata^LVBEPVIS(epis,66)
 ..s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ...i $l(PAR4),PAR4'=$e(ts) q
 ...i $l(PAR5),'$d(^TMP($zn,$j,"TWG",ts)) q
 ...i $l(PAR6),PAR6'=ts q
 ...s tscnt="" f cnt=0:1 s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....s vtsRID=epis_"||"_ts_"||"_tscnt i '$$select^LVBVISTS(vtsRID,"Y") d
 .....s site=$s($l(PLIST(29)):PLIST(29),1:USite)
 .....i $l(PAR3),PAR3'=site q
 .....i '$l(site) s site=" "
 .....s $p(^TMP($zn,$j,"REPORT",site,$e(ts),ts),"\",1)=$p($g(^TMP($zn,$j,"REPORT",site,$e(ts),ts)),"\",1)+1
 .....s $p(^TMP($zn,$j,"REPORT",site,$e(ts),ts),"\",2)=$p($g(^TMP($zn,$j,"REPORT",site,$e(ts),ts)),"\",2)+'cnt
 .....s $p(^TMP($zn,$j,"REPORT",site,$e(ts)),"\",1)=$p($g(^TMP($zn,$j,"REPORT",site,$e(ts))),"\",1)+1
 .....s $p(^TMP($zn,$j,"REPORT",site,$e(ts)),"\",2)=$p($g(^TMP($zn,$j,"REPORT",site,$e(ts))),"\",2)+'cnt
 .....s $p(^TMP($zn,$j,"REPORT",site),"\",1)=$p($g(^TMP($zn,$j,"REPORT",site)),"\",1)+1
 .....s $p(^TMP($zn,$j,"REPORT",site),"\",2)=$p($g(^TMP($zn,$j,"REPORT",site)),"\",2)+'cnt
 .....s $p(^TMP($zn,$j,"REPORT"),"\",1)=$p($g(^TMP($zn,$j,"REPORT")),"\",1)+1
 .....s $p(^TMP($zn,$j,"REPORT"),"\",2)=$p($g(^TMP($zn,$j,"REPORT")),"\",2)+'cnt
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s site="" f  s site=$o(^TMP($zn,$j,"REPORT",site)) q:site=""  d
 ..k xxx i '$$select^LVBCTUSL(site,"Y") s xxx(1)=PLIST(2)_$c(1)_PLIST(3)
 ..s dep="" f  s dep=$o(^TMP($zn,$j,"REPORT",site,dep)) q:dep=""  d
 ...s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",site,dep,ts)) q:ts=""  d
 ....i '$$select^LVBCTTS(ts,"Y") s xxx(2)=PLIST(2)_$c(1)_$e(PLIST(2)_$j("",6),1,6)_PLIST(3)
 ....s xxx(3)=$p(^TMP($zn,$j,"REPORT",site,dep,ts),"\",1)
 ....s xxx(4)=$p(^TMP($zn,$j,"REPORT",site,dep,ts),"\",2)
 ....d PRINT^PRTREP00(,.N) k xxx
 ...d PRINT^PRTREP00(,.N) k xxx
 ...s xxx(1)="Total"_$c(1)_"Total for Department"
 ...s xxx(3)=$p(^TMP($zn,$j,"REPORT",site,dep),"\",1)
 ...s xxx(4)=$p(^TMP($zn,$j,"REPORT",site,dep),"\",2)
 ...d PRINT^PRTREP00(,.N) k xxx
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 ..d PRINT^PRTREP00(,.N) k xxx
 ..s xxx(1)="Total"_$c(1)_"Total for Site"
 ..s xxx(3)=$p(^TMP($zn,$j,"REPORT",site),"\",1)
 ..s xxx(4)=$p(^TMP($zn,$j,"REPORT",site),"\",2)
 ..d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d PRINT^PRTREP00(,.N) k xxx
 .s xxx(1)="Total"_$c(1)_"Total for Practice"
 .s xxx(3)=$p(^TMP($zn,$j,"REPORT"),"\",1)
 .s xxx(4)=$p(^TMP($zn,$j,"REPORT"),"\",2)
 .d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="TSST"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
