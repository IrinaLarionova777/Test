<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LHOSPINT01" type="INT" languagemode="0" generated="1"><![CDATA[
LHOSPINT01 ; IG 12/07/98 ; Hospital Interface - Trak
 Q
RESULT(HOSP) N (HOSP) k ^TMP($zn,$j)
 i $$select^LVBCTRL(HOSP) q
 s dir=PLIST(7),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.hl7"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""results.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""results.txt""")
 s $zt="Error1^"_$zn
 s FILE="results.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  u xFILE r MESS s seq=$o(^TMP($zn,$j,xxFILE,"IN",""),-1)+1,^TMP($zn,$j,xxFILE,"IN",seq)=MESS
 s $zt=""
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s cnt=0
 .s seq="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=^(seq) d
 ..i $p(xx,"|")="OBR" s cnt=cnt+1,^TMP($zn,$j,xFile,"IN-1",cnt,0)=xx
 ..i $p(xx,"|")="OBX",$d(^TMP($zn,$j,xFile,"IN-1",cnt)) s x=$o(^TMP($zn,$j,xFile,"IN-1",cnt,""),-1)+1,^TMP($zn,$j,xFile,"IN-1",cnt,x)=xx
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .k ^TMPimport(xFile)
 .s seq1="" f  s seq1=$o(^TMP($zn,$j,xFile,"IN-1",seq1)) q:seq1=""  d
 ..s (epis,ts,tscnt,vts)="",done="bad"
 ..s seq2="" f  s seq2=$o(^TMP($zn,$j,xFile,"IN-1",seq1,seq2)) q:seq2=""  s xx=^(seq2) d
 ...i $p(xx,"|")="OBR" d
 ....s epis=$p($p($p(xx,"|",4),"^"),"  ") i $l(epis) s ts=$p($p($p(xx,"|",4),"^"),"  ",2) i $l(ts) s tscnt=$p($p($p(xx,"|",4),"^"),"  ",3)
 ....i $l(epis),$l(ts),$l(tscnt) s vts=epis_"||"_ts_"||"_tscnt
 ....i '$l($$seldata^LVBVISTS(vts,1)) s vts=""
 .... ; only file results in RTF test set
 ....i $$seldata^LVBCTTS(ts,31)'="Y" s vts=""
 ....i $l(vts) s reason=$p(xx,"|",12) i $l(reason) s ^TMP($zn,$j,xFile,"OUT",vts,"R")=reason
 ...i $l(vts),$p(xx,"|")="OBX",$p(xx,"|",3)="FT" s result=$p(xx,"|",6) d
 ....f j1=1:1:$l(result,"\.br\") d
 .....s xx=$p(result,"\.br\",j1)
 .....f j2="S" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(16)_$p(xx,x2,2,$l(xx,x2))
 .....f j2="F" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(17)_$p(xx,x2,2,$l(xx,x2))
 .....f j2="T" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"&"_$p(xx,x2,2,$l(xx,x2))
 .....f j2="R" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"~"_$p(xx,x2,2,$l(xx,x2))
 .....f j2="H","N","E" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$p(xx,x2,2,$l(xx,x2))
 .....s result(j1)=xx
 ....s ^TMPimport(xFile,"index",vts)=seq1
 .... ; find TS left margin
 ....s tsLM=""
 ....i 'tsLM s tsLM=$$seldata^LVBCTTS(ts,41),dep=$$seldata^LVBCTTS(ts,26)
 ....i 'tsLM s tsLM=$$seldata^LVBCTDEP(dep,13)
 ....i 'tsLM s tsLM=$$seldata^MVBCFLAB(1,43)
 .... ; strip left margin
 ....i tsLM d
 .....s (stop,j)="" f  s j=$o(result(j)) q:j=""  i $tr($e(result(j),1,tsLM)," ")'="" s stop=1 q
 .....i 'stop s j="" f  s j=$o(result(j)) q:j=""  s result(j)=$e(result(j),tsLM+1,$l(result(j)))
 ....m ^TMP($zn,$j,xFile,"OUT",vts,"T")=result
 ....k result s done="process"
 ..m ^TMPimport(xFile,done,seq1)=^TMP($zn,$j,xFile,"IN-1",seq1)
 ..k ^TMP($zn,$j,xFile,"IN-1",seq1)
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s vts="" f  s vts=$o(^TMP($zn,$j,xFile,"OUT",vts)) q:vts=""  d
 ..s reason="" i $d(^TMP($zn,$j,xFile,"OUT",vts,"R")) s reason=^("R") k ^TMP($zn,$j,xFile,"OUT",vts,"T")
 ..s (text,cnt)="" f  s cnt=$o(^TMP($zn,$j,xFile,"OUT",vts,"T",cnt)) q:cnt=""  s text=text_$s($l(text):"|",1:"")_^(cnt)
 ..s epis=$p(vts,"||",1),ts=$p(vts,"||",2),tscnt=$p(vts,"||",3)
 ..i $$vtsLock^SSLOCK("IM",epis,vts,"trak")=-1 d
 ...i $$status^LVBVIS1(vts)="A",$l(text) d  q
 ....k xx m xx=^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
 ....k ^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
 ....s max=0
 ....f j=1:1 q:'$d(xx(j))  i max<$l(xx(j)) s max=$l(xx(j))
 ....f j=1:1:$l(text,"|") i max<$l($p(text,"|",j)) s max=$l($p(text,"|",j))
 ....s line="",$p(line,"-",max+1)=""
 ....s text=$j("",max-22\2)_"--- Amended Result ---"_"||"_text_"||"_line
 ....f j=1:1:$l(text,"|") s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1)+1,^TEPI(epis,1,ts,tscnt,"DATA","T","REM",x)=$p(text,"|",j)
 ....f j=1:1 q:'$d(xx(j))  s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1)+1,^TEPI(epis,1,ts,tscnt,"DATA","T","REM",x)=xx(j)
 ....s x=$o(^TEPI(epis,1,ts,tscnt,"DATA","T","REM",""),-1),^TEPI(epis,1,ts,tscnt,"DATA","T","REM",0)=x
 ...i $$status^LVBVIS1(vts)'="A" d  q
 ....i $l(reason) d  q
 .....i '$$select^LVBVISTS(vts,"Y") s PLIST(58)=reason i $$update^LVBVISTS(vts)
 ....k PLIST
 ....s PLIST(1)="DATA"_$c(1)_"T"_$c(1)_text
 ....s PLIST=$o(PLIST(""),-1)
 ....i $$TScheck^LVBVIS7(vts,"A","","B")
 ....s (VQ,ok)="" f j=1:1:PLIST d  i ok="N" q
 .....i $e($p(PLIST(j),$c(1)))="E" s ok="N" q
 .....i $e($p(PLIST(j),$c(1)),1,2)="VQ" s VQ=$p(PLIST(j),$c(1),2)
 ....k PLIST s PLIST(1)="T\"_text
 ....s PLIST=$o(PLIST(""),-1)
 ....s err=$$save^LVBVIS4(vts,"A","trak","N")
 ....s ^TMPimport(xFile,"index",vts)=$g(^TMPimport(xFile,"index",vts))_" err"
 ....i 'err d
 .....i '$$select^LVBVISTS(vts,"Y") s PLIST(29)=$$seldata^LVBEPVIS(epis,66) i $$update^LVBVISTS(vts)
 .....k PLIST
 .....s PLIST=$o(PLIST(""),-1)
 .....i $$TScheck^LVBVIS7(vts,"A","","A")
 .....f j=1:1:PLIST d
 ......s val=$p($p(PLIST(j),$c(1),2),$c(2))
 ......i $e($p(PLIST(j),$c(1)),1,2)="VQ",$$VQ^LVBCOM13(vts,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="TA",$$TAR^LVBCOM13($p(vts,"||"),,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SA",$$SA^LVBCOM13($p(vts,"||"),val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SF",$$SF^LVBCOM13(vts,$p(PLIST(j),$c(1),6),val)
 ......i $e($p(PLIST(j),$c(1)),1,3)="ACT",$$ACT^LVBCOM13($p(vts,"||"),$p(val,"-",1),$p(val,"-",2))
 ..i $$vtsLockClr^SSLOCK("IM",epis,vts)
 s (param,xFile)="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  s param=param_$s($l(param):",",1:"")_xFile
 i $l(param),$$START^PRTREP00("","",HOSP_"\"_param,"RLI")
 k ^TMP($zn,$j)
 q
 ; Error log
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
 ; Error log
Error2 i $ze'["ENDOFFILE" d BACK^%ETN q
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
 ; import episodes
EPISODE() N  k ^TMP($zn,$j) s delim=","
 k CFSM i '$$select^MVBCFSM(1) m CFSM=PLIST
 s dir=$tr(CFSM(12),$c(16,17,18),"^\|") f j="/","\" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
 i $zf(-1,"DIR "_dir_"*.txt /B > ""import.txt""")
 s file="import.txt" o file:"WAS" u file w "End_Of_File",! c file
 o file:("RS"::$c(10,13))
 f  u file r x q:x="End_Of_File"  s xfile=$p(x," ",$l(x," ")) i xfile[".",$l(xfile) s ^TMP($zn,$j,"FileName",xfile)=""
 c file
 i $zf(-1,"DIR "_dir_"*.hl7 /B > ""import.hl7""")
 s file="import.hl7" o file:"WAS" u file w "End_Of_File",! c file
 o file:("RS"::$c(10,13))
 f  u file r x q:x="End_Of_File"  s xfile=$p(x," ",$l(x," ")) i xfile[".",$l(xfile) s ^TMP($zn,$j,"FileName",xfile)=""
 c file
 ; extract file content
Read s $zt="Error^"_$zn,xCurrent=$p($g(^CF("SM",1,"SITE")),"^",2)
 s file=$o(^TMP($zn,$j,"FileName","")) i $l(file) d
 .s dir=$tr(CFSM(12),$c(16,17,18),"^\|") f j="/","\" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
 .s fullname=dir_file
 .o fullname:("RS"::$c(10,13)):1 e  q
 .k ^TMP($zn,$j,file)
 .u fullname s line="" f  r line:10 s cnt=$o(^TMP($zn,$j,file,""),-1)+1,^TMP($zn,$j,file,cnt)=line
 k ^TMP($zn,$j,"FileName")
 s file="" f  s file=$o(^TMP($zn,$j,file)) q:file=""  d
 .
 . ; Trak generic file
 .
 .i $$UPPER^SSUTIL4($p(file,".",2))="TXT" d
 ..s cnt="" f  s cnt=$o(^TMP($zn,$j,file,cnt)) q:cnt=""  d
 ...k xx f j=1:1:200 s xx(j)=$p(^(cnt),$c(28),j)
 ...s epis=xx(2),tests=xx(200)
 ...i $l(epis),'$d(^TEPI(epis)) d
 ....k PLIST m PLIST=xx
 ....s PLIST=$o(PLIST(""),-1)
 ....d checkEPIS
 ....s err=$$save^LVBEPVIS(epis) i err s ^TMPlog("import-txt",file,epis)=err q
 ....f jx=1:1:$l(tests,",") d
 .....s vts=$p(tests,",",jx) i $l(vts) d
 ......k PLIST s PLIST(3)=$p(vts,"||",2)
 ......s PLIST(29)=xCurrent
 ......s PLIST=$o(PLIST(""),-1)
 ......d checkTS
 ......s err=$$insert^LVBVISTS(epis) i err s ^TMPlog("import-txt",file,epis,vts)=err q
 .
 . ; HL7 file
 .
 .i $$UPPER^SSUTIL4($p(file,".",2))="HL7" d
 ..s (from,sender,cnt)="" f  s cnt=$o(^TMP($zn,$j,file,cnt)) q:cnt=""  d  q:$l(from)
 ...k xx f j=1:1:$l(^(cnt),"|") s xx(j)=$p(^(cnt),"|",j)
 ...i xx(1)="MSH",xx(5)="PATH_ORDER",$p(xx(9),"^")="ORM",xx(6)=CFSM(43) s from=xx(4),sender=xx(3)
 ..i '$l(from) s ^TMPlog("import-hl7",file)="Has not been processed" q
 ..i $l(from) d
 ...s (epis,cnt,foundVTS)="" f  s cnt=$o(^TMP($zn,$j,file,cnt)) q:cnt=""  d
 ....k xx f j=1:1:$l(^(cnt),"|") s xx(j-1)=$p(^(cnt),"|",j)
 ....i xx(0)="PID" d  q
 .....s foundVTS="" k VISIT
 .....f j=1:1:$l(xx(3),"~") s xx=$p(xx(3),"~",j) d
 ......i $p(xx,"^",5)="MREXT" s $p(VISIT(65),",",2)=$p(xx,"^",1)
 ......i $p(xx,"^",5)="EPEXT" s $p(VISIT(65),",",1)=$p(xx,"^",1)
 ......i $p(xx,"^",5)="MRLT" s VISIT(20)=$p(xx,"^",1)
 ......i $p(xx,"^",5)="MC" s VISIT(8)=$p(xx,"^",1),VISIT(108)=$p(xx,"^",2)
 ......i $p(xx,"^",5)="DVA" s VISIT(89)=$p(xx,"^",1)
 ......i $p(xx,"^",5)="PEN" s VISIT(88)=$p(xx,"^",1)
 ......i $p(xx,"^",5)="EPLT" s (epis,VISIT(2))=$p(xx,"^",1)
 ......i $e($p(xx,"^",5),1,3)="NI-" s VISIT(126)=$p(xx,"^",1),VISIT(127)=$p($p(xx,"^",5),"-",2)
 .....s VISIT(5)=xx(8),VISIT(60)=xx(10),VISIT(161)=$p(xx(15),"^",1)
 .....s VISIT(43)=$p(xx(13),"^",7) i '$l(VISIT(43)) s VISIT(43)=$p(xx(13),"^",1)
 .....s VISIT(44)=$p(xx(14),"^",7) i '$l(VISIT(44)) s VISIT(44)=$p(xx(14),"^",1)
 .....s VISIT(3)=$p(xx(5),"^",1),VISIT(4)=$p(xx(5),"^",2),VISIT(7)=$p(xx(5),"^",5),VISIT(99)=$p(xx(5),"^",3),VISIT(105)=xx(30)
 .....i $l(xx(7)) s VISIT(6)=$$intdate^SSUTIL4($e(xx(7),5,6)_"/"_$e(xx(7),7,8)_"/"_$e(xx(7),1,4))
 .....f j=1:1:5 s VISIT(31+j)=$p(xx(11),"^",j)
 .....i $l(epis) d
 ......i '$$select^LVBEPVIS(epis,"Y") s ^TMPlog("import-hl7",file,epis)="PID message: Episode already exist" q
 ......k PLIST m PLIST=VISIT
 ......s PLIST=$o(PLIST(""),-1)
 ......d checkEPIS
 ......s err=$$save^LVBEPVIS(epis) i err s ^TMPlog("import-hl7",file,epis)="PID message: Episode save error - "_err q
 ....i xx(0)="PV1" d  q
 .....s (foundVTS,reason)=""
 .....s VISIT(22)=$p(xx(3),"^",1),VISIT(112)=$p(xx(3),"^",2),VISIT(113)=$p(xx(3),"^",3)
 .....s VISIT(123)=xx(6),VISIT(93)=xx(18)
 .....s VISIT(9)=$p(xx(20),"^",1),VISIT(23)=$p(xx(20),"^",2)
 .....i $l(xx(44)) s VISIT(47)=$$intdate^SSUTIL4($e(xx(44),5,6)_"/"_$e(xx(44),7,8)_"/"_$e(xx(44),1,4))
 .....i $l(xx(45)) s VISIT(165)=$$intdate^SSUTIL4($e(xx(45),5,6)_"/"_$e(xx(45),7,8)_"/"_$e(xx(45),1,4))
 .....i $l(xx(19)),'$l($p($g(VISIT(65)),",",1)),sender'="LABTRAK" s $p(VISIT(65),",",1)=xx(19)
 .....i $l(xx(19)),sender="LABTRAK" s (VISIT(2),epis)=xx(19)
 .....i $p(xx(8),"^",13)="DN" s VISIT(15)=$p(xx(8),"^",1)
 .....i $l(epis) d
 ......i '$$select^LVBEPVIS(epis,"Y") s ^TMPlog("import-hl7",file,epis)="PV1 message: Episode already exist" q
 ......k PLIST m PLIST=VISIT
 ......s PLIST=$o(PLIST(""),-1)
 ......d checkEPIS
 ......s err=$$save^LVBEPVIS(epis) i err s ^TMPlog("import-hl7",file,epis)="PV1 message: Episode save error - "_err q
 ....i xx(0)="IN1" d  q
 .....s foundVTS=""
 .....s VISIT(106)=xx(3),VISIT(107)=xx(36)
 ....i xx(0)="ORC",xx(5)="CA" s reason=$$seldata^MVBCFML(1,18)
 ....i xx(0)="OBR" d  q
 .....k VISTS s foundVTS=""
 .....i '$l(xx(5)),$l($p(xx(27),"^",6)) s xx(5)=$p(xx(27),"^",6)
 .....i '$l(xx(6)),$l($p(xx(27),"^",4)) s xx(6)=$p(xx(27),"^",4)
 .....s VISTS(13)=xx(5),VISTS(3)=$p(xx(4),"^"),VISTS(2)=$p(xx(4),"^",5),vts=$tr(xx(2),"^","|"),VISTS(38)=xx(2)
 .....i $l(xx(6)) s VISIT(14)=$$intdate^SSUTIL4($e(xx(6),5,6)_"/"_$e(xx(6),7,8)_"/"_$e(xx(6),1,4)),VISIT(111)=$$inttime^SSUTIL4($e(xx(6),9,10)_":"_$e(xx(6),11,12))
 .....i '$l(xx(6)) s VISIT(14)=+$h,VISIT(111)=$p($h,",",2)
 .....i $l(xx(14)) d
 ......s VISIT(61)=$$intdate^SSUTIL4($e(xx(14),5,6)_"/"_$e(xx(14),7,8)_"/"_$e(xx(14),1,4)),VISIT(62)=$$inttime^SSUTIL4($e(xx(14),9,10)_":"_$e(xx(14),11,12))
 ......s VISTS(62)=$$intdate^SSUTIL4($e(xx(14),5,6)_"/"_$e(xx(14),7,8)_"/"_$e(xx(14),1,4)),VISTS(63)=$$inttime^SSUTIL4($e(xx(14),9,10)_":"_$e(xx(14),11,12))
 .....i $l(xx(7)) s VISIT(12)=$$intdate^SSUTIL4($e(xx(7),5,6)_"/"_$e(xx(7),7,8)_"/"_$e(xx(7),1,4)),VISIT(13)=$$inttime^SSUTIL4($e(xx(7),9,10)_":"_$e(xx(7),11,12))
 .....s VISTS(23)=$tr(xx(13),"~","|")
 .....s VISTS(48)=$p(xx(15),"^",2),VISTS(47)=$p(xx(15),"^",1),VISTS(65)=$p(xx(15),"^",3),VISTS(57)=$p(xx(15),"^",4)
 .....i $p(xx(16),"^",13)="DN" s VISIT(15)=$p(xx(16),"^",1)
 .....k ^TMP($zn,$j,"copy")
 .....f jk=1:1:$l(xx(28),"~") s xx=$p(xx(28),"~",jk) i $l(xx) d
 ......s CDcnt=$o(^TMP($zn,$j,"copy",""),-1)+1
 ......i $p(xx,"^",13)="NN" s $p(^TMP($zn,$j,"copy",CDcnt),$c(28),4)=$p(xx,"^",2)
 ......i $p(xx,"^",13)="DN" s code=$p(xx,"^") d
 .......s zz="" f j=6,2,3 s zz=zz_$s($l(zz):" ",1:"")_$p(xx,"^",j)
 .......i $l(code),$d(^TTAB("DR",code)) s $p(^TMP($zn,$j,"copy",CDcnt),$c(28),3)=$p(xx,"^",1)
 .......i '$l(code) s $p(^TMP($zn,$j,"copy",CDcnt),$c(28),4)=zz
 ......i $p(xx,"^",13)="HN" s code=$p(xx,"^") d
 .......i $l(code),$d(^TTAB("RH",code)) s $p(^TMP($zn,$j,"copy",CDcnt),$c(28),17)=$p(xx,"^",1)
 .......i '$l(code) s $p(^TMP($zn,$j,"copy",CDcnt),$c(28),4)=$p(xx,"^",2)
 .....i $l(vts) d
 ......s (epis,VISIT(2))=$p(vts,"||")
 ......i $l(epis) d
 .......i '$$select^LVBEPVIS(epis,"Y") s ^TMPlog("import-hl7",file,epis)="OBR message: Episode already exist" q
 .......k PLIST m PLIST=VISIT
 .......s PLIST=$o(PLIST(""),-1)
 .......d checkEPIS
 .......s err=$$save^LVBEPVIS(epis) i err s ^TMPlog("import-hl7",file,epis)="OBR message: Episode save error - "_err q
 ......i $l(vts,"||")'=3 s vts=epis_"||"_VISTS(3)_"||"_VISTS(2)
 ......i $l(reason),'$l($$seldata^LVBVISTS(vts,1)),$l(epis),$l(VISTS(3)) s VISTS(2)=$o(^TEPI(epis,1,VISTS(3),""),-1),vts=epis_"||"_VISTS(3)_"||"_VISTS(2)
 ......i $l(reason),'$$select^LVBVISTS(vts,"Y") d  q
 .......s PLIST(58)=reason
 .......s PLIST(59)=+$h
 .......s PLIST(60)=$p($h,",",2)
 .......s PLIST(61)=""
 .......i $$update^LVBVISTS(vts)
 ......i '$$select^LVBVISTS(vts,"Y") s foundVTS=vts,^TMPlog("import-hl7",file,epis,vts)="OBR message: Test already exist" q
 ......k PLIST m PLIST=VISTS
 ......s PLIST(29)=xCurrent
 ......s PLIST=$o(PLIST(""),-1)
 ......d checkTS
 ......s err=$$insert^LVBVISTS(epis) i err s ^TMPlog("import-hl7",file,epis,vts)="OBR message: Test save error - "_err q
 ......s foundVTS=PLIST(1)
 ......i $d(^TMP($zn,$j,"copy")) d
 .......k PLIST m PLIST=^TMP($zn,$j,"copy")
 .......s PLIST=$o(PLIST(""),-1)
 .......i $$save^LVBVISDC(epis,"","")
 ....i xx(0)="OBX",$l(foundVTS) d  q
 .....s epis=$p(foundVTS,"||",1),ts=$p(foundVTS,"||",2),tscnt=$p(foundVTS,"||",3) i $l(epis),$l(ts),$l(tscnt) d
 ......i $$status^LVBVIS1(foundVTS)="A" q
 ......s ti=$p(xx(3),"^")
 ......i $l(ti),$d(^TTAB("TC",ti,1,ts)),$l(xx(5)),$l($$seldata^LVBVISTS(foundVTS,1)) d
 .......k PLIST
 .......s PLIST(1)=foundVTS_"||"_ti_"\"_ti_"\"_xx(5)
 .......s x="" f  s x=$o(^TEPI(epis,1,ts,tscnt,"DATA",x)) q:x=""  i x'=ti d
 ........s cnt1=$o(PLIST(""),-1)+1
 ........s PLIST(cnt1)=foundVTS_"||"_x_"\"_x_"\"_$p(^(x),"\",1)
 ........s $p(PLIST(cnt1),"\",5)=$p(^(x),"\",2)
 ........s $p(PLIST(cnt1),"\",6)=$p(^(x),"\",3)
 ........s $p(PLIST(cnt1),"\",9)=$p(^(x),"\",6)
 ........s $p(PLIST(cnt1),"\",13)=$p(^(x),"\",10)
 ........s $p(PLIST(cnt1),"\",14)=$p(^(x),"\",11)
 .......s PLIST=$o(PLIST(""),-1)
 .......i $$save^LVBVIS4(foundVTS,"E","trak")
 k ^TMP($zn,$j)
 q
 ; error log
Error i $ze'["ENDOFFILE" d BACK^%ETN q
 c fullname
 ; del the file
 k ^TMP($zn,$j,"FileName",file)
 i '$zf(-1,"del "_fullname)
 g Read
 ; check EPVIS fields
checkEPIS k ^TMP($zn,$j,"check") m ^TMP($zn,$j,"check")=PLIST k PLIST
 i '$d(^TMP($zn,$j,"CFML")),'$$select^MVBCFML(1) m ^TMP($zn,$j,"CFML")=PLIST
 s x=$p($g(^TMP($zn,$j,"check",9)),$c(1)) i $l(x),$$select^LVBCTPC(x,"Y") s ^TMP($zn,$j,"check",9)=$p($g(^TMP($zn,$j,"CFML",5)),$c(1))
 s x=$p($g(^TMP($zn,$j,"check",15)),$c(1)) i $l(x),$$seldata^LVBCTDR(x,1)="" s ^TMP($zn,$j,"check",15)=$p($g(^TMP($zn,$j,"CFML",2)),$c(1))
 s x=$p($g(^TMP($zn,$j,"check",17)),$c(1)) i $l(x),$$select^LVBCTPR(x,"Y") s ^TMP($zn,$j,"check",17)=$p($g(^TMP($zn,$j,"CFML",6)),$c(1))
 s x=$p($g(^TMP($zn,$j,"check",18)),$c(1)) i $l(x),$$select^LVBCTIC(x,"Y") s ^TMP($zn,$j,"check",18)=$p($g(^TMP($zn,$j,"CFML",3)),$c(1))
 s x=$p($g(^TMP($zn,$j,"check",19)),$c(1)) i $l(x),$$select^LVBCTFAS(x,"Y") s ^TMP($zn,$j,"check",19)=$p($g(^TMP($zn,$j,"CFML",4)),$c(1))
 s x=$p($g(^TMP($zn,$j,"check",28)),$c(1)) i $l(x),$$select^LVBCTPER(x,"Y") s ^TMP($zn,$j,"check",28)=$p($g(^TMP($zn,$j,"CFML",8)),$c(1))
 s x=$p($g(^TMP($zn,$j,"check",31)),$c(1)) i $l(x),$$selrow^MVBSSUSR(x,"Y") s ^TMP($zn,$j,"check",31)=$p($g(^TMP($zn,$j,"CFML",7)),$c(1))
 k PLIST m PLIST=^TMP($zn,$j,"check") k ^TMP($zn,$j,"check")
 i '$l($g(PLIST(61))) s PLIST(61)=+$h
 i '$l($g(PLIST(62))) s PLIST(62)=$p($h,",",2)
 q
 ; check EPVIS fields
checkTS k ^TMP($zn,$j,"check") m ^TMP($zn,$j,"check")=PLIST k PLIST
 i '$d(^TMP($zn,$j,"CFML")),'$$select^MVBCFML(1) m ^TMP($zn,$j,"CFML")=PLIST
 s x=$p($g(^TMP($zn,$j,"check",13)),$c(1)) i $l(x),$$select^LVBCTPR(x,"Y") s ^TMP($zn,$j,"check",13)=$p($g(^TMP($zn,$j,"CFML",6)),$c(1))
 f j=6,9,39,54,61,77 s x=$p($g(^TMP($zn,$j,"check",j)),$c(1)) i $l(x),$$selrow^MVBSSUSR(x,"Y") s ^TMP($zn,$j,"check",j)=$p($g(^TMP($zn,$j,"CFML",7)),$c(1))
 k PLIST m PLIST=^TMP($zn,$j,"check") k ^TMP($zn,$j,"check")
 i '$l($g(PLIST(62))) s PLIST(62)=+$h
 i '$l($g(PLIST(63))) s PLIST(63)=$p($h,",",2)
 q
]]></Routine>
</Export>
