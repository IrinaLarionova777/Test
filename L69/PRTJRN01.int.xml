<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTJRN01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTJRN01 ; ML 23/06/99 ; JOURNAL LIST
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Journal code
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Journal List"_$s($l(PAR3):" ( "_CTJO(2)_" - "_CTJO(3)_" )",1:"")
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 i $l(PAR3),'$$select^LVBCTJO(PAR3) m CTJO=PLIST
 q
BODY s jrncode="" f  s jrncode=$o(^TJOU(jrncode)) q:jrncode=""  d
 .i $l(PAR3),PAR3'=jrncode q
 .s z=$g(^TTAB("JO",jrncode)),jrntype=$p(z,"\",2)
 .s date=PAR1-1 f  s date=$o(^TJOU(jrncode,date)) q:date=""  q:date>PAR2  d
 ..s epis="" f  s epis=$o(^TJOU(jrncode,date,epis)) q:epis=""  d
 ...s comp=$$epcomp^LVBVIS1(epis) i '$l(comp) s comp=" "
 ...s jrnord="" f  s jrnord=$o(^TJOU(jrncode,date,epis,jrnord)) q:jrnord=""  d
 ....s z=$g(^TJOU(jrncode,date,epis,jrnord)),amt=$p(z,"\",3)
 ....s ^TMP($zn,$j,"REPORT",comp,jrncode,date,epis,jrnord)=$p(z,"\",1,5)
 ....i jrntype="C" s $p(^TMP($zn,$j,"REPORT",comp,jrncode),"\",1)=$p($g(^TMP($zn,$j,"REPORT",comp,jrncode)),"\",1)+amt
 ....i jrntype="D" s $p(^TMP($zn,$j,"REPORT",comp,jrncode),"\",2)=$p($g(^TMP($zn,$j,"REPORT",comp,jrncode)),"\",2)+amt
 ....i jrntype="C" s $p(^TMP($zn,$j,"REPORT",comp),"\",1)=$p($g(^TMP($zn,$j,"REPORT",comp)),"\",1)+amt
 ....i jrntype="D" s $p(^TMP($zn,$j,"REPORT",comp),"\",2)=$p($g(^TMP($zn,$j,"REPORT",comp)),"\",2)+amt
 ....i jrntype="C" s $p(^TMP($zn,$j,"REPORT"),"\",1)=$p($g(^TMP($zn,$j,"REPORT")),"\",1)+amt
 ....i jrntype="D" s $p(^TMP($zn,$j,"REPORT"),"\",2)=$p($g(^TMP($zn,$j,"REPORT")),"\",2)+amt
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s comp="" f i=1:1:10 s xxx(i)=""
 .f  s comp=$o(^TMP($zn,$j,"REPORT",comp)) q:comp=""  d
 ..s xxx(1)=comp,jrncode="" f  s jrncode=$o(^TMP($zn,$j,"REPORT",comp,jrncode)) q:jrncode=""  d
 ...s xxx(2)=jrncode,date="" f  s date=$o(^TMP($zn,$j,"REPORT",comp,jrncode,date)) q:date=""  d
 ....s xxx(3)=$$extdate2^SSUTIL4(date),epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",comp,jrncode,date,epis)) q:epis=""  d
 .....s jrnord="" f  s jrnord=$o(^TMP($zn,$j,"REPORT",comp,jrncode,date,epis,jrnord)) q:jrnord=""  d
 ......s z=^(jrnord)
 ......q:$$select^LVBEPVIS(epis)
 ......s invoice=$p(PLIST(57),$c(1)) i $l(invoice) s x1=$p(invoice,"||",1),x2=$p(invoice,"||",2) i $l(x1),$l(x2) s invoice=x1_"-"_x2_"-"_$p(^TMCB(x1,x2,epis),"\",2)
 ......s xxx(4)=invoice,xxx(5)=jrnord,xxx(6)=$p(z,"\",1),xxx(7)=$p(z,"\",2),xxx(8)=$p(z,"\",3)
 ......s xxx(9)=$p(z,"\",4),xxx(10)=$p(z,"\",5)
 ......d PRINT^PRTREP00(.PLIST,.N)
 ......k PLIST,xxx f i=1:1:10 s xxx(i)=""
 ...s xxx(2)="Total :"
 ...s (amt,cr)=""
 ...s amt1=$p($g(^TMP($zn,$j,"REPORT",comp,jrncode)),"\",1) i amt1 s amt=amt1,cr="CR"
 ...s amt2=$p($g(^TMP($zn,$j,"REPORT",comp,jrncode)),"\",2) i amt2 s amt=amt2,cr=""
 ...s xxx(8)=amt,xxx(9)=cr
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ...d PRINT^PRTREP00(,.N)
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..s amt1=$p($g(^TMP($zn,$j,"REPORT",comp)),"\",1)
 ..s amt2=$p($g(^TMP($zn,$j,"REPORT",comp)),"\",2)
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","?1  Totals for Company : "_comp)
 ..d ADDLINE^PRTREP00(REPID,"PRT","?45 Debits"),ADDLINE^PRTREP00(REPID,"PRT","?66 "_$j(amt2,9,2))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","?45 Credits"),ADDLINE^PRTREP00(REPID,"PRT","?66 "_$j(amt1,9,2)_" CR")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..i $o(^TMP($zn,$j,"REPORT",comp))'="" d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .s amt1=$p(^TMP($zn,$j,"REPORT"),"\",1)
 .s amt2=$p(^TMP($zn,$j,"REPORT"),"\",2)
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?1  Total Debits"),ADDLINE^PRTREP00(REPID,"PRT","?66 "_$j(amt2,9,2))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?1  Total Credits"),ADDLINE^PRTREP00(REPID,"PRT","?66 "_$j(amt1,9,2)_" CR")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="JRN"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
