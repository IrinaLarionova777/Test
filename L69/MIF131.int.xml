<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF131" type="INT" languagemode="0" generated="1"><![CDATA[
MIF131(mi) ; ML 14/06/01 ; BEHNK ELEKTRONIC Thrombolyser
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),eot=$c(4),ack=$c(6),lf=$c(10),cr=$c(13),nak=$c(21),etb=$c(23),fs=$c(28),gs=$c(29),rs=$c(30)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main f  r *x:60 i $c(x)=stx d
 .s record="" s record=$$read^MIF000(rs,gs)
 . ; attach rs/gs for checksum calculation
 .s rec=rs_record_gs,recx=$tr(record,$c(13,10)),recx=$tr(recx,$c(30),"^")
 .s record=$$read^MIF000("",lf)
 .s chsum=$p(record,cr) d trace^MIF000(mi,recx_" "_chsum,"H<--M")
 .i $$CHSUM(rec)'=chsum d  q
 ..w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 .d ACK
 .s rec=$p(rec,rs,2,99),rec=$p(rec,gs,1)
 .f i=1:1:$l(rec,rs) s recx=$p(rec,rs,i) d
 .. ; proc type : 1 = complete pat. results, transmit from data bank
 .. ;           : 9 = complete pat. results, automatic download
 ..s type=$p(recx,"|") i type'=1,type'=9 q
 ..s (sample,epis,surname,result,date,time,QC)=""
 ..s epis=$tr($p(recx,"|",2)," ")
 .. ; res type : 0 = PT
 .. ;          : 1 = APTT
 ..s restype=$tr($p(recx,"|",3)," ") i restype'=0,restype'=1 q
 ..s res=$p(recx,"|",6,8)
 ..f j=1:1:$l(res,"|") d
 ...i restype=0 s x1=$p($t(0)," ",j+2)
 ...i restype=1 s x1=$p($t(1)," ",j+2)
 ...s x2=$tr($tr($p(res,"|",j)," "),$c(13,10)),x3=""
 ...s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 ..d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
0 ; PTtime PTvalue PTinr
1 ; APTTtime APTTvalue
]]></Routine>
</Export>
