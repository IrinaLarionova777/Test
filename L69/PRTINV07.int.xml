<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV07" type="INT" languagemode="0"><![CDATA[
PRTINV07 ; IG 07/10/98 ; PRIVATE INVOICES - UNIVERSAL
 Q
 ; Parameters :  1.Payment Code
 ;               2.Batch Number
 ;               3.Episode Number
 ;               4.Sequence Number
 ;               5.Reminder
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($ZN,$J)
 D INIT,BODY,PRINT
CREND I 'PAR5,'$$select^LVBPRPR(REPID) S PLIST(12)=PAR1_"-"_PAR2 I $$update^LVBPRPR(REPID)
 I PAR5,'$$select^LVBPRPR(REPID) S PLIST(12)="REM-"_PAR5 I $$update^LVBPRPR(REPID)
 K ^TMP($ZN,$J),^TMP("PRTREP00",$J)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
 ; create temporary printing file
BODY ; print single patient
 i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
 .s xx1=$p(PLIST(9),$c(1)),xx2=$e(PLIST(57),$l(xx1)+1,$l(PLIST(57)))
 . ; find sequence
 .s xx3="" i $l(xx1),$l(xx2) s xx3=$p($g(^TMCB(xx1,xx2,PAR3)),"\",2)
 . ; collect all episodes for this sequence
 .i $l(xx3) d
 .. ; get provider name and provider number
 ..k INVOICE i '$$select^LVBINVB(xx1_"||"_xx2) m INVOICE=PLIST
 ..s ^TMP($zn,$j,"REPORT")=$p(INVOICE(10),$c(1),2)_" - "_INVOICE(5)
 ..s epis="" f  s epis=$o(^TMCB(xx1,xx2,epis)) q:epis=""  i xx3=$p(^TMCB(xx1,xx2,epis),"\",2) d
 ...i $$CT007^at345(epis,xx1_"||"_xx2)="Y" q
 ...i '$$select^LVBEPVIS(epis,"Y") d  q
 ....s ^TMP($ZN,$J,"REPORT",xx3,"EPISODE",epis)=PLIST(23)
 ....s ^TMP($ZN,$J,"REPORT",xx3)=xx1_"-"_xx2_"-"_xx3
 ; print batch
 i $l(PAR1),$l(PAR2) d
 .k INVOICE i '$$select^LVBINVB(PAR1_"||"_PAR2) m INVOICE=PLIST
 .s COMP(10)=$$seldata^LVBCTCO(INVOICE(6),10)
 .s ^TMP($zn,$j,"REPORT")=$p(INVOICE(10),$c(1),2)_" - "_INVOICE(5)
 .s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
 ..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
 ..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
 ..i $l(PAR4),PAR4'=seq q
 ..i '$$select^LVBEPVIS(epis,"Y") d  q
 ...s ^TMP($ZN,$J,"REPORT",seq,"EPISODE",epis)=PLIST(23)
 ...s ^TMP($ZN,$J,"REPORT",seq)=PAR1_"-"_PAR2_"-"_seq
 ; check credits
 i PAR5 d
 .s seq="" f  s seq=$o(^TMP($zn,$j,"REPORT",seq)) q:seq=""  d
 ..s (credit,epis)="" f cnt=1:1 s epis=$O(^TMP($ZN,$J,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ...s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 ...s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ....s rowid=deb_"||"_date_"||"_ep
 ....i $$open^LVBDEBVT(rowid)
 ....f  q:$$fetch^LVBDEBVT(rowid)  d
 .....f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .....k TRAN m TRAN=PLIST
 .....i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) d
 ......s credit=credit-PLIST(14)
 ......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate2^SSUTIL4(PLIST(4))
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="JOURNAL "_PLIST(3)_"-"_PLIST(6)
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(14)
 .....i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) d
 ......s credit=credit-PLIST(16)
 ......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 ......s x1=$p(PLIST(1),"||",1),x2=$p(PLIST(1),"||",2),receipt=$p(^TBAT(x1,x2),"\",3)
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate2^SSUTIL4(PLIST(12))
 ......i $l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Receipt "_receipt
 ......i '$l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Credit "_x1_"-"_x2
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(16)
 ......s xLength=$l(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2))+1
 ......i $$open^LVBCRBLA(TRAN(2))
 ......f jj=0:1 q:$$fetch^LVBCRBLA(TRAN(2))  d
 .......f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1) i jj s x=x+1
 .......f j=1:1:3 s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j))
 .......s xx=$e($s(PLIST(3)="CQ":"Cheque",PLIST(3)="CC":"Cr.Card",1:"Cash")_$s($l(PLIST(8)):" "_PLIST(8),1:""),1,30)
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)=$e(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)_$j("",xLength),1,xLength)_"("_xx_")"
 ......i $$close^LVBCRBLA()
 .....i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) d
 ......s credit=credit-PLIST(9)
 ......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 ......s x1=$p(PLIST(1),"||",1),x2=$p(PLIST(1),"||",2),receipt=$p(^TBAT(x1,x2),"\",3)
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate2^SSUTIL4(PLIST(5))
 ......i $l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Receipt "_receipt
 ......i '$l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Credit "_x1_"-"_x2
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(9)
 ......s xLength=$l(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2))+1
 ......i $$open^LVBCRBLA(TRAN(2))
 ......f jj=0:1 q:$$fetch^LVBCRBLA(TRAN(2))  d
 .......f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1) i jj s x=x+1
 .......f j=1:1:3 s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j))
 .......s xx=$e($s(PLIST(3)="CQ":"Cheque",PLIST(3)="CC":"Cr.Card",1:"Cash")_$s($l(PLIST(8)):" "_PLIST(8),1:""),1,30)
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)=$e(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)_$j("",xLength),1,xLength)_"("_xx_")"
 ......i $$close^LVBCRBLA()
 ....i $$close^LVBDEBVT()
 ..i credit s ^TMP($zn,$j,"REPORT",seq,"CREDIT")=credit
 ; set events
 s seq="" f  s seq=$o(^TMP($zn,$j,"REPORT",seq)) q:seq=""  s invoice=^(seq) d
 .s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..i '$l(PAR5) d events^LVBCOM01(epis,"ACC","Original invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ..i PAR5="R" d events^LVBCOM01(epis,"ACC","Receipt "_PAR6_$s($l(USER):" printed by "_USER,1:"")) q
 ..i PAR5="H" d events^LVBCOM01(epis,"ACC","Invoice history "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ..i 'PAR5 d events^LVBCOM01(epis,"ACC","Duplicate invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ..d events^LVBCOM01(epis,"ACC","Reminder "_PAR5_$s($l(USER):" printed by "_USER,1:""))
 Q
PRINT s max=17
 s seq="" f  s seq=$O(^TMP($ZN,$J,"REPORT",seq)) q:seq=""  d
 . ; get pay code invoice type
 .s pc=$p(^TMP($ZN,$J,"REPORT",seq),"-",1)
 .s (pcit,pcMessage)="" i '$$select^LVBCTPC(pc,"Y") s pcit=PLIST(25),pcMessage=PLIST(10)
 . ; calculate amount
 .s (message,amount,amountGST)="" k list s INVOICE=^TMP($ZN,$J,"REPORT",seq)
 .s epis="" f cnt=1:1 s epis=$O(^TMP($ZN,$J,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..i '$l(message) s message=^(epis)
 ..s list(cnt)=epis i $$select^LVBEPVIS(epis) q
 ..s item="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 ...s amount=amount+^(item)
 ...i $p(^(item),"\",4)="GST" s amountGST=amountGST+$p(^(item),"\")
 .s credit=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT")),amount=amount-credit
 . ; print invoice
 .s (start,last,autocyte)=0
 .s epis="" f  s epis=$O(^TMP($ZN,$J,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis) k VISIT m VISIT=PLIST
 ..s icDescr="" i '$$select^LVBCTIC(VISIT(18),"Y") s icDescr=PLIST(5)
 ..i icDescr="" s icDescr="Processing Fee"
 ..i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") k DOC m DOC=PLIST
 ..i 'start d S1 s start=1
 ..d S2
 ..s item="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  s xx=^(item) d
 ...k x s x(1)=$$extdate2^SSUTIL4(VISIT(12)),x(2)=item,x(3)=$p(xx,"\",2),x(4)=$p(xx,"\",1),x(5)=$p(xx,"\",4)
 ...i item=VISIT(54),'$l(x(3)) s x(3)=icDescr
 ...d S3
 ..i VISIT(45)="Y" d
 ...k x s (x(1),x(2),x(4),x(5))="",x(3)="Rule 3 exemption"
 ...d S3
 .. ; print LCC after billing items
 ..i $l(VISIT(18)),$l(VISIT(27)) d
 ...i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1))) d
 ....d ADD^SSPRINT(REPID,"PRT","?5  SCP : "_$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))),LN  s line=line+1
 .i PAR5 d S6
 .s last=1 d S5
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 i '$L(ADDRESS(1)) s ADDRESS(1)=$p(VISIT(7),$c(1),2)_" "_$e(VISIT(4))_" "_VISIT(3)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)="",ADDRESS(2)'[ADDRESS(6) s ADDRESS(2)=ADDRESS(2)_" "_ADDRESS(6)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(6)
 q
 ; print invoice header
S1 d ADD^SSPRINT(REPID,"PRT","#")
 d ADDRESS
 f j=1:1:6 d LN
 d ADD^SSPRINT(REPID,"PRT","?13 ABN: 53 078 725 103")
 f j=1:1:2 d LN
 d ADD^SSPRINT(REPID,"PRT","?10 APA No: 000931")
 d ADD^SSPRINT(REPID,"PRT","?50 "_ADDRESS(1)),LN
 d ADD^SSPRINT(REPID,"PRT","?50 "_ADDRESS(2)),LN
 d ADD^SSPRINT(REPID,"PRT","?10 "_^TMP($zn,$j,"REPORT"))
 d ADD^SSPRINT(REPID,"PRT","?50 "_ADDRESS(3)),LN
 d ADD^SSPRINT(REPID,"PRT","?50 "_ADDRESS(4)),LN f j=1:1:7 d LN
 d ADD^SSPRINT(REPID,"PRT","~NORMAL"),LN
 d ADD^SSPRINT(REPID,"PRT","?4  Date Serv.")
 i '$l(pcit) d ADD^SSPRINT(REPID,"PRT","?16 Item No")
 d ADD^SSPRINT(REPID,"PRT","?25 Services Provided")
 d ADD^SSPRINT(REPID,"PRT","?69 Amount"),LN,LN
 s line=0
 q
 ; print episode header
S2 i line+3>max d S5,S1
 d ADD^SSPRINT(REPID,"PRT","~BOLD")
 d ADD^SSPRINT(REPID,"PRT","Services provided for "_$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101)))
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 d LN s line=line+1
 s provider=DOC(18) i VISIT(12)<DOC(37) s provider=DOC(36)
 d ADD^SSPRINT(REPID,"PRT","?1  Req# "_epis_" "_$$extdate2^SSUTIL4(VISIT(14))_" - "_$$doc($p(VISIT(15),$c(1),1))_" ("_provider_")")
 d LN s line=line+1
 q
 ; print doctors name(s)
doc(doc) s doc=$g(doc) n (doc) s result=""
 i '$$select^LVBCTDR(doc,"Y") s result=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 q result
 ; print single item
S3 i line+3>max d S5,S1
 ; split description
 s x(3,1)=x(3) i $l(x(3))>45 s x=0 d
 .f j=1:1:$l(x(3)," ") i $l($p(x(3)," ",1,j))>45 s x=x+1,x(3,x)=$p(x(3)," ",1,j-1),(x(3),x(3,x+1))=$p(x(3)," ",j,999) q:$l(x(3))'>45
 d ADD^SSPRINT(REPID,"PRT","?5  "_x(1))
 i '$l(pcit),$l(x(2)) d ADD^SSPRINT(REPID,"PRT","?16 "_x(2)_" "_$s(x(5)="Y":"#",1:" "))
 d ADD^SSPRINT(REPID,"PRT","?25 "_x(3,1))
 i $l(x(4)) d ADD^SSPRINT(REPID,"PRT","?70 "_$j(x(4),7,2))
 d LN s line=line+1
 f j=2:1 q:'$d(x(3,j))  d ADD^SSPRINT(REPID,"PRT","?25 "_x(3,j)),LN s line=line+1
 i x(2)=90001 s autocyte=1
 q
S6 ; print credits
 d LN s line=line+1 i line+7>max d S5,S1
 d LN s line=line+1 i line+7>max d S5,S1
 d ADD^SSPRINT(REPID,"PRT","?5  Payment Date")
 d ADD^SSPRINT(REPID,"PRT","?20 Payment Reference #")
 d ADD^SSPRINT(REPID,"PRT","?67 Amount"),LN s line=line+1
 d ADD^SSPRINT(REPID,"PRT","?5  ------------")
 d ADD^SSPRINT(REPID,"PRT","?20 -------------------")
 d ADD^SSPRINT(REPID,"PRT","?67 ----------"),LN s line=line+1
 s credit=$d(^TMP($zn,$j,"REPORT",seq,"CREDIT"))\10
 i credit s xx="" f  s xx=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",xx)) q:xx=""  d
 .i line+4>max d S5,S1
 .f j=1,2,3 s @("x"_j)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",xx,j))
 .d ADD^SSPRINT(REPID,"PRT","?5  "_x1)
 .d ADD^SSPRINT(REPID,"PRT","?20 "_x2)
 .d ADD^SSPRINT(REPID,"PRT","?67 "_$j(x3,10,2)),LN s line=line+1
 i 'credit d ADD^SSPRINT(REPID,"PRT","?5  No payments have been received for this account")
 i (line+3)<max f i=1:1:max-line-3 d LN s line=line+1
 i amount d
 .i PAR5=1 d
 ..d ADD^SSPRINT(REPID,"PRT","~BOLD"),ADD^SSPRINT(REPID,"PRT",$c(13))
 ..d ADD^SSPRINT(REPID,"PRT","?5  Our records show that this account is outstanding."),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","?5  If payment has been forwarded, please disregard this reminder."),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","?5  Otherwise prompt payment would be appreciated."),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","~NORMAL"),ADD^SSPRINT(REPID,"PRT",$c(13))
 .i PAR5=2 d
 ..d ADD^SSPRINT(REPID,"PRT","~BOLD"),ADD^SSPRINT(REPID,"PRT",$c(13))
 ..d ADD^SSPRINT(REPID,"PRT","?5  This account is now overdue. Payment is required within 14 days, as"),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","?5  futher action may commence. Please call us if you have any queries."),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","~NORMAL"),ADD^SSPRINT(REPID,"PRT",$c(13))
 q
S5 i line<max f i=1:1:max-line d LN
 d
 .i $l(pcMessage) d ADD^SSPRINT(REPID,"PRT","?5  "_pcMessage) q
 .i $l(pcit) d ADD^SSPRINT(REPID,"PRT","?5  PLEASE NOTE THAT THESE ITEMS ARE NOT CLAIMABLE FROM MEDICARE.") q
 .i $l(message) d ADD^SSPRINT(REPID,"PRT","?5  Paycode reference : "_message) q
 d LN,LN
 i 'last d ADD^SSPRINT(REPID,"PRT","?62 Continued over .....")
 i amountGST d LN,ADD^SSPRINT(REPID,"PRT","?1  # indicates taxable items")
 d LN,LN
 d ADD^SSPRINT(REPID,"PRT","~BOLD")
 d ADD^SSPRINT(REPID,"PRT","?1  This is a Tax Invoice"),LN
 i $l(PAR5),'PAR5 d ADD^SSPRINT(REPID,"PRT","?1  ** DUPLICATE **")
 i $l(PAR5),PAR5 d ADD^SSPRINT(REPID,"PRT","?1  ** REMINDER **")
 i '$l(PAR5) d ADD^SSPRINT(REPID,"PRT","?1  ** ORIGINAL **")
 d ADD^SSPRINT(REPID,"PRT","?30 Total owing on this invoice .....")
 d ADD^SSPRINT(REPID,"PRT","?69 $"_$j(amount,7,2)),LN
 i amountGST d
 .d ADD^SSPRINT(REPID,"PRT","?30 Total includes GST of .....")
 .d ADD^SSPRINT(REPID,"PRT","?69 $"_$j(amountGST,7,2))
 d ADD^SSPRINT(REPID,"PRT","~NORMAL"),LN,LN
 s aLINE="",$p(aLINE,"_",80)=""
 d ADD^SSPRINT(REPID,"PRT","~BOLD"),LN
 d ADD^SSPRINT(REPID,"PRT","?15  SEND ONLY THIS PORTION TO MEDICARE"),LN
 d ADD^SSPRINT(REPID,"PRT",aLINE)
 d ADD^SSPRINT(REPID,"PRT","~NORMAL"),LN
 d ADD^SSPRINT(REPID,"PRT","?5  PLEASE DETACH AND RETURN THIS LOWER PORTION WITH YOUR PAYMENT"),LN
 d ADD^SSPRINT(REPID,"PRT","?5  Please make cheques payable to: UNIVERSAL DIAGNOSTIC LABORATORIES"),LN
 d ADD^SSPRINT(REPID,"PRT","?5  Postal address: P.O. Box 260, Fairfield, NSW 2165"),LN
 d ADD^SSPRINT(REPID,"PRT","?68 "_INVOICE)
 d ADD^SSPRINT(REPID,"PRT","~BOLD"),LN
 ; print first line of episodes
 f j=1,4,7 i $d(list(j)) d
 .i j=1 d ADD^SSPRINT(REPID,"PRT","?50 "_list(j))
 .i j=4 d ADD^SSPRINT(REPID,"PRT","?62 "_list(j))
 .i j=7 d ADD^SSPRINT(REPID,"PRT","?74 "_list(j))
 d LN
 ; print second line of episodes
 f j=2,5,8 i $d(list(j)) d
 .i j=2 d ADD^SSPRINT(REPID,"PRT","?50 "_list(j))
 .i j=5 d ADD^SSPRINT(REPID,"PRT","?62 "_list(j))
 .i j=8 d ADD^SSPRINT(REPID,"PRT","?74 "_list(j))
 d LN
 ; print third line of episodes
 d ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(1))
 f j=3,6,9 i $d(list(j)) d
 .i j=3 d ADD^SSPRINT(REPID,"PRT","?50 "_list(j))
 .i j=6 d ADD^SSPRINT(REPID,"PRT","?62 "_list(j))
 .i j=9 d ADD^SSPRINT(REPID,"PRT","?74 "_list(j))
 d LN,ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(2))
 d LN,ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(3))
 d LN,ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(4))
 d ADD^SSPRINT(REPID,"PRT","?68 "_$$extdate2^SSUTIL4(+$h))
 d LN,LN,LN
 d ADD^SSPRINT(REPID,"PRT","?51 Invoice Balance")
 d ADD^SSPRINT(REPID,"PRT","?69 $"_$j(amount,7,2))
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 q
LN d ADD^SSPRINT(REPID,"PRT","!")
 q
]]></Routine>
</Export>
