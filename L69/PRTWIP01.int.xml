<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTWIP01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTWIP01 ; ML 19/10/98 ; WORK IN PROGRESS
 q
 ; Parameters :  1. Full         - Y
 ;                  Summary      - N
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Work in Progress"_$s(PAR1="Y":" - Full",1:" - Summary")
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="To Date : "_$$extdate2^SSUTIL4(+$h)
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 ; work in progress
 s status="WIP"
 f stat="C","E","P" d
 .s (epis,ts)="" f  s ts=$o(^TEPIi("STATUS-TEST",stat,ts)) q:ts=""  d
 ..f  s epis=$o(^TEPIi("STATUS-TEST",stat,ts,epis)) q:epis=""  s ^TMP($zn,$j,"EPIS",epis)=""
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d price(status,epis)
 k ^TMP($zn,$j,"EPIS")
 ; to be priced
 s status="WTP"
 s epis="" f  s epis=$o(^TBIL(9,epis)) q:epis=""  d price(status,epis)
 ; to be manually priced
 s status="WMP"
 s epis="" f  s epis=$o(^TBIL(8,epis)) q:epis=""  d
 .s pc=$$seldata^LVBEPVIS(epis,9),mrn=$$seldata^LVBEPVIS(epis,20),icdr=$$seldata^LVBEPVIS(epis,18),inpat=$$seldata^LVBCTIC(icdr,4)
 .k PLIST i $l(pc),$l(mrn) d
 ..s item="" f j=1:1 s item=$o(^TBIL(8,epis,"AM",item)) q:item=""  s PLIST(j)=item_"\"_^(item)
 ..s PLIST=$o(PLIST(""),-1) d Set(status,pc,mrn,epis,inpat)
 ; to be invoiced
 s status="WTI"
 s comp="" f  s comp=$o(^TBIL(2,comp)) q:comp=""  d
 .s payc="" f  s payc=$o(^TBIL(2,comp,payc)) q:payc=""  d
 ..s epis="" f  s epis=$o(^TBIL(2,comp,payc,epis)) q:epis=""  d
 ...s pc=$$seldata^LVBEPVIS(epis,9),mrn=$$seldata^LVBEPVIS(epis,20),icdr=$$seldata^LVBEPVIS(epis,18),inpat=$$seldata^LVBCTIC(icdr,4)
 ...k PLIST i $l(pc),$l(mrn) d
 ....s item="" f j=1:1 s item=$o(^TEPI(epis,7,item)) q:item=""  s PLIST(j)=item_"\"_^(item)
 ....s PLIST=$o(PLIST(""),-1) d Set(status,pc,mrn,epis,inpat)
 q
Set(status,pc,mrn,epis,inpat) n (status,pc,mrn,epis,inpat,PLIST)
 s (value1,value2)="" f j=1:1:PLIST d
 .s it=$p(PLIST(j),"\",1),am=$p(PLIST(j),"\",2) i $l(it),am d
 ..s value1=value1+$p(PLIST(j),"\",2)
 ..i $p(PLIST(j),"\",5)="GST" s value2=value2+$p(PLIST(j),"\",2)
 ..s ^TMP($zn,$j,"REPORT",status,pc,mrn,epis,it)=$p(PLIST(j),"\",2,3)_"\"_$p(PLIST(j),"\",5)
 i value1 d
 .s $p(^TMP($zn,$j,"REPORT",status,pc,mrn,epis),"\",1)=value1
 .s $p(^TMP($zn,$j,"REPORT",status,pc,mrn),"\",1)=$p($g(^TMP($zn,$j,"REPORT",status,pc,mrn)),"\",1)+value1
 .s $p(^TMP($zn,$j,"REPORT",status,pc),"\",1)=$p($g(^TMP($zn,$j,"REPORT",status,pc)),"\",1)+value1
 .s $p(^TMP($zn,$j,"REPORT",status),"\",1)=$p($g(^TMP($zn,$j,"REPORT",status)),"\",1)+value1
 .s $p(^TMP($zn,$j,"REPORT"),"\",1)=$p($g(^TMP($zn,$j,"REPORT")),"\",1)+value1
 .i value2 d
 ..s $p(^TMP($zn,$j,"REPORT",status,pc,mrn,epis),"\",2)=value2
 ..s $p(^TMP($zn,$j,"REPORT",status,pc,mrn),"\",2)=$p($g(^TMP($zn,$j,"REPORT",status,pc,mrn)),"\",2)+value2
 ..s $p(^TMP($zn,$j,"REPORT",status,pc),"\",2)=$p($g(^TMP($zn,$j,"REPORT",status,pc)),"\",2)+value2
 ..s $p(^TMP($zn,$j,"REPORT",status),"\",2)=$p($g(^TMP($zn,$j,"REPORT",status)),"\",2)+value2
 ..s $p(^TMP($zn,$j,"REPORT"),"\",2)=$p($g(^TMP($zn,$j,"REPORT")),"\",2)+value2
 .
 .s ^TMP($zn,$j,"TOTAL",pc,(inpat="Y"))=$g(^TMP($zn,$j,"TOTAL",pc,(inpat="Y")))+1
 .s ^TMP($zn,$j,"TOTAL",pc)=$g(^TMP($zn,$j,"TOTAL",pc))+1
 .s ^TMP($zn,$j,"TOTAL")=$g(^TMP($zn,$j,"TOTAL"))+1
 q
price(status,epis) n (status,epis,PLIST) s epis=$g(epis) k PLIST s (mrn,pc,pc1,con,init,inpat,xInit)=""
 s mrn=$$seldata^LVBEPVIS(epis,20),pc=$$seldata^LVBEPVIS(epis,9),init=$$seldata^LVBEPVIS(epis,18),inpat=$$seldata^LVBCTIC(init,4),con=$$seldata^LVBCTPC(pc,22)
 i '$l(pc)!'$l(mrn) q
 i '$$init^LVBQUOTE(init,pc) s xInit=$g(PLIST(1))
 k PLIST s ts="" f j1=1:1 s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 .s (j2,tscnt)="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ..s rowid=epis_"||"_ts_"||"_tscnt
 ..s type=$$seldata^LVBCTTS($$seldata^LVBVISTS(rowid,3),15),supress=$$seldata^LVBVISTS(rowid,37)
 .. ; no charge test set
 ..i type="N" q
 .. ; supress billing
 ..i supress="Y" q
 ..s j2=j2+1
 .i j2 s PLIST(j1)=ts_"\"_j2,PLIST=j1
 i '$$quote^LVBQUOTE(pc,con,xInit) d Set(status,pc,mrn,epis,inpat)
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s status="" f  s status=$o(^TMP($zn,$j,"REPORT",status)) q:status=""  d
 ..s xxx(1)=$s(status="WIP":"Work in Progress",status="WTP":"Waiting to Price",status="WMP":"To be Manually Priced",1:"Waiting to Invoice")
 ..s xxx(5)=$p(^TMP($zn,$j,"REPORT",status),"\",1)
 ..s xxx(7)=$p(^TMP($zn,$j,"REPORT",status),"\",2)
 ..s xxx(8)=xxx(5)-xxx(7)
 ..d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ..s pc="" f  s pc=$o(^TMP($zn,$j,"REPORT",status,pc)) q:pc=""  d
 ...s xxx(2)="" i '$$select^LVBCTPC(pc,"Y") s xxx(2)=pc_$c(1)_$p(PLIST(3),$c(1)) k PLIST
 ...s:xxx(2)="" xxx(2)=pc_$c(1)_pc
 ...s xxx(5)=$p(^TMP($zn,$j,"REPORT",status,pc),"\",1)
 ...s xxx(7)=$p(^TMP($zn,$j,"REPORT",status,pc),"\",2)
 ...s xxx(8)=xxx(5)-xxx(7)
 ...i PAR1="Y" d
 ....d ADDLINE^PRTREP00(REPID,"PRT","!")
 ....d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ....d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 ...d PRINT^PRTREP00(,.N) k xxx
 ...i PAR1="Y" d
 ....d ADDLINE^PRTREP00(REPID,"PRT","!")
 ....d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ....d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 ...i PAR1="Y" d
 ....s mrn="" f  s mrn=$o(^TMP($zn,$j,"REPORT",status,pc,mrn)) q:mrn=""  d
 .....s epis="" f cnt=0:1 s epis=$o(^TMP($zn,$j,"REPORT",status,pc,mrn,epis)) q:epis=""  d
 ......i $$select^LVBEPVIS(epis)
 ......s xxx(5)=$p(^TMP($zn,$j,"REPORT",status,pc,mrn,epis),"\",1)
 ......s xxx(7)=$p(^TMP($zn,$j,"REPORT",status,pc,mrn,epis),"\",2)
 ......s xxx(8)=xxx(5)-xxx(7)
 ......s item="" f  s item=$o(^TMP($zn,$j,"REPORT",status,pc,mrn,epis,item)) q:item=""  d
 .......s xxx(3)=item,xxx(4)=$p(^(item),"\",1),xxx(6)=$p(^(item),"\",2)
 .......d PRINT^PRTREP00(.PLIST,.N) k PLIST,xxx
 .....i cnt>1 d
 ......s xxx(5)=$p(^TMP($zn,$j,"REPORT",status,pc,mrn),"\",1)
 ......s xxx(7)=$p(^TMP($zn,$j,"REPORT",status,pc,mrn),"\",2)
 ......s xxx(8)=xxx(5)-xxx(7)
 ......d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 .s (t1,t2,pc)="" f  s pc=$o(^TMP($zn,$j,"TOTAL",pc)) q:pc=""  d
 ..s type=1 s t1=t1+$g(^TMP($zn,$j,"TOTAL",pc,type))
 ..s type=0 s t2=t2+$g(^TMP($zn,$j,"TOTAL",pc,type))
 .s $p(xxx(1)," |",1)=$j(+t1,6)
 .s $p(xxx(1)," |",2)=$j(+t2,6)
 .s $p(xxx(1)," |",3)=$j(t1+t2,6)
 .s xxx(2)="Grand Total"
 .s xxx(5)=$p(^TMP($zn,$j,"REPORT"),"\",1)
 .s xxx(7)=$p(^TMP($zn,$j,"REPORT"),"\",2)
 .s xxx(8)=xxx(5)-xxx(7)
 .d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s $p(xxx(1)," |",1)=$j("In  ",6)
 .s $p(xxx(1)," |",2)=$j("Out ",6)
 .s $p(xxx(1)," |",3)=$j("Total",6)
 .d PRINT^PRTREP00(,.N) k PLIST,xxx
 .s pc="" f  s pc=$o(^TMP($zn,$j,"TOTAL",pc)) q:pc=""  d
 ..s xxx(2)="" i '$$select^LVBCTPC(pc,"Y") s xxx(2)=pc_$c(1)_$p(PLIST(3),$c(1)) k PLIST
 ..s:xxx(2)="" xxx(2)=pc_$c(1)_pc
 ..s type=1 s $p(xxx(1)," |",1)=$j(+$g(^TMP($zn,$j,"TOTAL",pc,type)),6)
 ..s type=0 s $p(xxx(1)," |",2)=$j(+$g(^TMP($zn,$j,"TOTAL",pc,type)),6)
 ..s $p(xxx(1)," |",3)=$j(+$g(^TMP($zn,$j,"TOTAL",pc)),6)
 ..d PRINT^PRTREP00(,.N) k PLIST,xxx
 s report="WIP"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
