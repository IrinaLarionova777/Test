<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCL00" type="INT" languagemode="0" generated="1"><![CDATA[
PRTCL00 ; IG 25/06/99 ; Collection List  ; Compiled February 28, 2000 14:07:43 for Cache for Windows
 ;
 ; Parameters :  1.Date from
 ;               2.Date to
 ;               3.Collector
 ;               4.User Location
 ;               5.Print Labels
 ;               6.Print Collection List
 ;               7.Create
 ;
 ;               8.Collection List
 ;               9.Collection List Number
 ;              11.Time from
 ;              12.Time to
 ;              13.Patient type: IN -InPatient
 ;                               OUT-OutPatient
 ;                                  -All
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql)
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 ; print Single Collection List
 i $l(PAR8),$l(PAR9) d  q
 .i PAR6="Y" d
 ..s xx=PAR8_"\"_PAR9 d START^PRTREP00(USER,,xx,"CLA",,)
 ..i '$$select^LVBCLN(PAR8_"||"_PAR9) s PLIST(7)=+$h,PLIST(8)=$p($h,",",2) i $$update^LVBCLN(PAR8_"||"_PAR9)
 .i PAR5="Y" d
 ..s xx=PAR8_"\"_PAR9 d START^PRTREP00(USER,,xx,"CLB",,)
 ; create Collection Lists
 i PAR7="Y" d
 . ; clear old collection lists
 .s cl="" f  s cl=$o(^TTCL(cl)) q:cl=""  d
 ..i $l(PAR8),PAR8'=cl q
 ..s cln="" f  s cln=$o(^TTCL(cl,"LIST",cln)) q:cln=""  i '$$select^LVBCLN(cl_"||"_cln) d
 ...i $l(PAR1),PLIST(5)<PAR1 q
 ...i $l(PAR2),PLIST(5)>PAR2 q
 ...i $l(PAR3),$p(PLIST(6),$c(1))'=PAR3 q
 ...i $l(PAR4),$p(PLIST(9),$c(1))'=PAR4 q
 ...i $p(PLIST(11),$c(1))'=PAR13 q
 ...i $$delete^LVBCLN(cl_"||"_cln)
 .k ^TMP($zn,$j)
 .s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 ..s epis="" f  s epis=$o(^TDAY(1,date,3,epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 ...i $l(PAR3),PAR3'=$p(PLIST(28),$c(1)) q
 ...i $l(PAR4),PAR4'=$p(PLIST(66),$c(1)) q
 ...i $l(PAR11),PAR11>$p(PLIST(13),$c(1)) q
 ...i $l(PAR12),PAR12<$p(PLIST(13),$c(1)) q
 ...i $l(PAR13),PAR13="IN" s ok=0 d  i 'ok q
 ....i $l($p(PLIST(22),$c(1))) d
 .....s result=$$seldata^LVBCTHOS(PLIST(22),19)
 .....i result="Y" s ok=1
 ...i $l(PAR13),PAR13="OUT" s ok=1 d  i 'ok q
 ....i $l($p(PLIST(22),$c(1))) d
 .....s result=$$seldata^LVBCTHOS(PLIST(22),19)
 .....i result="Y" s ok=0
 ...s col=$p(PLIST(28),$c(1)) i '$l(col) s col=" "
 ...s loc=$p(PLIST(66),$c(1)) i '$l(loc) s loc=" "
 ...s time=$p(PLIST(13),$c(1)) i '$l(time) s time=" "
 ...s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ....s label=$$seldata^LVBCTTS(ts,23)
 ....i $l(label) d
 .....s cnt="" f  s cnt=$o(^TEPI(epis,1,ts,cnt)) q:cnt=""  d
 ......s cl="" f  s cl=$o(^TTAB("LB",label,"LIST",cl)) q:cl=""  d
 .......i $l(PAR8),PAR8'=cl q
 .......s ^TMP($zn,$j,1,cl,date,time,col,loc,epis,label)=$g(^TMP($zn,$j,1,cl,date,time,col,loc,epis,label))_","_ts
 . ; move all outstanding patients into current CL
 .s cl="" f  s cl=$o(^TMP($zn,$j,1,cl)) q:cl=""  d
 ..s date="" f  s date=$o(^TMP($zn,$j,1,cl,date)) q:date=""  d
 ...i date'=(+$h),$d(^TMP($zn,$j,1,cl,date," ")) d
 ....s time="" f  s time=$o(^TMP($zn,$j,1,cl,date,time)) q:time=""  i $l(time),time'=" " q
 ....i $l(time) m ^TMP($zn,$j,1,cl,date,time)=^TMP($zn,$j,1,cl,date," ")
 ...i date=(+$h) d
 ....s (first,time)="" f  s time=$o(^TMP($zn,$j,1,cl,date,time)) q:time=""  d
 .....i '$l(first),time>$p($h,",",2) s first=time
 ....i '$l(first) s first=" "
 ....s time="" f  s time=$o(^TMP($zn,$j,1,cl,date,time)) q:time=""  i time'=" " d
 .....i time<$p($h,",",2) m ^TMP($zn,$j,1,cl,date,first)=^TMP($zn,$j,1,cl,date,time) k ^TMP($zn,$j,1,cl,date,time)
 .s cl="" f  s cl=$o(^TMP($zn,$j,1,cl)) q:cl=""  d
 ..s max=$$seldata^LVBCL(cl,4)
 ..s date="" f  s date=$o(^TMP($zn,$j,1,cl,date)) q:date=""  d
 ...s time="" f  s time=$o(^TMP($zn,$j,1,cl,date,time)) q:time=""  d
 ....s col="" f  s col=$o(^TMP($zn,$j,1,cl,date,time,col)) q:col=""  d
 .....s loc="" f  s loc=$o(^TMP($zn,$j,1,cl,date,time,col,loc)) q:loc=""  d
 ......s cln=$$new(),pat="" i cln d
 .......s epis="" f  s epis=$o(^TMP($zn,$j,1,cl,date,time,col,loc,epis)) q:epis=""  d
 ........s pat=pat+1 i max,pat>max s cln=$$new(),pat=1
 ........k PLIST s PLIST(2)=epis,PLIST=$o(PLIST(""),-1) i $$insert^LVBCLNP(cl_"||"_cln) q
 ........s label="" f  s label=$o(^TMP($zn,$j,1,cl,date,time,col,loc,epis,label)) q:label=""  d
 .........k PLIST s PLIST(2)=label,PLIST(3)=$e(^(label),2,999),PLIST=$o(PLIST(""),-1)
 .........i $$insert^LVBCLNPL(cl_"||"_cln_"||"_epis)
 .s cl="" f  s cl=$o(^TMP($zn,$j,0,cl)) q:cl=""  d
 ..s cln="" f  s cln=$o(^TMP($zn,$j,0,cl,cln)) q:cln=""  d
 ...i $$START^PRTREP00(USER,"",cl_"\"_cln,"CLA")
 ...i '$$select^LVBCLN(cl_"||"_cln) s PLIST(7)=+$h,PLIST(8)=$p($h,",",2) i $$update^LVBCLN(cl_"||"_cln)
 k ^TMP($zn,$j)
 q
new() s ok=0
 k PLIST s PLIST(3)=+$h,PLIST(4)=$p($h,",",2),PLIST(5)=date,PLIST(6)=$s(col=" ":"",1:col),PLIST(9)=$s(loc=" ":"",1:loc)
 s PLIST=$o(PLIST(""),-1) i '$$insert^LVBCLN(cl) s ok=$p(PLIST(2),$c(1)),^TMP($zn,$j,0,cl,ok)=""
 q ok
]]></Routine>
</Export>
