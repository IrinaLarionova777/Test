<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF504" type="INT" languagemode="0" generated="1"><![CDATA[
MIF504(mi) ; ML 31/8/99 ; ABBOTT Cell Dyne 3200 ; QC - Yes
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),nak=$c(21),can=$c(24),del=$c(127)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000(stx,etx) i '$l(record) q
 d trace^MIF000(mi,record,"H<--M")
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 s type=$p(record,",",1) q:type'["   "
 ; process results record
 s (sample,epis,surname,result,date,time,spec,QC)=""
 s epis=$p(record,",",7),epis=$tr(epis," "),epis=$tr(epis,"""")
 q:epis=""  i $p(record,",",6)=1 s QC="\"_epis
 s surname=$p(record,",",8),surname=$tr(surname,"""",""),surname=$tr(surname,"-","")
 s date=$p(record,",",14),time=$p(record,",",15)
 s date=$tr(date,"""",""),time=$tr(time,"""","")
 s date=$$intdate^SSUTIL4(date),time=$$inttime^SSUTIL4(time)
 s res="",record=$p(record,",",19,40)
 f j=1:1:22 d
 .s code=$p($t(CODE)," ",j+2),res=$p(record,",",j),res=+$tr(res," ")
 .s result=result_code_$c(par10)_$s($e(res)=".":"0"_res,1:res)_$c(par11)
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
CODE ; WBC NEU LYM MONO EOS BASO RBC HGB HCT MCV MCH MCHC RDW PLT MPV PCT PDW NEU% LYM% MONO% EOS% BASO% 
]]></Routine>
</Export>
