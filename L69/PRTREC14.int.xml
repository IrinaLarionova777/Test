<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTREC14" type="INT" languagemode="0"><![CDATA[
PRTREC14 ; ML 27/06/01 ; RECEIPTS - Central Coast Pathology Services
 q
 ; Parameters :  1. Receipt Number
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) S PLIST(12)=$g(bs)_"-"_PAR1
 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
 ; create temporary printing file
BODY s epis="" i '$$select^LVBRCPT(PAR1) d
 .f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 .k REC m REC=PLIST
 .i '$$select^LVBCRBL(REC(8)) d
 ..f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ..k crLINE m crLINE=PLIST
 .. ; get single episodes
 ..i $l(crLINE(3)),crLINE(7) i '$l(epis) s epis=crLINE(3)
 .. ; get episode from multiple episodes
 ..i $$open^LVBCRBLB(REC(8))
 ..i '$$fetch^LVBCRBLB(REC(8)) s epis=$p(PLIST(2),$c(1))
 ..i $$close^LVBCRBLB()
 ; update receipt
 i REC(3)="" d
 .s REC(3)=+$h,REC(4)=$p($h,",",2)
 .k PLIST m PLIST=REC i $$update^LVBRCPT(PAR1) 
 i $l(epis),'$$select^LVBEPVIS(epis,"Y") d
 .s ^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW")=crLINE(7)
 .s xx1=$p(PLIST(57),"||",1),xx2=$p(PLIST(57),"||",2)
 .s credit="" i $l(PLIST(87)) s epis=PLIST(87)
 . ; get provider name and provider number
 .k INVOICE i '$$select^LVBINVB(xx1_"||"_xx2) m INVOICE=PLIST
 .s ^TMP($zn,$j,"REPORT")=$p(INVOICE(10),$c(1),2)_" - "_INVOICE(5)
 .s ^TMP($zn,$j,"REPORT",epis)=xx1_"-"_xx2
 .s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 .s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ..s rowid=deb_"||"_date_"||"_ep
 ..i $$open^LVBDEBVT(rowid)
 ..f  q:$$fetch^LVBDEBVT(rowid)  d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k TRAN m TRAN=PLIST
 ...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) s credit=credit-PLIST(14)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) s credit=credit-PLIST(16)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) s credit=credit-PLIST(9)
 ..i $$close^LVBDEBVT()
 .i credit s ^TMP($zn,$j,"REPORT",epis,"CREDIT-TOTAL")=credit
 Q
PRINT ; print report
 d QDR
 s max=20
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101))
 .d ITEM,ADDRESS,PHEAD
 .s (line,nextpg,item)=""
 .f  s item=$o(^TMP($zn,$j,"REPORT",epis,"ITEM",item)) q:item=""  d
 ..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2)
 .. ; check item desc length
 ..s x="" s xdesc=desc k desc s desc=xdesc
 ..i $l(desc)<23 s desc(1)=desc
 ..i $l(desc)>22 d
 ...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>22 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>22
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a500h"_vert_"V"_$$extdate2^SSUTIL4(VISIT(14)))
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1400h"_vert_"V"_$$extdate2^SSUTIL4(VISIT(12)))
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2250h"_vert_"V"_desc(1)),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3700h"_vert_"V"_item),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4250h"_vert_"V"_$j(amt,7,2)),LN s line=line+1
 ..f i=2:1 q:'$d(desc(i))  d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2250h"_vert_"V"_desc(i)),LN s line=line+1
 ..i line=16,$o(^TMP($zn,$j,"REPORT",epis,"ITEM",item))'="" d
 ...d LN,ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h4750VContinued over..")
 ...s nextpg=1 d PHEAD s (line,nextpg)=""
 . ; print LCC/SCP after billing item
 .i $l(VISIT(18)),$l(VISIT(27)) d
 ..i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1))) d
 ...d LN s line=line+1
 ...d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a500h"_vert_"V"_"SCP Number : "_$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))),LN  s line=line+1
 .s tot=$g(^TMP($zn,$j,"REPORT",epis,"TOT")),credit=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-TOTAL"))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b4148T")
 .s current=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW"))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h4870V")
 .d ADD^PRTREP00(REPID,"PRT","Payment Received      "_$j(current,0,2)),LN
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h4990V")
 .d ADD^PRTREP00(REPID,"PRT","Outstanding Balance   "_$j(tot-credit,0,2)),LN
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a500h4870VThank you for the payment")
 q
PHEAD ; print patient details
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p9v0s0b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h1200V"_name)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a500h1320VAcc to:")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h1320V"_ADDRESS(1))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4350h1320V"_epis)
 s vert=1200 f i=2:1:4 d
 .s pos=vert+(120*i) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h"_pos_"V"_ADDRESS(i))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4350h1720V"_$$extdate2^SSUTIL4(VISIT(56)))
 d DOC
 i drdet'="" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a500h2120V"_drdet)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3200h2120V"_$p(^TMP($zn,$j,"REPORT")," - ",1)_" Prov No: "_$p(^TMP($zn,$j,"REPORT")," - ",2))
 s vert=2720
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 .s ^TMP($zn,$j,"REPORT",epis,"ITEM",item)=det
 s ^TMP($zn,$j,"REPORT",epis,"TOT")=amount
 q
DOC ; extract referring doctor details
 s drdet=""
 q:$$select^LVBCTDR(VISIT(15))  m DOC=PLIST k PLIST
 s drdet=$$NameFormat^LVBCOM03("DR",DOC(26),$e(DOC(4)),DOC(3),DOC(49),DOC(50),DOC(51))_" Prov No: "_DOC(18)
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 i '$l(ADDRESS(1)) s ADDRESS(1)=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),$e(VISIT(4)),VISIT(3),VISIT(99),VISIT(100),VISIT(101))
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
LN ; add line to report
 s vert=vert+120
 q
QDR ; set header
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"E")
 s x="" f  s x=$o(^SS("QDR","CCP-REC",x)) q:x=""  d ADD^PRTREP00(REPID,"HDR",^(x))
 q
]]></Routine>
</Export>
