<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBCCR01NSW" type="INT" languagemode="0" generated="1"><![CDATA[
LVBCCR01NSW ; ML 24/12/99 ; CCR file creation - Trak (NSW)
 ;
 ; supported calls
 ;
 ; $$create^LVBCCR01NSW(type,TYPE,DATE)
 ;
create(type,TYPE,DATE) n (type,TYPE,DATE) s type=$g(type),TYPE=$g(TYPE),DATE=$g(DATE),file="",state="NSW" k ^TMP($zn,$j)
 i $$select^MVBCFLAB(1) q
 f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 s dir=PLIST(48),labs=PLIST(46),rid=$s(type="P":PLIST(47),1:PLIST(49))
 s lab="" f i=1:1:$l(labs,",") i $p($p(labs,",",i),"=",1)=state s lab=$p($p(labs,",",i),"=",2) q
 f j="\","/" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
 s delim=$c(9)
 ; create file
 i $d(^TCCR(type,"DATA",TYPE,DATE)),$l(lab) d
 .s xd=$$extdate2^SSUTIL4(DATE)
 .i TYPE="D" s stop=0 d  i stop q
 ..i type="B" s file=dir_$e($p(xd,"/",3),3,4)_$p(xd,"/",2)_$p(xd,"/",1)_"h1."_lab
 ..i type="P" s file=dir_$e($p(xd,"/",3),3,4)_$p(xd,"/",2)_$p(xd,"/",1)_"e1."_lab
 ..o file:"WNS":5 e  s stop=1
 .i TYPE="R" s stop=0 d  i stop q
 ..i type="B" s file=dir_$e($p(xd,"/",3),3,4)_$p(xd,"/",2)_$p(xd,"/",1)_"u1."_lab
 ..i type="P" s file=dir_$e($p(xd,"/",3),3,4)_$p(xd,"/",2)_$p(xd,"/",1)_"r1."_lab
 ..o file:"WNS":5 e  s stop=1
 .s (CNT,epis)="" f  s epis=$o(^TCCR(type,"DATA",TYPE,DATE,epis)) q:epis=""  d
 ..i $g(^(epis))'=state q
 ..i $$select^LVBEPVIS(epis) q
 ..f j=1:1:PLIST s PLIST(j)=$$UPPER^SSUTIL4($p(PLIST(j),$c(1)))
 ..k VISIT m VISIT=PLIST
 ..i $$select^LVBCTDR(VISIT(15)) q
 ..f j=1:1:PLIST s PLIST(j)=$$UPPER^SSUTIL4($p(PLIST(j),$c(1)))
 ..k DOC m DOC=PLIST
 ..i '$l(DOC(18)) s DOC(18)="0000000Y"
 ..s ts="" f  s ts=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts)) q:ts=""  d
 ...s cnt="" f  s cnt=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)) q:cnt=""  d
 ....s rid=rid+1,CNT=CNT+1,^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)=rid
 ....s acc="" f  s acc=$o(^THIR(epis,acc)) q:acc=""  i $d(^THIR(epis,acc,"TEST",epis_"||"_ts_"||"_cnt)) q
 ....s consent="y" i $l(acc) s xx=$$seldata^LVBHIVAC(epis_"||"_acc,4) i $l(xx) s consent=$tr($p(xx,$c(1)),"YN","yn")
 .... ; create episode record
 ....s RECORD1=$$txt(lab,3)_delim_$$txt(epis,20)_delim_$$txt($s(consent'="n":rid,1:""),40)_delim
 ....s RECORD1=RECORD1_$$txt(consent,1)_delim_$$txt("n",1)_delim
 ....i consent'="n" d
 .....s RECORD1=RECORD1_$$txt(VISIT(3),30)_delim_$$txt(VISIT(4),30)_delim_$$txt("",30)_delim
 .....s RECORD1=RECORD1_$$txt(VISIT(32),40)_delim_$$txt(VISIT(33),40)_delim_$$txt(VISIT(34),20)_delim_$$txt(VISIT(36),4)_delim
 ....i consent="n" d
 .....s RECORD1=RECORD1_$$txt("",30)_delim_$$txt("",30)_delim_$$txt("",30)_delim
 .....s RECORD1=RECORD1_$$txt("",40)_delim_$$txt("",40)_delim_$$txt("",20)_delim_$$txt("",4)_delim
 ....s RECORD1=RECORD1_$s($l(VISIT(6)):$$date(VISIT(6)),1:"01010001")_delim_$$txt(DOC(18),8)_delim_$$date(VISIT(14))
 ....i TYPE="D" s ^TMP($zn,$j,"RECORD",file,CNT)=RECORD1
 .... ; create result record
 ....i type="P" d
 .....s RECORD2=$$txt(lab,3)_delim_$$txt(epis,20)_delim_$$txt("n",1)_delim
 .....s RECORD2=RECORD2_$$txt("s",1)_delim_$s($l(VISIT(6)):$$date(VISIT(6)),1:"01010001")
 .....f j="A","B","S","E","O","R" d
 ......s xx=$g(^TEPI(epis,1,ts,cnt,"CCR",j)) i $l(xx) s xx=j_xx
 ......s RECORD2=RECORD2_delim_$$txt(xx,2)
 ....i type="B" d
 .....s RECORD2=$$txt(lab,3)_delim_$$txt(epis,20)_delim_$$txt("n",1)_delim
 .....s RECORD2=RECORD2_$s($l(VISIT(6)):$$date(VISIT(6)),1:"01010001")_delim_$$txt("",1)
 ....i TYPE="R" s ^TMP($zn,$j,"RECORD",file,CNT)=RECORD2
 .i TYPE="D" d
 ..s (sum,x)="" f  s x=$o(^TMP($zn,$j,"RECORD",file,x)) q:x=""  d
 ...f j=1:1:$l(^(x)) s sum=sum+($a(^(x),j)*j)
 ..s RECORD1=$$txt(lab,3)_$e($p(xd,"/",3),3,4)_$p(xd,"/",2)_$p(xd,"/",1)_$s(type="P":"e1",type="B":"h1")_delim_$tr($j(CNT,5)," ",0)_delim
 ..s RECORD1=RECORD1_$$txt(sum,20)_delim_$$txt($$date(+$h)_$$time(),30)
 ..u file w RECORD1,!
 ..s x="" f  s x=$o(^TMP($zn,$j,"RECORD",file,x)) q:x=""  u file w ^(x),!
 ..c file
 .i TYPE="R" d
 ..s (sum,x)="" f  s x=$o(^TMP($zn,$j,"RECORD",file,x)) q:x=""  d
 ...f j=1:1:$l(^(x)) s sum=sum+($a(^(x),j)*j)
 ..s RECORD2=$$txt(lab,3)_$e($p(xd,"/",3),3,4)_$p(xd,"/",2)_$p(xd,"/",1)_$s(type="P":"r1",type="B":"u1")_delim_$tr($j(CNT,5)," ",0)_delim
 ..s RECORD2=RECORD2_$$txt(sum,20)_delim_$$txt($$date(+$h)_$$time(),30)
 ..u file w RECORD2,!
 ..s x="" f  s x=$o(^TMP($zn,$j,"RECORD",file,x)) q:x=""  u file w ^(x),!
 ..c file
 .i '$$select^MVBCFLAB(1) d
 ..f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ..i type="P" s PLIST(47)=rid
 ..i type="B" s PLIST(49)=rid
 ..i $$update^MVBCFLAB(1)
 q file
txt(xx1,xx2) n (xx1,xx2) s xx1=$tr($g(xx1),""""),xx2=$g(xx2)
 q $e(xx1,1,xx2)
date(date) n (date) s xx=$$extdate2^SSUTIL4($g(date))
 q $p(xx,"/",1)_$p(xx,"/",2)_$p(xx,"/",3)
time() s time=$p($h,",",2) q $tr($j(time\3600,2)," ",0)_$tr($j(time\60#60,2)," ",0)
]]></Routine>
</Export>
