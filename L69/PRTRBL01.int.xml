<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTRBL01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTRBL01 ; ML 23/10/98 ; REBILL LIST
 q
 ; Parameters :  1. From Date
 ;               2. To Date
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Rebill List"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TRBL(date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TRBL(date,epis)) q:epis=""  d
 ..s (old,new)="",comp=$$epcomp^LVBVIS1(epis) i '$l(comp) s comp=" "
 ..s (start,stop,x)="" f  s x=$o(^TEPI(epis,6,x)) q:x=""  d  i stop q
 ...i $p(^(x),"\",1)="RB",$p(^(x),"\",2)=date s start=1,old=$p(^(x),"\",4) q
 ...i start d
 ....i $P(^(x),"\",1)="RB" s stop=1
 ....i $P(^(x),"\",1)="IN" s new=$p($p(^(x),"\",4)," ",2,3),stop=1
 ..s ^TMP($zn,$j,"REPORT",comp,date,epis)=old_"\"_new
 q
PRINT ; print report
 i '$d(^TMP($zn,$j,"REPORT")) d
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","   No Rebills For Requested Period")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 i $d(^TMP($zn,$j,"REPORT")) d
 .s ^TMP($zn,$j,"TOTAL")=""
 .s comp="" f  s comp=$o(^TMP($zn,$j,"REPORT",comp)) q:comp=""  d
 ..s xxx(2)=comp,^TMP($zn,$j,"TOTAL",comp)=""
 ..s date="" f  s date=$o(^TMP($zn,$j,"REPORT",comp,date)) q:date=""  d
 ...s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",comp,date,epis)) q:epis=""  d
 ....s old=$p(^(epis),"\",1),new=$p(^(epis),"\",2),(oldval,newval,newdate)=""
 ....i $l(old),'$$select^LVBINVBP($p(old," ",1)_"||"_$p(old," ",2)_"||"_epis) s oldval=PLIST(3)
 ....i $l(new),'$$select^LVBINVBP($p(new," ",1)_"||"_$p(new," ",2)_"||"_epis) s newval=PLIST(3),newdate=PLIST(6)
 ....s xxx(3)=$$extdate2^SSUTIL4(date),xxx(4)=old,xxx(5)=$p(old," "),xxx(6)=oldval
 ....i $l(newdate) s xxx(7)=$$extdate2^SSUTIL4(newdate),xxx(8)=new,xxx(9)=$p(new," "),xxx(10)=newval
 ....i $$select^LVBEPVIS(epis)
 ....f j=6,10 d
 .....s $p(^TMP($zn,$j,"TOTAL"),"\",j)=$p(^TMP($zn,$j,"TOTAL"),"\",j)+$g(xxx(j))
 .....s $p(^TMP($zn,$j,"TOTAL",comp),"\",j)=$p(^TMP($zn,$j,"TOTAL",comp),"\",j)+$g(xxx(j))
 ....d PRINT^PRTREP00(.PLIST,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s comp1=$o(^TMP($zn,$j,"TOTAL","")),comp2=$o(^TMP($zn,$j,"TOTAL",""),-1)
 .i comp1'=comp2 s comp="" f  s comp=$o(^TMP($zn,$j,"TOTAL",comp)) q:comp=""  d
 ..s oldval=$p(^(comp),"\",6),newval=$p(^(comp),"\",10)
 ..s name="" i '$$select^LVBCTCO(comp) s name=PLIST(3)
 ..d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","?   Totals for "_name)
 ..d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","?50 Old Value"),ADDLINE^PRTREP00(REPID,"PRT","?75 "_$j(oldval,10,2))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","?50 New Value"),ADDLINE^PRTREP00(REPID,"PRT","?75 "_$j(newval,10,2))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s oldval=$p(^TMP($zn,$j,"TOTAL"),"\",6),newval=$p(^TMP($zn,$j,"TOTAL"),"\",10)
 .d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?   Grand Total")
 .d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?50 Old Value"),ADDLINE^PRTREP00(REPID,"PRT","?75 "_$j(oldval,10,2))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?50 New Value"),ADDLINE^PRTREP00(REPID,"PRT","?75 "_$j(newval,10,2))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="RBL"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
