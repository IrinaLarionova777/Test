<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR2ESP03" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR2ESP03 ; Doctors Reports printing (single test set - antibiotic panel) - Sant Pau
 ;
 ; supported calls :
 ;
 ; $$print^PRTDR2ESP03(vtsRID,type,DH)
 ; $$print2^PRTDR2ESP03(vtsRID,type,DH,list)
 ;
 ; type  : P - print
 ;         V - view from Enquiry
 ;           - view from Entry
 ; DH    : D - Doctor
 ;         H - Hospital
print(vtsRID,type,DH) q 0
print2(vtsRID,type,DH,list) n (vtsRID,type,DH,list,PartialHeader) s vtsRID=$g(vtsRID),type=$g(type),DH=$g(DH),list=$g(list)
 s test=$p(vtsRID,"||",2),ColWidth=13,ColLeft=30
 ; check for margin changes
 s margin="",%routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 i $l(%routine) s LineRoutine="Margin^"_%routine i $l($t(@LineRoutine)) x "s margin=$$Margin^"_%routine_"()"
 s tsLM=0,tsWD=80
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .s tsLM=PLIST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 i '$$select^MVBCFLAB(1) d
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 k ^TMP($zn,$j,"ANT")
 k FORMAT i '$$getit^LVBCTTSF(test,,"Y") m FORMAT=PLIST
 ; print an antibiotic panel
 s x1="",pathcnt=0 f  s x1=$o(FORMAT(x1)) q:x1=""  i $p(FORMAT(x1),$c(2),18)="V" d
 .i $l(list),(","_list_",")'[(","_$p(FORMAT(x1),$c(2),10)_",") q
 .s rowid=vtsRID_"||"_$p(FORMAT(x1),$c(2),10)
 .i '$$select^LVBVISTD(rowid),$l(PLIST(3)) d
 ..i '$$select^LVBCTBUG(PLIST(3),"Y") d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...s pathcnt=pathcnt+1
 ...s cnt=$o(^TMP($zn,$j,"ANT",0,""),-1)+1,^TMP($zn,$j,"ANT",0,cnt)=pathcnt_"\"_$s($l(PLIST(3))<(ColWidth-4):PLIST(3),1:PLIST(2))
 ...i $$open^LVBVISTA(rowid)
 ...f  q:$$fetch^LVBVISTA(rowid)  d
 ....i $l(PLIST(2)),$l(PLIST(3)) d
 .....i $l(type),PLIST(4)'="Y" q
 .....f j=2:1:6 s $p(^TMP($zn,$j,"ANT",0,cnt,$p(PLIST(2),$c(1))),$c(2),j)=PLIST(j)
 ...i $$close^LVBVISTA()
 ... ; check if there is a reportable antibiotic
 ...i $d(^TMP($zn,$j,"ANT",0,cnt))\10=0 k ^TMP($zn,$j,"ANT",0,cnt)
 ...s (ok,ant)="" f  s ant=$o(^TMP($zn,$j,"ANT",0,cnt,ant)) q:ant=""  d  i ok q
 ....i $l(type),$p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),4)'="Y" q
 ....s ok=1
 ...i 'ok k ^TMP($zn,$j,"ANT",0,cnt)
 ; sort antibiotics
 s cnt="" f  s cnt=$o(^TMP($zn,$j,"ANT",0,cnt)) q:cnt=""  d
 .s ant="" f  s ant=$o(^TMP($zn,$j,"ANT",0,cnt,ant)) q:ant=""  d
 ..s seq=$$seldata^LVBCTANT(ant,4)
 ..s ^TMP($zn,$j,"ANT",1,+seq,ant,cnt)=""
 i '$d(^TMP($zn,$j,"ANT",0)) q
 s xx1=$o(PartialHeader(""),-1)
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="!"
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="~FONTSIZE=8"
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="!"
 d LN^PRTDR00(),ADD^PRTDR00(,"~FONTSIZE=8"),LN^PRTDR00()
 ; print header panel
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="?"_$e((tsLM+margin)_"   ",1,3)_"Antibiograma:"
 d ADD^PRTDR00(tsLM+margin,"Antibiograma:")
 s cnt="" f  s cnt=$o(^TMP($zn,$j,"ANT",0,cnt)) q:cnt=""  d
 .s r=^TMP($zn,$j,"ANT",0,cnt),pathcnt=$p(r,"\",1),name=$p(r,"\",2)
 .s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="?"_$e((tsLM+ColLeft+(ColWidth*(cnt-1))+margin)_"   ",1,3)_cnt_". "_name
 .d ADD^PRTDR00(tsLM+ColLeft+(ColWidth*(cnt-1))+margin,pathcnt_". "_name)
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="!"
 d LN^PRTDR00()
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="?"_$e((tsLM+margin)_"   ",1,3)_"CMI(mg/L)"
 d ADD^PRTDR00(tsLM+margin,"CMI(mg/L)")
 s cnt="" f  s cnt=$o(^TMP($zn,$j,"ANT",0,cnt)) q:cnt=""  d
 .s (cmi,x)="" f  s x=$o(^TMP($zn,$j,"ANT",0,cnt,x)) q:x=""  s z=$p(^(x),$c(2),5),z=$s(z=0:"",1:z) i $l(z) s cmi=1 q
 .i cmi d
 ..s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="?"_$e((tsLM+ColLeft+(ColWidth*(cnt-1))+margin+5)_"   ",1,3)_"CMI"
 ..d ADD^PRTDR00(tsLM+ColLeft+(ColWidth*(cnt-1))+margin+5,"CMI")
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="!"
 d LN^PRTDR00()
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="?"_$e((tsLM+margin)_"   ",1,3)_"-------------------"
 d ADD^PRTDR00(tsLM+margin,"-------------------") s line="",$p(line,"-",ColWidth-1)=""
 s cnt="" f  s cnt=$o(^TMP($zn,$j,"ANT",0,cnt)) q:cnt=""  d
 .s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="?"_$e((tsLM+ColLeft+(ColWidth*(cnt-1))+margin)_"   ",1,3)_line
 .d ADD^PRTDR00(tsLM+ColLeft+(ColWidth*(cnt-1))+margin,line)
 s xx2=$o(PartialHeader(xx1,""),-1)+1,PartialHeader(xx1,xx2)="!"
 d LN^PRTDR00()
 s (last,seq)="" f  s seq=$o(^TMP($zn,$j,"ANT",1,seq)) q:seq=""  d
 .s ant="" f  s ant=$o(^TMP($zn,$j,"ANT",1,seq,ant)) q:ant=""  s last=ant
 s (xm,seq)="" f  s seq=$o(^TMP($zn,$j,"ANT",1,seq)) q:seq=""  d
 .s ant="" f  s ant=$o(^TMP($zn,$j,"ANT",1,seq,ant)) q:ant=""  d
 ..s cnt="" f first=0:1 s cnt=$o(^TMP($zn,$j,"ANT",1,seq,ant,cnt)) q:cnt=""  d
 ...s (cmi,x)="" f  s x=$o(^TMP($zn,$j,"ANT",0,cnt,x)) q:x=""  s z=$p(^(x),$c(2),5),z=$s(z=0:"",1:z) i $l(z) s cmi=1 q
 ...i 'first d ADD^PRTDR00(tsLM+margin,$p($p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),2),$c(1),2))
 ... ; non printable/viewable antibiotics
 ...i $l(type),$p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),4)'="Y" q
 ... ; print result
 ...s result=$p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),3)
 ...i cmi s z=$p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),5),z=$s(z=0:"",1:z),result=result_"    "_z
 ...e  i $l(result)=1 s result="     "_result
 ...e  i $l(result)>1 s result=$p($g(^TTAB("SENS",result)),"\",1)
 ...i $p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),4)'="Y" s result=$e(result_$j("",7),1,7)_"NI"
 ...i $l(result) d ADD^PRTDR00(tsLM+ColLeft+(ColWidth*(cnt-1))+margin-2,"  "_result)
 ..s xm=xm+1 i (xm#3=0)!(ant=last) d
 ...s cnt=$o(^TMP($zn,$j,"ANT",0,""),-1)
 ...s line="",$p(line,"-",ColLeft+(ColWidth*cnt)-1)=""
 ...d LN^PRTDR00()
 ...d ADD^PRTDR00(tsLM+margin,line)
 ..d LN^PRTDR00()
 d ADD^PRTDR00(,"~FONTSIZE=9")
 d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 k ^TMP($zn,$j,"ANT")
 q
]]></Routine>
</Export>
