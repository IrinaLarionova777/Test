<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LBACKGR14" type="INT" languagemode="0" generated="1"><![CDATA[
LBACKGR14	; BackGround Job - 14 - Email EDI
	q
	;
DOIT(zn,job,DATE,TIME)	s zn=$g(zn),job=$g(job),DATE=$g(DATE),TIME=$g(TIME)
	l +^TMP(zn,job,"DOIT14"):0 e  q
	s zt=$zt,$zt="ERROR^"_$zn
	s $p(^TMP(zn,job,"DOIT14",DATE,TIME),"^",1)="START : "_$h
	s subject="Pathology results"
	s EmailTo="" f  s EmailTo=$o(^TEDI("EMAIL",EmailTo)) q:EmailTo=""  d
	.s seq="" f  s seq=$o(^TEDI("EMAIL",EmailTo,seq)) q:seq=""  d email(EmailTo,seq,subject)
	s $p(^TMP(zn,job,"DOIT14",DATE,TIME),"^",2)="FINISH : "_$h
	l -^TMP(zn,job,"DOIT14")
	s $zt=zt
	q
email(to,seq,subject)	s to=$g(to),seq=$g(seq),subject=$g(subject),ext=$g(^TEDI("EMAIL",to,seq))
	i $$select^MVBCFLAB(1) q
	s ip=PLIST(11),from=PLIST(17) i '$l(ip) q
	// compile mail
	s s=##class(%Net.SMTP).%New()
	s s.smtpserver=ip
	s m=##class(%Net.MailMessage).%New()
	s m.From=from
	d m.To.Insert(to)
	s m.Subject=subject
	do m.TextData.Write(subject_" attached ...")
	do m.TextData.Write($char(13,10))
	do m.TextData.Write($char(13,10))
	i $$select^MVBCFSM(1) q
	i $l(PLIST(35)) s dir=PLIST(35) f j="\","/" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
	s dev=dir_subject_"_"_seq_"."_ext
	o dev:("NSW"):2 e  q
	s x="" f  s x=$o(^TEDI("EMAIL",to,seq,x)) q:x=""  u dev w ^(x),!
	c dev
	s status=m.AttachFile(dir,subject_"_"_seq_"."_ext)
	set status=s.Send(m)
	do m.%Close()
	do s.%Close()
	k ^TEDI("EMAIL",to,seq)
	q
ERROR d BACK^%ETN
 s $zt=$g(zt)
 q 
]]></Routine>
</Export>
