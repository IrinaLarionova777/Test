<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTICR02" type="INT" languagemode="0" generated="1"><![CDATA[
PRTICR02 ; IG 1/11/98 ; Initiation code report (SJOG only)
 Q
 ; Parameters :  1.Date Start
 ;               2.Date End
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($zn,$j)
 K xHEADER D INIT
 s line=1,xHEADER(line)="Initiation code report"
 D HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
CREND K ^TMP($zn,$j),^TMP("PRTREP00",$j)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 K xSORT D H^PRTREP00(PAR10,$zn)
 S X=xLINE(2),MAX=$L(X),$P(SPACE," ",MAX)="",$P(LINE,"-",MAX)=""
 Q
 ; create temporary printing file
BODY i 'PAR1 s PAR1=+$H-1
 i 'PAR2 s PAR2=PAR1
 f X=PAR1:1:PAR2 s epis="" f  s epis=$o(^TDAY(1,X,0,epis)) q:epis=""  d
 .i $$select^LVBEPVIS(epis,"Y") k ^TDAY(1,X,0,epis) q
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 . ; Outpatient with G010 or G020 only
 .i " COL DOC DOM SELF PAP "[(" "_VISIT(18)_" ") d
 ..i $o(^TEPI(epis,1,""))="G010",$o(^TEPI(epis,1,"G010"))="" d
 ...i $o(^TEPI(epis,1,"G010",""))=$o(^TEPI(epis,1,"G010",""),-1) d
 ....i VISIT(18)'="PAP" s ^TMP($zn,$j,"REPORT",epis)="PAP"
 ..i $o(^TEPI(epis,1,""))="G020",$o(^TEPI(epis,1,"G020"))="" d
 ...i $o(^TEPI(epis,1,"G020",""))=$o(^TEPI(epis,1,"G020",""),-1) d
 ....i VISIT(18)'="PAP" s ^TMP($zn,$j,"REPORT",epis)="PAP"
 . ; Inpatient with R010 only
 .i " HOSP IPH "[(" "_VISIT(18)_" ")!$l(VISIT(22)) d
 ..i $o(^TEPI(epis,1,""))="R010",$o(^TEPI(epis,1,"R010"))="" d
 ...i $o(^TEPI(epis,1,"R010",""))=$o(^TEPI(epis,1,"R010",""),-1) d
 ....i VISIT(18)'="IPH" s ^TMP($zn,$j,"REPORT",epis)="IPH"
 . ; Outpatient with R010 only
 .i " COL DOC DOM INST OPH "[(" "_VISIT(18)_" ") d  q
 ..i $o(^TEPI(epis,1,""))="R010",$o(^TEPI(epis,1,"R010"))="" d
 ...i $o(^TEPI(epis,1,"R010",""))=$o(^TEPI(epis,1,"R010",""),-1) d
 ....i VISIT(18)'="OPH" s ^TMP($zn,$j,"REPORT",epis)="OPH"
 q
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s (epis,BTOT)="" f CNT=0:1 s epis=$O(^TMP($zn,$j,"REPORT",epis)) q:epis=""  d
 ..k xxx s x=^TMP($zn,$j,"REPORT",epis) i '$$select^LVBCTIC(x,"Y") s xxx(1)=x_$c(1)_PLIST(3)
 ..i $$select^LVBEPVIS(epis) q
 ..d PRINT^PRTREP00(.PLIST,.N)
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="ICR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
