<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBEDI01" type="INT" languagemode="0" generated="1"><![CDATA[
LVBEDI01 ; EDI module
 ;
 ; supported calls:
 ;
 ; $$open^LVBEDI01(type,p1,p2,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetch^LVBEDI01(type,p1,p2,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetchbuffer^LVBEDI01(type,p1,p2,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetchall^LVBEDI01(type,p1,p2,p3,p4,p5,p6,p7,p8,p9)
 ; $$close^LVBEDI01()
 ;
 ; list ALL records to be sent
 ; PLIST - 1.Doctor/Hospital
 ;         2.Client
 ;         3.Recipient
 ;         4.Episode
 ;         5.Demographics/Result
 ;         6.vtsRID
 ;         7.Other : field/value/type/Date,Time
open(type,p1,p2,p3,p4,p5,p6,p7,p8,p9) s type=$g(type) k ^TMP($zn,$j,"open")
 s x1="" f  s x1=$o(^TEDI("OUT",type,x1)) q:x1=""  d
 .s x2="" f  s x2=$o(^TEDI("OUT",type,x1,x2)) q:x2=""  d
 ..s x3="" f  s x3=$o(^TEDI("OUT",type,x1,x2,x3)) q:x3=""  d
 ...s x4="" f  s x4=$o(^TEDI("OUT",type,x1,x2,x3,x4)) q:x4=""  d
 ....s x5="" f  s x5=$o(^TEDI("OUT",type,x1,x2,x3,x4,x5)) q:x5=""  d
 .....s x6="" f  s x6=$o(^TEDI("OUT",type,x1,x2,x3,x4,x5,x6)) q:x6=""  d
 ......s x7="" f  s x7=$o(^TEDI("OUT",type,x1,x2,x3,x4,x5,x6,x7)) q:x7=""  s xx=^(x7) d
 .......s x8=$s(x5="42":"D",$p(xx,"\",3)="Add":"D",$p(xx,"\",3)="Del":"D",1:"R")
 .......i x8="R",'$d(^TEPI($p(x6,"||",1),1,$p(x6,"||",2),$p(x6,"||",3),"DATA")) q
 .......i x5="46" f j=1,2 s $p(xx,"\",j)=""
 .......s ^TMP($zn,$j,"open",x1,x2,x3,x4,x8,x6,x7)=xx
 q 0
fetch(type,p1,p2,p3,p4,p5,p6,p7,p8,p9) s type=$g(type),SQLCODE=100 k PLIST
 f j=1:1:7 s @("x"_j)=$p($g(^TMP($zn,$j,"open")),"\",j)
 f  d:$l(x1)  q:'SQLCODE  s x1=$o(^TMP($zn,$j,"open",x1)) q:x1=""
 .f  d:$l(x2)  q:'SQLCODE  s x2=$o(^TMP($zn,$j,"open",x1,x2)) q:x2=""
 ..f  d:$l(x3)  q:'SQLCODE  s x3=$o(^TMP($zn,$j,"open",x1,x2,x3)) q:x3=""
 ...f  d:$l(x4)  q:'SQLCODE  s x4=$o(^TMP($zn,$j,"open",x1,x2,x3,x4)) q:x4=""
 ....f  d:$l(x5)  q:'SQLCODE  s x5=$o(^TMP($zn,$j,"open",x1,x2,x3,x4,x5)) q:x5=""
 .....f  d:$l(x6)  q:'SQLCODE  s x6=$o(^TMP($zn,$j,"open",x1,x2,x3,x4,x5,x6)) q:x6=""
 ......f  s x7=$o(^TMP($zn,$j,"open",x1,x2,x3,x4,x5,x6,x7)) q:x7=""  d  q
 .......s PLIST(1)=x1
 .......s PLIST(2)=x2
 .......s PLIST(3)=x3
 .......s PLIST(4)=x4
 .......s PLIST(5)=x5
 .......s PLIST(6)=x6
 .......s PLIST(7)=^(x7)
 .......s SQLCODE=0
 s PLIST=$o(PLIST(""),-1)
 i 'SQLCODE d adjust
 f j=1:1:7 s $p(^TMP($zn,$j,"open"),"\",j)=@("x"_j)
 q SQLCODE
 ; fetch all data
fetchbuffer(type,p1,p2,p3,p4,p5,p6,p7,p8,p9) k ^TMP($zn,$j,"fb") s type=$g(type)
 s max=$s($g(PLIST(1)):PLIST(1),1:100) i max>100 s max=100
 f xxx=1:1:max s SQLCODE=$$fetch(type) q:SQLCODE  d
 .s cnt=$o(^TMP($zn,$j,"fb",""),-1)+1 f j=1:1:PLIST s $p(^TMP($zn,$j,"fb",cnt),$c(28),j)=$g(PLIST(j))
 k PLIST m PLIST=^TMP($zn,$j,"fb") k ^TMP($zn,$j,"fb")
 s PLIST=$o(PLIST(""),-1)
 q SQLCODE
fetchall(type,p1,p2,p3,p4,p5,p6,p7,p8,p9) n (type,PLIST) k ^TMP($zn,$j,"fa") s type=$g(type)
 i $$open(type)
 f cnt=1:1 q:$$fetch(type)  s ^TMP($zn,$j,"fa",cnt)="" f j=1:1:PLIST s $p(^TMP($zn,$j,"fa",cnt),$c(28),j)=$g(PLIST(j))
 i $$close()
 k PLIST m PLIST=^TMP($zn,$j,"fa")
 s PLIST=$o(PLIST(""),-1)
 k ^TMP($zn,$j,"fa")
 q 0
close() k ^TMP($zn,$j,"open")
 q 0
 ; Post-filing adjustments
adjust N plist,I,J,SQLCODE,X d PostSQL^SSUTIL4("")
 f J=2 i $g(PLIST(J))'="" d
 .k plist m plist=PLIST k PLIST
 .s SQLCODE=$$select^LVBCTCLN(plist(J),"Y")
 .s X="" i 'SQLCODE s X=PLIST(2)_$c(1)_PLIST(3)
 .k PLIST m PLIST=plist s PLIST(J)=X k plist
 i PLIST(1)="DR" f J=3 i $g(PLIST(J))'="" d
 .k plist m plist=PLIST k PLIST
 .s SQLCODE=$$select^LVBCTDR(plist(J),"Y")
 .s X="" i 'SQLCODE s X=PLIST(2)_$c(1)_$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 .k PLIST m PLIST=plist s PLIST(J)=X k plist
 i PLIST(1)="HOSP" f J=3 i $g(PLIST(J))'="" d
 .k plist m plist=PLIST k PLIST
 .s SQLCODE=$$select^LVBCTHOS(plist(J),"Y")
 .s X="" i 'SQLCODE s X=PLIST(2)_$c(1)_PLIST(3)
 .k PLIST m PLIST=plist s PLIST(J)=X k plist
 f J=4 i $g(PLIST(J))'="",$p($g(^TEPI($p(PLIST(J),$c(1)))),"\",44)'=9 d
 .k plist m plist=PLIST k PLIST
 .s SQLCODE=$$select^LVBEPVIS(plist(J),"Y")
 .s X="" i 'SQLCODE d
 ..s X=PLIST(2)_$c(1)_$$NameFormat^LVBCOM03("PT",$p(PLIST(7),$c(1),2),PLIST(4),PLIST(3),PLIST(99),PLIST(100),PLIST(101))
 .k PLIST m PLIST=plist s PLIST(J)=X k plist
 f J=6 i $g(PLIST(J))'="" d
 .k plist m plist=PLIST k PLIST
 .s SQLCODE=$$selskip^LVBCTTS($p(plist(J),"||",2))
 .s X=plist(J) i 'SQLCODE s X=plist(J)_$c(1)_PLIST(3)_$c(1)_PLIST(4)
 .k PLIST m PLIST=plist s PLIST(J)=X k plist
 q
]]></Routine>
</Export>
