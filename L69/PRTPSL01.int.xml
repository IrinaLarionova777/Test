<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTPSL01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTPSL01 ; ML 20/10/98 ; Packing slip
 q
 ; Parameters :  1. Packing slip Number
 ;               2. From User Site
 ;               3. To User Site
 ;               4. To Refferal Lab
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 i '$l(PAR2) d  q
 .s p2="" f  s p2=$o(^TPSL(PAR1,p2)) q:p2=""  d
 ..i $l(PAR3),$d(^TPSL(PAR1,p2,"US",PAR3)) d START^PRTREP00(USER,,PAR1_"\"_p2_"\"_PAR3_"\"_PAR4,"PSL")
 ..i $l(PAR4),$d(^TPSL(PAR1,p2,"RL",PAR4)) d START^PRTREP00(USER,,PAR1_"\"_p2_"\"_PAR3_"\"_PAR4,"PSL")
 s line=1,xHEADER(line)="Packing Slip "_PAR1
 s line=2,xHEADER(line)=""
 i $l(PAR2) d
 .s line=line+1,xHEADER(line)="From User Site : "_FROM(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR3) d
 .s line=line+1,xHEADER(line)="To User Site : "_TO(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR4) d
 .s line=line+1,xHEADER(line)="To Referral Laboratory : "_TO(3)
 .s line=line+1,xHEADER(line)=""
 d BODY
 i CNT1 d
 .s line=line+1,xHEADER(line)="Total number of specimens : "_CNT1
 .s line=line+1,xHEADER(line)=""
 i CNT2 d
 .s line=line+1,xHEADER(line)="Total number of tests : "_CNT2
 .s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),PRINT i $l(PAR4) d FILE
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR2_"-"_PAR1 I $$update^LVBPRPR(REPID)
 K ^TMP($zn,$j),^TMP("PRTREP00",$j)
 Q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 k FROM i '$$select^LVBCTUSL(PAR2) m FROM=PLIST
 k TO i $l(PAR3),'$$select^LVBCTUSL(PAR3) m TO=PLIST
 i $l(PAR4),'$$select^LVBCTRL(PAR4) m TO=PLIST
 q
BODY ; create temporary printing file
 s (CNT1,CNT2)=""
 i $l(PAR3) d
 .s epis="" f  s epis=$o(^TPSL(PAR1,PAR2,"US",PAR3,epis)) q:epis=""  d
 ..s rowid=PAR1_"||"_PAR2_"||US||"_PAR3 i '$$select^LVBPSL(rowid) d  i $$update^LVBPSL(rowid)
 ...s PLIST(6)=+$h,PLIST(7)=$p($h,",",2),PLIST(8)=USER
 ...s PLIST(9)=+$h,PLIST(10)=$p($h,",",2),PLIST(11)=USER
 ..s move="" f  s move=$o(^TPSL(PAR1,PAR2,"US",PAR3,epis,move)) q:move=""  d
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s CNT2=CNT2+1
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"SP",x)) q:x=""  s CNT1=CNT1+1
 ...s ^TMP($zn,$j,"REPORT",epis_"||"_move)=""
 i $l(PAR4) d
 .s epis="" f  s epis=$o(^TPSL(PAR1,PAR2,"RL",PAR4,epis)) q:epis=""  d
 ..s rowid=PAR1_"||"_PAR2_"||RL||"_PAR4 i '$$select^LVBPSL(rowid) d  i $$update^LVBPSL(rowid)
 ...s PLIST(6)=+$h,PLIST(7)=$p($h,",",2),PLIST(8)=USER
 ...s PLIST(9)=+$h,PLIST(10)=$p($h,",",2),PLIST(11)=USER
 ..s move="" f  s move=$o(^TPSL(PAR1,PAR2,"RL",PAR4,epis,move)) q:move=""  d
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s CNT2=CNT2+1
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"SP",x)) q:x=""  s CNT1=CNT1+1
 ...s ^TMP($zn,$j,"REPORT",epis_"||"_move)=""
 ...i $l(TO(6)) s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s ^TMP($zn,$j,"FILE",epis,x)=""
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s (move,old)="" f xc=0:1 s move=$o(^TMP($zn,$j,"REPORT",move)) q:move=""  d
 ..k VISIT s epis=$p(move,"||",1),mv=$p(move,"||",2) i '$$select^LVBEPVIS(epis) d
 ...m VISIT=PLIST
 ...i $d(^TMP($zn,$j,"FILE",epis)) s xxx(4)="MRN : RL_"_$p(VISIT(20),$c(1))
 ...s x="" f cnt=0:1 s x=$o(^TEPI(epis,9,mv,"VTS",x)) q:x=""  d
 ....s xx=$p(x,"||",2) i '$$select^LVBCTTS(xx,"Y") s xxx(1)=$g(xxx(1))_$s($g(xxx(1))'="":", ",1:"")_PLIST(4)
 ....s xxx(4)=$g(xxx(4))_$s($l($g(xxx(4))):" ; ",1:"")_$s('cnt:"Ref.No: ",1:" , ")_$p(x,"||",1)_"-"_$p(x,"||",2)_"-"_$p(x,"||",3)
 ...s x="" f  s x=$o(^TEPI(epis,9,mv,"SP",x)) q:x=""  d
 ....s xx=$p(x,"||",2) i '$$select^LVBCTSPE(xx,"Y") s xxx(2)=$g(xxx(2))_$s($g(xxx(2))'="":", ",1:"")_PLIST(3)
 ..i old=epis k VISIT
 ..i xc,old'=epis d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d PRINT^PRTREP00(.VISIT,.N) k VISIT,xxx
 ..s old=epis
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="PSL"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
FILE ; create file
 i $d(^TMP($zn,$j,"FILE")) d
 .i TO(8)="TRK" d
 ..s file=TO(6) f j="\","/" i file[j s:$e(file,$l(file))'=j file=file_j
 ..s file=file_PAR4_"_"_PAR2_"_"_$$UPPER^SSUTIL4(PAR1)_".txt"
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","Start creating file <"_file_">")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..o file:"WNS":2 e  d  q
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT","Cannot create file <"_file_">")
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"FILE",epis)) q:epis=""  d
 ...i '$$select^LVBEPVIS(epis,"Y") d
 ....s (xx,vts)="" f j=1:1 s vts=$o(^TMP($zn,$j,"FILE",epis,vts)) q:vts=""  s $p(xx,",",j)=vts
 ....s str="" f j=1:1:PLIST s $p(str,$c(28),j)=$p($g(PLIST(j)),$c(1))
 ....s $p(str,$c(28),200)=xx
 ....u file w str,!
 ..c file
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","Finish creating file <"_file_">")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .
 .i TO(8)="HL7" d
 ..s file=TO(6) f j="\","/" i file[j s:$e(file,$l(file))'=j file=file_j
 ..s file=file_PAR4_"_"_PAR2_"_"_$$UPPER^SSUTIL4(PAR1)_".hl7"
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","Start creating file <"_file_">")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..o file:"WNS":2 e  d  q
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT","Cannot create file <"_file_">")
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 .. ; MSH message
 ..s msh(3)=PAR2
 ..s msh(5)=PAR4
 ..s msh(6)=$$dt(+$h,$p($h,",",2))
 ..s msh(9)=$$new()
 ..s msh(10)=$p($g(CFLAB(45)),$c(1)) i msh(10)="L" s msh(10)="P"
 ..u file w "MSH|^~\&|LABTRAK|"_msh(3)_"|PATH_ORDER|"_msh(5)_"|"_msh(6)_"||ORM|"_msh(9)_"|"_msh(10)_"|2.3",!
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"FILE",epis)) q:epis=""  d
 ...
 ... ; PID message
 ...
 ...k VISIT i '$$select^LVBEPVIS(epis,"Y") m VISIT=PLIST f j=1:1:VISIT i $d(VISIT(j)) s VISIT(j)=$p(VISIT(j),$c(1))
 ...k DOC i '$$select^LVBCTDR(VISIT(15),"Y") m DOC=PLIST f j=1:1:DOC i $d(DOC(j)) s DOC(j)=$p(DOC(j),$c(1))
 ...s pid(2)=$$mrn^LVBEPVIS(epis)
 ...s pid(3)=""
 ...i $l($p(VISIT(65),",",2)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_$p(VISIT(65),",",2)_"^^^^MREXT"
 ...i $l($p(VISIT(65),",",1)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_$p(VISIT(65),",",1)_"^^^^EPEXT"
 ...i $l(VISIT(20)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_$p(VISIT(20),$c(1))_"^^^^MRLT"
 ...i $l(VISIT(8)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_VISIT(8)_"^"_VISIT(108)_"^^^MC"
 ...i $l(VISIT(89)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_VISIT(89)_"^^^^DVA"
 ...i $l(VISIT(88)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_VISIT(88)_"^^^^PEN"
 ...i $l(VISIT(2)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_VISIT(2)_"^^^^EPLT"
 ...i $l(VISIT(126)) s pid(3)=pid(3)_$s($l(pid(3)):"~",1:"")_VISIT(126)_"^^^^NI-"_VISIT(127)
 ...s pid(5)=VISIT(3)_"^"_VISIT(4)_"^"_VISIT(99)_"^^"_VISIT(7),pid(8)=VISIT(5),pid(10)=VISIT(60)
 ...s pid(7)=$$dt(VISIT(6),"")
 ...s pid(11)=VISIT(32)_"^"_VISIT(33)_"^"_VISIT(34)_"^"_VISIT(35)_"^"_VISIT(36)
 ...s pid(13)="" i $l(VISIT(43)) s pid(13)="^^^^^^"_VISIT(43)
 ...s pid(14)="" i $l(VISIT(44)) s pid(14)="^^^^^^"_VISIT(44)
 ...s pid(15)="" i $l(VISIT(161)) s pid(15)=VISIT(161)_"^^LT"
 ...s pid(30)=VISIT(105)
 ...u file w "PID||"_pid(2)_"|"_pid(3)_"||"_pid(5)_"||"_pid(7)_"|"_pid(8)_"||"_pid(10)_"|"_pid(11)_"||"_pid(13)_"|"_pid(14)_"|"_pid(15)_"|||||||||||||||"_pid(30),!
 ...
 ... ; PV1 message
 ...
 ...s pv1(3)="" i $l(VISIT(22)) s pv1(3)=VISIT(22)_"^"_VISIT(112)_"^"_VISIT(113)
 ...s pv1(6)="" i $l(VISIT(123)) s pv1(6)=VISIT(123)
 ...s pv1(8)=DOC(2)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)_"^^^LABTRAK^^^^DN"
 ...s pv1(18)=VISIT(93)
 ...s pv1(19)=epis
 ...s pv1(20)="" i $l(VISIT(9)) s pv1(20)=VISIT(9)_"^"_VISIT(23)
 ...s pv1(44)="" i $l(VISIT(47)) s pv1(44)=$$dt(VISIT(47),"")
 ...s pv1(45)="" i $l(VISIT(165)) s pv1(45)=$$dt(VISIT(165),"")
 ...u file w "PV1|||"_pv1(3)_"|||"_pv1(6)_"||"_pv1(8)_"||||||||||"_pv1(18)_"|"_pv1(19)_"|"_pv1(20)_"||||||||||||||||||||||||"_pv1(44)_"|"_pv1(45),!
 ...
 ... ; OBR message
 ...
 ...s vts="" f obr=1:1 s vts=$o(^TMP($zn,$j,"FILE",epis,vts)) q:vts=""  d
 ....k VISTS i '$$select^LVBVISTS(vts,"Y") m VISTS=PLIST f j=1:1:VISTS i $d(VISTS(j)) s VISTS(j)=$p(VISTS(j),$c(1))
 ....s obr(1)=obr
 ....s obr(2)=$tr(vts,"|","^")
 ....s obr(4)=$p(vts,"||",2)_"^"_$$seldata^LVBCTTS($p(vts,"||",2),4)_"^LT"
 ....s obr(5)=$$priority^LVBVISTS(vts)
 ....s obr(6)=$$dt(VISIT(14),VISIT(111))
 ....s obr(7)=$$dt(VISIT(12),VISIT(13)) i $l(VISTS(49)) s obr(7)=$$dt(VISTS(49),VISTS(50))
 ....s obr(13)="" i $l(VISTS(23)) s obr(13)=$tr(VISTS(23),"|","~")
 ....s obr(14)=$$dt(VISIT(61),VISIT(62)) i $l(VISTS(62)) s obr(14)=$$dt(VISTS(62),VISTS(63))
 ....s obr(15)=VISTS(47)_"^"_VISTS(48)_"^"_VISTS(65)_"^LT"
 ....s obr(28)=""
 ....s x="" f  s x=$o(^TEPI(epis,5,x)) q:x=""  d
 .....s copy=$p(^TEPI(epis,5,x),"\",1) i $l(copy),'$$select^LVBCTDR(copy,"Y") d  q
 ......s obr(28)=obr(28)_$s($l(obr(28)):"~",1:"")_PLIST(2)_"^"_PLIST(3)_"^"_PLIST(4)_"^"_PLIST(49)_"^"_PLIST(50)_"^"_PLIST(26)_"^^^LABTRAK^^^^DN"
 .....s copy=$p(^TEPI(epis,5,x),"\",15) i $l(copy),'$$select^LVBCTHOS(copy,"Y") d  q
 ......s obr(28)=obr(28)_$s($l(obr(28)):"~",1:"")_PLIST(2)_"^"_PLIST(3)_"^^^^"_"^^^LABTRAK^^^^HN"
 .....s copy=$p(^TEPI(epis,5,x),"\",2) i $l(copy) s obr(28)=obr(28)_$s($l(obr(28)):"~",1:"")_"^"_copy_"^^^^"_"^^^LABTRAK^^^^NN"
 ....u file w "OBR|"_obr(1)_"|"_obr(2)_"||"_obr(4)_"|"_obr(5)_"|"_obr(6)_"|"_obr(7)_"||||||"_obr(13)_"|"_obr(14)_"|"_obr(15)_"|||||||||||||"_obr(28),!
 ..c file
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","Finish creating file <"_file_">")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 q
new() l +^CF("USLOC",PAR3_"-SEQ")
 s num=$g(^CF("USLOC",PAR3_"-SEQ"))+1,^CF("USLOC",PAR3_"-SEQ")=num
 l -^CF("USLOC",PAR3_"-SEQ")
 q num
dt(date,time)
 q $s($l(date):$tr($zd(date,3),"-",""),1:"")_$s($l(time):$tr($$exttime^SSUTIL4(time),":",""),1:"")
]]></Routine>
</Export>
