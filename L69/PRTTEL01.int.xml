<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTTEL01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTTEL01 ; ML 18/12/98 ; TELEPHONE LISTING
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Company
 ;               4. Paycode
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 d ADDLINE^PRTREP00(REPID,"HDR","~BOLD")
 d ADDLINE^PRTREP00(REPID,"PRT","~FONTSIZE=7")
 s text="Telephone Listing" s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s text="Companies : "_$s(comp'="":comp,1:"All") s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s text="Pay Codes : "_$s(payc'="":payc,1:"All") s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s text="From "_$$extdate2^SSUTIL4(PAR1)_" to "_$$extdate2^SSUTIL4(PAR2) s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 d ADDLINE^PRTREP00(REPID,"HDR","~NORMAL")
 d BODY,PRINT
 d ADDLINE^PRTREP00(REPID,"PRT","~FONTSIZE=10")
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
TAB(text) ; calculate header position
 s textlen=80-$l(text),tab=textlen/2_"   "
 q $e(tab,1,3)
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s comp=PAR3,payc=PAR4
 s PAR3=","_PAR3_",",PAR4=","_PAR4_","
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)="",$p(und,"-",90)=""
 q
BODY ; create temporary printing file
 s (comp,payc)=""
 f  s comp=$o(^TAGE(comp)) q:comp=""  d
 .i PAR3'[",,",PAR3'[(","_comp_",") q
 .f  s payc=$o(^TAGE(comp,payc)) q:payc=""  d
 ..i PAR4'[",,",PAR4'[(","_payc_",") q
 ..s date=PAR1-1
 ..f  s date=$o(^TAGE(comp,payc,date)) q:date=""  q:date>PAR2  d
 ...s epis="" f  s epis=$o(^TAGE(comp,payc,date,epis)) q:epis=""  d
 ....q:$$select^LVBEPVIS(epis,"Y")  k visdet m vistdet=PLIST k PLIST
 ....f i="surn:3","eppayc:9","datecoll:12","refdr:15","mrn:20" s @$p(i,":",1)=$p(visdet($p(i,":",2)),$c(1))
 ....i eppayc'=payc q
 ....f i="init:54","dateinv:56","invbatch:57" s @$p(i,":",1)=$p(visdet($p(i,":",2)),$c(1))
 ....i dateinv'=date q
 ....s drcomp=$p(^TTAB("DR",refdr,0),"\",5)
 ....i drcomp'=comp q
 ....s z=$g(^TDEB(mrn,datecoll,0,epis)) s DEB1=$p(z,"\",1)
 ....i 'DEB1 q
 ....q:DEB1<0
 ....s (ts,datechk)=""
 ....q:$$getall^LVBVISTS(epis,,,,,,,,"ALL")  k vists m vists=PLIST k PLIST
 ....f i=1:1:vists s tsstr=$p(vists(i),$c(2)),ts=$p(tsstr,"||",2),nn=$p(tsstr,"||",3) d
 .....s dateauth=$p(^TEPI(epis,1,ts,nn),"\",4)
 .....i dateauth>datechk s datechk=dateauth
 ....i datechk>PAR2!(datechk="") q
 ....s ^TMP($zn,$j,"SN",comp,surn,payc,datechk,epis)=""
 q
PRINT ; print report
 s (comp,payc,surn,date,epis)=""
 f  s comp=$o(^TMP($zn,$j,"SN",comp)) q:comp=""  d
 .f  s surn=$o(^TMP($zn,$j,"SN",comp,surn)) q:surn=""  d
 ..f  s payc=$o(^TMP($zn,$j,"SN",comp,surn,payc)) q:payc=""  d
 ...f  s date=$o(^TMP($zn,$j,"SN",comp,surn,payc,date)) q:date=""  d
 ....f  s epis=$o(^TMP($zn,$j,"SN",comp,surn,payc,date,epis)) q:epis=""  d
 .....q:$$select^LVBEPVIS(epis,"Y")  k visdet m visdet=PLIST k PLIST
 .....f i="surn:3","givn:4","datecoll:12","refdr:15","mrn:20","dateinv:56" s @($p(i,":",1))=$p(visdet($p(i,":",2)),$c(1))
 .....f i="add1:32","add2:33","add3:34","sub:35","post:36","homeph:43","workph:44" s @($p(i,":",1))=$p(visdet($p(i,":",2)),$c(1))
 .....f i="datecoll","dateinv" s @(i)=$$extdate2^SSUTIL4(@(i))
 .....s (drsurn,drph)=""
 .....i '$$select^LVBCTDR(refdr,"Y") s drsurn=$p($g(PLIST(3)),$c(1)),drph=$p($g(PLIST(5)),$c(1)) k PLIST
 .....s z=$g(^TDEB(mrn,datecoll,0,epis)) f i=1:1:5 s @("deb"_i)=$p(z,"\",i)
 .....s z=$g(^TDEB(mrn)) f i=1:1:4 s @("dea"_i)=$p(z,"\",i)
 .....s prt="",prt=$e($$NameFormat^LVBCOM03("PT","",givn,surn,visdet(99),visdet(100),visdet(101)),1,25) f i="Reference : "_epis_"::30","Total Balance : "_$j(dea1,1,2)_"::55",deb3_"::80" d PRT(i)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .....s prt="",prt=add1,i=deb4_"::80" d PRT(i)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .....s prt="",prt=add2 f i="Date Seen : "_datecoll_"::30","Ref Amt Billed : "_$j(deb2,1,2)_"::55",dateinv_"::80" d PRT(i)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .....s prt="",prt=add3,i="Referring Doctor : "_refdr_" ("_drsurn_")   Phone : "_drph_"::30" d PRT(i)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .....s prt="",prt=sub_" "_post f i="Pay Code : "_payc_"::30","Ref Balance : "_$j(deb1,1,2)_"::55" d PRT(i)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .....s prt="",prt="Home Phone No : "_homeph,i="Work Phone No : "_workph_"::30" d PRT(i)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .....s title="Other References :",(datex,episx)=""
 .....f  s datex=$o(^TDEB(mrn,datex)) q:datex=""  i $d(^TDEB(mrn,date,0)) d
 ......f  s episx=$o(^TDEB(mrn,datex,0,episx)) q:episx=""  i episx'=epis d
 .......s z=^TDEB(mrn,datex,0,episx) f i=1:1:4 s @("deb"_i)=$p(z,"\",i)
 .......q:'deb1
 .......s prt="",prt=title f i=episx_"::30",$j(deb2,1,2)_"::55",$j(deb1,1,2)_"::80" d PRT(i)
 .......d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .......s title=""
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_und),LN
 .....d events^LVBCOM01(epis,"SM","Summons List")
 q
PRT(det) ; create line to print
 s text=$p(det,"::",1),tab=$p(det,"::",2)
 s prt=prt_$j(" ",tab-$l(prt))_text
 q
HDLN ; add line to header
 d ADDLINE^PRTREP00(REPID,"HDR","!")
 q
LN ; add line to report
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 q
]]></Routine>
</Export>
