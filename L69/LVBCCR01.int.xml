<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBCCR01" type="INT" languagemode="0" generated="1"><![CDATA[
LVBCCR01 ; ML 24/12/99 ; CCR file creation - Trak (common)
 ;
 ; supported calls
 ;
 ; $$create^LVBCCR01(type,TYPE,DATE,defstate)
 ;
create(type,TYPE,DATE,defstate) n (type,TYPE,DATE,defstate) s type=$g(type),TYPE=$g(TYPE),DATE=$g(DATE),defstate=$g(defstate) k ^TMP($zn,$j)
 i '$l(DATE) s DATE=+$h
 ; find list of test sets
 i $$open^LVBCTTS()
 f  q:$$fetch^LVBCTTS()  i PLIST(42)=type s ^TMP($zn,$j,1,PLIST(2))=""
 i $$close^LVBCTTS()
 ; find list of patients
 s file=""
 i $d(^TMP($zn,$j,1)) d
 .s Start=$o(^TCCR(type,"DATA",TYPE,DATE),-1)-1
 .s epis="" f  s epis=$o(^TCCR(type,"DATA",TYPE,DATE,epis)) q:epis=""  d
 ..s ts="" f  s ts=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts)) q:ts=""  d
 ...s cnt="" f  s cnt=$o(^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)) q:cnt=""  d
 ....k ^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)
 ....i $d(^TCCR(type,"DATA",TYPE,DATE,epis))<10 k ^TCCR(type,"DATA",TYPE,DATE,epis)
 .
 . ;Results
 .
 .i TYPE="R" s date=Start,End=DATE-1 f  s date=$o(^TEPIi("AUTHORISED",date)) q:date=""  q:date>End  d
 ..s epis="" f  s epis=$o(^TEPIi("AUTHORISED",date,epis)) q:epis=""  d
 ...s state=$$seldata^LVBEPVIS(epis,35)
 ...i '$l(state) s pc=$e($$seldata^LVBEPVIS(epis,36)) s state=$s(pc=6:"WA",pc=2:"NSW",pc=3:"VIC",pc=4:"QLD",pc=7:"TAS",1:"")
 ...i '$l(state) s doc=$$seldata^LVBEPVIS(epis,15),state=$$seldata^LVBCTDR(doc,12)
 ...i '$l(state) s doc=$$seldata^LVBEPVIS(epis,15),pc=$e($$seldata^LVBCTDR(doc,13)) s state=$s(pc=6:"WA",pc=2:"NSW",pc=3:"VIC",pc=4:"QLD",pc=7:"TAS",1:"")
 ...i '$l(state) s state=defstate
 ...i '$l(state) s state="NSW"
 ...s ts="" f  s ts=$o(^TEPIi("AUTHORISED",date,epis,ts)) q:ts=""  d
 ....i $d(^TMP($zn,$j,1,ts)) s cnt="" f  s cnt=$o(^TEPIi("AUTHORISED",date,epis,ts,cnt)) q:cnt=""  d
 .....i $$seldata^LVBVISTS(epis_"||"_ts_"||"_cnt,11)="Y" q
 .....i $d(^TEPI(epis,1,ts,cnt,"CCR")) d
 ......s STATES(state)=""
 ......s ^TCCR(type,"DATA",TYPE,DATE,epis)=state
 ......s ^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)=""
 .
 . ;Demographics 
 .
 .i TYPE="D" s date=Start,End=DATE f  s date=$o(^TDAY(1,date)) q:date=""  q:date>End  d
 ..s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ...s state=$$seldata^LVBEPVIS(epis,35)
 ...i '$l(state) s pc=$e($$seldata^LVBEPVIS(epis,36)) s state=$s(pc=6:"WA",pc=2:"NSW",pc=3:"VIC",pc=4:"QLD",pc=7:"TAS",1:"")
 ...i '$l(state) s doc=$$seldata^LVBEPVIS(epis,15),state=$$seldata^LVBCTDR(doc,12)
 ...i '$l(state) s doc=$$seldata^LVBEPVIS(epis,15),pc=$e($$seldata^LVBCTDR(doc,13)) s state=$s(pc=6:"WA",pc=2:"NSW",pc=3:"VIC",pc=4:"QLD",pc=7:"TAS",1:"")
 ...i '$l(state) s state=defstate
 ...i '$l(state) s state="NSW"
 ...s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ....i $d(^TMP($zn,$j,1,ts)) s cnt="" f  s cnt=$o(^TEPI(epis,1,ts,cnt)) q:cnt=""  d
 .....i $$seldata^LVBVISTS(epis_"||"_ts_"||"_cnt,11)="Y" q
 .....s STATES(state)=""
 .....s ^TCCR(type,"DATA",TYPE,DATE,epis)=state
 .....s ^TCCR(type,"DATA",TYPE,DATE,epis,ts,cnt)=""
 .
 . ; create file
 .
 .s filelist=""
 .i $d(STATES("WA")) s file=$$create^LVBCCR01WA(type,TYPE,DATE) i $l(file) s filelist=$s($l(filelist):filelist_", "_file,1:file)
 .i $d(STATES("NSW")) s file=$$create^LVBCCR01NSW(type,TYPE,DATE) i $l(file) s filelist=$s($l(filelist):filelist_", "_file,1:file)
 .i $d(STATES("VIC")) s file=$$create^LVBCCR01VIC(type,TYPE,DATE) i $l(file) s filelist=$s($l(filelist):filelist_", "_file,1:file)
 .i $d(STATES("QLD")) s file=$$create^LVBCCR01QLD(type,TYPE,DATE) i $l(file) s filelist=$s($l(filelist):filelist_", "_file,1:file)
 .i $d(STATES("TAS")) s file=$$create^LVBCCR01TAS(type,TYPE,DATE) i $l(file) s filelist=$s($l(filelist):filelist_", "_file,1:file)
 .i $d(STATES("SA")) s file=$$create^LVBCCR01SA(type,TYPE,DATE) i $l(file) s filelist=$s($l(filelist):filelist_", "_file,1:file)
 q filelist
]]></Routine>
</Export>
