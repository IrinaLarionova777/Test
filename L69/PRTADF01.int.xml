<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTADF01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTADF01 ; ML 12/10/98 ; AGED DEBTORS LIST - FULL
 q
 ; Parameters :  1. Company
 ;               2. Paycode
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Aged Debtor Listing - Full"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Company : "_comp_"     Pay Code : "_payc
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s comp=$p(^TTAB("CO",PAR1),"\")_"("_PAR1_")"
 s payc="ALL" i $l(PAR2) s payc=$p(^TTAB("BI",PAR2),"\")_"("_PAR2_")"
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s epis="" f  s epis=$o(^TAGE(epis)) q:epis=""  d
 .s sum=$p(^(epis),"\",1),date=$p(^(epis),"\",2),payc=""
 .s diff=$h-date\30 s:diff>6 diff=6 s:date="" diff=8
 .s comp=$$epcomp^LVBVIS1(epis) i $l(PAR1),PAR1'=comp q
 .s payc=$$seldata^LVBEPVIS(epis,9)
 .s mrn=$$seldata^LVBEPVIS(epis,20)
 .i $l(PAR2),payc'=PAR2 q
 .s ^TMP($zn,$j,"REPORT",payc,mrn,epis)=sum_"\"_diff
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .k tot1
 .s payc="" f  s payc=$o(^TMP($zn,$j,"REPORT",payc)) q:payc=""  d
 ..i '$l(PAR2) d
 ...k CTPC i '$$select^LVBCTPC(payc) m CTPC=PLIST
 ...D ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT"," Payment code : "_CTPC(3)_" ("_payc_")")
 ...D ADDLINE^PRTREP00(REPID,"PRT","!")
 ...D ADDLINE^PRTREP00(REPID,"PRT","!")
 ..k tot2
 ..s mrn="" f  s mrn=$o(^TMP($zn,$j,"REPORT",payc,mrn)) q:mrn=""  d
 ...s epis="" f cnt=0:1 s epis=$o(^TMP($zn,$j,"REPORT",payc,mrn,epis)) q:epis=""  d
 ....s xxx(8)=$g(xxx(8))+$p(^(epis),"\")
 ....s diff=$p(^(epis),"\",2),xxx(diff+1)=$p(^(epis),"\")
 ....s tot1(8)=$g(tot1(8))+xxx(8)
 ....s tot2(8)=$g(tot2(8))+xxx(8)
 ....s tot1(diff+1)=$g(tot1(diff+1))+xxx(diff+1)
 ....s tot2(diff+1)=$g(tot2(diff+1))+xxx(diff+1)
 ....i $$select^LVBEPVIS(epis)
 ....i cnt f j=20,3,4,7 k PLIST(j)
 ....d PRINT^PRTREP00(.PLIST,.N) k xxx
 ..D ADDLINE^PRTREP00(REPID,"PRT","!")
 ..D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..k PLIST s PLIST(2)="Total :"
 ..m xxx=tot2 d PRINT^PRTREP00(.PLIST,.N) k xxx,PLIST
 ..D ADDLINE^PRTREP00(REPID,"PRT","!")
 ..D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..D ADDLINE^PRTREP00(REPID,"PRT","!")
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .k PLIST s PLIST(2)="Grand Total :"
 .m xxx=tot1 d PRINT^PRTREP00(.PLIST,.N) k xxx,PLIST
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 .D ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .D ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="ADF"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
