<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LHOSPINT04" type="INT" languagemode="0" generated="1"><![CDATA[
LHOSPINT04 ; IG 12/07/98 ; Hospital Interface - Darwin
 Q
FIX s deb="" f  s deb=$o(^URNO(deb)) q:deb=""  d
 .s rec1=$g(^URNO(deb,0))
 .s epis=0 f  s epis=$o(^URNO(deb,epis)) q:epis=""  d
 ..s rec2=^(epis) d file(deb,epis,rec1,rec2) w !,deb," ",epis
 q
RESULT(HOSP) N (HOSP) k ^TMP($zn,$j)
 i $$select^LVBCTRL(HOSP) q
 s HOSP(3)=PLIST(3)
 s dir=PLIST(7),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.*"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""results.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""results.txt""")
 s $zt="Error1^"_$zn
 s FILE="results.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  u xFILE r MESS s seq=$o(^TMP($zn,$j,xxFILE,"IN",""),-1)+1,^TMP($zn,$j,xxFILE,"IN",seq)=MESS
 s $zt=""
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s cnt=0,(msh,pid)=""
 .s seq="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=^(seq) d
 ..i $p(xx,"|")="PID" s pid=xx
 ..i $p(xx,"|")="MSH" s msh=xx
 ..i $p(xx,"|")="ORC" s cnt=cnt+1,^TMP($zn,$j,xFile,"IN-1",cnt,0)=xx,^TMP($zn,$j,xFile,"Extra",cnt,"MSH")=msh,^TMP($zn,$j,xFile,"Extra",cnt,"PID")=pid
 ..i $p(xx,"|")="OBR",$d(^TMP($zn,$j,xFile,"IN-1",cnt)) s x=$o(^TMP($zn,$j,xFile,"IN-1",cnt,""),-1)+1,^TMP($zn,$j,xFile,"IN-1",cnt,x)=xx
 ..i $p(xx,"|")="OBX",$d(^TMP($zn,$j,xFile,"IN-1",cnt)) s x=$o(^TMP($zn,$j,xFile,"IN-1",cnt,""),-1)+1,^TMP($zn,$j,xFile,"IN-1",cnt,x)=xx
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .k ^TMPimport(xFile)
 .s seq1="" f  s seq1=$o(^TMP($zn,$j,xFile,"IN-1",seq1)) q:seq1=""  d
 ..s (epis,ts,tscnt,vts)="",done="bad"
 ..s seq2="" f  s seq2=$o(^TMP($zn,$j,xFile,"IN-1",seq1,seq2)) q:seq2=""  s xx=^(seq2) d
 ...i '$l(vts),$p(xx,"|")="OBR" d
 ....s epis=$p($p(xx,"|",3),"^"),lab=$p($p(xx,"|",4),"^")
 ....i $l(epis) d
 .....s xts=$p($p(xx,"|",5),"^")
 .....s (stop,xts0,ts)="" f  s xts0=$o(^TTAB("RL",HOSP,"TS-index",xts0)) q:xts0=""  i (","_xts0_",")[(","_xts_",") d  i stop q
 ......s ts="" f  s ts=$o(^TTAB("RL",HOSP,"TS-index",xts0,ts)) q:ts=""  i $d(^TEPI(epis,1,ts)) s stop=1 q
 .....i $l(ts) s tscnt=$o(^TEPI(epis,1,ts,"")),vts=epis_"||"_ts_"||"_tscnt
 ....i '$l($$seldata^LVBVISTS(vts,1)) s vts="" q
 .... ; only file results in Word/RTF test set
 ....s ts=$p(vts,"||",2) i $$seldata^LVBCTTS(ts,31)'="Y" s vts="" q
 ...i $l(vts),$p(xx,"|")="OBX",$p(xx,"|",3)="FT" s result=$p(xx,"|",6) d
 ....f j1=1:1:$l(result,"\.br\") d
 .....s xx=$p(result,"\.br\",j1)
 .....f j2="S" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(16)_$p(xx,x2,2,$l(xx,x2))
 .....f j2="F" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(17)_$p(xx,x2,2,$l(xx,x2))
 .....f j2="T" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"&"_$p(xx,x2,2,$l(xx,x2))
 .....f j2="R" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"~"_$p(xx,x2,2,$l(xx,x2))
 .....f j2="H","N","E" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$p(xx,x2,2,$l(xx,x2))
 .....s result(j1)=xx s done="process"
 ....s j1=$o(result(""),-1)+1,result(j1)=""
 ....s j1=$o(result(""),-1)+1,result(j1)="Testing Laboratory: "_HOSP(3)
 ....s j1=$o(result(""),-1)+1,result(j1)="Reference: "_lab
 ....s ^TMPimport(xFile,"index",vts,seq1)=""
 ....m ^TMP($zn,$j,xFile,"OUT",vts,seq1,"T")=result
 ....k result
 ..m ^TMPimport(xFile,done,seq1)=^TMP($zn,$j,xFile,"IN-1",seq1)
 ..i done="bad" d
 ...s stop=0
 ...s file="" f j=1:1 d  i stop=1 q
 ....s x=$s(dir["\":"\",1:"/")
 ....s file=$p(dir,x,1,$l(dir,x)-1)_x_"ERROR"_x_$s($l(epis):epis,1:"X")_"-"_$s($l(ts):ts,1:"X")_"-"_j_".hl7"
 ....o file::0 i  c file q
 ....s stop=1
 ...s ^TMPimport(xFile,done,seq1)=file
 ...f j="MSH","PID" s ^TMPimport(xFile,done,seq1,j)=$g(^TMP($zn,$j,xFile,"Extra",seq1,j))
 ..k ^TMP($zn,$j,xFile,"IN-1",seq1)
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s erseq="" f  s erseq=$o(^TMPimport(xFile,"bad",erseq)) q:erseq=""  d
 ..s fn=$g(^(erseq))
 ..i $l(fn) d
 ...o fn:("WNS"):10
 ...f j="MSH","PID" i $d(^TMPimport(xFile,"bad",erseq,j)) u fn w ^TMPimport(xFile,"bad",erseq,j),!
 ...s sub="" f  s sub=$o(^TMPimport(xFile,"bad",erseq,sub)) q:sub=""  u fn w ^(sub),!
 ...c fn
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s vts="" f  s vts=$o(^TMP($zn,$j,xFile,"OUT",vts)) q:vts=""  d
 ..s seq="" f  s seq=$o(^TMP($zn,$j,xFile,"OUT",vts,seq)) q:seq=""  d
 ...s (text,cnt)="" f  s cnt=$o(^TMP($zn,$j,xFile,"OUT",vts,seq,"T",cnt)) q:cnt=""  s text=text_$s($l(text):"|",1:"")_^(cnt)
 ...s epis=$p(vts,"||",1),ts=$p(vts,"||",2),tscnt=$p(vts,"||",3)
 ...i $$vtsLock^SSLOCK("IM",epis,vts,"trak")=-1 d
 ....i $$status^LVBVIS1(vts)="A" d
 .....i $$delall^LVBVISTQ(vts)
 .....i '$$select^LVBVISTS(vts,"Y") s (PLIST(7),PLIST(8),PLIST(9))="" i $$update^LVBVISTS(vts)
 ....i $d(^TEPI(epis,1,ts,tscnt,"DATA","T")) d
 .....k xx m xx=^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
 .....s max=0,xx=""
 .....f j=1:1 q:'$d(xx(j))  s xx=xx_$s($l(xx):"|",1:"")_xx(j) i max<$l(xx(j)) s max=$l(xx(j))
 .....f j=1:1:$l(text,"|") i max<$l($p(text,"|",j)) s max=$l($p(text,"|",j))
 .....s line="",$p(line,"-",max+1)=""
 .....s text=$j("",max-22\2)_"--- Amended Result ---"_"||"_text_"||"_line_"|"_xx
 ....k PLIST
 ....s PLIST(1)="DATA"_$c(1)_"T"_$c(1)_text
 ....s PLIST=$o(PLIST(""),-1)
 ....i $$TScheck^LVBVIS7(vts,"A","","B")
 ....s (VQ,ok)="" f j=1:1:PLIST d  i ok="N" q
 .....i $e($p(PLIST(j),$c(1)))="E" s ok="N" q
 .....i $e($p(PLIST(j),$c(1)),1,2)="VQ" s VQ=$p(PLIST(j),$c(1),2)
 ....i $$delall^LVBVISTQ(vts)
 ....k PLIST s PLIST(1)="T\"_text
 ....s PLIST=$o(PLIST(""),-1)
 ....s err=$$save^LVBVIS4(vts,"A","trak","N") i err s ^TMPimport(xFile,"index",vts,seq)="err = "_err
 ....i 'err d
 .....i '$$select^LVBVISTS(vts,"Y") s PLIST(29)=$$seldata^LVBEPVIS(epis,66) i $$update^LVBVISTS(vts)
 .....k PLIST
 .....s PLIST=$o(PLIST(""),-1)
 .....i $$TScheck^LVBVIS7(vts,"A","","A")
 .....f j=1:1:PLIST d
 ......s val=$p($p(PLIST(j),$c(1),2),$c(2))
 ......i $e($p(PLIST(j),$c(1)),1,2)="VQ",$$VQ^LVBCOM13(vts,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="TA",$$TAR^LVBCOM13($p(vts,"||"),,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SA",$$SA^LVBCOM13($p(vts,"||"),val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SF",$$SF^LVBCOM13(vts,$p(PLIST(j),$c(1),6),val)
 ......i $e($p(PLIST(j),$c(1)),1,3)="ACT",$$ACT^LVBCOM13($p(vts,"||"),$p(val,"-",1),$p(val,"-",2))
 ...i $$vtsLockClr^SSLOCK("IM",epis,vts)
 s (param,xFile)="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  s param=param_$s($l(param):",",1:"")_xFile
 i $l(param),$$START^PRTREP00("","",HOSP_"\"_param,"RLI")
 k ^TMP($zn,$j)
 q
 ; Error log
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
 ; Error log
Error2 i $ze'["ENDOFFILE" d BACK^%ETN s Error=1
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
file(deb,epis,recPID,recPV1) n (deb,epis,recPID,recPV1) s deb=$g(deb),epis=$g(epis),recPID=$g(recPID),recPV1=$g(recPV1)
 k pid f j=1:1:38 s pid(j)=$p(recPID,"|",j)
 k pv1 f j=1:1:52 s pv1(j)=$p(recPV1,"|",j)
 s HOSP="H" k PLIST,x
 ; check if admission already exists
 s exist="N" i '$$select^LVBHOSP(HOSP_"||"_deb_"||"_epis) s exist="Y"
 ; file data
 s x(2)=deb
 s x(3)=epis
 s x(4)=$$space($p(pid(5),"^",2))
 s x(5)=$$space($p(pid(5),"^",1))
 s x(7)=$$intdate^SSUTIL4($e(pid(7),5,6)_"/"_$e(pid(7),7,8)_"/"_$e(pid(7),1,4))
 s x(8)=pid(8)
 s x(9)=$$space($p(pid(11),"^",1))
 s x(10)=$$space($p(pid(11),"^",2))
 s x(11)=$$space($p(pid(11),"^",3))
 s x(12)=$$space($p(pid(11),"^",4))
 s x(13)=$$space(pid(13))
 s x(14)=$$space(pv1(2))
 s x(15)=$$space($p(pv1(3),"^",1)_$p(pv1(3),"^",4)) i $l(x(15)) d
 .n (HOSP,x)
 .s X="" i '$$select^LVBHOSCL(HOSP_"||"_x(15)) s X=PLIST(3)
 .s x(15)=X
 s x(28)=$$space($p(pid(5),"^",4))
 s x(21)=$$intdate^SSUTIL4($e(pv1(44),5,6)_"/"_$e(pv1(44),7,8)_"/"_$e(pv1(44),1,4))
 s x(22)=($e($e(pv1(44),9,14),1,2)*3600)+($e($e(pv1(44),9,14),3,4)*60)+$e($e(pv1(44),9,14),5,6)
 s x(24)=$e($$space(pid(19)),1,10)
 s x(43)=$e($$space(pid(19)),11)
 s x(33)=$$space(pid(10)) i x(33)=9 s x(33)=""
 s x(36)=$$space(pid(27))
 s x(45)=$$space(pid(18))
 s x="" f  s x=$o(x(x)) q:x=""  i $l(x(x)) s PLIST(x)=x(x)
 s PLIST=$o(PLIST(""),-1)
 ; update patient information
 i exist="Y" d  q
 .s err=$$update^LVBHOSP(HOSP_"||"_deb_"||"_epis) i err d trace^LVBPMI04("Insert error - "_err,"file ")
 ; insert new patient information
 i exist="N" d  q
 .s err=$$insert^LVBHOSP(HOSP) i err d trace^LVBPMI04("Update error - "_err,"file ")
 q
 ; delete end spaces
space(x) 
 f  q:'$l(x)  q:$e(x,$l(x))'=" "  s x=$e(x,1,$l(x)-1)
 f  q:'$l(x)  q:$e(x)'=" "  s x=$e(x,2,$l(x))
 q x
]]></Routine>
</Export>
