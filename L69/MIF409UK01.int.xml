<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF409UK01" type="INT" languagemode="0" generated="1"><![CDATA[
MIF409UK01(mi) ; ML 12/1/99 ; ASTM protocol  -  J & J Vitros ECi
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4)
 s lf=$c(10),cr=$c(13),nak=$c(21),(result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:20 e  d
 .i $d(^TMIF(mi,10))," L "[(" "_par9_" ") d loadlist^MIF000(mi)
 .i '$d(^TMIF(mi,10))," B "[(" "_par9_" ") d ORDER
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK s enqcnt=""
 .f  r *R:10 q:$c(R)=eot  q:R=-1  d
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",lf) q:'$l(record)
 ..s record=$e(record,1,$l(record)-1)
 ..d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
 .. ; check record validity
 ..s chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
 ..i $$CHSUM(chrecord)'=chsum d  q
 ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 ..d ACK
 .. ; query record
 ..i $e(record,2)="Q" d  q
 ...s epis=$p($p(record,"|",3),"^",2)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="" q
 ...s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix q
 .. ; header record
 ..i $e(record,2)="H" d  q
 ...d Last
 .. ; patient record
 ..i $e(record,2)="P" d  q
 ...s surname=$tr($tr($p(record,"|",6)," "),"^"," ")
 .. ; order record
 ..i $e(record,2)="O" d  q
 ...s QC="",epis=$tr($p($p(record,"|",3),"^",1)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
 ...; i " insert qc names here "[(" "_$$ALPHAUP^SSUTIL4(epis)_" ") s QC=1
 ...i QC=1 s epis=$$ALPHAUP^SSUTIL4(epis),QC="\"_epis
 .. ; result record
 ..i $e(record,2)="R" d  q
 ...s x1=$tr($p($p(record,"|",3),"+",2)," "),x2=$tr($p(record,"|",4)," "),x3=$tr($p($p(record,"|",7),"^",2)," ")
 ...i $l(x1),$l(x2),x2'="NoResult" d
 ....i x1="030" s x=$o(xxx(""),-1)+1,xxx(x)=x2
 ....s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 ... ; date and time
 ...i $l($tr($p(record,"|",13)," ")) d
 ....s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
 ....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
 .. ; last record
 ..i $e(record,2)="L" d Last q
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 i $d(^TMIF(mi,10)) d Send
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d
 .i $o(xxx(""),-1)>1 d
 ..s xxx="" f j=1:1:$o(xxx(""),-1) s xxx=xxx+xxx(j)
 ..i xxx s xxx=xxx/j,result=result_"030"_$c(par10)_xxx
 .d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)="" k xxx
 q
ORDER ; create list of orders if exists
 d scanall^MIF000(mi)
 s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
 .d PATDET(epis)
 .d SET(epis,"SCAN")
 q:'$d(^TMIF(mi,10))
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
 d Send
 k ^TMIF("MIF-SCAN",$j,mi)
 q
SAVE(mi) ; create loadlist records
 s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s sample=^TMP("MIF000",$j,"LL","SAMPLE")
 d PATDET(epis)
 d SET(epis,"LOAD")
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 q 0
PATDET(epis) ; set patient details record
 i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"|||"_sex_"|||||||||"
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||^||||||||"
 q
SET(epis,mode) ; set record to be sent to instrument
 s tcx=""
 ; process a single episode scan
 i mode="QUERY",$d(^TMP("MIF-SINGLE",$j,mi,epis)) d
 .s tc="" f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  s tcx=tcx_tc_"+1\"
 .s tcx=$e(tcx,1,$l(tcx)-1) i $l(tcx) s tcx="1.0+"_tcx
 ; process an all episode scan
 i mode="SCAN",$d(^TMP("MIF-SCAN",$j,mi,epis)) d
 .s tc="" f  s tc=$o(^TMP("MIF-SCAN",$j,mi,epis,tc)) q:tc=""  s tcx=tcx_tc_"+1\"
 .s tcx=$e(tcx,1,$l(tcx)-1) i $l(tcx) s tcx="1.0+"_tcx
 ; process a load list episode scan
 i mode="LOAD" d
 .s tc="" f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  s tcx=tcx_tc_"+1\"
 .s tcx=$e(tcx,1,$l(tcx)-1) i $l(tcx) s tcx="1.0+"_tcx
 i $l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_"||^^^"_tcx_"|||||||N||||4||||||||||O|||||"
 e  s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_"||^^^|||||||C||||4||||||||||O|||||"
 q
Send ; send list of orders if exists
 q:'$d(^TMIF(mi,10))
 ; read in spare characters
 f  r *x:1 q:x=-1
 w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M") f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 i $c(R)=enq d trace^MIF000(mi,"ENQ","H<--M") q
 i $c(R)'=ack d  q
 .d trace^MIF000(mi,R,"H<--M") w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 d trace^MIF000(mi,"ACK","H<--M")
 q:$$SEND("1H|\^&|||LABTRAK|||||||P|1")
 s (ok,x)="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  s ok=$$SEND(FRAME#8_^TMIF(mi,10,x)) q:ok
 h 1 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 i 'ok k ^TMIF(mi,10)
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 s str=$e(str,1,$l(str)-2) d trace^MIF000(mi,str_" "_chsum,"H-->M")
 f j=1:1:6 r *R:10 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
NEXTTRAY(tray) 
 q tray
]]></Routine>
</Export>
