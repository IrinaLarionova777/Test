<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDST01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDST01 ; ML 27/10/98 ; DR SINGLE TEST STATISTICS INCLUDING RESULTS
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Doctor Code
 ;               4. Test Set
 ;               5. Print Comment - Y/N
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Dr Single Test Statistics"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Test Set : "_TEST(3)_" ("_PAR4_")"
 s line=4,xHEADER(line)=""
 s line=5,xHEADER(line)="Doctor : "_$s($l(PAR3):$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51))_" ("_PAR3_")",1:"All Doctors")
 s line=6,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 i $l(PAR3),'$$select^LVBCTDR(PAR3,"Y") k DOC m DOC=PLIST
 i '$$select^LVBCTTS(PAR4,"Y") k TEST m TEST=PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 i $l(PAR3) d SINGLE(PAR3)
 i '$l(PAR3) s doc="" f  s doc=$o(^TIND(3,doc)) q:doc=""  d SINGLE(doc)
 q
SINGLE(doc) ; process single doctor
 s date=PAR1-1 f  s date=$o(^TIND(3,doc,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TIND(3,doc,date,epis)) q:epis=""  d
 ..i $d(^TEPI(epis,1,PAR4)) s ^TMP($zn,$j,"REPORT",doc,date,epis)=""
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s (Count1,doc)="" f  s doc=$o(^TMP($zn,$j,"REPORT",doc)) q:doc=""  d
 ..i '$$select^LVBCTDR(doc,"Y") k DOC m DOC=PLIST
 ..i '$l(PAR3) d
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ...d ADDLINE^PRTREP00(REPID,"PRT","?1  "_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51))_" ( "_doc_" )")
 ...d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..s (Count2,date)="" f  s date=$o(^TMP($zn,$j,"REPORT",doc,date)) q:date=""  d
 ...s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",doc,date,epis)) q:epis=""  d
 ....q:$$select^LVBEPVIS(epis)  m VISIT=PLIST k PLIST
 ....s Count1=Count1+1,Count2=Count2+1
 ....s tscnt="" f  s tscnt=$o(^TEPI(epis,1,PAR4,tscnt)) q:tscnt=""  d
 .....s vtsRID=epis_"||"_PAR4_"||"_tscnt i '$$results^LVBVISTS(vtsRID,,,,"Y") k VISTS m VISTS=PLIST k PLIST
 .....f j1=1:1:VISTS d
 ......s ti=$p(VISTS(j1),$c(1),2),Res1=$p(VISTS(j1),$c(1),3),Res2=$p(VISTS(j1),$c(1),4)
 ......s Synonym=$$seldata^LVBCTTC(ti,14)
 ......i $l(Res1) d
 .......s format=$$seldata^LVBCTTC(ti,5)
 .......i format="D",Res1?1.5N s Res1=$$extdate2^SSUTIL4(Res1)
 .......i format="T",Res1?1.5N s Res1=$$exttime^SSUTIL4(Res1)
 .......s xxx(1)=Synonym_": "_$s($d(^TTAB("TC",ti,2,Res1)):Res1_" "_$g(^TTAB("TC",ti,2,Res1,1)),1:Res1)
 .......d PRINT^PRTREP00(.VISIT,.N) k VISIT,xxx
 ......i $l(Res2),PAR5="Y" k PLIST d
 .......s x=0 f  s x=$o(^TEPI(epis,1,PAR4,tscnt,"DATA",ti,"REM",x)) q:x=""  s PLIST(x)=1_$c(1)_ti_$c(1)_^(x)
 .......s PLIST=$o(PLIST(""),-1) i '$$COMMENTS^LVBCOM01("",PAR4,"P") d
 ........f j2=1:1:PLIST s xxx(1)="Comments : "_PLIST(j2) d PRINT^PRTREP00(.VISIT,.N) k VISIT,xxx
 ..i '$l(PAR3) d
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT","?1  Total Patients for "_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51))_" ( "_doc_" ) : "_Count2)
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?1  Total Patients : "_Count1)
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="DST"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
