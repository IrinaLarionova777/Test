<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPLUAE01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTEPLUAE01 ; ML 30/08/2000 ; Episode Patient Label - United Arab Emirates - ADNOC
 q
 ; Parameters :  1. Episode number
 ;               2. Label code
 ;               3. Quantity
 ;               4. Computer Name
 ;               5. Worksheet
 ;               6. Worksheet No.
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($ZN,$J)
 D INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR4_"-"_PAR1,PLIST(9)="M" i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 q
BODY ; create temporary printing file
 ; label and quantity entered
 i $l(PAR1),$l(PAR2),$l(PAR3) s ^TMP($zn,$j,"REPORT",PAR1,PAR2)=PAR3 q
 ; print labels for a patient
 i $l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)
 ..f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBWKSRT()
 .i '$l(PAR5) d
 ..i $$open^LVBVISTS(PAR1)
 ..f  q:$$fetch^LVBVISTS(PAR1)  s ts=$p(PLIST(3),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBVISTS()
 i '$l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRE(PAR5_"||"_PAR6)
 ..f  q:$$fetch^LVBWKSRE(PAR5_"||"_PAR6)  s epis=$p(PLIST(2),$c(1)) d
 ...i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)
 ...f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,epis,ts)=$g(^TMP($zn,$j,1,epis,ts))+1
 ...i $$close^LVBWKSRT()
 ..i $$close^LVBWKSRE()
 s epis="" f  s epis=$o(^TMP($zn,$j,1,epis)) q:epis=""  d
 .s ts="" f  s ts=$o(^TMP($zn,$j,1,epis,ts)) q:ts=""  d
 ..s (label,max)=""
 ..s label=$$seldata^LVBCTTS(ts,23),max=$$seldata^LVBCTLB(label,4)
 ..i $l(label),max d
 ...i $l(PAR2),PAR2'=label q
 ...s ^TMP($zn,$j,"REPORT",epis,label,ts)=^TMP($zn,$j,1,epis,ts)
 ...s (cnt,ts1)="" f  s ts1=$o(^TMP($zn,$j,"REPORT",epis,label,ts1)) q:ts1=""  s cnt=cnt+^(ts1)
 ...s xx=cnt\max s:cnt#max xx=xx+1 s ^TMP($zn,$j,"REPORT",epis,label)=xx
 q
PRINT 
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  i '$$select^LVBEPVIS(epis) d
 .k VISIT m VISIT=PLIST s ReqLabel=""
 .i $l($p($g(VISIT(7)),$c(1),2)) s name=$p($g(VISIT(7)),$c(1),2)_" "_$g(VISIT(3))_","_$g(VISIT(3))
 .e  s name=$g(VISIT(3))_","_$g(VISIT(4))
 .s spec="" i $p(VISIT(51),$c(1))'="",'$$select^LVBCTSI($p(VISIT(51),$c(1))) s spec=PLIST(3)
 .s label="" f  s label=$o(^TMP($zn,$j,"REPORT",epis,label)) q:label=""  d
 ..s cnt=^(label),LabelDesc=label i '$$select^LVBCTLB(label) s LabelDesc=PLIST(3)
 ..s (TestSyn,ts)="" f  s ts=$o(^TMP($zn,$j,"REPORT",epis,label,ts)) q:ts=""  s TestSyn=TestSyn_$s(TestSyn="":"",1:" ")_$$seldata^LVBCTTS(ts,4)
 ..f j=1:1:cnt d LABEL
 .s ReqLabel=1 f i=1:1:3 d LABEL
 q
LABEL ; print label
 d ADD^SSPRINT(REPID,"PRT","^XA")
 d ADD^SSPRINT(REPID,"PRT","^LH10,20")
 d ADD^SSPRINT(REPID,"PRT","^FO5,5^ADN,30^FD"_$p(VISIT(1),$c(1))_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^FO250,2^ADN,12^FD"_$p(VISIT(20),$c(1))_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^FO250,22^ADN,12^FD"_spec_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^FO25,40^BY"_$s(label=2:1,1:2)_",1,70^B3N,N,70,N,N^FD"_$p(VISIT(1),$c(1))_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^FO5,115^ADN,12^FD"_name_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^FO5,135^ADN,12^FD"_"Doctor: "_$p(VISIT(15),$c(1),2)_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^FO5,155^ADN,12^FD"_"Coll. Date: "_$$extdate2^SSUTIL4(VISIT(12))_$s(VISIT(13)'="":" "_$$exttime^SSUTIL4(VISIT(13)),1:"")_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^FO5,175^ADN,12,12^FD"_$s(ReqLabel="":"Tests: "_$s($l(TestSyn)<24:TestSyn,1:LabelDesc),1:"Request Label")_"^FS")
 d ADD^SSPRINT(REPID,"PRT","^XZ")
 q
]]></Routine>
</Export>
