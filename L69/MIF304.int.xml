<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF304" type="INT" languagemode="0" generated="1"><![CDATA[
MIF304(mi) ; ML 15/7/98 ; BECKMAN ARRAY 360CE (Bi-Directional)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s soh=$c(1),stx=$c(2),etx=$c(3),eot=$c(4),enq=$c(5),ack=$c(6),lf=$c(10),cr=$c(13),nak=$c(21),chk=0
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *x:15 e  q
 d trace^MIF000(mi,$s($c(x)=soh:"SOH",$c(x)=eot:"EOT",$c(x)=enq:"ENQ",1:x),"H<--M")
 i $c(x)=enq d  q
 .w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 q:$c(x)'=eot
 r *x:5 e  q
 q:$c(x)'=soh  d trace^MIF000(mi,"SOH","H<--M"),ACK
 s eoenq=""
 f  s record=$$read^MIF000("",lf) q:'$l(record)  d  q:eoenq
 .s record=$c(x)_record i $e(record)'="[",record["[" f  q:$e(record)="["  s record=$e(record,2,999)
 .d trace^MIF000(mi,record,"H<--M")
 .d CHKREC i 'ok w nak,*-3 d trace^MIF000(mi,"NAK","H-->M") q
 .d ACK
 .s str=$p(record,",",2),fun=+$p(record,",",3)
 . ; end of host query transmission
 .i fun=10 s eoenq=1 q
 . ; process host query
 .i str=901 d A1 q
 . ; process results record
 .i str=902 d A2 q
 r *R:5 i $c(R)'=eot q
 d trace^MIF000(mi,"EOT","H<--M")
 i '$d(^TMIF(mi,10)) q
 s cnt="",cont=0
 f  s cnt=$o(^TMIF(mi,10,cnt)) q:cnt=""  d  q:cont
 .s rec=^(cnt) d SEND(rec) q:cont  q:fail
 .k ^TMIF(mi,10,cnt)
 q
A1 ; process query record
 q:fun'=8
 s epis=$p($p(record,",",5),"]"),epis=$tr(epis," ")
 i epis'="" d BUILD
 i 'exist s $p(rec,",",3)="09",rec=rec_"]",rec=rec_$$CHSUM(rec)
 s cnt=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,cnt)=rec
 q
A2 ; process results record
 i fun=2 d  q
 .s (sample,epis,surname,result,date,time,QC)=""
 .s epis=$p(record,",",7),epis=$tr(epis," ")
 .i $p(record,",",11)="CO" d  q
 ..s QC=1,date=$p(record,",",5),date=$e(date,5,6)_$e(date,3,4)_$e(date,1,2)
 ..s:date'?6N date="" q
 ..s date=date+19000000 s:date<19940000 date=date+1000000
 ..s date=$$intdate^SSUTIL4(date),time=($p(record,",",6)\100*60+($p(record,",",6)#100)*60)
 .i $l(epis)>7 s epis=$e(epis,$l(epis)-6,99)
 i fun=4 d  q
 .s rng=$p(record,",",12),res=$p(record,",",8),res=$tr(res," ")
 .s result=result_$tr($p(record,",",5)," ")_$c(par10)_$s((rng="LN")!(rng="LO"):"<",(rng="HC")!(rng="HI")!(rng="HX"):">",1:"")_(+res)_$c(par11)
 i fun=8,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
BUILD ; build reply to query string
 s rec=$p(record,"]",1),$p(rec,",",3)="01",exist="0"
 ; extract pending episode details
 i $d(^TEPI(epis))#10 d
 .s patdet=^TEPI(epis),sx=$p(patdet,"\",3) i "MF"'[sx s sx=" "
 .s rec=rec_",SE,0000,"_sx_",RO,N"
 .s exist=1
 ; extract pre-entered episode details
 s:$d(^TPRE(epis))#10 rec=rec_",SE,0000, ,RO,N",exist=1
 q:'exist
 d scanone^MIF000(mi,epis)
 s tcch="" f  s tcch=$o(^TMP("MIF-SINGLE",$j,mi,epis,tcch)) q:tcch=""  d
 .i tcch'?." " s tst(tcch)=""
 s tst="" f i=1:1:20 s tst=$o(tst(tst)) q:tst=""  s rec=rec_","_$e(tst_"   ",1,3)
 f j=i:1:20 s rec=rec_",   "
 s $p(rec,",",4)=$tr($p(rec,",",4)," ","0"),rec=rec_"]",rec=rec_$$CHSUM(rec)
 k ^TMP("MIF-SINGLE",$j,mi,epis) 
 q
BID ; bid for line
 s fail=0 h 10
 w eot,*-3,soh,*-3 d trace^MIF000(mi,"EOT - SOH","H-->M")
 r *R:30 e  w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 d trace^MIF000(mi,$s($c(R)=soh:"SOH",$c(R)=eot:"EOT",$c(R)=enq:"ENQ",$c(R)=ack:"ACK",$c(R)=etx:"ETX",1:R),"H<--M")
 i $c(R)=eot r *R:2 d TRACE(R) i $c(R)=soh s fail=1 q
 q
SEND(str) ; send string to instrument
 d BID q:fail
 w str,cr,lf ;,*-3
 d trace^MIF000(mi,str,"H-->M")
 r *R d TRACE(R) w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 r *R d TRACE(R) i $c(R)'=eot w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 r *R d TRACE(R) w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 s record=$$read^MIF000("",lf) d trace^MIF000(mi,record,"H<--M")
 w etx,*-3 d trace^MIF000(mi,"ETX","H-->M")
 r *R d TRACE(R)
 q
CHKREC ; check sum received = check sum calculated
 s ok=$$CHSUM($p(record,"]",1)_"]")=$e($p(record,"]",2),1,2)
 i $$CHSUM($P(record,"]",1)_"]")="0",$e($p(record,"]",2),1,2)="00" S ok=1
 q
CHSUM(x) ; calculate check sum for record
 n y,z s z=0
 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=256-(z#256)
 s z=$e("0123456789ABCDEF",z\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send ack/etx alternately to instrument
 w $s(chk=0:ack,1:etx),*-3 d trace^MIF000(mi,$s(chk=0:"ACK",1:"ETX"),"H-->M")
 s chk=$s(chk=0:1,1:0)
 q
TRACE(x) ; file into trace
 d trace^MIF000(mi,$s($c(x)=soh:"SOH",$c(x)=eot:"EOT",$c(x)=enq:"ENQ",$c(x)=ack:"ACK",$c(x)=etx:"ETX",1:x),"H<--M")
 q
]]></Routine>
</Export>
