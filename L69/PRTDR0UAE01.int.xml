<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR0UAE01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR0UAE01 ; ML 26/07/2000 ; Doctor's Report HEADER - United Arab Emirates - ADNOC
 q
 ; supported calls
 ;
 ; INIT^PRTDRUAE01
 ; QDR^PRTDRUAE01
 ; Header^PRTDRUAE01(page,par1,par2)
 ; Footer^PRTDRUAE01(page,par1,par2)
 ;
 ; client specific parameters
INIT s MaxLine=20
 q
 ; print header
Header(page,par1,par2) n (page,par1,par2,VISIT,DOC,HOSP,REPID,CopyToDoctor)
 i '$d(^PR(REPID,"HDR")) d QDR
 d ADD^PRTREP00(REPID,"PRT","#")
 s (copyto,copyfrom,specimen)="",epis=$p(VISIT(2),$c(1))
 ; special interest
 s spec="" i $p(VISIT(51),$c(1))'="",'$$select^LVBCTSI($p(VISIT(51),$c(1))) s spec=PLIST(3)
 ; find copy-to only for DR's copies
 i $d(DOC),$p(VISIT(15),$c(1))=$p(DOC(2),$c(1)) d
 .i $$open^LVBVISDC(epis)
 .f  q:$$fetch^LVBVISDC(epis)  s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 .i $$close^LVBVISDC()
 .i $l(VISIT(22)) s copyto=copyto_$s($l(copyto):", ",1:"")_$p(VISIT(22),$c(1),2)
 ; find copy-from
 i $d(DOC),$p(VISIT(15),$c(1))'=$p(DOC(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 i $d(HOSP),$p(VISIT(22),$c(1))'=$p(HOSP(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 ; find specimens
 i $$open^LVBVISSP(epis)
 f  q:$$fetch^LVBVISSP(epis)  s specimen=specimen_$s($l(specimen):", ",1:"")_$p(PLIST(2),$c(1))
 i $$close^LVBVISSP()
 i $l(specimen)>20 f j=1:1 i $l($p(specimen,",",1,j))>20 s specimen=$p(specimen,",",1,j-1) q
 s servtime="" i $l(VISIT(13)) s servtime=$e(VISIT(13)\3600+100,2,3)_":"_$e(VISIT(13)\60#60+100,2,3)
 s servdate="" i $l(VISIT(12)) s servdate=$$extdate2^SSUTIL4(VISIT(12))
 s (xdate,tsx)="" f  s tsx=$o(^TEPI(epis,1,tsx)) q:tsx=""  d
 .s tsxcnt="" f  s tsxcnt=$o(^TEPI(epis,1,tsx,tsxcnt)) q:tsxcnt=""  d
 ..s authdate=$p(^(tsxcnt),"\",4) s:authdate>xdate xdate=authdate
 s authdate="" s:xdate'="" authdate=$$extdate2^SSUTIL4(xdate)
 i authdate="" s authdate="Prelim. Rep."
 ; format Doctor's address
 i $d(DOC) d Address(DOC(9),DOC(10),DOC(11),DOC(12),DOC(13),.dAddress)
 i $d(HOSP) d Address(HOSP(4),HOSP(5),HOSP(6),HOSP(7),HOSP(8),.hAddress)
 ; format Patient's address
 i VISIT(33)=VISIT(34) d Address(VISIT(32),"",VISIT(34),VISIT(35),VISIT(36),.pAddress)
 e  d Address(VISIT(32),VISIT(33),VISIT(34),VISIT(35),VISIT(36),.pAddress)
 i '$l($p(VISIT(6),$c(1)))!(VISIT(26)=500) s dob="Unknown"
 e  s dob=$$extdate2^SSUTIL4(VISIT(6))
 s sex="U" i $l(VISIT(5)) s sex=$p(VISIT(5),$c(1))
 s homephone=$p(VISIT(43),$c(1))
 ; patient details
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d DeptHeader
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a800h500V"_$$NameFormat^LVBCOM03("PT","",VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101)))
 d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a800h700V"_sex)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a800h900V"_dob)
 i $d(DOC) d
 .d ADD^PRTREP00(REPID,"PRT","~BOLD")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h500V"_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51)))
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 i $d(HOSP) d
 .d ADD^PRTREP00(REPID,"PRT","~BOLD")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h500V"_HOSP(3))
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h700V"_servdate)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4800h700V"_epis)
 d ADD^PRTREP00(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h900V"_spec)
 d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4800h900V"_$p(VISIT(20),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 i dept="C" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h1120V")
 e  d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h1020V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
dt(date,time) n (date,time) s date=$g(date),time=$g(time),result=""
 s result=$e(time\3600+100,2,3)_":"_$e(time\60#60+100,2,3)_" "_$$extdate2^SSUTIL4(date)
 q result
path(dep) n (dep) s dep=$g(dep),result=""
 i $l(dep),'$$select^LVBCTDEP(dep) s result=$p(PLIST(10),$c(1),2)
 q result
 ; print department header
DeptHeader s rowid="" f  s rowid=$o(^TMP("REPORT",$j,page,"TEST",rowid)) q:rowid=""  i $p(rowid,"||",3) d  i $l(deptname) q
 .s dept=$e($p(rowid,"||",2)) i '$$select^LVBCTDEP(dept,"Y") s deptname=PLIST(3)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v1s2b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s(dept="N":3800,dept="Y":3950,dept="P":4100,1:4000)_"h180V"_deptname)
 q
 ; print footer
Footer(page,par1,par2) n (page,PAR1,PAR2,REPID) s page=$g(page),REPID=$g(REPID)
 s (authdate,upuser)=""
 s rowid="" f  s rowid=$o(^TMP("REPORT",$j,page,"TEST",rowid)) q:rowid=""  q:$p(rowid,"||",3)
 i '$$select^LVBVISTS(rowid),$p(PLIST(7),$c(1))'="" d
 .s authdate=$$extdate2^SSUTIL4($p(PLIST(7),$c(1))),month=$p(authdate,"/",2)
 .s authdate=$p(authdate,"/")_" "_$s(month="01":"Jan",month="02":"Feb",month="03":"Mar",month="04":"Apr",month="05":"May",month="06":"Jun",month="07":"Jul",month="08":"Aug",month="09":"Sept",month="10":"Oct",month="11":"Nov",1:"Dec")_" "_$p(authdate,"/",3)
 .s upuser=$p(PLIST(6),$c(1))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s0b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a400h3750VReq. rpt: "_authdate)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2500h3750VBy: "_upuser)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4400h3750VPathologist:")
 q
 ; print header
QDR s QDR="ADNOC",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
 q
 ; compile address
Address(x1,x2,x3,x4,x5,Address) n (x1,x2,x3,x4,x5,Address) k Address s cnt=0 f j=1:1:3 s Address(j)=""
 i $l(x1) s cnt=cnt+1,Address(cnt)=x1
 i $l(x2) s cnt=cnt+1,Address(cnt)=x2
 s cnt=cnt+1
 i $l(x3) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):",",1:"")_x3
 i $l(x4) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):",",1:"")_x4
 i $l(x5) d
 .i cnt>1,'$l(Address(cnt)) s cnt=cnt-1
 .s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):",",1:"")_x5
 q
]]></Routine>
</Export>
