<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF133" type="INT" languagemode="0" generated="1"><![CDATA[
MIF133(mi)      ; ML 02/11/2002 ; BIOMERIEUX Mini API
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s soh=$c(1),stx=$c(2),etx=$c(3),eot=$c(4),ack=$c(6),nak=$c(21)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  q
 i $c(R)=soh d
 .d trace^MIF000(mi,"SOH","H<--M")
 .d ACK
 .f  r *R:10 q:$c(R)=eot  q:R=-1  d
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",etx) q:'$l(record)
 ..d trace^MIF000(mi,record,"H<--M")
 ..s chsum=$e(record,$l(record))
 ..d ACK
 .. ; process simplified sensitivity test block only
 ..s block=$e(record,6) q:block'=1
 ..s (sample,epis,surname,result,date,time,QC)=""
 ..s epis=$tr($e(record,7,26)," "),org=$tr($e(record,28,30)," ","0"),record=$e(record,33,9999)
 ..s result="DUMMY"_$c(par10)_epis
 ..k AntiBiotic f i=1:1:30 d
 ...s rec=$e(record,1,4),record=$e(record,5,9999)
 ...q:$tr($e(rec,1,3),"0")=""  q:$tr($e(rec,4)," ")=""
 ...s Antibiotic($e(rec,1,3))=$e(rec,4)
 ..i $l(epis),$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 .. ; file organism/antibiotic result
 ..i $d(Antibiotic) d
 ...s AntiRes=org_$c(par10)
 ...s anti="" f  s anti=$o(Antibiotic(anti)) q:anti=""  s AntiRes=$g(AntiRes)_anti_$c(par28)_$g(Antibiotic(anti))_$c(par27)
 ...d fileant^MIF000(mi,sample,epis,AntiRes)
 q
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
]]></Routine>
</Export>
