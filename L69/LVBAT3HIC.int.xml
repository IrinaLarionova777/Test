<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBAT3HIC" type="INT" languagemode="0" generated="1"><![CDATA[
LVBAT3HIC ; IG 24/07/96 ; Receiving Accounts payments - HIC
 ;
 ; supported calls :
 ;
 ; $$start^LVBAT3HIC(type,create,user)
 ;
 ; 
 ; type : HIC
start(type,create,user) n (type,create,user) s type=$g(type),create=$g(create),user=$g(user) k ^TMP($zn,$j)
 d Log^LVBAT201(type,"Start Processing <"_type_">...")
 d Log^LVBAT201(type," ")
 k CFLAB i '$$select^MVBCFLAB(1) m CFLAB=PLIST
 i $$select^LVBCTPCT(type) q 100
 s dir=$tr(PLIST(6),$c(17),"\"),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.*"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""edi.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""edi.txt""")
 s $zt="Error1^"_$zn
 s FILE="edi.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xxFILE=$p(x," ",$l(x," ")) i xxFILE[".",$l(xxFILE) s ^TMP($zn,$j," ",xxFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .d Log^LVBAT201(type,"Processing file <"_xFILE_">...")
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  d
 ..s MESS=$$read(1) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 ..s MESS=$p(MESS,$c(1),2) i MESS'="*" q
 ..s MESS=$$read(1) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 ..s MESS="*"_$p(MESS,$c(1),2)
 ..i MESS="*A" d  k:Error ^TMP($zn,$j,"REPORT-A",xxFILE) q
 ...s MESS=$$read(98) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 ...s MESS=$p(MESS,$c(1),2)
 ...s MESS="*A"_MESS,^TMP($zn,$j,"REPORT-A",xxFILE)=MESS
 ...f j=1:1:$tr($e(MESS,3,5)," ") d  i Error q
 ....s MESS=$$read(100) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 ....s MESS=$p(MESS,$c(1),2)
 ....i $e(MESS,1,2)'="*P" s Error=2 d Log^LVBAT201(type,Error) q
 ....s xLine=$o(^TMP($zn,$j,"REPORT-A",xxFILE,""),-1)+1,^TMP($zn,$j,"REPORT-A",xxFILE,xLine)=MESS
 ....f i=1:1:$tr($e(MESS,3,4)," ") d  i Error q
 .....s MESS=$$read(40) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 .....s MESS=$p(MESS,$c(1),2)
 .....i $e(MESS,1,2)'="*C" s Error=3 d Log^LVBAT201(type,Error) q
 .....s x=$o(^TMP($zn,$j,"REPORT-A",xxFILE,xLine,""),-1)+1,^TMP($zn,$j,"REPORT-A",xxFILE,xLine,x)=MESS
 ..i MESS="*S" d  k:Error ^TMP($zn,$j,"REPORT-S",xxFILE) q
 ...s MESS=$$read(98) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 ...s MESS=$p(MESS,$c(1),2)
 ...s MESS="*S"_MESS,^TMP($zn,$j,"REPORT-S",xxFILE)=MESS
 ...f jj=1:1:$tr($e(MESS,3,5)," ") d  i Error q
 ....s MESS=$$read(60) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 ....s MESS=$p(MESS,$c(1),2)
 ....i $e(MESS,1,2)'="*X" s Error=2 d Log^LVBAT201(type,Error) q
 ....s CLAIMID=$e(MESS,7,11),^TMP($zn,$j,"REPORT-S",xxFILE,CLAIMID)=MESS
 ....f i=1:1:$tr($e(MESS,3,6)," ") d  i Error q
 .....s MESS=$$read(83) i $p(MESS,$c(1)) s Error=10 d Log^LVBAT201(type,Error) q
 .....s MESS=$p(MESS,$c(1),2)
 .....i $e(MESS,1,2)'="*E" s Error=3 d Log^LVBAT201(type,Error) q
 .....s x=$o(^TMP($zn,$j,"REPORT-S",xxFILE,CLAIMID,""),-1)+1,^TMP($zn,$j,"REPORT-S",xxFILE,CLAIMID,x)=MESS
 s $zt="Error^"_$zn
 ; print payments reports
 s xFILE="" f  s xFILE=$o(^TMP($zn,$j,"REPORT-A",xFILE)) q:xFILE=""  d
 .k ^TMP("EDI",$j,"REPORT-A",xFILE)
 .m ^TMP("EDI",$j,"REPORT-A",xFILE)=^TMP($zn,$j,"REPORT-A",xFILE)
 .i $$START^PRTREP00(user,"",xFILE,"MCHIRA")
 .k ^TMP("EDI",$j,"REPORT-A",xFILE)
 ; print exceptions reports
 s xFILE="" f  s xFILE=$o(^TMP($zn,$j,"REPORT-S",xFILE)) q:xFILE=""  d
 .k ^TMP("EDI",$j,"REPORT-S",xFILE)
 .m ^TMP("EDI",$j,"REPORT-S",xFILE)=^TMP($zn,$j,"REPORT-S",xFILE)
 .i $$START^PRTREP00(user,"",xFILE,"MCHIRS")
 .k ^TMP("EDI",$j,"REPORT-S",xFILE)
 i create="Y" d
 .s xFile="" f  s xFile=$o(^TMP($zn,$j,"REPORT-A",xFile)) q:xFile=""  d
 ..s seq1="" f  s seq1=$o(^TMP($zn,$j,"REPORT-A",xFile,seq1)) q:seq1=""  d
 ...s amount=+$e(^(seq1),60,68)/100
 ...s seq2="" f line=0:1 s seq2=$o(^TMP($zn,$j,"REPORT-A",xFile,seq1,seq2)) q:seq2=""
 ...i 'line q
 ...k PLIST
 ...s PLIST(2)=type_$$new^LVBCRBT(type)
 ...s PLIST(3)="U"
 ...s PLIST(4)=amount
 ...s PLIST(5)=line
 ...s PLIST(6)=+$h
 ...s PLIST=$o(PLIST(""),-1)
 ...f j=1:1:PLIST s PLIST(j)=$g(PLIST(j))
 ...s err=$$insert^LVBCRBT()
 ...i err d Log^LVBAT201(type,"Error <"_err_"> Creating Credit Batch ...") q
 ...i 'err s CRBatch=PLIST(1) d
 ....d Log^LVBAT201(type,"Created Credit Batch <"_CRBatch_">...")
 ....s seq2="" f line=1:1 s seq2=$o(^TMP($zn,$j,"REPORT-A",xFile,seq1,seq2)) q:seq2=""  d
 .....s amount=+$e(^(seq2),17,25)/100
 .....k PLIST
 .....s PLIST(2)=line
 .....s PLIST(7)=amount
 .....s claim=$e(^(seq2),3,7)
 .....i '$l(claim) d Log^LVBAT201(type,"Error <No claim> Creating Batch-Line <"_CRBatch_"-"_line_">...") q
 .....i '$d(^TMCBi(2,claim)) d Log^LVBAT201(type,"Error <Claim "_claim_" has not been processed> Creating Batch-Line <"_CRBatch_"-"_line_">...") q
 .....s xx=^TMCBi(2,claim)
 .....s PLIST(6)=$p(xx,"\",1)_"-"_$p(xx,"\",2)
 .....s PLIST=$o(PLIST(""),-1)
 .....f j=1:1:PLIST s PLIST(j)=$g(PLIST(j))
 .....s err=$$insert^LVBCRBL(CRBatch)
 .....i err d Log^LVBAT201(type,"Error <"_err_"> Creating Batch-Line <"_CRBatch_"-"_line_">...") q
 .....i 'err s CRBLine=PLIST(1) d
 ...... ; file payment
 ......k PLIST
 ......s PLIST(2)=1
 ......s PLIST(3)="EDI"
 ......s PLIST(4)=amount
 ......s PLIST=$o(PLIST(""),-1)
 ......f j=1:1:PLIST s PLIST(j)=$g(PLIST(j))
 ......s err=$$insert^LVBCRBLA(CRBLine)
 ......i err d Log^LVBAT201(type,"Error <"_err_"> Creating Batch-Line payment <"_CRBatch_"-"_line_">...") q
 ......i 'err d
 ....... ; file patients amount
 .......f j=1,2 s @("x"_j)=$p(CRBLine,"||",j)
 .......k ^TBAT(x1,x2,1)
 .......s ep="" f  s ep=$o(^TMCBi(2,claim,ep)) q:ep=""  i $e(ep,1,2)'="xx" d
 ........s (am,x1)="" f  s x1=$o(^TMCBi(2,claim,ep,x1)) q:x1=""  d
 .........i $l($p(^(x1),"\",5)) s am=am+$p(^(x1),"\",5)
 .........e  i $l($p(^(x1),"\",4)) s am=am+$p(^(x1),"\",4)
 .........e  i $l($p(^(x1),"\",2)) s am=am+$p(^(x1),"\",2)
 ........k PLIST
 ........s PLIST(2)=ep
 ........s PLIST(3)=am
 ........s PLIST=$o(PLIST(""),-1)
 ........f j=1:1:PLIST s PLIST(j)=$g(PLIST(j))
 ........s err=$$insert^LVBCRBLB(CRBLine)
 ........i err d Log^LVBAT201(type,"Error <"_err_"> Creating Batch-Line patients <"_CRBatch_"-"_line_"-"_ep_">...")
 ....s ^TMP($zn,$j,"REPORT-X",xFile)=CRBatch
 s (xFile,list)="" f  s xFile=$o(^TMP($zn,$j,"REPORT-X",xFile)) q:xFile=""  i $l($g(^(xFile))) s list=list_$s($l(list):",",1:"")_^(xFile)
 i $l(list),$$START^PRTREP00(user,"",list,"UPB")
 k ^TMP($zn,$j)
 d Log^LVBAT201(type," ")
 d Log^LVBAT201(type,"Finish Processing <"_type_">...")
 q 0
read(xx) s (result,ok)="" u xFILE f j=1:1:xx r *x:1 d  i ok q
 .i  s result=result_$c(x)
 .e  s ok=100
 q ok_$c(1)_result
 ; Error log
Error d BACK^%ETN
 q
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
Error2 i $ze'["ENDOFFILE" d BACK^%ETN q
 d Log^LVBAT201(type,"Finish processing file <"_xFILE_">...")
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
]]></Routine>
</Export>
