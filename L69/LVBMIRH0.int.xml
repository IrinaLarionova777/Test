<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBMIRH0" type="INT" languagemode="0" generated="1"><![CDATA[
LVBMIRH0 ; IG 6/5/98 ; Machine interface data access
 ;
 ; supported calls:
 ;
 ; $$testlist^LVBMIRH0(rowid,testitem,TF)
 ; $$QCsearch^LVBMIRH0(machine,item,date)
 ; $$status^LVBMIRH0(rowid)
 ; $$tsetlst^LVBMIRH0(rowid)
 ;
QCsearch(machine,item,date) n (machine,item,date,PLIST) s machine=$g(machine),item=$g(item),date=$g(date) k ^TMP($zn,$j)
 i $l(machine),$l(item),$l(date) s x1="" f  s x1=$o(^TTQC(x1)) q:x1=""  d
 .s x2="" f  s x2=$o(^TTQC(x1,"LOT",x2)) q:x2=""  d
 ..s x3="" f  s x3=$o(^TTQC(x1,"LOT",x2,"TYPE","M",machine,"TEST",item,"DATA",x3)) q:x3=""  d
 ...i $d(^TTQC(x1,"LOT",x2,"TYPE","M",machine,"TEST",item,"DATA",x3,date)) s ^TMP($zn,$j,x1,x2,x3)=""
 s (cnt,x1)="" f  s x1=$o(^TMP($zn,$j,x1)) q:x1=""  d
 .s x2="" f  s x2=$o(^TMP($zn,$j,x1,x2)) q:x2=""  d
 ..s x3="" f  s x3=$o(^TMP($zn,$j,x1,x2,x3)) q:x3=""  s cnt=cnt+1,PLIST(cnt)=x1_"||"_x2_"||M||"_machine_"||"_item_"||"_x3
 s PLIST=$o(PLIST(""),-1)
 k ^TMP($zn,$j)
 q 0
 ; test items result list (only active)
 ; testitem : list of testitems to get results for
titemlst(rowid,testitem,EPIS) n (rowid,testitem,EPIS) s rowid=$g(rowid),testitem=$g(testitem),EPIS=$g(EPIS),result=""
 i $l(testitem) s testitem=","_testitem_","
 f  q:'$f(testitem,",,")  s testitem=$p(testitem,",,",1)_","_$p(testitem,",,",2,999)
 s machine=$p(rowid,"||",1),sample=$p(rowid,"||",2)
 i $l(machine),$l(EPIS) d
 .s ts="" f  s ts=$o(^TTRQi(EPIS,ts)) q:ts=""  d
 ..s tscnt="" f  s tscnt=$o(^TTRQi(EPIS,ts,tscnt)) q:tscnt=""  d
 ...s ti="" f  s ti=$o(^TTRQi(EPIS,ts,tscnt,ti)) q:ti=""  d
 ....i $d(^TMIF(machine,2,ti)) d
 .....i $l(sample),'$d(^TMIF(machine,1,sample,"DATA",ti)) s $p(res(ti),"\",2)=ti,$p(res(ti),"\",3)="TF"
 .....i '$l(sample) s $p(res(ti),"\",2)=ti,$p(res(ti),"\",3)="TF"
 i $l(machine),$l(sample) d
 .s ti="" f  s ti=$o(^TMIF(machine,1,sample,"DATA",ti)) q:ti=""  d
 ..i $l(testitem),testitem'[(","_ti_",") q
 .. ; find active result
 ..s tiActive=$o(^TMIF(machine,1,sample,"DATA",ti,""),-1)
 ..s ticnt="" f  s ticnt=$o(^TMIF(machine,1,sample,"DATA",ti,ticnt),-1) q:ticnt=""  i $p(^(ticnt),"\",5)="Y" s tiActive=ticnt q
 ..i $l(tiActive) d
 ...s mirow=machine_"||"_sample_"||"_ti_"||"_tiActive
 ...i '$$select^LVBMIRI(mirow) f j=1:1:PLIST s $p(res(ti),"\",j)=PLIST(j)
 s ti="" f j=1:1 s ti=$o(res(ti)) q:ti=""  s $p(result,"~",j)=res(ti)
 q result
 ; test set list
tsetlst(rowid) n (rowid) s rowid=$g(rowid),result=""
 s machine=$p(rowid,"||",1),sample=$p(rowid,"||",2),epis=$p($g(^TMIF(machine,1,sample)),"\")
 i $l(epis) d
 .s tc="" f  s tc=$o(^TMIF(machine,1,sample,"DATA",tc)) q:tc=""  d
 ..k res
 ..s ts="" f  s ts=$o(^TTAB("TC",tc,1,ts)) q:ts=""  i $d(^TMIF(machine,2,tc,0,ts)) d
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d  q:$d(res(ts))
 ....i '$d(^TEPI(epis,1,ts,tscnt,"DATA",tc)) s res(ts)=tscnt
 .s ts="" f j=1:1 s ts=$o(res(ts)) q:ts=""  s $p(result,",",j)=ts
 q result
 ; episode status
status(rowid) n (rowid) s rowid=$g(rowid),result="NE"
 s machine=$p(rowid,"||",1),sample=$p(rowid,"||",2),epis=$p($g(^TMIF(machine,1,sample)),"\")
 i $l(epis) s result=$s('$d(^TEPI(epis)):"NE",$p(^TEPI(epis),"\",44)=1:"PR",1:"N")
 q result
]]></Routine>
</Export>
