<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTSLR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTSLR01 ; ML 20/10/2000 ; STOCK BY LOCATION
 q
 ; Parameters :  1. Site
 ;               2. Location
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Stock by Location"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s(PAR1="":"All Sites",1:$p($g(^TTAB("USLOC",PAR1)),"\"))
 s line=4,xHEADER(line)=""
 s line=5,xHEADER(line)="Location : "_$s(PAR2="":"All Locations",1:LocName_" ("_PAR2_")")
 s line=6,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 i PAR2'="",'$$select^LVBBBL(PAR2) s LocName=PLIST(3)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s $p(extra,$c(1),6)="IN-1" i $$fetchall^LVBBBP("","","","","",PAR1,PAR2,"","",extra)
 q:'$d(PLIST(1))  m PackDet=PLIST
 s i="" f  s i=$o(PackDet(i)) q:i=""  d
 .s PackID=$p(PackDet(i),$c(28),2)_" "_$p(PackDet(i),$c(28),3),DateColl=$p(PackDet(i),$c(28),4),product=$p($p(PackDet(i),$c(28),8),$c(1))
 .s BloodGrp=$p($p(PackDet(i),$c(28),9),$c(1)),location=$p($p(PackDet(i),$c(28),11),$c(1),2),site=$p($p(PackDet(i),$c(28),11),$c(1),3)
 .s DateExp=$p(PackDet(i),$c(28),13)
 .i site="" s site=" "
 .i location="" s location=" "
 .s ^TMP($zn,$j,site,location,PackID)=product_"\"_BloodGrp_"\"_DateColl_"\"_DateExp
 q
PRINT ; print report
 s site="" f  s site=$o(^TMP($zn,$j,site)) q:site=""  d
 .s xxx(1)=site,location="" f  s location=$o(^TMP($zn,$j,site,location)) q:location=""  d
 ..s xxx(2)=location,PackID="" f  s PackID=$o(^TMP($zn,$j,site,location,PackID)) q:PackID=""  d
 ...s xxx(3)=PackID,PackDet=^(PackID)
 ...s xxx(4)=$p(PackDet,"\",1),xxx(5)=$p(PackDet,"\",2),xxx(6)=$$extdate2^SSUTIL4($p(PackDet,"\",3)),xxx(7)=$$extdate2^SSUTIL4($p(PackDet,"\",4))
 ...d PRINT^PRTREP00(,.N)
 ...k xxx f i=1:1:12 s xxx(i)=""
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="SLR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
