<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF402" type="INT" languagemode="0" generated="1"><![CDATA[
MIF402(mi) ; ML 29/6/98 ; ASTM protocol  -  MLA 1***c (Bi-Directional)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s unk=$c(0),stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),lf=$c(10),cr=$c(13),nak=$c(21)
 s (sample,epis,surname,result,date,time,QC)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  d ORDER q
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK
 .f  r *R:10 q:$c(R)=eot  d
 ..q:$c(R)=lf
 ..i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") s stop=1 q
 ..s record=$c(R)_$$read^MIF000("",cr) q:'$l(record)
 .. ; strip unknown control character
 ..s record=$tr(record,unk,"")
 ..d trace^MIF000(mi,record,"H<--M")
 ..d ACK
 .. ; query record
 ..i $e(record)="Q" d  q
 ...s epis=$p($p(record,"|",3),"^",2)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="|"_$p($p(record,"|",3),"^",3) q
 ...s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix_"|"_$p($p(record,"|",3),"^",3) q
 .. ; header record
 ..i $e(record,2)="H" d  q
 ...d Last s (sample,epis,surname,result,date,time,QC)=""
 .. ; patient record
 ..i $e(record)="P" d  q
 ...s surname=$tr($tr($p(record,"|",6)," "),"^"," ")
 .. ; order record
 ..i $e(record)="O" d  q
 ...s sample=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," "),epis=sample
 ...i QC="Q" s QC="Y"
 .. ; result record
 ..i $e(record)="R" d  q
 ...s x1=$tr($p($p(record,"|",3),"^",4)," ")_"~"_$tr($p($p(record,"|",3),"^",5)," ")
 ...s x2=$tr($p(record,"|",4)," "),x3=$p($p(record,"|",9),"^",2)
 ...i '$l(x2)," 2 3 5 "[(" "_x3_" ") s x2="*"_x3_"*"
 ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 ... ; date and time
 ...i $l($tr($p(record,"|",13)," ")) d
 ....s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
 ....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
 .. ; last record
 ..i $e(record)="L" d Last q
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 . ; d ORDER
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)=""
 q
ORDER ; create list of orders if exists
 s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  d
 .d scanone^MIF000(mi,epis)
 .d PATDET(epis)
 .s (tc,tcx)=""
 .f N=1:1 s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d
 ..q:$p(tc,"~",1)=tcx  s tcx=$p(tc,"~",1) q:tcx="014"
 ..d SET(epis,tcx)
 .k ^TMP($zn,$j,"ENQ",epis)
 .d Send k ^TMP("MIF-SINGLE",$j,mi,epis)
 q
SAVE(mi) ; create loadlist records
 s epis=^TMIF("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s sample=^TMIF("MIF000",$j,"LL","SAMPLE")
 d PATDET(epis)
 s (tc,tcx)="" f  s tc=$o(^TMIF("MIF000",$j,"LL","TEST",tc)) q:tc=""  d
 .q:$p(tc,"~",1)=""  s tcx=$p(tc,"~",1)
 .d SET(epis,tcx)
 q 0
PATDET(epis) ; set patient details record
 i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"|||"_sex_"|||||"_$g(PLIST(15))
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||^||||||||"
 q
SET(epis,tcx) ; set record to be sent to instrument
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_N_"|"_epis_"|"_^TMP($zn,$j,"ENQ",epis)_"|^^^"_tcx_"|||||||A||||||||||||||Q"
 q
Send ; send list of orders if exists
 q:'$d(^TMIF(mi,10))
 f j=1:1:10 d  q:$c(R)=ack
 .d trace^MIF000(mi,"ENQ","H-->M") w enq,*-3
 .r *R:2 d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=eot:"EOT",$c(R)=nak:"NAK",1:R),"H<--M")
 .i $c(R)=ack!($c(R)=nak) q
 .i $c(R)'=nak w nak,*-3 d trace^MIF000(mi,"NAK","H-->M") q
 i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 q:$$SEND("1H|\^&|||LABTRAK|||||||P|1")
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="",'$$SEND(FRAME#8_"L|1|F") k ^TMIF(mi,10)
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 f j=1:1:10 d  q:$c(R)=ack
 .w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 .r *R:15 d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=eot:"EOT",$c(R)=nak:"NAK",1:R),"H<--M")
 .i $c(R)=ack!($c(R)=nak) q
 .i $c(R)'=nak w nak,*-3 d trace^MIF000(mi,"NAK","H-->M")
 i $c(R)=ack q 0
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q 1
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
]]></Routine>
</Export>
