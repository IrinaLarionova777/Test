<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBTS01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBTS01 ; ML 8/12/98 ; BILLED TEST SET STATISTICS
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Test Set List
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Billed Test Set Statistics"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Test Set/s : "_$s(PAR3="":"All",1:PAR3)
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..q:$$select^LVBEPVIS(epis,"Y")
 ..k VISIT m VISIT=PLIST k PLIST
 ..i $l(VISIT(21)) d
 ...i $$open^LVBVISTS(epis,,"Y")
 ...f  q:$$fetch^LVBVISTS(epis,,"Y")  d
 ....i $l(PAR3),(","_PAR3_",")'[(","_PLIST(3)_",") q
 ....i PLIST(33)'="A" q
 ....s dateeff=$o(^TTAB("TS",PLIST(3),3,PLIST(12)),-1) i $l(dateeff) d
 .....s (item,itsyn)="" f  s item=$o(^TTAB("TS",PLIST(3),3,dateeff,item)) q:item=""  s itsyn=itsyn_$s($l(itsyn):",",1:"")_$p(^(item),"\",2)
 .....s (item,items)="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  i (","_$tr(itsyn," ")_",")[(","_$tr($p($p(^(item),"\",2),"x")," ")_",") d
 ......s items=items_$s($l(items):",",1:"")_item
 .....s ^TMP($zn,$j,"REPORT",PLIST(3),PLIST(7),VISIT(15),epis)=items
 ...i $$close^LVBVISTS()
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s (TotalFee,ts)="" f  s ts=$o(^TMP($zn,$j,"REPORT",ts)) q:ts=""  d
 ..q:$$select^LVBCTTS(ts,"Y")  k TestSet m TestSet=PLIST k PLIST
 ..s TestFee="",xxx(1)=TestSet(3)
 ..s date="" f  s date=$o(^TMP($zn,$j,"REPORT",ts,date)) q:date=""  d
 ...s xxx(2)=$$extdate2^SSUTIL4(date)
 ...s doc="" f  s doc=$o(^TMP($zn,$j,"REPORT",ts,date,doc)) q:doc=""  d
 ....q:$$select^LVBCTDR(doc,"Y")  k DOC m DOC=PLIST k PLIST
 ....s xxx(3)=$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51))
 ....s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",ts,date,doc,epis)) q:epis=""  d
 .....q:$$select^LVBEPVIS(epis)
 .....s items=$g(^TMP($zn,$j,"REPORT",ts,date,doc,epis))
 .....f j=1:1:$l(items,",") s xxx(4)=$p(items,",",j) q:xxx(4)=""  d
 ......s xxx(5)=$p($g(^TEPI(epis,7,xxx(4))),"\")
 ......s TestFee=TestFee+xxx(5),TotalFee=TotalFee+xxx(5)
 ......d PRINT^PRTREP00(.PLIST,.N) k xxx,PLIST
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","|")
 ..d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ..s text="Total Fees for "_TestSet(3)_" :",tab=(MAX-10)-$l(text),tab=tab_$j(" ",3-$l(tab))
 ..d ADDLINE^PRTREP00(REPID,"PRT","?"_tab_text)
 ..s text=$j(TestFee,10,2),tab=(MAX-11),tab=tab_$j(" ",3-$l(tab))
 ..d ADDLINE^PRTREP00(REPID,"PRT","?"_tab_text)
 ..d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ..d ADDLINE^PRTREP00(REPID,"PRT","|")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 .s text="Total Fees :",tab=(MAX-10)-$l(text),tab=tab_$j(" ",3-$l(tab))
 .d ADDLINE^PRTREP00(REPID,"PRT","?"_tab_text)
 .s text=$j(TotalFee,10,2),tab=(MAX-11),tab=tab_$j(" ",3-$l(tab))
 .d ADDLINE^PRTREP00(REPID,"PRT","?"_tab_text)
 .d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="BTS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
