<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LHOSPINT02" type="INT" languagemode="0" generated="1"><![CDATA[
LHOSPINT02 ; IG 12/07/98 ; Hospital Interface - SJOG
 Q
RESULT(HOSP) N (HOSP) k ^TMP($zn,$j)
 i $$select^LVBCTRL(HOSP) q
 s dir=PLIST(7),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.*"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""results.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""results.txt""")
 s $zt="Error1^"_$zn
 s FILE="results.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  u xFILE r MESS s seq=$o(^TMP($zn,$j,xxFILE,"IN",""),-1)+1,^TMP($zn,$j,xxFILE,"IN",seq)=MESS
 s $zt=""
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s (vts,seq)="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=^(seq) d
 ..i $p(xx,"|")="OBR" d
 ...s vts=$tr($p($p(xx,"|",3),"^")," ","|") i '$l(vts) s vts=$tr($p($p(xx,"|",4),"^")," ","|")
 ... ; only file results in Word/RTF test set
 ...s test=$p(vts,"||",2) i '$$select^LVBCTTS(test,"Y"),'((PLIST(31)="Y")!(PLIST(54)="Y")) s vts=""
 ..i $l(vts),$p(xx,"|")="OBX",$p(xx,"|",3)="FT",$p($p(xx,"|",4),"^")="TXT" s result=$p($p(xx,"|",6),"\.br\",2,999) d
 ...f j1=1:1:$l(result,"\.br\") d
 ....s xx=$p(result,"\.br\",j1)
 ....f j2="S" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(16)_$p(xx,x2,2,$l(xx,x2))
 ....f j2="F" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(17)_$p(xx,x2,2,$l(xx,x2))
 ....f j2="T" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"&"_$p(xx,x2,2,$l(xx,x2))
 ....f j2="R" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"~"_$p(xx,x2,2,$l(xx,x2))
 ....f j2="H","N","E" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$p(xx,x2,2,$l(xx,x2))
 ....s result(j1)=xx
 ...m ^TMP($zn,$j,xFile,"OUT",vts,"T")=result
 ...k result s vts=""
 .s vts="" f  s vts=$o(^TMP($zn,$j,xFile,"OUT",vts)) q:vts=""  d
 ..s text=""
 ..s cnt="" f  s cnt=$o(^TMP($zn,$j,xFile,"OUT",vts,"T",cnt)) q:cnt=""  s text=text_$s($l(text):"|",1:"")_^(cnt)
 ..k PLIST s PLIST(1)="T\"_text
 ..s PLIST=$o(PLIST(""),-1)
 ..i $$save^LVBVIS4(vts,"A","SJOG","N")
 k ^TMP($zn,$j)
 q
 ; Error log
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
 ; Error log
Error2 i $ze'["ENDOFFILE" d BACK^%ETN q
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
 ; --------------------------------------------------------------------------------------------------------
 ; check hospital files for updates
START(HOSP) n (HOSP) k ^TMP($zn,$j)
 i HOSP="GH" d START^LHOSPINT02G(HOSP) q
 i $$select^LVBHOS(HOSP) q
 s file=PLIST(5) i file="" q
 s $zt="ERROR1^"_$zn
 l +^THOS(HOSP):10 e  q
 ; Hollywood data extract
 i HOSP="H" d
 .o file::0 e  l -^THOS(HOSP) q
 . ; delete previous data
 .k ^THOSi(HOSP) f j="PATIENT","PATIENT-INDEX","Date" k ^THOS(HOSP,j)
 .f  u file r MESSAGE q:'$l(MESSAGE)  d
 ..k x f j=1:1:21 s x(j)=$$space($p(MESSAGE,"|",j))
 ..s x(6)=HOSP_x(6)
 .. ; check sex
 ..i $l(x(11)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCS(HOSP_"||"_x(11)) s X=PLIST(3)
 ...s x(11)=X
 .. ; check location
 ..i $l(x(9)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCL(HOSP_"||"_x(9)) s X=PLIST(3)
 ...s x(9)=X
 .. ; find doctor
 ..i $l(x(3)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCD(HOSP_"||"_x(3)) s X=PLIST(3)
 ...s x(3)=X
 .. ; check date fields
 ..f j=2,14 i $l(x(j)) s x(j)=$$intdate^SSUTIL4($e(x(j),5,6)_"/"_$e(x(j),7,8)_"/"_$e(x(j),1,4))
 .. ; check if admission already exists
 ..s exist="N" i '$$select^LVBHOSP(HOSP_"||"_x(6)_"||"_x(1)) s exist="Y"
 .. ; file data
 ..k PLIST
 ..s PLIST(2)=x(6)
 ..s PLIST(3)=x(1)
 ..s PLIST(4)=x(10)
 ..s PLIST(5)=x(9)
 ..s PLIST(7)=x(16)
 ..s PLIST(8)=x(13)
 ..s PLIST(9)=x(14)
 ..s PLIST(10)=x(15)
 ..s PLIST(11)=x(17)
 ..s PLIST(12)=x(18)
 ..s PLIST(15)=x(11)
 ..s PLIST(20)=x(3)
 ..s PLIST(21)=x(2)
 ..s PLIST(24)=x(20)
 ..s PLIST(36)=x(19)
 ..s PLIST(43)=x(21)
 ..s PLIST(44)=x(7)
 ..s PLIST(45)=x(8)
 ..s PLIST=$o(PLIST(""),-1)
 .. ; update patient information
 ..i exist="Y" d  q
 ...i $$update^LVBHOSP(HOSP_"||"_x(6)_"||"_x(1))
 .. ; insert new patient information
 ..i exist="N" d  q
 ...i $$insert^LVBHOSP(HOSP)
 .c file
 ; URNO, AdmitNo, LastName, FirstName, Title, DOB, Sex, Add1, Add2, Add3_suburb, Add4_state, Add5_postcode, Ward, Doctor, AdmitDate, MedicareNo, HealthFund, HealthFundNo, ClaimType 
 ; East/West data extract
 i " L N BE SJ NR PL W D B G M S A "[(" "_HOSP_" ") d
 .o file::0 e  l -^THOS(HOSP) q
 . ; delete previous data
 .k ^THOSi(HOSP) f j="PATIENT","PATIENT-INDEX","Date" k ^THOS(HOSP,j)
 .f  u file r MESSAGE q:'$l(MESSAGE)  d
 ..k x f j=1:1:19 s x(j)=$$space($p(MESSAGE,",",j))
 ..s x(1)=HOSP_x(1)
 .. ; check title
 ..i $l(x(5)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCT(HOSP_"||"_x(5)) s X=PLIST(3)
 ...s x(5)=X
 .. ; check sex
 ..i $l(x(7)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCS(HOSP_"||"_x(7)) s X=PLIST(3)
 ...s x(7)=X
 .. ; check location
 ..i $l(x(13)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCL(HOSP_"||"_x(13)) s X=PLIST(3)
 ...s x(13)=X
 .. ; find doctor
 ..i $l(x(14)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCD(HOSP_"||"_x(14)) s X=PLIST(3)
 ...s x(14)=X
 .. ; check date fields
 ..f j=6,15 i $l(x(j)) s x(j)=$$intdate^SSUTIL4($e(x(j),5,6)_"/"_$e(x(j),7,8)_"/"_$e(x(j),1,4))
 .. ; check if admission already exists
 ..s exist="N" i '$$select^LVBHOSP(HOSP_"||"_x(1)_"||"_x(2)) s exist="Y"
 .. ; file data
 ..k PLIST
 ..s PLIST(2)=x(1)
 ..s PLIST(3)=x(2)
 ..s PLIST(4)=x(4)
 ..s PLIST(5)=x(3)
 ..s PLIST(6)=x(5)
 ..s PLIST(7)=x(6)
 ..s PLIST(8)=x(7)
 ..s PLIST(9)=x(8)
 ..s PLIST(10)=x(9)
 ..s PLIST(11)=x(10)
 ..s PLIST(12)=x(12)
 ..s PLIST(15)=x(13)
 ..s PLIST(17)=x(14)
 ..s PLIST(21)=x(15)
 ..s PLIST(31)=x(11)
 ..s x(16)=$tr(x(16)," "),PLIST(24)=$e(x(16),1,10),PLIST(43)=$e(x(16),11,12)
 ..s PLIST(44)=x(17)
 ..s PLIST(45)=x(18)
 ..s PLIST=$o(PLIST(""),-1)
 .. ; update patient information
 ..i exist="Y" d  q
 ...i $$update^LVBHOSP(HOSP_"||"_x(1)_"||"_x(2))
 .. ; insert new patient information
 ..i exist="N" d  q
 ...i $$insert^LVBHOSP(HOSP)
 .c file
 ; Refferal Lab data extract
 i $e(HOSP,1,2)="RL" d
 .o file::0 e  l -^THOS(HOSP) q
 .f  u file r MESSAGE q:'$l(MESSAGE)  d
 ..k x f j=1:1:200 s x(j)=$p(MESSAGE,$c(28),j)
 ..s xAll="" f j=1:1:199 i $l(x(j)) s $p(xAll,"|",j)=$tr(x(j),"^\|",$c(16,17,18))
 ..s x(20)=HOSP_x(20)
 .. ; check title
 ..i $l(x(7)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCT(HOSP_"||"_x(7)) s X=PLIST(3)
 ...s x(7)=X
 .. ; check sex
 ..i $l(x(5)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCS(HOSP_"||"_x(5)) s X=PLIST(3)
 ...s x(5)=X
 .. ; check location
 ..i $l(x(22)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCL(HOSP_"||"_x(22)) s X=PLIST(3)
 ...s x(22)=X
 .. ; find doctor
 ..i $l(x(15)) d
 ...n (HOSP,x)
 ...s X="" i '$$select^LVBHOSCD(HOSP_"||"_x(15)) s X=PLIST(3)
 ...s x(15)=X
 .. ; check tests
 ..f j=1:1:$l(x(200),",") s x=$p(x(200),",",j) i $l(x) d
 ...n (HOSP,x,j)
 ...s X="" i '$$select^LVBHOSTS(HOSP_"||"_$p(x,"||",2)) s X=PLIST(3)
 ...s x(200,j)=X,x(199,j)=x
 .. ; check if admission already exists
 ..s exist="N" i '$$select^LVBHOSP(HOSP_"||"_x(20)_"||"_x(2)) s exist="Y"
 .. ; file data
 ..k PLIST
 ..s PLIST(2)=x(20)
 ..s PLIST(3)=x(2)
 ..s PLIST(4)=x(4)
 ..s PLIST(5)=x(3)
 ..s PLIST(6)=x(7)
 ..s PLIST(7)=x(6)
 ..s PLIST(8)=x(5)
 ..s PLIST(9)=x(32)
 ..s PLIST(10)=x(33)
 ..s PLIST(11)=x(34)
 ..s PLIST(31)=x(35)
 ..s PLIST(12)=x(36)
 ..s PLIST(13)=x(43)
 ..s PLIST(15)=x(22)
 ..s PLIST(17)=x(15)
 ..s PLIST(21)=x(47)
 ..s PLIST(23)=xAll
 ..s PLIST(24)=x(8)
 ..s PLIST(28)=x(66)
 ..s PLIST(32)=x(44)
 ..s PLIST(33)=x(60)
 ..s PLIST(34)=x(51)
 ..s PLIST(35)=x(73)
 ..s PLIST(36)=x(89)
 ..s PLIST(37)=x(88)
 ..s PLIST(38)=x(9)
 ..s PLIST(39)=x(99)
 ..s PLIST(40)=x(100)
 ..s PLIST(41)=x(101)
 ..s PLIST=$o(PLIST(""),-1)
 .. ; update patient information
 ..i exist="Y" d  q
 ...i '$$update^LVBHOSP(HOSP_"||"_x(20)_"||"_x(2)) s rowid=%ROWID d
 ....f jj1=1:1 q:'$d(x(200,jj1))  i $l(x(200,jj1)) d
 .....k PLIST
 .....s PLIST(2)=x(200,jj1)
 .....s PLIST(4)=x(199,jj1)
 .....s PLIST=$o(PLIST(""),-1)
 .....i $$insert^LVBHOSPT(rowid)
 .. ; insert new patient information
 ..i exist="N" d  q
 ...i '$$insert^LVBHOSP(HOSP) s rowid=%ROWID d
 ....f jj1=1:1 q:'$d(x(200,jj1))  i $l(x(200,jj1)) d
 .....k PLIST
 .....s PLIST(2)=x(200,jj1)
 .....s PLIST(4)=x(199,jj1)
 .....s PLIST=$o(PLIST(""),-1)
 .....i $$insert^LVBHOSPT(rowid)
 .c file
end1 ; update hospital level information
 l -^THOS(HOSP)
 i '$$select^LVBHOS(HOSP) d
 .s PLIST(3)=+$h,PLIST(4)=$P($h,",",2)
 .i $$update^LVBHOS(HOSP)
 q
 ; delete end spaces
space(x) 
 f  q:'$l(x)  q:$e(x,$l(x))'=" "  s x=$e(x,1,$l(x)-1)
 f  q:'$l(x)  q:$e(x)'=" "  s x=$e(x,2,$l(x))
 q x
ERROR1 i $ze'["ENDOFFILE" D BACK^%ETN
 c file l -THOS(HOSP) s $zt=""
 ; Move the file
 ; MS-DOS based system
 i '$zf(-1,"copy "_file_" "_file_"old"),$zf(-1,"del "_file)
 ; UNIX based system
 ;i $zf(-1,"mv "_file_" "_file_"old")
 g end1
ReadMessage k x u file r x
 i $e(x,1,5)="<SOS>" d
 .f  u file r x q:$e(x,1,5)="<EOS>"  d
 ..f j1="EPN","URN","SEX","DOB","NAME","RAD","MEDN","PENN","REPN","FNDN","ADMD","FUND","DRTP","WING","DRTN" d
 ...s j2="<"_j1_">"
 ...i $e(x,1,$l(j2))=j2 s x(j2)=$$space($p($p(x,"<EOF>",1),j2,2))
 ...i j1="WING",$d(x(j2)) s x(j2)=$tr(x(j2)," ")
 q
]]></Routine>
</Export>
