<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV20A1" type="INT" languagemode="0"><![CDATA[
PRTINV20 ; ML 11/01/02 ; PRIVATE INVOICES - ABBOTT PATHOLOGY  ; Compiled January 18, 2002 15:30:20 for Cache for Windows
 q
 ; Parameters :  1.Payment Code
 ;               2.Batch Number
 ;               3.Episode Number
 ;               4.Sequence Number
 ;               5.Reminder
 ;
 ;       14/05/2002 - added printing of Medicare Number, Paycode, and Patient Location
 ;                  - to avoid these having to be written on the invoice by hand
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i 'PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)=PAR1_"-"_PAR2 i $$update^LVBPRPR(REPID)
 i PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)="REM-"_PAR5 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
BODY ; create temporary printing file
 ; print single patient
 i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
 .m VISIT=PLIST k PLIST s epis=PAR3
 .s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)
 .d ITEM
 ; print batch
 i $l(PAR1),$l(PAR2) d
 .s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
 ..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
 ..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
 ..i $l(PAR4),PAR4'=seq q
 ..i '$$select^LVBEPVIS(epis,"Y") d  q
 ...m VISIT=PLIST k PLIST
 ...s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq
 ...d ITEM
 ; check credits
 s seq="" f  s seq=$o(^TMP($zn,$j,"EPIS",seq)) q:seq=""  d
 .s (credit,epis)="" f cnt=1:1 s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
 .. d %0Ao
 ..s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ...s rowid=$p(deb,$c(1))_"||"_$p(date,$c(1))_"||"_ep
 ...i $$open^LVBDEBVT(rowid)
 ...f  q:$$fetch^LVBDEBVT(rowid)  d
 ....f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ....k TRAN m TRAN=PLIST
 ....i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) d
 .....s credit=credit-PLIST(14)
 ....i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) d
 .....s credit=credit-PLIST(16)
 ....i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) d
 .....s credit=credit-PLIST(9)
 ...i $$close^LVBDEBVT()
 ..i credit s ^TMP($zn,$j,"EPIS",epis,"CREDIT")=credit,credit=""
 q
PRINT ; print report
 d QDR,PATH
 s max=16
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3))
 .s (inpat,icDescr)="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y") d  k PLIST
 ..s icDescr=PLIST(5) i PLIST(4)="Y" s inpat=1
 .s (line,nextpg,item)=""
 .d ADDRESS,MEDI,PHEAD
 .f  s item=$o(^TMP($zn,$j,"EPIS",epis,"ITEM",item)) q:item=""  d
 ..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2),Rule3=$p(det,"\",3),tax=$p(det,"\",4)
 ..i item=VISIT(54),'$l(desc) s desc=icDescr
 .. ; check item desc length
 ..s x="" s xdesc=desc k desc s desc=xdesc
 ..i $l(desc)<23 s desc(1)=desc
 ..i $l(desc)>22 d
 ...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>22 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>22
 ..d ADD^PRTREP00(REPID,"PRT","?8  "_$$extdate2^SSUTIL4(VISIT(14)))
 ..d ADD^PRTREP00(REPID,"PRT","?23 "_$$extdate2^SSUTIL4(VISIT(12)))
 ..d ADD^PRTREP00(REPID,"PRT","?38 "_desc(1)),ADD^PRTREP00(REPID,"PRT","?61 "_item_" "_$s(inpat=1:"*",1:"")_$s(tax="Y":"#",1:"")),ADD^PRTREP00(REPID,"PRT","?70 "_$j(amt,7,2)),LN s line=line+1
 ..f i=2:1 q:'$d(desc(i))  d ADD^PRTREP00(REPID,"PRT","?38 "_desc(i)),LN s line=line+1
 ..i Rule3="Y" d ADD^PRTREP00(REPID,"PRT","?38 Rule 3 Exemption"),LN s line=line+1
 ..i line>15,$o(^TMP($zn,$j,"EPIS",epis,"ITEM",item))'="" d
 ...d LN,ADD^PRTREP00(REPID,"PRT","?61 Continued over..")
 ...s nextpg=1 d PHEAD s (line,nextpg)=""
 . ; print scp/lcc number
 .s lcc="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1)),"Y") s lcc=$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))
 .i $l(lcc) d
 ..d LN,LN s line=line+2
 ..d ADD^PRTREP00(REPID,"PRT","?8  SCP No: "_lcc) ;
 .i $l(VISIT(22)) d
 ..d ADD^PRTREP00(REPID,"PRT","  Hospital: "_HOSPITAL) ; HOSPITAL added MS 14/05/2002
 ..d ADD^PRTREP00(REPID,"PRT"," Prov No: "_HOSPROV)
 .f i=1:1:15-line d LN
 .s tot=$g(^TMP($zn,$j,"EPIS",epis,"TOT")),credit=$g(^TMP($zn,$j,"EPIS",epis,"CREDIT")),GST=$g(^TMP($zn,$j,"EPIS",epis,"GST"))
 .i PAR5=1 d
 ..d ADD^PRTREP00(REPID,"PRT","?8  Our records show that this account is outstanding."),LN
 ..d ADD^PRTREP00(REPID,"PRT","?8  If payment has been forwarded, please disregard this reminder."),LN
 ..d ADD^PRTREP00(REPID,"PRT","?8  Otherwise prompt payment would be appreciated."),LN
 .i PAR5=2 d
 ..d LN
 ..d ADD^PRTREP00(REPID,"PRT","?8  This account is now overdue. Payment is required within 14 days, as"),LN
 ..d ADD^PRTREP00(REPID,"PRT","?8  futher action may commence. Please call us if you have any queries."),LN
 . ; print legend
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4680V")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD"),LN
 .d ADD^PRTREP00(REPID,"PRT","?8  LEGEND: ")
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADD^PRTREP00(REPID,"PRT","* - inpatient, # - taxable items"),LN
 .i credit d
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4800V")
 ..d ADD^PRTREP00(REPID,"PRT","~BOLD"),LN
 ..d ADD^PRTREP00(REPID,"PRT","?51 Payments Received $"_$j(credit,7,2)_" CR")
 ..d ADD^PRTREP00(REPID,"PRT","~NORMAL"),LN
 . ; print captions
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4920V")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD"),LN
 .i '$l(PAR5) d ADD^PRTREP00(REPID,"PRT","?8  ** ORIGINAL **")
 .i $l(PAR5) d
 ..i 'PAR5 d ADD^PRTREP00(REPID,"PRT","?8  ** DUPLICATE **") q
 ..d ADD^PRTREP00(REPID,"PRT","?8  ** REMINDER **")
 .d ADD^PRTREP00(REPID,"PRT","?48 Total Due (inc. GST) $")
 . ; print amounts
 .;d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4920V"),LN
 .d ADD^PRTREP00(REPID,"PRT",$j(tot-credit,7,2)) ; "?70 "_
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 .d LN,ADD^PRTREP00(REPID,"PRT","?8  ---------------------------------------------------------------------------------")
 .f i=1:1:4 d LN
 .d ADD^PRTREP00(REPID,"PRT","?8  Make cheques payable to Abbott Pathology."),LN
 .d ADD^PRTREP00(REPID,"PRT","?8  Accounts payable by "_$$extdate2^SSUTIL4($h+30))
 .f i=1:1:3 d LN
 .d ADD^PRTREP00(REPID,"PRT","?17 "_$s(ADDRESS(1)="":name,1:ADDRESS(1))),LN
 .f i=2:1:4 d ADD^PRTREP00(REPID,"PRT","?17 "_ADDRESS(i)),LN
 .f i=1:1:2 d LN
 .d ADD^PRTREP00(REPID,"PRT","?20  "_epis)
 .d ADD^PRTREP00(REPID,"PRT","?43 "_$$extdate2^SSUTIL4(+$h))
 .d ADD^PRTREP00(REPID,"PRT","?73 $"_$j(tot-credit,7,2))
 .f i=1:1:2 d LN
 .s episdet=$g(^TMP($zn,$j,"EPIS",epis)),batch=$p(episdet,"\",2),payc=$p(episdet,"\",3)
 .d ADD^PRTREP00(REPID,"PRT","?43 "_$p(batch,"||")_" "_$p(batch,"||",2)_" "_$p(batch,"||",3)),LN
 q
PHEAD ; print patient details
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s2b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s('PAR5:1500,1:1400)_"h1000V")
 i 'PAR5 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&f0STax Invoice for Pathology Services Rendered")
 i PAR5 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&f0SReminder for Professional Pathology Services")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&f1S")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h0V")
 f i=1:1:10 d LN
 d ADD^PRTREP00(REPID,"PRT","?17 "_name_" ") 
 if $l(MEDICARE) do
 .d ADD^PRTREP00(REPID,"PRT","?53 "_"Medicare "_MEDICARE_" ") ;MEDICARE added MS 14/05/2002
 d LN
 d ADD^PRTREP00(REPID,"PRT","?8  Acc to:")
 d ADD^PRTREP00(REPID,"PRT","?17 "_ADDRESS(1))
 d ADD^PRTREP00(REPID,"PRT","?53 "_PAYCODE) ;PAYCODE added MS 14/05/2002
 d ADD^PRTREP00(REPID,"PRT","?73 "_epis),LN
 f i=2:1:4 d ADD^PRTREP00(REPID,"PRT","?17 "_ADDRESS(i)) i i'=4 d LN
 s episdet=$g(^TMP($zn,$j,"EPIS",epis)),batch=$p(episdet,"\",2),payc=$p(episdet,"\",3)
 d ADD^PRTREP00(REPID,"PRT","?53 "_$p(batch,"||")_" "_$p(batch,"||",2)_" "_$p(batch,"||",3)),ADD^PRTREP00(REPID,"PRT","?73 "_$$extdate2^SSUTIL4(VISIT(56)))
 f i=1:1:3 d LN
 d DOC
 i drdet'="" d ADD^PRTREP00(REPID,"PRT","?8  "_drdet_$s($g(DOC(38))="Y":" (sd)",1:""))
 d ADD^PRTREP00(REPID,"PRT","?53 "_pathdr_" Prov No: "_pathprv)
 f i=1:1:4 d LN
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det,GST)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 .i $p(det,"\",4)="GST" s GST=$p(det,"\")
 .s ^TMP($zn,$j,"EPIS",epis,"ITEM",item)=det
 s ^TMP($zn,$j,"EPIS",epis,"TOT")=amount
 s ^TMP($zn,$j,"EPIS",epis,"GST")=GST
 q
DOC ; extract referring doctor details
 s drdet=""
 q:$$select^LVBCTDR(VISIT(15))  m DOC=PLIST k PLIST
 s drdet=$$NameFormat^LVBCOM03("DR",DOC(26),$e(DOC(4)),DOC(3))_" Prov No: "_DOC(18)
 q
MEDI ; added MS - Abbott Pathology May 2002
 ; extract Medicare number
 s MEDICARE=VISIT(8)
 s PAYCODE=VISIT(23) ; set to paycode
 ; extract the hospital name
 if $l(VISIT(22)) do
 .q:$$select^LVBCTHOS(VISIT(22))  m HOS=PLIST k PLIST
 .s HOSPITAL=HOS(3)
 .s HOSPROV=HOS(10)
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 i '$l(ADDRESS(1)) s ADDRESS(1)=$p(VISIT(7),$c(1),2)_" "_$e(VISIT(4))_" "_VISIT(3)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
PATH ; extract pathology doctor details
 s (pathdr,pathprv)=""
 i '$$select^LVBINVB(PAR1_"||"_PAR2) s pathdr=$p(PLIST(10),$c(1),2),pathprv=PLIST(5)
 q
LN ; add line to report
 d ADD^PRTREP00(REPID,"PRT","!")
 q
QDR ; set header
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"E")
 s x="" f  s x=$o(^SS("QDR","ABB-INV",x)) q:x=""  d ADD^PRTREP00(REPID,"HDR",^(x))
 q
%0ABs1 s %val=+$g(%val) s:%val<1 %val="" q:%val=""  s %val=$ZDATE(%val,4,-1,4) q
%0Ao n %mmmsqlc,%mmmsqld,%mmmsqle,%mmmsqlE,%mmmsqlf,%mmmsqll,%mmmsqlL,%mmmsqlm,%mmmsqln,%mmmsqlp,%mmmsqlr,%mmmsqlR,%mmmsqls,%mmmsqlt,%val
 s %mmmsqlE=$zt,$zt="%0Aerr^"_$zn,%mmmsqlf=0 s $ze=""
 s %mmmsqld(8)=$P(epis,$C(1))
 s:%mmmsqld(8)="" %mmmsqld(8)=$E(epis,2,999)
 s SQLCODE=100
   ;  init
 ; asl MOD# 2
 s %mmmsqld(10)=$p($g(%mmmsqld(8)),"||",1,1)
 s %mmmsqld(7)=$g(%mmmsqld(8))
 i %mmmsqld(10)'="",$d(^TEPI(%mmmsqld(10)))
 e  g %0ABdun
 s %mmmsqln=$g(^TEPI(%mmmsqld(10),7))
 s list=$p(%mmmsqln,"\",2)
 s %mmmsqln=$g(^TEPI(%mmmsqld(10)))
 s deb=$p(%mmmsqln,"\",18) s date=$p(%mmmsqln,"\",10)
 
 s %val=$P(date,$C(1)) d %0ABs1:%val'="" s $P(date,$C(1),2)=%val
 s SQLCODE=0 s:SQLCODE=0 %mmmsqlf=%mmmsqlf+1 g %0Ac
%0ABdun 
%0AAdun 
%0Ac q
%0Aerr ; Non Cursor-Based Error Trap Module...
 s $zt=$g(%mmmsqlE),SQLCODE=-20603,%msg=$ze s $ze="" g %0AAdun
]]></Routine>
</Export>
