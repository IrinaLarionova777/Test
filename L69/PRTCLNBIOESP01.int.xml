<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCLNBIOESP01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTCLNBIOESP01  ;Rutina de exportación de datos para BioRad
 q
 ; Oct 26, 2005 by Mc_Fly - TrakHealth BCN
 ;
 ; Parameters :  1. From Date
 ;               2. To Date
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Exportación de datos a Biorad"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s fdat=+PAR1,tdat=+PAR2
 i '$d(fdat) s fdat=+$h
 i '$d(tdat) s tdat=+$h
 i $$select^MVBCFSM(1)
 s file=PLIST(35)_"\biorad\BIORAD"_$tr($zdt($h,8)," :")_".txt" 
 o file:"WNS":20 e  d  q
 . d ADDLINE^PRTREP00(REPID,"PRT","!")
 . d ADDLINE^PRTREP00(REPID,"PRT","No se puede abrir la ruta "_file)
 u file
 s (mat,lotord,mach,tc,level,lote,run)="",swline=0,tot=0
 f  s mat=$o(^TTQC(mat))  q:mat=""  d
 . i $e(mat,1,4)'="LBIO" q
 . f  s lotord=$o(^TTQC(mat,"LOT",lotord))  q:lotord=""  d
 .. s lotnum=$p(^TTQC(mat,"LOT",lotord),"\",1)
 .. f  s mach=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach))  q:mach=""  d
 ... f  s tc=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc))  q:tc=""  d
 .... f  s level=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level))  q:level=""  d 
 ..... s dat=fdat-1
 ..... f  s dat=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat)) q:dat=""  q:dat>tdat  d
 ...... f  s run=$o(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat,run))  q:run=""  d 
 ....... i '$d(^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat,run)) q
 ....... s reg=^TTQC(mat,"LOT",lotord,"TYPE","M",mach,"TEST",tc,"DATA",level,dat,run)
 ....... i $p(reg,"\",2)="Y" q
 ....... i swline w !
 ....... w lotnum_"|"_level_"|"_mach_"|"_tc_"|"_$zd(dat)_"|"_$p(reg,"\"),"|"
 ....... s swline=1,tot=tot+1
 c file
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT","Se ha creado el fichero "_file)
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT","Se han traspasado "_tot_" controles")
 q
]]></Routine>
</Export>
