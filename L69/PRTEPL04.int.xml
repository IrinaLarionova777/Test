<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPL04" type="INT" languagemode="0" generated="1"><![CDATA[
PRTEPL04 ; ML 18/05/2004 ; Episode Patient Label - DARWIN
 q
 ; Parameters :  1. Episode number
 ;               2. Label code
 ;               3. Quantity
 ;               4. Computer Name
 ;               5. Worksheet
 ;               6. Worksheet No.
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR2_"-"_PAR1,PLIST(9)="M" i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 q
BODY ; create temporary printing file
 ; print labels for a patient
 i $l(PAR1) d
 . ; label and quantity entered
 .i $l(PAR2),$l(PAR3) s ^TMP($zn,$j,"REPORT",PAR1,PAR2)=PAR3 q
 . ; labels for all tests
 .i '$l(PAR5) d
 ..i $$open^LVBVISTS(PAR1)
 ..f  q:$$fetch^LVBVISTS(PAR1)  s ts=$p(PLIST(3),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBVISTS()
 . ; labels for WS
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)
 ..f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBWKSRT()
 i '$l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRE(PAR5_"||"_PAR6)
 ..f  q:$$fetch^LVBWKSRE(PAR5_"||"_PAR6)  s epis=$p(PLIST(2),$c(1)) d
 ...i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)
 ...f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,epis,ts)=$g(^TMP($zn,$j,1,epis,ts))+1
 ...i $$close^LVBWKSRT()
 ..i $$close^LVBWKSRE()
 s epis="" f  s epis=$o(^TMP($zn,$j,1,epis)) q:epis=""  d
 .s ts="" f  s ts=$o(^TMP($zn,$j,1,epis,ts)) q:ts=""  d
 ..s (label,max)=""
 ..s label=$$seldata^LVBCTTS(ts,23),max=$$seldata^LVBCTLB(label,4)
 ..i $l(label),max d
 ...i $l(PAR2),PAR2'=label q
 ...s ^TMP($zn,$j,"REPORT",epis,label,ts)=^TMP($zn,$j,1,epis,ts)
 .s label="" f  s label=$o(^TMP($zn,$j,"REPORT",epis,label)) q:label=""  d
 ..s (cnt,ts)="" f  s ts=$o(^TMP($zn,$j,"REPORT",epis,label,ts)) q:ts=""  s cnt=cnt+^(ts)
 ..s xx=cnt\max s:cnt#max xx=xx+1 s ^TMP($zn,$j,"REPORT",epis,label)=xx
 q
PRINT 
 i $d(^TMP($zn,$j,"REPORT")) d
 .s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  d
 ..k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 ..s label="" f  s label=$o(^TMP($zn,$j,"REPORT",epis,label)) q:label=""  d
 ...s cnt=^(label) f j=1:1:cnt d
 ....s LF=$c(10),LABnum=1
 ....d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_LABnum_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_LABnum_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","Q200,24"_LF)
 ....;d ADD^SSPRINT(REPID,"PRT","R16,0"_LF)
 ....d ADD^SSPRINT(REPID,"PRT","A320,8,0,3,1,1,N,"""_PLIST(2)_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","A320,33,0,2,1,1,N,"""_PLIST(3)_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","A320,53,0,2,1,1,N,"""_PLIST(4)_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","A320,73,0,2,1,1,N,"""_$$extdate2^SSUTIL4($g(PLIST(6)))_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","B310,93,0,1,2,4,68,N,"""_PLIST(2)_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","FE"_LF)
 ....d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_LABnum_""""_LF)
 ....d ADD^SSPRINT(REPID,"PRT","P1"_LF)
 q
LN d ADD^SSPRINT(REPID,"PRT","!")
 q
]]></Routine>
</Export>
