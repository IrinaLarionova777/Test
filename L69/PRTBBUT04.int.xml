<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBUT04" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBUT04 ; ML 08/11/2000 ; Blood Unit Transaction Summary - Darwin
 q
 ; Parameters :  1. Site
 ;               2. Product
 ;               3. Date from
 ;               4. Date to
 ;               5. Product group
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Blood Unit Transaction Summary"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s('$l(PAR1):"All Sites",1:CTUSL(3))
 s line=4,xHEADER(line)=""
 i $l(PAR2) d
 .s line=line+1,xHEADER(line)="for product <"_PAR2_"> "_BBP(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR5) d
 .s line=line+1,xHEADER(line)="for product group <"_PAR5_"> "_BBPG(3)
 .s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR3,PAR4),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 i $l(PAR1),'$$select^LVBCTUSL(PAR1,"Y") m CTUSL=PLIST
 i $l(PAR2),'$$select^LVBBBBP(PAR2,"Y") m BBP=PLIST
 i $l(PAR5),'$$select^LVBBBPG(PAR5,"Y") m BBPG=PLIST
 s date=PAR3-1 f  s date=$o(^TBBPi("TR-Date",date)) q:date=""  q:date>PAR4  d
 .s tr="" f  s tr=$o(^TBBPi("TR-Date",date,tr)) q:tr=""  d
 ..s PackID="" f  s PackID=$o(^TBBPi("TR-Date",date,tr,PackID)) q:PackID=""  d
 ...s SecondID="" f  s SecondID=$o(^TBBPi("TR-Date",date,tr,PackID,SecondID)) q:SecondID=""  d
 ....s (ok,x,site)="" f  s x=$o(^TBBPi("TR-Date",date,tr,PackID,SecondID,x)) q:x=""  d  q:ok
 .....i '$$select^LVBBBPT(PackID_"||"_SecondID_"||"_x),PLIST(13)'="Y" s ok=1,site=$p(PLIST(11),$c(1),3)
 ....i ok d
 .....q:$$select^LVBBBP(PackID_"||"_SecondID)  m PackDet=PLIST k PLIST
 .....s product=$p(PackDet(8),$c(1))
 .....i $l(PAR1),PAR1'=site q
 .....i $l(PAR2),PAR2'=product q
 .....i $l(PAR5),PAR5'=$p(PackDet(8),$c(1),4) q
 .....s ^TMP($zn,$j,"tran",tr)=""
 ; extract transaction descriptions
 i $$open^LVBBBTR()
 s (cnt,Caption)=0 f  q:$$fetch^LVBBBTR()  d
 .i '$d(^TMP($zn,$j,"tran",$p(PLIST(2),$c(1)))) q
 .s cnt=cnt+1,Caption(cnt)=PLIST(2)_$c(2)_""_$c(2)_PLIST(2)_$c(2)_$s($l(PLIST(2))>4:$l(PLIST(2)),1:4),Caption=cnt
 i $$close^LVBBBTR()
 s xLINE(2)="|  Site   | Product | Blood | Total  ||"
 f j=1:1:Caption s xLINE(2)=xLINE(2)_$$PART^PRTREP00($p(Caption(j),$c(2),1),$p(Caption(j),$c(2),4))_"|"
 s xLINE(3)="|         |         | Group |        ||"
 f j=1:1:Caption s xLINE(3)=xLINE(3)_$$PART^PRTREP00($p(Caption(j),$c(2),2),$p(Caption(j),$c(2),4))_"|"
 s width=$l(xLINE(2))-2,xLINE(4)="|" f j=1:1:width s xLINE(4)=xLINE(4)_"-"
 s xLINE(4)=$e(xLINE(4),1,$l(xLINE(4)))_"|"
 s length=$l(xLINE(2)),$p(xLINE(1),"-",length)=""
 s xLINE(1)=" "_$e(xLINE(1),1,$l(xLINE(1))-1)
 s cnt=1,N(cnt,1)="xxx",N(cnt,2)=cnt f j=3:1:7 s N(cnt,j)=""
 s cnt=2,N(cnt,1)="xxx",N(cnt,2)=cnt f j=3:1:7 s N(cnt,j)=""
 s cnt=3,N(cnt,1)="xxx",N(cnt,2)=cnt f j=3:1:7 s N(cnt,j)=""
 s cnt=4,N(cnt,1)="xxx",N(cnt,2)=cnt f j=3:1:7 s N(cnt,j)=""
 s cnt=5,N(cnt,1)="xxx",N(cnt,2)=cnt f j=3:1:7 s N(cnt,j)=""
 f j1=1:1:Caption f j2=1:1:$l($p(Caption(j1),$c(2),2),$c(28)) d
 .s cnt=cnt+1,N(cnt,1)="xxx",N(cnt,2)=cnt f j3=3:1:7 s N(cnt,j3)=""
 .s N(cnt,"Tran")=$p(Caption(j1),$c(2),3)
 s X=xLINE(2)
 s N(1,"Z")=1 f j=2:1 q:'$F(X,"|",N(j-1,"Z")+1)  s N(j,"Z")=$f(X,"|",N(j-1,"Z")+1)-1
 f j=1:1 q:'$d(N(j+1,"Z"))  s N(j,"L")=N(j+1,"Z")-N(j,"Z")-1
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR3-1 f  s date=$o(^TBBPi("TR-Date",date)) q:date=""  q:date>PAR4  d
 .s tr="" f  s tr=$o(^TBBPi("TR-Date",date,tr)) q:tr=""  d
 ..s cnt=5 f  s cnt=$o(N(cnt)) q:cnt=""  q:$g(N(cnt,"Tran"))=tr
 ..i cnt d
 ...s PackID="" f  s PackID=$o(^TBBPi("TR-Date",date,tr,PackID)) q:PackID=""  d
 ....s SecondID="" f  s SecondID=$o(^TBBPi("TR-Date",date,tr,PackID,SecondID)) q:SecondID=""  d
 .....s (ok,x,site)="" f  s x=$o(^TBBPi("TR-Date",date,tr,PackID,SecondID,x)) q:x=""  d  q:ok
 ......i '$$select^LVBBBPT(PackID_"||"_SecondID_"||"_x),PLIST(13)'="Y" s ok=1,site=$p(PLIST(11),$c(1),3)
 .....i ok d
 ......q:$$select^LVBBBP(PackID_"||"_SecondID)  m PackDet=PLIST k PLIST
 ......s product=$p(PackDet(8),$c(1)),BloodGrp=$p(PackDet(9),$c(1))
 ......i $l(PAR1),PAR1'=site q
 ......i $l(PAR2),PAR2'=product q
 ......i $l(PAR5),PAR5'=$p(PackDet(8),$c(1),4) q
 ......s:site="" site=" " s:product="" product=" " s:BloodGrp="" BloodGrp=" "
 ......s $p(^TMP($zn,$j,"REPORT",site,product,BloodGrp),"\",cnt)=$p($g(^TMP($zn,$j,"REPORT",site,product,BloodGrp)),"\",cnt)+1
 ......s $p(^TMP($zn,$j,"REPORT",site,product),"\",cnt)=$p($g(^TMP($zn,$j,"REPORT",site,product)),"\",cnt)+1
 ......s $p(^TMP($zn,$j,"REPORT",site),"\",cnt)=$p($g(^TMP($zn,$j,"REPORT",site)),"\",cnt)+1
 ......i '$d(^TMP($zn,$j," ",tr,PackID_"||"_SecondID)) d
 .......s ^TMP($zn,$j," ",tr,PackID_"||"_SecondID)=""
 .......s $p(^TMP($zn,$j," ",site,product,BloodGrp),"\",cnt)=$p($g(^TMP($zn,$j," ",site,product,BloodGrp)),"\",cnt)+1
 .......s $p(^TMP($zn,$j," ",site,product),"\",cnt)=$p($g(^TMP($zn,$j," ",site,product)),"\",cnt)+1
 .......s $p(^TMP($zn,$j," ",site),"\",cnt)=$p($g(^TMP($zn,$j," ",site)),"\",cnt)+1
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s site="" f  s site=$o(^TMP($zn,$j,"REPORT",site)) q:site=""  d
 ..s xxx(1)=site i '$$select^LVBCTUSL(site,"Y") s xxx(1)=PLIST(2)_$c(1)_PLIST(3)
 ..s product="" f  s product=$o(^TMP($zn,$j,"REPORT",site,product)) q:product=""  d
 ...s xxx(2)=product i '$$select^LVBBBBP(product,"Y") s xxx(2)=PLIST(2)_$c(1)_PLIST(3)
 ...s BloodGrp="" f  s BloodGrp=$o(^TMP($zn,$j,"REPORT",site,product,BloodGrp)) q:BloodGrp=""  d
 ....s xxx(3)=BloodGrp i '$$select^LVBBBBG(BloodGrp,"Y") s xxx(3)=PLIST(2)_$c(1)_PLIST(3)
 ....f j=5:1:$l(^TMP($zn,$j,"REPORT",site,product,BloodGrp),"\") d
 .....s xxx(4)=$g(xxx(4))+$p(^TMP($zn,$j,"REPORT",site,product,BloodGrp),"\",j)
 .....i $l($p(^TMP($zn,$j,"REPORT",site,product,BloodGrp),"\",j)) s xxx(j)=$p(^(BloodGrp),"\",j)
 ....d PRINT^PRTREP00(,.N) k xxx
 ...s xxx(2)="Total:"
 ...f j=5:1:$l(^TMP($zn,$j,"REPORT",site,product),"\") d
 ....s xxx(4)=$g(xxx(4))+$p(^TMP($zn,$j,"REPORT",site,product),"\",j)
 ....i $l($p(^TMP($zn,$j,"REPORT",site,product),"\",j)) s xxx(j)=$p(^(product),"\",j)
 ...d PRINT^PRTREP00(,.N) k xxx
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 ...d PRINT^PRTREP00(,.N) k xxx
 ..s xxx(1)="Total:"
 ..f j=5:1:$l(^TMP($zn,$j,"REPORT",site),"\") d
 ...s xxx(4)=$g(xxx(4))+$p(^TMP($zn,$j,"REPORT",site),"\",j)
 ...i $l($p(^TMP($zn,$j,"REPORT",site),"\",j)) s xxx(j)=$p(^(site),"\",j)
 ..d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 ..d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .i $$open^LVBBBTR()
 .f cnt=1:1 q:$$fetch^LVBBBTR()  d
 ..d ADDLINE^PRTREP00(REPID,"PRT","   "_$e(PLIST(2)_$j("",15),1,15)_PLIST(3))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .i $$close^LVBBBTR()
 s report="BBUT"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
