<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LBACKGR10" type="INT" languagemode="0" generated="1"><![CDATA[
LBACKGR10 ; BackGround Job - 10 - Send movements between DB's
 q
 ;
DOIT(zn,job,DATE,TIME) s zn=$g(zn),job=$g(job),DATE=$g(DATE),TIME=$g(TIME) k ^TMP($zn,$j)
 l +^TMP(zn,job,"DOIT10"):0 e  q
 s zt=$zt,$zt="ERROR^"_$zn
 s $p(^TMP(zn,job,"DOIT10",DATE,TIME),"^",1)="START : "_$h
 s xCurrent=$p($g(^CF("SM",1,"SITE")),"^",2)
 s SiteFrom="" f  s SiteFrom=$o(^TMOVE("OUT",SiteFrom)) q:SiteFrom=""  d
 .s SiteTo="" f  s SiteTo=$o(^TMOVE("OUT",SiteFrom,SiteTo)) q:SiteTo=""  d
 ..i SiteTo=xCurrent q
 .. ; extract folder details
 ..i '$$select^LVBCTUSL(SiteTo) d
 ...m CTUSL=PLIST k PLIST i $l(CTUSL(6)) d
 ....s dir=$tr(CTUSL(6),$c(16,17,18),"^\|") f j="/","\" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
 ....i $zf(-1,"DIR "_dir_"in"_$s(dir["/":"/",1:"\")_SiteFrom_"_*.txt /B > ""move.tmp""")
 ....s file="move.tmp"
 ....o file:"WAS" u file w "End_Of_File",! c file
 ....o file:("RS"::$c(10,13))
 ....f  u file r x q:x="End_Of_File"  s xfile=$p(x," ",$l(x," ")) i xfile[".",$l(xfile) s ^TMP($zn,$j,"FileName",xfile)=""
 ....c file
 ....s file="" f  s file=$o(^TMP($zn,$j,"FileName",file)) q:file=""  d
 .....i '$zf(-1,"rename "_dir_"in"_$s(dir["/":"/",1:"\")_file_" "_$e(file,1,$l(file)-3)_"move")
 ....s file=dir_"in"_$s(dir["/":"/",1:"\")_SiteFrom_"_"_$e($$new(SiteFrom)+1000000000000,2,99)_".txt"
 ....o file:"WNS" s size=0
 ....s epis="" f  s epis=$o(^TMOVE("OUT",SiteFrom,SiteTo,epis)) q:epis=""  d  q:size>1000000
 .....s seq="" f  s seq=$o(^TMOVE("OUT",SiteFrom,SiteTo,epis,seq)) q:seq=""  d
 ......l +^TMOVE("OUT",SiteFrom,SiteTo,epis,seq):5 e  q
 ......s line=SiteFrom_","_SiteTo_","_epis_","_seq_","_^TMOVE("OUT",SiteFrom,SiteTo,epis,seq)
 ......s x1="" f  s x1=$o(^TMOVE("OUT",SiteFrom,SiteTo,epis,seq,x1)) q:x1=""  d
 .......i $d(^(x1))#10 s line=line_$c(26)_x1_$c(27)_$tr(^(x1),$c(10,13),$c(21,22))
 .......s x2="" f  s x2=$o(^TMOVE("OUT",SiteFrom,SiteTo,epis,seq,x1,x2)) q:x2=""  d
 ........i $d(^(x2))#10 s line=line_$c(26)_x1_$c(30)_x2_$c(27)_$tr(^(x2),$c(10,13),$c(21,22))
 ........s x3="" f  s x3=$o(^TMOVE("OUT",SiteFrom,SiteTo,epis,seq,x1,x2,x3)) q:x3=""  d
 .........i $d(^(x3))#10 s line=line_$c(26)_x1_$c(30)_x2_$c(30)_x3_$c(27)_$tr(^(x3),$c(10,13),$c(21,22))
 .........s x4="" f  s x4=$o(^TMOVE("OUT",SiteFrom,SiteTo,epis,seq,x1,x2,x3,x4)) q:x4=""  d
 ..........i $d(^(x4))#10 s line=line_$c(26)_x1_$c(30)_x2_$c(30)_x3_$c(30)_x4_$c(27)_$tr(^(x4),$c(10,13),$c(21,22))
 ..........s x5="" f  s x5=$o(^TMOVE("OUT",SiteFrom,SiteTo,epis,seq,x1,x2,x3,x4,x5)) q:x5=""  d
 ...........i $d(^(x5))#10 s line=line_$c(26)_x1_$c(30)_x2_$c(30)_x3_$c(30)_x4_$c(30)_x5_$c(27)_$tr(^(x5),$c(10,13),$c(21,22))
 ......u file w line,! s size=size+$l(line)
 ......s x=$o(^TMOVE("TEMP-OUT",SiteFrom,SiteTo,epis,""),-1)+1 m ^TMOVE("TEMP-OUT",SiteFrom,SiteTo,epis,x)=^TMOVE("OUT",SiteFrom,SiteTo,epis,seq)
 ......k ^TMOVE("OUT",SiteFrom,SiteTo,epis,seq)
 ......l -^TMOVE("OUT",SiteFrom,SiteTo,epis,seq)
 ....c file
 .... ; rename the file
 ....s fn="" f j="/","\" i file[j s fn=$p(file,j,$l(file,j))
 ....i '$zf(-1,"rename "_file_" "_$e(fn,1,$l(fn)-3)_"move")
 s $p(^TMP(zn,job,"DOIT10",DATE,TIME),"^",2)="FINISH : "_$h
 l -^TMP(zn,job,"DOIT10")
 k ^TMP($zn,$j)
 s $zt=zt
 q
 ; create new file number
new(SiteFrom) 
 l +^CF("HOSP-OUT",SiteFrom)
 s num=$g(^CF("HOSP-OUT",SiteFrom))+1,(num,^CF("HOSP-OUT",SiteFrom))=$s(num=10000000000:1,1:num)
 l -^CF("HOSP-OUT",SiteFrom)
 q num
ERROR d BACK^%ETN
 s $zt=$g(zt)
 q
]]></Routine>
</Export>
