<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTINV01 ; IG 07/10/98 ; PRIVATE INVOICES
 Q
 ; Parameters :  1.Payment Code
 ;               2.Batch Number
 ;               3.Episode Number
 ;               4.Sequence Number
 ;               5.Reminder
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($zn,$j)
 D INIT,BODY
 i PAR5'=3 d PRINT
 i PAR5=3 d PRINT1
CREND I 'PAR5,'$$select^LVBPRPR(REPID) S PLIST(12)=PAR1_"-"_PAR2 I $$update^LVBPRPR(REPID)
 I PAR5,'$$select^LVBPRPR(REPID) S PLIST(12)="REM-"_PAR5 I $$update^LVBPRPR(REPID)
 K ^TMP($zn,$j),^TMP("PRTREP00",$J)
 Q
EDI(inv) s inv=$g(inv),par=$p(inv,"||",1,2),un=""
 i $$open^LVBINVBP(par)
 f  q:$$fetch^LVBINVBP(par)  i inv=(par_"||"_PLIST(4)) d
 .i '$l(un) d
 ..i $l(PLIST(13)) s un=PLIST(13)
 ..i '$l(PLIST(13)) s un=$$newEDI()
 .i $l(un),un'=PLIST(13) s PLIST(13)=un i $$update^LVBINVBP(PLIST(1))
 q un
newEDI() l +^TMCBi("UN")
 s result=$o(^TMCBi("UN",""),-1)
 s result=$e(result,3,$l(result)-1)
 s result="47"_$e(result+1+(10**13),2,14)
 s (xx1,xx2)=0 f j=$l(result):-1:1 s xx2=xx2+1 d
 .i xx2#2=1 s xx=$e(result,j)*2 s:$l(xx)=2 xx=$e(xx)+$e(xx,2) s xx1=xx1+xx
 .i xx2#2=0 s xx=$e(result,j)*1 s:$l(xx)=2 xx=$e(xx)+$e(xx,2) s xx1=xx1+xx
 s result=result_(10#(10-(xx2#10)))
 l -^TMCBi("UN")
 q result
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
 ; create temporary printing file
BODY ; print single patient
 i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
 .s xx1=$p(PLIST(9),$c(1)),xx2=$e(PLIST(57),$l(xx1)+1,$l(PLIST(57)))
 . ; find sequence
 .s xx3="" i $l(xx1),$l(xx2) s xx3=$p($g(^TMCB(xx1,xx2,PAR3)),"\",2)
 . ; collect all episodes for this sequence
 .i $l(xx3) d
 .. ; get provider name and provider number
 ..k INVOICE i '$$select^LVBINVB(xx1_"||"_xx2) m INVOICE=PLIST
 ..s ^TMP($zn,$j,"REPORT")=$p(INVOICE(10),$c(1),2)_" - "_INVOICE(5)
 ..s epis="" f  s epis=$o(^TMCB(xx1,xx2,epis)) q:epis=""  d
 ...i $$CT007^at345(epis,xx1_"||"_xx2)="Y" q
 ...s seq=$p(^TMCB(xx1,xx2,epis),"\",2) i '$l(seq) s seq=" "
 ...i $l(xx3),xx3'=seq q
 ...i '$$select^LVBEPVIS(epis,"Y") d  q
 ....s ^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)=PLIST(23)
 ....s ^TMP($zn,$j,"REPORT",seq)=xx1_"-"_xx2_"-"_seq
 ; print batch
 i $l(PAR1),$l(PAR2) d
 . ; get billing location
 .s ^TMP($zn,$j,"REPORT")=$p(^TMCB(PAR1,PAR2),"\",6)
 .s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
 ..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
 ..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
 ..i $l(PAR4),PAR4'=seq q
 ..i '$$select^LVBEPVIS(epis,"Y") d  q
 ...s ^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)=PLIST(23)
 ...s ^TMP($zn,$j,"REPORT",seq)=PAR1_"-"_PAR2_"-"_seq
 ; set EDI Unique number
 s seq="" f  s seq=$o(^TMP($zn,$j,"REPORT",seq)) q:seq=""  i seq'=" " s inv=^(seq) d
 .s $p(^TMP($zn,$j,"REPORT",seq),$c(1),2)=$$EDI($p(inv,"-",1)_"||"_$p(inv,"-",2)_"||"_$p(inv,"-",3))
 ; check credits
 i PAR5 d
 .s seq="" f  s seq=$o(^TMP($zn,$j,"REPORT",seq)) q:seq=""  d
 ..s (credit,epis)="" f cnt=1:1 s epis=$O(^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 ...s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ....s rowid=deb_"||"_date_"||"_ep
 ....i $$open^LVBDEBVT(rowid)
 ....f  q:$$fetch^LVBDEBVT(rowid)  d
 .....f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .....k TRAN m TRAN=PLIST
 .....i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) d
 ......s credit=credit-PLIST(14)
 ......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate2^SSUTIL4(PLIST(4))
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="JOURNAL "_PLIST(3)_"-"_PLIST(6)
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(14)
 .....i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) d
 ......s credit=credit-PLIST(16)
 ......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 ......s x1=$p(PLIST(1),"||",1),x2=$p(PLIST(1),"||",2),receipt=$p(^TBAT(x1,x2),"\",3)
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate2^SSUTIL4(PLIST(12))
 ......i $l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Receipt "_receipt
 ......i '$l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Credit "_x1_"-"_x2
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(16)
 .....i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) d
 ......s credit=credit-PLIST(9)
 ......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 ......s x1=$p(PLIST(1),"||",1),x2=$p(PLIST(1),"||",2),receipt=$p(^TBAT(x1,x2),"\",3)
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate2^SSUTIL4(PLIST(5))
 ......i $l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Receipt "_receipt
 ......i '$l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Credit "_x1_"-"_x2
 ......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(9)
 ....i $$close^LVBDEBVT()
 ..i credit s ^TMP($zn,$j,"REPORT",seq,"CREDIT")=credit
 ; set events
 s seq="" f  s seq=$o(^TMP($zn,$j,"REPORT",seq)) q:seq=""  s invoice=^(seq) d
 .s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..i '$l(PAR5) d events^LVBCOM01(epis,"ACC","Original invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ..i PAR5="R" d events^LVBCOM01(epis,"ACC","Receipt "_PAR6_$s($l(USER):" printed by "_USER,1:"")) q
 ..i PAR5="H" d events^LVBCOM01(epis,"ACC","Invoice history "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ..i 'PAR5 d events^LVBCOM01(epis,"ACC","Duplicate invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ..d events^LVBCOM01(epis,"ACC","Reminder "_PAR5_$s($l(USER):" printed by "_USER,1:""))
 Q
PRINT1 s seq="" f  s seq=$O(^TMP($zn,$j,"REPORT",seq)) q:seq=""  s AutoCytePrinted=0 d
 .s xx1=$p(^(seq),$c(1)),xx2=$p(^(seq),$c(1),2)
 . ; get billing site
 .s bs=$$BS($p(xx1,"-",1),$p(xx1,"-",2))
 . ; calculate amount
 .s amount="" k VISIT s INVOICE=$p(^TMP($zn,$j,"REPORT",seq),$c(1))
 .s epis="" f cnt=1:1 s epis=$O(^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..i '$d(VISIT),'$$select^LVBEPVIS(epis) m VISIT=PLIST
 ..s item="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  s amount=amount+^(item)
 .s credit=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT")),amount=amount-credit
 .D ADD^SSPRINT(REPID,"HDR","~FONT-COURIER")
 .D ADD^SSPRINT(REPID,"HDR","~FONTSIZE=10")
 .D ADD^SSPRINT(REPID,"HDR","~NORMAL") f j=1:1:10 d LN
 .d ADD^SSPRINT(REPID,"PRT","?10 "_$$extdate2^SSUTIL4(+$h))
 .d ADD^SSPRINT(REPID,"PRT","~BOLD"),LN
 .d ADD^SSPRINT(REPID,"PRT","?60 All enquiries to "),LN
 .i bs="H" d
 ..d ADD^SSPRINT(REPID,"PRT","?60 Hollywood Hospital Pathology"),LN
 .i bs="M" d
 ..d ADD^SSPRINT(REPID,"PRT","?60 Mercy Hospital Pathology"),LN
 .i bs="S" d
 ..d ADD^SSPRINT(REPID,"PRT","?60 St John of God Pathology"),LN
 .d ADD^SSPRINT(REPID,"PRT","?60 on Tel. (08) 9346 6483") f j=1:1:3 d LN
 .d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 .f j=1:1:5 d LN
 .d ADDRESS
 .d ADD^SSPRINT(REPID,"PRT","?10 "_ADDRESS(1)),LN
 .d ADD^SSPRINT(REPID,"PRT","?10 "_ADDRESS(2)),LN
 .d ADD^SSPRINT(REPID,"PRT","?10 "_ADDRESS(3)),LN
 .d ADD^SSPRINT(REPID,"PRT","?10 "_ADDRESS(4)) f j=1:1:6 d LN
 .
 .d ADD^SSPRINT(REPID,"PRT","?10 Dear Sir/Madam") f j=1:1:3 d LN
 .d ADD^SSPRINT(REPID,"PRT","~BOLD"),LN
 .d ADD^SSPRINT(REPID,"PRT","?10 RE:") d
 ..i bs="H" d
 ...d ADD^SSPRINT(REPID,"PRT","?15 Hollywood Hospital Pathology")
 ..i bs="M" d
 ...d ADD^SSPRINT(REPID,"PRT","?15 Mercy Hospital Pathology")
 ..i bs="S" d
 ...d ADD^SSPRINT(REPID,"PRT","?15 St John of God Pathology")
 .d LN,LN
 .d ADD^SSPRINT(REPID,"PRT","?10 ACCOUNT NUMBER: "_INVOICE)
 .d ADD^SSPRINT(REPID,"PRT","?45 AMOUNT OWING: $"_amount)
 .d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 .f j=1:1:2 d LN
 .d ADD^SSPRINT(REPID,"PRT","?10 We have been instructed by the abovenamed client to act on their behalf in"),LN 
 .d ADD^SSPRINT(REPID,"PRT","?10 the matter of their overdue accounts.  The amount shown is beyond our"),LN
 .d ADD^SSPRINT(REPID,"PRT","?10 Client's payment terms and we must ask for immediate settlement.")
 .d ADD^SSPRINT(REPID,"PRT","~BOLD")
 .f j=1:1:3 d LN
 .d ADD^SSPRINT(REPID,"PRT","?10 Payment must be made direct to our Client:"),LN,LN
 .i bs="H" d
 ..d ADD^SSPRINT(REPID,"PRT","?30 Hollywood Hospital Pathology"),LN
 .i bs="M" d
 ..d ADD^SSPRINT(REPID,"PRT","?30 Mercy Hospital Pathology"),LN
 .i bs="S" d
 ..d ADD^SSPRINT(REPID,"PRT","?30 St John of God Pathology"),LN
 .d ADD^SSPRINT(REPID,"PRT","?30 PO BOX 646"),LN
 .d ADD^SSPRINT(REPID,"PRT","?30 WEMBLEY, WA, 6014")
 .d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 .f j=1:1:3 d LN
 .d ADD^SSPRINT(REPID,"PRT","?10 Failure to pay this account within seven (7) days will leave our Client"),LN
 .d ADD^SSPRINT(REPID,"PRT","?10 with no option but to seek further action through our Solicitors and will"),LN
 .d ADD^SSPRINT(REPID,"PRT","?10 involve you in additional costs.")
 .d ADD^SSPRINT(REPID,"PRT","#")
 Q
PRINT s max=28
 s seq="" f  s seq=$O(^TMP($zn,$j,"REPORT",seq)) q:seq=""  s AutoCytePrinted=0 d
 .s xx1=$p(^(seq),$c(1)),xx2=$p(^(seq),$c(1),2)
 . ; get billing site
 .s bs=$$BS($p(xx1,"-",1),$p(xx1,"-",2))
 . ; get pay code invoice type
 .s pc=$p(xx1,"-",1),pcit="" i '$$select^LVBCTPC(pc,"Y") s pcit=PLIST(25)
 . ; calculate amount
 .s (message,amount)="" k list s INVOICE=$p(^TMP($zn,$j,"REPORT",seq),$c(1))
 .s epis="" f cnt=1:1 s epis=$O(^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..i '$l(message) s message=^(epis)
 ..s list(cnt)=epis
 ..s item="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  s amount=amount+^(item)
 .s credit=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT")),amount=amount-credit
 . ; print invoice
 .s (start,last,autocyte)=0
 .s epis="" f  s epis=$O(^TMP($zn,$j,"REPORT",seq,"EPISODE",epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis) k VISIT m VISIT=PLIST
 ..s icDescr="" i '$$select^LVBCTIC(VISIT(18),"Y") s icDescr=PLIST(5)
 ..i icDescr="" s icDescr="Processing Fee"
 ..i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") k DOC m DOC=PLIST
 ..s inpat="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(4)="Y" s inpat=1
 ..i 'start d S1 s start=1
 ..d S2
 ..s item="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  s xx=^(item) d
 ...k x s x(1)=$$extdate2^SSUTIL4(VISIT(12)),x(2)=item,x(3)=$p(xx,"\",2),x(4)=$p(xx,"\",1)
 ...i item=VISIT(54),'$l(x(3)) s x(3)=icDescr
 ...d S3
 ..i VISIT(45)="Y" d
 ...k x s (x(1),x(2),x(4))="",x(3)="Rule 3 exemption"
 ...d S3
 .. ; print AUTOCYTE comment after Billing item 90001
 ..i 'AutoCytePrinted,autocyte s AutoCytePrinted=1 d
 ...d LN s line=line+1
 ...d ADD^SSPRINT(REPID,"PRT","?5  The  Autocyte Pap Smear  is a non-rebatable item from  Medicare."),LN  s line=line+1
 ...d ADD^SSPRINT(REPID,"PRT","?5  Please submit this invoice to Medicare to claim all other items."),LN  s line=line+1
 ...d ADD^SSPRINT(REPID,"PRT","?5  Then forward the Medicare cheque, together with your payment for"),LN  s line=line+1
 ...d ADD^SSPRINT(REPID,"PRT","?5  the balance of this account to our office."),LN  s line=line+1
 .i PAR5 d S6
 .s last=1 d S5
 q
BS(P1,P2) N (P1,P2) S P1=$G(P1),P2=$G(P2),RESULT=""
 I $L(P1),$L(P2) S RESULT=$P($G(^TMCB(P1,P2)),"\",6)
 Q RESULT
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 i '$L(ADDRESS(1)) s ADDRESS(1)=$p(VISIT(7),$c(1),2)_" "_$e(VISIT(4))_" "_VISIT(3)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 d
 .i $l(ADDRESS(4)),ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(6),ADDRESS(6)="" q
 .i $l(ADDRESS(3)),ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6),ADDRESS(6)="" q
 .i $l(ADDRESS(2)),ADDRESS(2)'[ADDRESS(6) s ADDRESS(2)=ADDRESS(2)_" "_ADDRESS(6),ADDRESS(6)="" q
 q
 ; print invoice header
S1 d ADD^SSPRINT(REPID,"PRT"," !R! RES; SPSZ 8; SBM 11.5; PSRC "_$s(bs="S":4,bs="H":5,1:6)_"; EXIT;")
 d ADD^SSPRINT(REPID,"PRT","#")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a3270h-110V"_INVOICE_"      "_$s(inpat:"INPATIENT",1:"OUTPATIENT")_" INVOICE")
 d ADD^SSPRINT(REPID,"PRT","~BOLD"),LN,LN
 d ADDRESS
 d ADD^SSPRINT(REPID,"PRT","?37 "_ADDRESS(1)),LN
 d ADD^SSPRINT(REPID,"PRT","?3  APA No: 000393")
 d ADD^SSPRINT(REPID,"PRT","?37 "_ADDRESS(2)),LN
 d ADD^SSPRINT(REPID,"PRT","?3  "_^TMP($zn,$j,"REPORT"))
 d ADD^SSPRINT(REPID,"PRT","?37 "_ADDRESS(3)),LN
 d ADD^SSPRINT(REPID,"PRT","?37 "_ADDRESS(4)),LN,LN
 d ADD^SSPRINT(REPID,"PRT","~NORMAL"),LN
 d ADD^SSPRINT(REPID,"PRT","?4  Date Serv.")
 i '$l(pcit) d ADD^SSPRINT(REPID,"PRT","?16 Item #")
 d ADD^SSPRINT(REPID,"PRT","?25 Services Provided")
 d ADD^SSPRINT(REPID,"PRT","?69 Amount"),LN,LN
 s line=0
 q
 ; print episode header
S2 i line+3>max d S5,S1
 d ADD^SSPRINT(REPID,"PRT","~BOLD")
 d ADD^SSPRINT(REPID,"PRT","Services provided for "_$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101)))
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 d LN s line=line+1
 s provider=DOC(18) i VISIT(12)<DOC(37) s provider=DOC(36)
 d ADD^SSPRINT(REPID,"PRT","?1  Req# "_epis_" "_$$extdate2^SSUTIL4(VISIT(14))_" - "_$$doc($p(VISIT(15),$c(1),1))_" ("_provider_")")
 d LN s line=line+1
 q
 ; print doctors name(s)
doc(doc) s doc=$g(doc) n (doc) s result=""
 i '$$select^LVBCTDR(doc,"Y") s result=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 q result
 ; print single item
S3 i line+1>max d S5,S1
 ; split description
 s x(3,1)=x(3) i $l(x(3))>45 s x=0 d
 .f j=1:1:$l(x(3)," ") i $l($p(x(3)," ",1,j))>45 s x=x+1,x(3,x)=$p(x(3)," ",1,j-1),(x(3),x(3,x+1))=$p(x(3)," ",j,999) q:$l(x(3))'>45
 d ADD^SSPRINT(REPID,"PRT","?5  "_x(1))
 i '$l(pcit),$l(x(2)) d ADD^SSPRINT(REPID,"PRT","?16 "_x(2)_$s(inpat:"*",1:""))
 d ADD^SSPRINT(REPID,"PRT","?25 "_x(3,1))
 i $l(x(4)) d ADD^SSPRINT(REPID,"PRT","?70 "_$j(x(4),7,2))
 d LN s line=line+1
 f j=2:1 q:'$d(x(3,j))  d ADD^SSPRINT(REPID,"PRT","?25 "_x(3,j)),LN s line=line+1
 ; print LCC after Billing item 73907
 i x(2)=73907,$l(VISIT(18)),$l(VISIT(27)) d
 .i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1)),"Y") d
 ..d ADD^SSPRINT(REPID,"PRT","?35  LCC : "_$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))),LN  s line=line+1
 i x(2)=90001 s autocyte=1
 q
S6 ; print credits
 d LN s line=line+1 i line+4>max d S5,S1
 d LN s line=line+1 i line+4>max d S5,S1
 d ADD^SSPRINT(REPID,"PRT","?5  Payment Date")
 d ADD^SSPRINT(REPID,"PRT","?20 Payment Reference #")
 d ADD^SSPRINT(REPID,"PRT","?67 Amount"),LN s line=line+1
 d ADD^SSPRINT(REPID,"PRT","?5  ------------")
 d ADD^SSPRINT(REPID,"PRT","?20 -------------------")
 d ADD^SSPRINT(REPID,"PRT","?67 ----------"),LN s line=line+1
 s credit=$d(^TMP($zn,$j,"REPORT",seq,"CREDIT"))\10
 i credit s xx="" f  s xx=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",xx)) q:xx=""  d
 .i line+2>max d S5,S1
 .f j=1,2,3 s @("x"_j)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",xx,j))
 .d ADD^SSPRINT(REPID,"PRT","?5  "_x1)
 .d ADD^SSPRINT(REPID,"PRT","?20 "_x2)
 .d ADD^SSPRINT(REPID,"PRT","?67 "_$j(x3,10,2)),LN s line=line+1
 i 'credit d ADD^SSPRINT(REPID,"PRT","?5  No payments have been received for this account !")
 i (line+2)<max f i=1:1:max-line-2 d LN s line=line+1
 i amount d
 .i PAR5=1 d
 ..d ADD^SSPRINT(REPID,"PRT","~BOLD")
 ..d ADD^SSPRINT(REPID,"PRT","?5  This account is now overdue, prompt payment will be appreciated."),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","?5  If payment has been forwarded, please disregard this reminder."),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 .i PAR5=2 d
 ..d ADD^SSPRINT(REPID,"PRT","~BOLD")
 ..d ADD^SSPRINT(REPID,"PRT","?5  This account is now overdue. Payment is required within 14 days, as"),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","?5  futher action will commence. Please call us if you have any queries."),LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 q
S5 i line<max f i=1:1:max-line d LN
 d
 .i $l(pcit) d ADD^SSPRINT(REPID,"PRT","?2  PLEASE NOTE THAT THESE ITEMS ARE NOT CLAIMABLE FROM MEDICARE.") q
 .i $l(message) d ADD^SSPRINT(REPID,"PRT","?2  Paycode reference : "_message) q
 d LN,LN
 i 'last d ADD^SSPRINT(REPID,"PRT","?62 Continued over .....")
 d ADD^SSPRINT(REPID,"PRT","~BOLD")
 i $l(PAR5) d
 .i 'PAR5 d ADD^SSPRINT(REPID,"PRT","?1  ** DUPLICATE **")
 .i PAR5 d ADD^SSPRINT(REPID,"PRT","?1  ** REMINDER **")
 .d ADD^SSPRINT(REPID,"PRT","?69 $"_$j(amount,7,2))
 i '$l(PAR5) d
 .d ADD^SSPRINT(REPID,"PRT","?1  ** ORIGINAL **")
 .d ADD^SSPRINT(REPID,"PRT","?30 Total owing on this invoice .....")
 .d ADD^SSPRINT(REPID,"PRT","?69 $"_$j(amount,7,2))
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 f j=1:1:16 d LN
 s xx=$p(^TMP($zn,$j,"REPORT",seq),$c(1))
 i $l(xx) d ADD^SSPRINT(REPID,"PRT","?10 BPAY Ref."_xx)
 d ADD^SSPRINT(REPID,"PRT","?68 "_INVOICE)
 d ADD^SSPRINT(REPID,"PRT","~BOLD"),LN
 ; print first line of episodes
 f j=1,4,7 i $d(list(j)) d
 .i j=1 d ADD^SSPRINT(REPID,"PRT","?50 "_list(j))
 .i j=4 d ADD^SSPRINT(REPID,"PRT","?62 "_list(j))
 .i j=7 d ADD^SSPRINT(REPID,"PRT","?74 "_list(j))
 d LN
 ; print second line of episodes
 f j=2,5,8 i $d(list(j)) d
 .i j=2 d ADD^SSPRINT(REPID,"PRT","?50 "_list(j))
 .i j=5 d ADD^SSPRINT(REPID,"PRT","?62 "_list(j))
 .i j=8 d ADD^SSPRINT(REPID,"PRT","?74 "_list(j))
 d LN
 ; print third line of episodes
 d ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(1))
 f j=3,6,9 i $d(list(j)) d
 .i j=3 d ADD^SSPRINT(REPID,"PRT","?50 "_list(j))
 .i j=6 d ADD^SSPRINT(REPID,"PRT","?62 "_list(j))
 .i j=9 d ADD^SSPRINT(REPID,"PRT","?74 "_list(j))
 d LN,ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(2))
 d LN,ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(3))
 d LN,ADD^SSPRINT(REPID,"PRT","?13 "_ADDRESS(4))
 d ADD^SSPRINT(REPID,"PRT","?68 "_$$extdate2^SSUTIL4(+$h))
 d LN,LN,LN
 d ADD^SSPRINT(REPID,"PRT","?51 Invoice Balance")
 d ADD^SSPRINT(REPID,"PRT","?69 $"_$j(amount,7,2))
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 q
LN d ADD^SSPRINT(REPID,"PRT","!")
 q
]]></Routine>
</Export>
