<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR302G" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR302G ;IG 27/05/98 ;Doctors Reports printing (cumulative) - SJOG-Geelong
 ;
 ; supported calls :
 ;
 ; print^PRTDR302G(vtsRID,type,EpisodeList,DH)
 ;
 ; type          : P - print
 ;                 V - view from Enquiry
 ;                   - view from Entry
print(vtsRID,type,EpisodeList,DH,group,cOrder) n (vtsRID,type,EpisodeList,DH,group,cOrder,tsLM,tsWD,GroupName) 
 s vtsRID=$g(vtsRID),type=$g(type),EpisodeList=$g(EpisodeList),DH=$g(DH),group=$g(group),cOrder=$g(cOrder)
 s epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2),max=6,ColWidth=9,ColLeft=23,FullYear="N",tsLM=$g(tsLM),tsWD=$g(tsWD),(ExcludeList)=""
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS"),^TMP($zn,$j,"Legend")
 i '$$select^LVBEPVIS(epis) d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s range1="(",range2=")",(flaglow,flaghigh)="" i '$$select^MVBCFLAB(1) d
 .i $l(PLIST(18)) s flaglow=PLIST(18)
 .i $l(PLIST(19)) s flaghigh=PLIST(19)
 .i $l(PLIST(20)) s range1=PLIST(20)
 .i $l(PLIST(21)) s range2=PLIST(21)
 .i $l(PLIST(14)) s max=PLIST(14)
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 .i PLIST(37)="Y" s ColWidth=11,FullYear="Y"
 i $e(group)=3 s max=3,ColWidth=16
 k CFSM i '$$select^MVBCFSM(1) m CFSM=PLIST
 i type="P",$d(^TMP("PRTDR00",$j,"TYPE")) s tsLM=0
 k FORMAT,TS
 i '$$getit^LVBCTTSF(,vtsRID,"Y") d
 .k FORMAT f j=1:1:PLIST s tc=$p(PLIST(j),$c(2),10) i $l(tc) s:'$d(FORMAT(tc)) FORMAT(tc)=PLIST(j) s TS($p(vtsRID,"||",2),tc)="",FORMAT(tc,$p(vtsRID,"||",2))=PLIST(j)
 i $l(group),group'=" " d
 .i '$$select^LVBCTTWG(group) m CTTWG=PLIST
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ...i '$$getit^LVBCTTSF(,epis_"||"_ts_"||"_tscnt,"Y") d
 ....f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)="",FORMAT(ti,ts)=PLIST(j)
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..i '$$getit^LVBCTTSF(,epis_"||"_ts_"||1","Y") d
 ...f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)="",FORMAT(ti,ts)=PLIST(j)
 .s ti="" f cnt=1:1 s ti=$o(FORMAT(ti)) q:ti=""  d
 ..i $g(^TTAB("GR",group,"TI",ti)) s $p(FORMAT(ti),$c(2),27)=^(ti)
 ..e  s $p(FORMAT(ti),$c(2),27)=cnt
 ; create list of episodes and results
 k EPTS i $l(EpisodeList) d
 .f col=1:1:$l(EpisodeList,",") s ep=$p(EpisodeList,",",col) i $l(ep) d
 ..s date=$$seldata^LVBEPVIS(ep,12)
 ..s time=$$seldata^LVBEPVIS(ep,13)
 ..s date=$p(date,$c(1)),time=$tr($p(time,$c(1))," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time,vtsList=""
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt,vtsList=vtsList_$s($l(vtsList):",",1:"")_rowid i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt,vtsList=vtsList_$s($l(vtsList):",",1:"")_rowid i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="",vtsList=vtsList_$s($l(vtsList):",",1:"")_vtsRID d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt,vtsList=vtsList_$s($l(vtsList):",",1:"")_rowid i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..s $p(^TMP($zn,$j,"CUM",col),$c(1),4)=vtsList
 k EPTS i '$l(EpisodeList) d
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=VISIT(j)
 .s TestList=test i $l(group),group'=" " s ts="" f  s ts=$o(TS(ts)) q:ts=""  s TestList=TestList_","_ts
 .d CumList^PRTDR00(TestList,epis,visit,cOrder)
 .f col=1:1:PLIST d
 ..s ep=PLIST(col),(date,time)=""
 ..s date=$$seldata^LVBEPVIS(ep,12)
 ..s time=$$seldata^LVBEPVIS(ep,13)
 ..s date=$p(date,$c(1)),time=$tr($p(time,$c(1))," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time,vtsList=""
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt,vtsList=vtsList_$s($l(vtsList):",",1:"")_rowid i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt,vtsList=vtsList_$s($l(vtsList):",",1:"")_rowid i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="",vtsList=vtsList_$s($l(vtsList):",",1:"")_vtsRID d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt,vtsList=vtsList_$s($l(vtsList):",",1:"")_rowid i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..s $p(^TMP($zn,$j,"CUM",col),$c(1),4)=vtsList
 i '$d(^TMP($zn,$j,"CUM-TC")) d  q 100_$c(1)_EpisodeList
 .k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS"),^TMP($zn,$j,"Legend")
 i $$status^LVBVIS1(vtsRID)'="A" d
 .i '$$select^LVBVISTS(vtsRID) d
 ..i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p12h0s0b4099T"_$c(13),"Y")
 ..d ADD^PRTDR00(,"~BOLD","Y")
 ..d LN^PRTDR00("Y"),ADD^PRTDR00(tsLM,"Provisional Report","Y"),LN^PRTDR00("Y")
 ..i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T"_$c(13),"Y")
 ..d ADD^PRTDR00(,"~NORMAL","Y")
 ; print cumulative header
 i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T"_$c(13),"Y")
 s maxcol=$o(^TMP($zn,$j,"CUM",""),-1) s:maxcol>max maxcol=max
 s sample="" k xx f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .s xx="" i '$$select^LVBVISTS(vtsRID) s xx=$p(PLIST(47),$c(1),2) i $l(xx),'$d(xx(xx)) s xx(xx)="",sample=sample_$s($l(sample):"/",1:"")_xx
 i $l(sample) d
 .d ADD^PRTDR00(,"~BOLD","Y")
 .d LN^PRTDR00("Y")
 .d ADD^PRTDR00(tsLM,"Sample : "_sample,"Y")
 .d LN^PRTDR00("Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(,"~BOLD","Y")
 d ADD^PRTDR00(tsLM,"Date Collected","Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1),xx=$p(^TMP($zn,$j,"CUM",col),$c(1),2)
 .i xx s xx=$$extdate2^SSUTIL4(xx) i FullYear="N" s $p(xx,"/",3)=$e($p(xx,"/",3),3,4)
 .i ep=epis d ADD^PRTDR00(,"~BOLD","Y")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .i ep=epis d ADD^PRTDR00(,"~NORMAL","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(,"~BOLD","Y")
 d ADD^PRTDR00(tsLM,"Time Collected","Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .s xx=$$seldata^LVBEPVIS(ep,13)
 .s xx=$s(CFSM(8)=xx:"UNKNOWN",$l(xx):$$exttime^SSUTIL4(xx),1:"N/S")
 .i ep=epis d ADD^PRTDR00(,"~BOLD","Y")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .i ep=epis d ADD^PRTDR00(,"~NORMAL","Y")
 d ADD^PRTDR00(,"~BOLD","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM,"Date Received","Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 s maxcol=$o(^TMP($zn,$j,"CUM",""),-1) s:maxcol>max maxcol=max
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .s xx=$$seldata^LVBEPVIS(ep,61)
 .i xx s xx=$$extdate2^SSUTIL4(xx) i FullYear="N" s $p(xx,"/",3)=$e($p(xx,"/",3),3,4)
 .i ep=epis d ADD^PRTDR00(,"~BOLD","Y")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .i ep=epis d ADD^PRTDR00(,"~NORMAL","Y")
 d ADD^PRTDR00(,"~BOLD","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM,"Time Received","Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .s xx=$$seldata^LVBEPVIS(ep,62)
 .s xx=$s(CFSM(8)=xx:"UNKNOWN",$l(xx):$$exttime^SSUTIL4(xx),1:"N/S")
 .i ep=epis d ADD^PRTDR00(,"~BOLD","Y")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .i ep=epis d ADD^PRTDR00(,"~NORMAL","Y")
 d ADD^PRTDR00(,"~BOLD","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM,"Lab Number","Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s (ep,xx)=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .i ep=epis d ADD^PRTDR00(,"~BOLD","Y")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .i ep=epis d ADD^PRTDR00(,"~NORMAL","Y")
 d ADD^PRTDR00(tsLM+ColLeft+(max*ColWidth),"Units","Y")
 d ADD^PRTDR00(tsLM+ColLeft+(max*ColWidth)+9,"Range","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM,"==============","Y") s delim="",$p(delim,"=",ColWidth)=""
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),delim,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 i type'="P" d
 .d ADD^PRTDR00(tsLM,$e($j("",tsWD),1,ColLeft-3)_" :","Y")
 .s maxcol=$o(^TMP($zn,$j,"CUM",""),-1) s:maxcol>max maxcol=max
 .f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 ..s xx="<"_$p(^TMP($zn,$j,"CUM",col),$c(1),4)_">"
 ..d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 .d LN^PRTDR00("Y")
 ; print test set header only for Cum report page
 i $l(EpisodeList) d
 .i ('$l(group)!(group=" ")),$l(TEST(17)) d ADD^PRTDR00(,"~BOLD"),LN^PRTDR00(),ADD^PRTDR00(tsLM,$s(TEST(17)="*":TEST(3),1:TEST(17))),ADD^PRTDR00(,"~NORMAL"),LN^PRTDR00()
 .i $l(group),group'=" ",$l(GroupName) d ADD^PRTDR00(,"~BOLD"),LN^PRTDR00(),ADD^PRTDR00(tsLM,GroupName),ADD^PRTDR00(,"~NORMAL"),LN^PRTDR00() s GroupName=""
 ; print start of the group
 i $l($g(CTTWG(11))) d
 .d LN^PRTDR00()
 .f j=1:1:$l(CTTWG(11),"|") d ADD^PRTDR00(tsLM,$p(CTTWG(11),"|",j)),LN^PRTDR00()
 ; print results
 i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T"_$c(13)),ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 s order="" f  s order=$o(^TMP($zn,$j,"CUM-TC-ORDER",order)) q:order=""  d
 .s tc=^(order) i '$d(FORMAT(tc)) q
 .i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T"_$c(13)),ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .i type="",$p(FORMAT(tc),$c(2),15)'="Y" d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13))
 .d ADD^PRTDR00(tsLM,$e($tr($p(FORMAT(tc),$c(2),24),":")_$j("",tsWD),1,ColLeft-1))
 .f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 ..s result=$p($g(^TMP($zn,$j,"CUM",col,tc)),$c(1),1),flag=$p($g(^TMP($zn,$j,"CUM",col,tc)),$c(1),2),ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 ..i result="" d
 ...i $d(^TMP($zn,$j,"CUM-EXIST",col,tc)) d  q
 ....s mo=$p(FORMAT(tc),$c(2),8)
 ....i mo="M" s mo="O",x1="" f  s x1=$o(FORMAT(tc,x1)) q:x1=""  i $d(^TEPI(ep,1,x1)) s mo=$p(FORMAT(tc,x1),$c(2),8) i mo="M" q
 ....i mo="M" s result="TF",^TMP($zn,$j,"Legend","TF",1)="Result to follow"
 ....e  s result=""
 ..s result=$j(result,ColWidth-3)
 .. ; print extra info for numeric fields
 ..i flag="L" d
 ...i "MN"[$e($p(FORMAT(tc),$c(2),18)),$l(flaglow) s result=result_" "_flaglow
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ..i flag="H" d
 ...i "MN"[$e($p(FORMAT(tc),$c(2),18)),$l(flaghigh) s result=result_" "_flaghigh
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ..d ADD^PRTDR00(,$c(13))
 ..d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),result)
 ..i (flag="L")!(flag="H") d
 ...i type="",$p(FORMAT(tc),$c(2),15)'="Y" d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13)) q
 ...d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .s units=$p(FORMAT(tc),$c(2),17)
 .d ADD^PRTDR00(tsLM+ColLeft+(max*ColWidth),units)
 .i $l($p(FORMAT(tc),$c(2),22)),$l($p(FORMAT(tc),$c(2),23)) d
 ..i $e($p(FORMAT(tc),$c(2),22))?1N s $p(FORMAT(tc),$c(2),22)=$j($p(FORMAT(tc),$c(2),22),0,$e($p(FORMAT(tc),$c(2),18),2,4))
 ..i $e($p(FORMAT(tc),$c(2),23))?1N s $p(FORMAT(tc),$c(2),23)=$j($p(FORMAT(tc),$c(2),23),0,$e($p(FORMAT(tc),$c(2),18),2,4))
 ..d ADD^PRTDR00(tsLM+ColLeft+(max*ColWidth)+9,range1_$p(FORMAT(tc),$c(2),22)_"-"_$p(FORMAT(tc),$c(2),23)_range2)
 .i $l($p(FORMAT(tc),$c(2),22)),'$l($p(FORMAT(tc),$c(2),23)) d
 ..i $e($p(FORMAT(tc),$c(2),22))?1N s $p(FORMAT(tc),$c(2),22)=$j($j($p(FORMAT(tc),$c(2),22),0,$e($p(FORMAT(tc),$c(2),18),2,4)),$p(FORMAT(tc),$c(2),19)+$l(range1))
 ..d ADD^PRTDR00(tsLM+ColLeft+(max*ColWidth)+9,range1_$p(FORMAT(tc),$c(2),22)_range2)
 .i type="",$p(FORMAT(tc),$c(2),15)'="Y" d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .d LN^PRTDR00()
 i $d(^TMP($zn,$j,"Legend")) d
 .s xx1="" f  s xx1=$o(^TMP($zn,$j,"Legend",xx1)) q:xx1=""  d
 ..i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T")
 ..d ADD^PRTDR00(,"~BOLD"),ADD^PRTDR00(tsLM,xx1),ADD^PRTDR00(,"~NORMAL")
 ..s xx2="" f  s xx2=$o(^TMP($zn,$j,"Legend",xx1,xx2)) q:xx2=""  d
 ...s xx=^(xx2) i type="P" s xx=$tr(xx,$c(16,17,18),"^\|")
 ...i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T"_$c(13))
 ...d ADD^PRTDR00(tsLM+10,xx),LN^PRTDR00()
 i $d(^TMP($zn,$j,"CUM-COMMENTS")) d
 .s col="" f comm=0:0 s col=$o(^TMP($zn,$j,"CUM-COMMENTS",col)) q:col=""  d
 ..d LN^PRTDR00()
 ..i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p14h0s0b4099T")
 ..d ADD^PRTDR00(,"~Break"),ADD^PRTDR00(,"~BOLD"),ADD^PRTDR00(,"~ITALIC")
 ..d ADD^PRTDR00(tsLM,$e($s('comm:"Comment(s) for",1:"")_$j("",15),1,15)_^TMP($zn,$j,"CUM-COMMENTS",col,1))
 ..d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,"~ITALIC"),LN^PRTDR00()
 ..f j=2:1 q:'$d(^TMP($zn,$j,"CUM-COMMENTS",col,j))  d
 ...f j1=1:1:$l(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^") d
 ....s xx=$p(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^",j1) i type="P" s xx=$tr(xx,$c(16,17,18),"^\|")
 ....i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T"_$c(13))
 ....d ADD^PRTDR00(tsLM,xx)
 ...d LN^PRTDR00()
 ; print end of the group
 i $l($g(CTTWG(12))) d
 .f j=1:1:$l(CTTWG(12),"|") d ADD^PRTDR00(tsLM,$p(CTTWG(12),"|",j))
 i type="P",'$d(^TMP("PRTDR00",$j,"TYPE")) d ADD^PRTDR00(,$c(27)_"(s0p13h0s0b4099T"_$c(13)),ADD^PRTDR00(,"~NORMAL")
 d LN^PRTDR00()
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-COMMENTS"),^TMP($zn,$j,"Legend")
 q 0
 ; collect Test Items information
TestItems(col,rowid) n (col,rowid,ExcludeList,type,FORMAT,vtsRID,TS,tsLM,tsWD,ColWidth,CFSM,max) s col=$g(col),rowid=$g(rowid)
 i '$$select^LVBVISTS(rowid,"Y") d
 .m ^TMP($zn,$j,"CUM-EXIST",col)=TS($p(rowid,"||",2))
 .s status=$$status^LVBVIS1(rowid),Result=1
 .i status'="A",(type="V")!$d(^TMP("PRTDR00",$j,"TYPE")) d
 ..i $$checkView1^LVBVIS5(rowid) s Result=0 q
 ..i $$checkView2^LVBVIS5(rowid) s Result=0 q
 .i '$$results^LVBVISTS(rowid) f j=1:1:PLIST d
 ..s tc=$p(PLIST(j),$c(1),2),(result,flag)=""
 ..i Result s result=$p(PLIST(j),$c(1),3),flag=$p(PLIST(j),$c(1),5)
 ..i '$d(FORMAT(tc)) q
 .. ; not included in cumulative
 ..i $p(FORMAT(tc),$c(2),20)'="Y" q
 ..i $l(tc),$l(result) d
 ... ; non reportable test item
 ...i $l(type),$p(FORMAT(tc),$c(2),15)'="Y" q
 ...i $e($p(FORMAT(tc),$c(2),18),1,2)="B2" d IB2
 ...i $e($p(FORMAT(tc),$c(2),18))="N" d IN
 ...i $e($p(FORMAT(tc),$c(2),18))="D" d ID^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="T" d IT^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="Y" d IY^PRTDR00
 ...i $e($p(FORMAT(tc),$c(2),18))="S" s cnt=tc,resultx=result d IS^PRTDR00 d
 ....i $o(result(""),-1)=1,$l(result(1))<ColWidth s x=result(1) k result s result=x q
 ....m ^TMP($zn,$j,"Legend",resultx)=result k result s result=resultx
 ...s ^TMP($zn,$j,"CUM",col,tc)=result_$c(1)_flag
 ...i '$d(^TMP($zn,$j,"CUM-TC",tc)) d
 ....s order=$p(FORMAT(tc),$c(2),27) i '$l(order) s order=$p(FORMAT(tc),$c(2),2)
 ....i $l(order) s ^TMP($zn,$j,"CUM-TC-ORDER",order)=tc,^TMP($zn,$j,"CUM-TC",tc)=""
 . ; compile test set comments
 .k PLIST i '$$COMMENTS^LVBCOM01(rowid,,type,"N",,"N","Y",.ExcludeList) i PLIST d
 ..i '$d(^TMP($zn,$j,"CUM-COMMENTS",col,1)) d
 ...s epis=$p(rowid,"||")
 ...s date=$$seldata^LVBEPVIS(epis,12),time=$$seldata^LVBEPVIS(epis,13)
 ...s time=$tr(time," "),time=$s(CFSM(8)=time:"UNKNOWN",$l(time):$$exttime^SSUTIL4(time),1:"N/S")
 ...s ^TMP($zn,$j,"CUM-COMMENTS",col,1)="Date : "_$$extdate2^SSUTIL4(date)
 ...s ^TMP($zn,$j,"CUM-COMMENTS",col,1)=^TMP($zn,$j,"CUM-COMMENTS",col,1)_"  Time : "_time
 ...s ^TMP($zn,$j,"CUM-COMMENTS",col,1)=^TMP($zn,$j,"CUM-COMMENTS",col,1)_"  Episode : "_epis
 ..f j1=1:1:PLIST s x1=$o(^TMP($zn,$j,"CUM-COMMENTS",col,""),-1)+1,^TMP($zn,$j,"CUM-COMMENTS",col,x1)=PLIST(j1)
 q
IN ; Numeric
 i $l(result),$d(^TTAB("RC",result)) d  q
 .i $l($p(^TTAB("RC",result),"\"))<ColWidth d  q
 ..s result=$p(^TTAB("RC",result),"\")
 ..s result=$j(result,$e($p(FORMAT(tc),$c(2),18),2,9))
 .s ^TMP($zn,$j,"Legend",result,1)=$p(^TTAB("RC",result),"\")
 .s result=$j(result,$e($p(FORMAT(tc),$c(2),18),2,9))
 s Flag="" f  q:'$l(result)  q:"<>+-"'[$e(result)  s Flag=Flag_$e(result),result=$e(result,2,$l(result))
 s result=Flag_$j(result,0,$e($p(FORMAT(tc),$c(2),18),2,9))
 q
IB2 ; Blood Group
 n (result,tc,FORMAT,cnt)
 i $d(^TTAB("BB-BG",result)) s result=$p($p(^(result),"\",1),"/",1) q
 q
 ; check if prev. episode uses current ranges
CheckRange(vtsRID,rowid,tc) n (vtsRID,rowid,tc) s vtsRID=$g(vtsRID),rowid=$g(rowid),tc=$g(tc)
 s rowid(1)=vtsRID,rowid(2)=rowid
 f i=1,2 d
 . s date=$$seldata^LVBVISTS(rowid(i),7)
 .s date(i)=$p($g(date),$c(1)) i '$l(date(i)) s date(i)=+$h
 f i=1,2 i '$d(^TTAB("TC",tc,4,date(i))) s date(i)=$o(^TTAB("TC",tc,4,date(i)),-1)
 q $s(date(1)=date(2):"",1:"#")
]]></Routine>
</Export>
