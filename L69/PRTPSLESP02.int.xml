<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTPSLESP02" type="INT" languagemode="0" generated="1"><![CDATA[
PRTPSLESP02 ; Packing Slip BADALONA
 ; Copy from PRTPSL01
 ; Changed by Mc_Fly june, 2003
 ; Trak Health BCN
 q
 ; Parameters :  1. Packing slip Number
 ;               2. From User Site
 ;               3. To User Site
 ;               4. To Refferal Lab
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql)
 k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Tramesa número "_PAR1
 s line=2,xHEADER(line)="De : "_FROM(3)
 i $l(PAR3) d
 .s line=3,xHEADER(line)="A : "_TO(3)
 i $l(PAR4) d
 .s line=3,xHEADER(line)="A : "_TO(3)
 d BODY
 i CNT1 s line=line+1,xHEADER(line)=CNT1_" peticions"
 i CNT2 s line=line+1,xHEADER(line)=CNT2_" proves"
 s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),PRINT,FILE,LABELS
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 k FROM i '$$select^LVBCTUSL(PAR2) m FROM=PLIST
 k TO i $l(PAR3),'$$select^LVBCTUSL(PAR3) m TO=PLIST
 i $l(PAR4),'$$select^LVBCTRL(PAR4) m TO=PLIST
 q
BODY ; create temporary printing file
 s (CNT1,CNT2)=""
 i $l(PAR3) d
 .s epis="" f  s epis=$o(^TPSL(PAR1,PAR2,"US",PAR3,epis)) q:epis=""  d
 .. s CNT1=CNT1+1
 .. s move="" f  s move=$o(^TPSL(PAR1,PAR2,"US",PAR3,epis,move)) q:move=""  d
 ... s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s CNT2=CNT2+1
 ... s ^TMP($zn,$j,"REPORT",epis_"||"_move)=""
 i $l(PAR4) d
 .s epis="" f  s epis=$o(^TPSL(PAR1,PAR2,"RL",PAR4,epis)) q:epis=""  d
 .. s CNT1=CNT1+1
 ..s move="" f  s move=$o(^TPSL(PAR1,PAR2,"RL",PAR4,epis,move)) q:move=""  d
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s CNT2=CNT2+1
 ...s ^TMP($zn,$j,"REPORT",epis_"||"_move)=""
 ...i $l(TO(6)) s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  d
 .... ; No Waiting Items Mc_Fly June 26, 2003
 .... i $p(^TEPI(epis,9,move),"\",9)'="W" s ^TMP($zn,$j,"FILE",epis,x)="" 
 q
PRINT ; print report
 i '$d(^TMP($zn,$j,"REPORT")) q
 s (epis,move)=""
 f  s move=$o(^TMP($zn,$j,"REPORT",move)) q:move=""  d
 . k VISIT s oepis=epis,epis=$p(move,"||",1),mv=$p(move,"||",2)
 . i oepis'=epis,'$$select^LVBEPVIS(epis) m VISIT=PLIST
 . d test
 . d PRINT^PRTREP00(.VISIT,.N) k VISIT,xxx
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 k PLIST
 i $$select^LVBCTCLR("PSL")
 i $g(PLIST(19))="Y" d PRINTEND^PRTREP00(xLINE(1))
 q
LABELS 
 i $$START^PRTREP00(USER,,TO(3),"CLNPSL")
 q
test 
 s x=""
 f  s x=$o(^TEPI(epis,9,mv,"VTS",x)) q:x=""  d
 . s ts=$p(x,"||",2)
 . i '$$select^LVBCTTS(ts,"Y") d
 .. s xxx(1)=$g(xxx(1))_$s($g(xxx(1))'="":", ",1:"")_PLIST(4)
 .. s spec=$o(^TTAB("TS",ts,1,""))
 .. i spec'="" s xxx(2)=$g(xxx(2))_$p(^TTAB("SPEC",spec),"\",1)
 .. i spec="" s spec=" "
 .. s ^TMP($zn,$j,"LABELS",epis,spec)=$g(^TMP($zn,$j,"LABELS",epis,spec))+1
 q
FILE 
 ; ---------------------------
 ;  Create file
 ; ---------------------------
 i $e(TO(1),1,3)'="LRC" q
 i '$d(^TMP($zn,$j,"FILE")) q
 s error=0
 s pathfile=TO(6)
 i $e(pathfile,$l(pathfile))'="\" s pathfile=pathfile_"\"
 s ^fly($zn,$j,$i(^fly($zn,$j)))=$zd($h)_" "_$zt($p($h,",",2))_" pathfile="_pathfile
 s filename=$e($$datetime($h),1,12)_".pet"
 s file=pathfile_filename
 s filecopy=pathfile_"copia\"_filename
 d set("H|\^&|||HMB|||||LRC||P||"_$$datetime($h))
 s totalepi=0
 s epis=""
epis 
 s epis=$o(^TMP($zn,$j,"FILE",epis))
 i epis="" g end
 i $$select^LVBEPVIS(epis,"Y")'=0 g epis
 s surname=PLIST(3)
 s givenname=PLIST(4)
 s sex=PLIST(5)
 s dob=PLIST(6)
 i dob'="" s dob=$zd(dob,8)
 s collecdate=PLIST(12)
 s collectime=PLIST(13)
 s requestdate=PLIST(14)
 s doc=PLIST(15) ; ^TTAB("DR",*
 s mrn=PLIST(20)
 s service=PLIST(22) ; ^TTAB("RH",*
 s age=PLIST(26)
 s requesttime=PLIST(111)
 s wdiag=PLIST(53)
 s diag=$p($p(wdiag,"DIAGNÒSTIC:",2),"@@",1)
 i $e(diag,$l(diag))=" " s diag=$e(diag,1,$l(diag)-1)
 s obs=""
 s surname1=surname
 s surname2=""
 i $l(surname," ")=2 s surname1=$p(surname," ",1),surname2=$p(surname," ",2)
 s totalepi=totalepi+1
 s line="P|"_totalepi_"|"_epis_"|"_mrn_"||"_surname1_"^"_surname2_"^"_givenname
 s line=line_"||"_dob_"|"_sex_"|||||"_doc_"|"_diag_"||||"_obs
 s line=line_"|"_$$datetime(requestdate_","_requesttime)
 s line=line_"^"_$$datetime(collecdate_","_collectime)_"|||||||||"_service
 d set(line)
 s totalts=0
 k sendres
 s tsid=""
tsid 
 s tsid=$o(^TMP($zn,$j,"FILE",epis,tsid))
 i tsid="" d sendres g epis
 s ts=$p(tsid,"||",2)
 i $$select^LVBCTTS(ts,"Y")'=0 g tsid
 s lrctss=PLIST(36) ; National name Long. Syntax: {Codi prova LRC}, ...
 i lrctss="" d  g tsid
 . d ADDLINE^PRTREP00(REPID,"PRT","!")
 . d ADDLINE^PRTREP00(REPID,"PRT","Falta definir el codi de la prova "_ts_" pel LRC")
 . s error=1
 f n=1:1:$l(lrctss,",") s lrcts=$p(lrctss,",",n) s x=$$add(epis,lrcts)
 d findres
 g tsid
sendres 
 s (lrcts,lrcnum)=""
 f  s lrcts=$o(sendres(lrcts)) q:lrcts=""  d
 . i '$$add(epis,lrcts) q
 . f  s lrcnum=$o(sendres(lrcts,lrcnum)) q:lrcnum=""  d set(sendres(lrcts,lrcnum))
 q
add(epis,lrcts) 
 i lrcts="" q 0
 i $d(^TMP($zn,$j,"EPIS",epis,lrcts)) q 0
 s ^TMP($zn,$j,"EPIS",epis,lrcts)=""
 s totalts=totalts+1
 d set("O|"_totalts_"|"_epis_"||^^^"_lrcts_"|||||||A|||||||||||O")
 q 1
findres 
 s ord=""
ord s ord=$o(^TTAB("TS",ts,0,ord))
 i ord="" q
 s tc=$p(^(ord),"\",8)
 i tc="" g ord
 i $$select^LVBCTTC(tc,"Y")'=0 g ord
 s units=PLIST(4)
 s lrctsnum=PLIST(38) ; National name Long. Syntax {codi prova LRC}-{numero resultat}
 i lrctsnum="" g ord
 s lrcts=$p(lrctsnum,"-",1)
 s lrcnum=$p(lrctsnum,"-",2)
 i lrcnum="" d  g ord
 . d ADDLINE^PRTREP00(REPID,"PRT","!")
 . s mess="Falta informar el número de resultat pel TestItem "
 . s mess=mess_tc_" quan el codi del LRC és "_lrcts
 . d ADDLINE^PRTREP00(REPID,"PRT",mess)
 . s error=1
 s range=$$tcrange(tc,sex,age)
 s pat=""
 i $l($p(range,"^",1)),value<$p(range,"^",1) s pat="A"
 i $l($p(range,"^",2)),value>$p(range,"^",2) s pat="A"
 k PLIST
 i $$select^LVBVISTD(tsid_"||"_tc)
 s value=$g(PLIST(3))
 i value="" d  g ord
 . d ADDLINE^PRTREP00(REPID,"PRT","!")
 . d ADDLINE^PRTREP00(REPID,"PRT","Falta el resultat de la prova "_$p(tsid,"||",2)_", item "_tc_" de la petició "_epis)
 . s error=1
 s sendres(lrcts,lrcnum)="R|"_lrcnum_"|^^^"_lrcts_"|"_value_"|"_units_"|"_range_"|"_pat_"||F"
 g ord
end 
 d set("L|1|F")
 i error q
 i $d(^hmb2lrc(PAR1)) d  q
 . d ADDLINE^PRTREP00(REPID,"PRT","!")
 . d ADDLINE^PRTREP00(REPID,"PRT","Ja s'havia generat el fitxer per la tramesa "_PAR1)
 o filecopy:"WNS":2 e  d  q
 . d ADDLINE^PRTREP00(REPID,"PRT","!")
 . d ADDLINE^PRTREP00(REPID,"PRT","No es pot desar el fitxer <"_filecopy_">")
 u filecopy
 s i="",sw=0
 f  s i=$o(^TMP($zn,$j,"OUT",i)) q:i=""  d
 . i sw w !
 . w ^(i) s sw=1
 c filecopy
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT","Fitxer desat correctament <"_filecopy_">")
 s ok=$zf(-1,"copy "_filecopy_" "_file)
 i ok=0 s ^hmb2lrc(PAR1)=$h
 i ok=1 d
 . d ADDLINE^PRTREP00(REPID,"PRT","!")
 . d ADDLINE^PRTREP00(REPID,"PRT","No s'ha pogut fer la copia a <"_file_">")
 q
set(line) 
 s ^TMP($zn,$j,"OUT",$i(^TMP($zn,$j,"OUT")))=line
 q
datetime(h) 
 n date,time
 s date=$p(h,",",1)
 s time=$p(h,",",2)
 q $zd(date,8)_$tr($zt(time),":")
tcrange(tc,sex,age) 
 n (tc,sex,age)
 s date=$o(^TTAB("TC",tc,4,""),-1) i date="" q ""
 s (ret,ord)=""
 f  s ord=$o(^TTAB("TC",tc,4,date,0,ord)) q:ord=""  d  q:ret'=""
 . s reg=^TTAB("TC",tc,4,date,0,ord)
 . q:$p(reg,"\",22)'=sex
 . s ag=$p(reg,"\",23) i ag["-",$p(ag,"-",1)>age!($p(ag,"-",2)<age) q
 . s ret=$p(reg,"\",1)_"^"_$p(reg,"\",2)
 q ret
]]></Routine>
</Export>
