<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBXMR24" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBXMR24 ; ML 03/05/2004 ; BloodBank XMatch report - ARL (24)
 q
 ; Parameters :  1. vtsRID
 ;               2. Event : U-Update
 ;                           -Authorize
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j),^TMP("PRTDR00",$j),^TMP("REPORT",$j)
 k xHEADER d INIT,BODY,PRINT
 k ^TMP($zn,$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 s site="" i '$$select^MVBCFSM(1) s site=$p(PLIST(10),"-")
 q
BODY ; create report
 s vtsRID=PAR1,epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2)
 k VISTE i '$$getall^LVBVISTE(vtsRID) d
 .f j=1:1:PLIST d
 ..i PAR2="U",$p($p($p(PLIST(j),$c(2),4),$c(28),12),$c(1))'="Y" q
 ..s x=$p($p(PLIST(j),$c(2),4),$c(28),13),VISTE(x)=$p(PLIST(j),$c(2),4)
 i '$d(VISTE) q
 k VISTD i '$$results^LVBVISTS(vtsRID,,,,"Y") m VISTD=PLIST
 k VISTS i '$$select^LVBVISTS(vtsRID) m VISTS=PLIST
 k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 ; get doctor information
 s doc=$p(VISIT(15),$c(1)) k DOC i '$$select^LVBCTDR(doc,"Y") m DOC=PLIST
 s dep="",tsLM=0,tsWD=80
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s tsWD=80 i '$$select^MVBCFLAB(1) d
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 ; print xmatch grid report
 s $p(LINE,"_",85)="",TEXT="",UL=LINE,cntline="" 
 ; sort by transaction
 k TRANS s index="" f  s index=$o(VISTE(index)) q:index=""  d
 .s trans=$p($p(VISTE(index),$c(28),5),$c(1),1),transdesc=$p($p(VISTE(index),$c(28),5),$c(1),2)
 .q:'$l(trans)
 .f j=1:1:$l(VISTE(index),$c(28)) s @("x"_j)=$p(VISTE(index),$c(28),j)
 .k PACK i '$$select^LVBBBP($p(x2,$c(1))_"||"_$p(x3,$c(1))) m PACK=PLIST 
 .s product=$p(PACK(8),$c(1))
 .s TRANS(trans)=transdesc
 .s TRANS(trans,product,index)=""
 ; print pack details
 s trans="" f  s trans=$o(TRANS(trans)) q:trans=""  d
 .i $o(TRANS(trans),-1)'="" d ADD^PRTDR00(,"#")
 .d TranHead
 .s product=""
 . ; print blood group
 .d BloodGrp
 . ; insert grid
 .d XMGrid
 . ; insert title boxes
 .d Title
 .s prod="" f  s prod=$o(TRANS(trans,prod)) q:prod=""  d
 ..s (cntline,index)="" f  s index=$o(TRANS(trans,prod,index)) q:index=""  d
 ...f j=1:1:$l(VISTE(index),$c(28)) s @("x"_j)=$p(VISTE(index),$c(28),j)
 ...k PACK i '$$select^LVBBBP($p(x2,$c(1))_"||"_$p(x3,$c(1))) m PACK=PLIST
 ...i '$l(product) s product=$p(PACK(8),$c(1))
 ...s cntline=cntline+1
 ...s vert=$s(cntline=1:3770,cntline=2:4005,cntline=3:4240,cntline=4:4475,cntline=5:4710,1:4945)
 ...d ADD^PRTDR00(,$c(27)_"&a540h"_vert_"V"_$s($l($p(PACK(8),$c(1),2))>14:$p(PACK(8),$c(1)),1:$p(PACK(8),$c(1),2)))
 ...d ADD^PRTDR00(,$c(27)_"&a1455h"_vert_"V"_$p(PACK(2),$c(1)))
 ...d ADD^PRTDR00(,$c(27)_"&a2055h"_vert_"V"_$p($p(PACK(9),$c(1),2),"/",2))
 ...i cntline>5,$o(VISTE(index))'="" d  q
 ....d TranCom,XMComm
 ....d ADD^PRTDR00(,"#")
 ....d TranHead,BloodGrp,XMGrid,Title s cntline=""
 ...f i=1:1:2 d LN^PRTDR00()
 .. ; save patient events
 ..d events^LVBCOM01(epis,"XMR","Report type "_trans_"-"_$g(REPID))       
 .. ; transaction comment
 ..d TranCom
 .. ; xmatch comment
 ..d XMComm
 .. ; back to normal
 ..d ADD^PRTDR00(,$c(27)_"(s0p12h0s0b4099T"),LN^PRTDR00()
 ; create page for printing
 k PLIST m PLIST=^TMP("PRTDR00",$j,"RESULT") s PLIST=$o(PLIST(""),-1)
 s MaxLine=57
 k ^TMP("PRTDR00",$j)
 s page=1,^TMP("REPORT",$j,page,"TEST",vtsRID)=""
 s PageLine="" f jLine=1:1:PLIST d
 .i PLIST(jLine)="!" s PageLine=PageLine+1 i PageLine>MaxLine s PageLine="",page=page+1
 .i PLIST(jLine)="#" s PageLine="",page=page+1,^TMP("REPORT",$j,page,"TEST",vtsRID)="" q
 .s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1,^TMP("REPORT",$j,page,"TEXT",x)=PLIST(jLine)
 q
PRINT ; print report
 ; xPAR1 - U-User
 ;         C-Courier
 ; xPAR2 - User RowID/Courier
 s xPAR1="C",xPAR2="XMR"
 s page="" f  s page=$o(^TMP("REPORT",$j,page)) q:page=""  d  k ^TMP("REPORT",$j,page)
 . ; print page Header
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 .i $l(%routine) x "d Header^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 . ; print report
 .f j=1:1 q:'$d(^TMP("REPORT",$j,page,"TEXT",j))  s xx=^(j) d
 ..i xx="#" d  q
 ... ; print page Footer
 ...s vtsRID=PAR1
 ...s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 ...i $l(%routine) x "d Footer^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 ...d ADDLINE^PRTREP00(REPID,"PRT",xx)
 ... ; print page Header
 ...i $l(%routine) x "d Header^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 ..d ADDLINE^PRTREP00(REPID,"PRT",xx)
 . ; print page Footer
 .s vtsRID=PAR1
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 .i $l(%routine) x "d Footer^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 q
 ; transaction header
TranHead d LN^PRTDR00()
 d ADD^PRTDR00(,$c(27)_"(s0p10h0s0b4099T")
 d ADD^PRTDR00(,"~BOLD")
 d ADD^PRTDR00(tsLM-2,TRANS(trans))
 d ADD^PRTDR00(,"~NORMAL")
 d LN^PRTDR00()
 q
 ; transaction comment
TranCom k TranCom s TranCom=""
 i trans="EM" d
 .s TranCom="Suitable units offered before testing completed - Final report to follow.||Products Issued by:                                                      Scientist Name: "
 i trans="STDXM" d
 .s TranCom="Routine crossmatch and antibody screen using correctly collected and labelled sample finds no incompatibility|detected.|Products Issued by:                                                      Scientist Name: "
 i trans="EXTXM" d
 .s TranCom="Extended crossmatch using correctly collected and labelled sample finds no significant incompatibility in the above|units.|Products Issued by:                                                      Scientist Name: "
 i trans="MTRANS" d
 .s TranCom="Transfusion of greater than 10 units after which suitable units are offered without further testing||Products Issued by:                                                      Scientist Name: "
 i trans="GS" d
 .s TranCom="Products of suitable blood group.||Products Issued by:                                                      Scientist Name: "
 i trans="ISS" d
 .s TranCom="||Products Issued by:                                                      Scientist Name: "
 i trans="INCOM" d
 .s TranCom="O Negative blood issued due to the nature of the antibody/emergency transfuse with caution if clinically essential.||Products Issued by:                                                      Scientist Name: "
 i $l(TranCom) d
 .s item="B0100" f j=1:1:VISTD s ti=$p(VISTD(j),$c(1),2) i ti=item d  q
 ..s result=$p(VISTD(j),$c(1),3) i $l(result),'$$select^LVBCTTCT(item_"||"_result) d
 ...s result=PLIST(3) s TranCom=TranCom_PLIST(3)
 .d ADD^PRTDR00(,$c(27)_"(s1p9v0s0b4148T") s vert=5000
 .f i=1:1:3 s vert=vert+100 i $p(TranCom,"|",i)'="" d ADD^PRTDR00(,$c(27)_"&a520h"_vert_"V"_$p(TranCom,"|",i))
 q
 ; print blood group
BloodGrp d ADD^PRTDR00(,$c(27)_"(s0p12h0s0b4099T")
 d LN^PRTDR00()
 i $l(VISIT(69)) d
 .d ADD^PRTDR00(,"~BOLD")
 .d ADD^PRTDR00(tsLM,"Group: ")
 .d ADD^PRTDR00(,"~NORMAL")
 .d ADD^PRTDR00(tsLM+8,$p($$seldata^LVBBBBG($p(VISIT(69),$c(1),1),3),"/",2))
 . ; date valid till
 .s result="",item="B0101" d
 ..f j=1:1:VISTD s ti=$p(VISTD(j),$c(1),2) i ti=item d  q
 ...d ADD^PRTDR00(tsLM+40,"Valid Until: ")
 ...d ADD^PRTDR00(tsLM+54,$s($p(VISTD(j),$c(1),3)'="":$$extdate2^SSUTIL4($p(VISTD(j),$c(1),3)),1:""))
 .d LN^PRTDR00()
 ; antibody screen
 d
 .s result="",item="B0011"
 .f j=1:1:VISTD s ti=$p(VISTD(j),$c(1),2) i ti=item d  q
 ..s result=$p(VISTD(j),$c(1),3) i $l(result),'$$select^LVBCTTCT(item_"||"_result) d
 ...d ADD^PRTDR00(,"~BOLD")
 ...d ADD^PRTDR00(tsLM,"Antibody Screen: ")
 ...d ADD^PRTDR00(,"~NORMAL")
 ...s result=PLIST(3) f j1=1:1:$l(result,"|") d ADD^PRTDR00(tsLM+19,$p(result,"|",j1)),LN^PRTDR00()
 ; xmatch comment
 d
 .s result="",item="B0102"
 .f j=1:1:VISTD s ti=$p(VISTD(j),$c(1),2) i ti=item d  q
 ..s result=$p(VISTD(j),$c(1),3) i $l(result),'$$select^LVBCTTCT(item_"||"_result) d
 ...d LN^PRTDR00()
 ...s result=PLIST(3) f j1=1:1:$l(result,"|") d ADD^PRTDR00(tsLM,$p(result,"|",j1)),LN^PRTDR00()
 ..s result=$p(VISTD(j),$c(1),4) i $l(result) d
 ...d LN^PRTDR00()
 ...f j1=1:1:$l(result,"|") d ADD^PRTDR00(tsLM,$p(result,"|",j1)),LN^PRTDR00()
 q
 ; print xm grid
XMGrid s x="" f  s x=$o(^SS("QDR","GVH-XMR",x)) q:x=""  d ADD^PRTDR00(,^(x))
 d LN^PRTDR00()
 q
 ; title boxes
Title f i=1:1:2 d LN^PRTDR00()
 d ADD^PRTDR00(,"~BOLD")
 f i="27:Pack","46:Commencement","61:Finish","75:Commenced by" d ADD^PRTDR00($p(i,":"),$p(i,":",2))
 d LN^PRTDR00()
 f i="12:Product","26:Number","37:Group","48:Date/Time","60:Date/Time","71:Signature","82:Signature" d ADD^PRTDR00($p(i,":"),$p(i,":",2))
 d ADD^PRTDR00(,"~NORMAL")
 f i=1:1:2 d LN^PRTDR00()
 q
 ; xmatch comment
XMComm d ADD^PRTDR00(,$c(27)_"(s1p9v0s0b4148T") s vert=5500,xmCom=""
 s xmCom="To be completed by persons administering blood:|"
 s xmCom=xmCom_"Every blood transfusion unit must be setup or changed by a Medical Officer or Registered Nurse.  In every case a|"
 s xmCom=xmCom_"second Medical Officer or Registered Nurse must check the compatibility label.  These persons must sign this form|"
 s xmCom=xmCom_"against the relevant pack numbers and the pack label and thereby declare that:|"
 s xmCom=xmCom_"  1.  The pack number shown on the compatibility label is the same as the number on the pack.|"
 s xmCom=xmCom_"  2.  The patient's identity has been checked against the compatibility label and this sheet.|"
 s xmCom=xmCom_"  3.  The compatibility label and this sheet show the pack to be compatible with the patient.|"
 s xmCom=xmCom_"  4.  The expiry dates of the pack and crossmatch have been checked.|"
 s xmCom=xmCom_"Please note that some blood products do not require crossmatching and may be issued without compatibility label."
 f i=1:1:$l(xmCom,"|") s vert=vert+120 d ADD^PRTDR00(,$c(27)_"&a520h"_vert_"V"_$p(xmCom,"|",i))
 q
]]></Routine>
</Export>
