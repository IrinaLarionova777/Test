<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR004" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR004 ; IG 23/03/99 ; Doctor's Report HEADER - Territory Health Services
 q
 ; supported calls
 ;
 ; INIT^PRTDR004
 ; QDR^PRTDR004
 ; Header^PRTDR004(page,par1,par2)
 ; Footer^PRTDR004(page,par1,par2)
 ;
 ; client specific parameters
INIT s MaxLine=38
 q
 ; create normal layouts
Create1(vtsRID,cum,group,cOrder) 
 s DH=dh1,PrintTestHeader=1
 s type="P" i '$$SINGLE^PRTDR00(vtsRID,type,cum,,DH,group,cOrder),PLIST d
 .s BodyLength=$g(Breaks) k cPartialHeader
 .s TestHeader=0 f j=1:1:HEADER i HEADER(j)="!" s TestHeader=TestHeader+1
 .s TestBody=0 f j=1:1:PLIST i PLIST(j)="!",j<PLIST s TestBody=TestBody+1
 .s LeftLines=TestBody
 .s test=$p(vtsRID,"||",2),StopSplit=$$seldata^LVBCTTS(test,25)
 .s Start="Y",NewPage=ForceNewPage f jLine=1:1:PLIST d
 ..i $o(^TMP("REPORT",$j,""))="" s NewPage=1
 ..i 'NewPage,jLine=1,StopSplit="Y",PageLine+TestHeader+TestBody+2>MaxLine s NewPage=1
 ..i 'NewPage,jLine=1,BodyLength,StopSplit="T",PageLine+TestHeader+BodyLength+1>MaxLine s NewPage=1
 ..i 'NewPage,jLine=1,TestHeader,TestBody,PageLine+TestHeader+1>MaxLine s NewPage=1
 ..i 'NewPage,$d(Breaks(jLine)),PageLine+TestHeader+Breaks(jLine)>MaxLine s NewPage=1
 ..i 'NewPage,PageLine+LeftLines>MaxLine,PageLine+TestHeader>MaxLine s NewPage=1
 ..i (jLine=1)!NewPage d
 ...i 'NewPage d OldPage^PRTDR00(vtsRID)
 ...i NewPage d NewPage^PRTDR00(vtsRID)
 ..i Start="Y" d  s Start="N"
 ...i REPORT(5)="Y" d PrintDepHeader^PRTDR00
 ...d PrintSection^PRTDR00("H")
 ...d PrintTestHeader^PRTDR00(1)
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1,^TMP("REPORT",$j,page,"TEXT",x)=PLIST(jLine)
 ..i PLIST(jLine)="!" s PageLine=PageLine+1,LeftLines=LeftLines-1 i PageLine>MaxLine s NewPage=1 i jLine=PLIST q
 ..i NewPage s x="" f  s x=$o(PartialHeader(x)) q:x=""  i jLine>$p(PartialHeader(x),",",1),jLine<$p(PartialHeader(x),",",2) k cPartialHeader m cPartialHeader=PartialHeader(x)
 .i $d(COMMENTS) f jLine=1:1 q:'$d(COMMENTS(jLine))  d
 ..i NewPage d NewPage^PRTDR00(vtsRID)
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1,^TMP("REPORT",$j,page,"TEXT",x)=COMMENTS(jLine)
 ..i COMMENTS(jLine)="!" s PageLine=PageLine+1 i PageLine>MaxLine s NewPage=1
 .d CheckLastPage^PRTDR00
 .s (Word1,Word2,AuthUser,PathUser)=""
 .s Word1=$$seldata^LVBCTTS($$seldata^LVBVISTS(vtsRID,3),31)
 .s Word2=$$seldata^LVBCTTS($$seldata^LVBVISTS(vtsRID,3),54)
 .s AuthUser=$$seldata^LVBVISTS(vtsRID,9)
 .s PathUser=$$seldata^MVBSSUSR($$seldata^LVBVISTS(vtsRID,10),3)
 .i cum="",(Word1="Y")!(Word2="Y"),$l(PathUser) d
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1
 ..s ^TMP("REPORT",$j,page,"TEXT",x)="!"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+1)="!"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+2)="   Authorised by "_PathUser
 ..s ^TMP("REPORT",$j,page,"TEXT",x+3)="!"
 ..s PageLine=PageLine+3
 .i AuthUser="" d
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1
 ..s ^TMP("REPORT",$j,page,"TEXT",x)="!"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+1)="~BOLD"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+2)="!"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+3)="    *** This is an INTERIM result. It is subject to change and may be added to ***"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+4)="~NORMAL"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+5)="!"
 ..s PageLine=PageLine+3
 q
 ; create cumulative layouts
Create2(vtsRID,report,group,cOrder) k Breaks
 s DH=dh1,(EpisodeList,ReportType)="" i $l(report) d
 .s col="" f  s col=$o(^TMP("PRTDR00",$j,"EpisodeList",report,epis,col)) q:col=""   s EpisodeList=$s($l(EpisodeList):EpisodeList_",",1:"")_^(col)
 ; check client specific routine
 s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 i $l(%routine) s LineRoutine="MaxLines^"_%routine i $l($t(@LineRoutine)) x "d MaxLines^"_%routine_"("""_report_""","""_$p(PAR2,"||")_""")"
 s type="P" i '$$SINGLE^PRTDR00(vtsRID,type,"C",EpisodeList,DH,group,cOrder),PLIST d
 .s BodyLength=$g(Breaks) k cPartialHeader
 .s TestHeader=0 f j=1:1:HEADER i HEADER(j)="!" s TestHeader=TestHeader+1
 .s TestBody=0 f j=1:1:PLIST i PLIST(j)="!" s TestBody=TestBody+1
 .s LeftLines=TestBody
 .s test=$p(vtsRID,"||",2),StopSplit=$$seldata^LVBCTTS(test,25)
 .s Start="Y",NewPage=ForceNewPage f jLine=1:1:PLIST d
 ..i $o(^TMP("REPORT",$j,""))="" s NewPage=1
 ..i 'NewPage,jLine=1,StopSplit="Y",PageLine+TestBody+2>MaxLine s NewPage=1
 ..i 'NewPage,jLine=1,BodyLength,StopSplit="T",PageLine+BodyLength+1>MaxLine s NewPage=1
 ..i 'NewPage,jLine=1,TestHeader,TestBody,PageLine+TestHeader+1>MaxLine s NewPage=1
 ..i 'NewPage,$d(Breaks(jLine)),PageLine+Breaks(jLine)>MaxLine s NewPage=1
 ..i 'NewPage,PageLine+LeftLines>MaxLine,PageLine+TestHeader>MaxLine s NewPage=1
 ..i Start="Y",$g(PrintTestHeader) d
 ...i 'NewPage,jLine=1,StopSplit="Y",PageLine+TestHeader+TestBody+2>MaxLine s NewPage=1
 ...i 'NewPage,jLine=1,BodyLength,StopSplit="T",PageLine+TestHeader+BodyLength+1>MaxLine s NewPage=1
 ...i 'NewPage,$d(Breaks(jLine)),PageLine+TestHeader+Breaks(jLine)>MaxLine s NewPage=1
 ..i (jLine=1)!NewPage d
 ...i 'NewPage d OldPage^PRTDR00(vtsRID)
 ...i NewPage d NewPage^PRTDR00(vtsRID) s Start="YC"
 ..i Start["Y" d  s Start="N"
 ...i Start="Y",REPORT(5)="Y" d PrintDepHeader^PRTDR00
 ...i Start="Y" d PrintSection^PRTDR00("H")
 ...d PrintTestHeader^PRTDR00(1)
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1,^TMP("REPORT",$j,page,"TEXT",x)=PLIST(jLine)
 ..i PLIST(jLine)="!" s PageLine=PageLine+1,LeftLines=LeftLines-1 i PageLine>MaxLine s NewPage=1 i jLine=PLIST q
 ..i NewPage s x="" f  s x=$o(PartialHeader(x)) q:x=""  i jLine>$p(PartialHeader(x),",",1),jLine<$p(PartialHeader(x),",",2) k cPartialHeader m cPartialHeader=PartialHeader(x)
 .i $d(COMMENTS) f jLine=1:1 q:'$d(COMMENTS(jLine))  d
 ..i NewPage d NewPage^PRTDR00(vtsRID)
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1,^TMP("REPORT",$j,page,"TEXT",x)=COMMENTS(jLine)
 ..i COMMENTS(jLine)="!" s PageLine=PageLine+1 i PageLine>MaxLine s NewPage=1
 .d CheckLastPage^PRTDR00
 .s AuthUser=$$seldata^LVBVISTS(vtsRID,9)
 .i AuthUser="" d
 ..s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1
 ..s ^TMP("REPORT",$j,page,"TEXT",x)="!"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+1)="~BOLD"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+2)="!"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+3)="    *** This is an INTERIM result. It is subject to change and may be added to ***"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+4)="~NORMAL"
 ..s ^TMP("REPORT",$j,page,"TEXT",x+5)="!"
 ..s PageLine=PageLine+3
 q
 ; print header
Header(page,par1,par2) n (page,par1,par2,VISIT,DOC,HOSP,REPID)
 i '$d(^PR(REPID,"HDR")) d QDR
 d ADD^PRTREP00(REPID,"PRT","#")
 ; find location
 i VISIT(22)'="" s location=$p(VISIT(22),$c(1),2)
 e  i $d(DOC) s location=$p(DOC(9),$c(1))
 e  i $d(HOSP) s location=$p(HOSP(4),$c(1))
 s (copyto,copyfrom,specimen)="",epis=$p(VISIT(2),$c(1))
 ; find copy-to
 i $$open^LVBVISDC(epis)
 f  q:$$fetch^LVBVISDC(epis)  s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 i $$close^LVBVISDC()
 ; find specimens
 i $$open^LVBVISSP(epis)
 f  q:$$fetch^LVBVISSP(epis)  s specimen=specimen_$s($l(specimen):", ",1:"")_$p(PLIST(2),$c(1))
 i $$close^LVBVISSP()
 i $l(specimen)>20 f j=1:1 i $l($p(specimen,",",1,j))>20 s specimen=$p(specimen,",",1,j-1) q
 i par2["-FAX-" d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s0b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h0V"_"NORTHERN TERRITORY GOVERNMENT")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h150V"_"PATHOLOGY SERVICES.")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h740V"_$p(VISIT(20),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p14v0s3b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a720h740V"_$p(VISIT(3),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2750h740V"_$p(VISIT(4),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4500h740V"_$s('$l($p(VISIT(6),$c(1))):"Unknown",1:$$extdate2^SSUTIL4(VISIT(6))))
 i '$l($p(VISIT(5),$c(1),2)) s $p(VISIT(5),$c(1),2)="Unknown"
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a5190h740V"_$p(VISIT(5),$c(1),2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h1070V"_$p(VISIT(15),$c(1),2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h1070V"_location)
 i $l(copyto) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2050h1070V"_copyto)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h1480V"_$p(VISIT(2),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a720h1480V"_$$dt($p(VISIT(12),$c(1)),$p(VISIT(13),$c(1))))
 d ADD^PRTREP00(REPID,"PRT",$C(27)_"&a1800h1480V"_$$dt($p(VISIT(61),$c(1)),$p(VISIT(62),$c(1))))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2880h1480V"_specimen)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4500h1480V"_$$extdate2^SSUTIL4(+$h))
 s cln=$p(VISIT(53),"|") i $l(cln)>85 f j=1:1 i $l($p(cln," ",1,j))>85 s cln=$p(cln," ",1,j) q
 i $l(cln) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h1830V"_cln)
 d TABS
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p9v0s0b4101T")
 i $p(VISIT(66),$c(1))="RDH" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4250h350VNumber 3197")
 i $p(VISIT(66),$c(1))="ASH" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4250h350VNumber 3125")
 i $p(VISIT(66),$c(1))="KDH" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4250h350VNumber 3435")
 i $p(VISIT(66),$c(1))="GDH" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4250h350VNumber 14927")
 i $p(VISIT(66),$c(1))="TCH" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4250h350VNumber 15227")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h1900V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
	; print tabs down side of report (footer)
TABS	s (DepName,xx1,xx2,dep,pat,multi,text)=""
	s phone="8922 8299"
	s rowid="" f  s rowid=$o(^TMP("REPORT",$j,page,"TEST",rowid)) q:rowid=""  i $p(rowid,"||",3) d
	.s dep=$e($p(rowid,"||",2)) i '$$select^LVBCTDEP(dep,"Y") d 
	..i '$l(PLIST(10)) s multi=1 q
	..i $l(pat),PLIST(10)'=pat s multi=1
	..s pat=PLIST(10)
	.i " P T "[(" "_dep_" "),'$l(xx1),'$$select^LVBVISTS(rowid,"Y") s xx1=PLIST(7),xx2=PLIST(8)
	i $l(xx1) d
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s0b5T")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h7675VAuth Time/Date:")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a780h7675V"_$$dt(xx1,xx2))
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
	i par2'="XMR" d
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h7300VPrint Time/Date:")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h7400V"_$$dt(+$h,$p($h,",",2)))
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a180h7600VFor enquiries call "_phone)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a5150h7775V"_"Pg: "_page)
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b5T")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"*v0n1o1T")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1670h7700V")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"*c2140h151v0P")
	d
	.i '$l(multi),$l(pat) s text=$$seldata^MVBSSUSR(pat,3)_" "_$$seldata^MVBSSUSR(pat,14) q
	.i '$$select^LVBCTDEP("Z","Y") s pat=PLIST(10),text=$$seldata^MVBSSUSR(pat,3)_" "_$$seldata^MVBSSUSR(pat,14)
	i $l(text) d
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p9v0s0b5T")
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1680h7800v0P"_text)
	.d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b5T")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0p0h0V")
	d ADD^PRTREP00(REPID,"PRT",$c(27)_"*v0n0o0T")
	q
dt(date,time) n (date,time) s date=$g(date),time=$g(time),result=""
 s result=$e(time\3600+100,2,3)_":"_$e(time\60#60+100,2,3)_" "_$$extdate2^SSUTIL4(date)
 q result
 ; print footer
Footer(page,par1,par2) n (par1,par2,page,REPID,DOC,VISIT)
 ; print last page
 i par2'="XMR" d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s7B")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h7000VDoctor to Certify ______________")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h7200VFile in Patient Notes or Return to")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h7300VMedical Records")
 .i $d(DOC) d
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1050h6830V")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"%1BPD;PR0,-850;PR2850,0;PR0,850;PR-2850,0;")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"%1A")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1080h7000V"_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51)))
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1080h7100V"_DOC(9))
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1080h7200V"_DOC(10))
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1080h7300V"_DOC(11)_" "_DOC(12)_" "_DOC(13))
 i $o(^TMP("REPORT",$j,page))'="" d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4000h7500V")
 .d ADD^PRTREP00(REPID,"PRT","Continued Over..")
 q
QDR s QDR="DAR_2",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..i par2["-FAX-" q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
 q
]]></Routine>
</Export>
