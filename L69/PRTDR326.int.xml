<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR326" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR326 ;IG 27/05/98 ;Doctors Reports printing (cumulative) - APP
 ;
 ; supported calls :
 ;
 ; $$print^PRTDR326(vtsRID,type,EpisodeList,DH)
 ;
 ; type          : P - print
 ;                 V - view from Enquiry
 ;                   - view from Entry
print(vtsRID,type,EpisodeList,DH,group,cOrder) n (vtsRID,type,EpisodeList,DH,group,cOrder,dh1,dh2,tsLM,tsWD,GroupName) 
 s vtsRID=$g(vtsRID),type=$g(type),EpisodeList=$g(EpisodeList),DH=$g(DH),group=$g(group),cOrder=$g(cOrder),dh1=$g(dh1),dh2=$g(dh2)
 s epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2),max=6,ColWidth=15,ColLeft=17,FullYear="N",tsLM=$g(tsLM),tsWD=$g(tsWD)
 K ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-TC-ORDER")
 i '$$select^LVBEPVIS(epis,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s range1="(",range2=")",(flaglow,flaghigh,Alternate)="" i '$$select^MVBCFLAB(1) d
 .i $l(PLIST(18)) s flaglow=PLIST(18)
 .i $l(PLIST(19)) s flaghigh=PLIST(19)
 .i $l(PLIST(20)) s range1=PLIST(20)
 .i $l(PLIST(21)) s range2=PLIST(21)
 .i $l(PLIST(14)) s max=PLIST(14)
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 .i PLIST(37)="Y" s ColWidth=11,FullYear="Y"
 .i $l(PLIST(69)) s Alternate=PLIST(69)
 k FORMAT,TS
 s rowid=test_"||"_dh1_"||"_dh2,layout=""
 s layout=$$seldata^LVBCTTSP(rowid,4)
 s layout=$p(layout,$c(1))
 i '$$getit^LVBCTTSF(,vtsRID,"Y",,,layout) d
 .f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS($p(vtsRID,"||",2),ti)="",FORMAT(ti,$p(vtsRID,"||",2))=PLIST(j)
 i $l(group),group'=" " d
 .i '$$select^LVBCTTWG(group) m CTTWG=PLIST
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ...i '$$getit^LVBCTTSF(,epis_"||"_ts_"||"_tscnt,"Y") d
 ....f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)="",FORMAT(ti,ts)=PLIST(j)
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..i '$$getit^LVBCTTSF(ts,,"Y") d
 ...f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)="",FORMAT(ti,ts)=PLIST(j)
 .s ti="" f cnt=1:1 s ti=$o(FORMAT(ti)) q:ti=""  d
 ..i $g(^TTAB("GR",group,"TI",ti)) s $p(FORMAT(ti),$c(2),27)=^(ti)
 ..e  s $p(FORMAT(ti),$c(2),27)=cnt
 ; create list of episodes and results
 k EPTS i $l(EpisodeList) d
 .f col=1:1:$l(EpisodeList,",") s ep=$p(EpisodeList,",",col) i $l(ep) d
 ..s date=$$seldata^LVBEPVIS(ep,12)
 ..s time=$$seldata^LVBEPVIS(ep,13)
 ..s date=$p(date,$c(1)),time=$tr($p(time,$c(1))," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 k EPTS i '$l(EpisodeList) d
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=VISIT(j)
 .s TestList=test i $l(group),group'=" " s ts="" f  s ts=$o(TS(ts)) q:ts=""  s TestList=TestList_","_ts
 .d CumList^PRTDR00(TestList,epis,visit,cOrder)
 .f col=1:1:PLIST d
 ..s ep=PLIST(col),(date,time)=""
 ..s date=$$seldata^LVBEPVIS(ep,12)
 ..s time=$$seldata^LVBEPVIS(ep,13)
 ..s date=$p(date,$c(1)),time=$tr($p(time,$c(1))," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 i '$d(^TMP($zn,$j,"CUM-TC")) d  q 100_$c(1)_EpisodeList
 .k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST")
 ; print cumulative header
 ;i type="P" d ADD^PRTDR00(,"~FONTSIZE=8","Y")
 d ADD^PRTDR00(,"~BOLD","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM,$e("Date/Time Coll"_$j("",tsWD),1,ColLeft-3)_" :","Y")
 s maxcol=$o(^TMP($zn,$j,"CUM",""),-1) s:maxcol>max maxcol=max
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s xx1=$p(^TMP($zn,$j,"CUM",col),$c(1),2)
 .i xx1 s xx1=$$extdate2^SSUTIL4(xx1) i FullYear="N" s $p(xx1,"/",3)=$e($p(xx1,"/",3),3,4)
 .s xx2=$$seldata^LVBEPVIS(ep,13)
 .s xx2=$tr($p(xx2,$c(1))," "),xx2=$s($l(xx2):$$exttime^SSUTIL4(xx2),1:"N/S")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx1_" "_xx2,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM,"==============","Y") s delim="",$p(delim,"=",ColWidth)=""
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),delim,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 ; print test set header only for Cum report page
 i $l(EpisodeList) d
 .d ADD^PRTDR00(,"~RES-TSHEADER"),ADD^PRTDR00(,$c(13))
 .i ('$l(group)!(group=" ")),$l(TEST(17)) d
 ..s tsName=$s(TEST(17)="*":TEST(3),1:TEST(17))
 ..d LN^PRTDR00(),ADD^PRTDR00(tsLM,tsName),LN^PRTDR00()
 .i $l(group),group'=" ",$l(GroupName) d LN^PRTDR00(),ADD^PRTDR00(tsLM,GroupName),LN^PRTDR00() s GroupName=""
 .d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 ; print start of the group
 i $l($g(CTTWG(11))) d
 .d LN^PRTDR00()
 .f j=1:1:$l(CTTWG(11),"|") d ADD^PRTDR00(tsLM,$p(CTTWG(11),"|",j)),LN^PRTDR00()
 ; print results
 s order="" f  s order=$o(^TMP($zn,$j,"CUM-TC-ORDER",order)) q:order=""  d
 .s ti=^(order)
 . ;i type="P" d ADD^PRTDR00(,"~FONTSIZE=8")
 .i type="",$p(FORMAT(ti),$c(2),15)'="Y" d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13))
 .i order#1=0 d ADD^PRTDR00(tsLM,$e($tr($p(FORMAT(ti),$c(2),3),":")_$j("",tsWD),1,ColLeft-3)_" :")
 .f col=(1+(order#1)):1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 ..s result=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),1),flag=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),2),sr=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),3),ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 ..i result="" d
 ...i $d(^TMP($zn,$j,"CUM-EXIST",col,ti)) d  q
 ....s mo=$p(FORMAT(ti),$c(2),8)
 ....i mo="M" s mo="O",x1="" f  s x1=$o(FORMAT(ti,x1)) q:x1=""  i $d(TEPI(ep,1,x1)) s mo=$p(FORMAT(ti,x1),$c(2),8) i mo="M" q
 ....i mo="M" s result="TF"
 ....e  s result=""
 ...s result="-"
 .. ; print extra info
 ..i flag="L" d
 ...i "MN"[$e($p(FORMAT(ti),$c(2),18)),$l(flaglow) s result=result_" "_flaglow
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ..i flag="H" d
 ...i "MN"[$e($p(FORMAT(ti),$c(2),18)),$l(flaghigh) s result=result_" "_flaghigh
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ..d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),result)
 ..i (flag="L")!(flag="H") d
 ...i type="",($p(FORMAT(ti),$c(2),15)'="Y")!(sr="Y") d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13)) q
 ...d ADD^PRTDR00(,"~NORMAL")
 .i type="",($p(FORMAT(ti),$c(2),15)'="Y")!(sr="Y") d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .d LN^PRTDR00()
 ; print end of the group
 i $l($g(CTTWG(12))) d
 .d LN^PRTDR00()
 .f j=1:1:$l(CTTWG(12),"|") d ADD^PRTDR00(tsLM,$p(CTTWG(12),"|",j)),LN^PRTDR00()
 ;i type="P" d ADD^PRTDR00(,"~FONTSIZE=10")
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST")
 q 0
 ; collect Test Items information
TestItems(col,rowid) n (col,rowid,type,FORMAT,vtsRID,Alternate,TS) s col=$g(col),rowid=$g(rowid)
 i '$$select^LVBVISTS(rowid,"Y") d
 .m ^TMP($zn,$j,"CUM-EXIST",col)=TS($p(rowid,"||",2))
 .s status=$$status^LVBVIS1(rowid),Result=1
 .i status'="A",(type="V")!$d(^TMP("PRTDR00",$j,"TYPE")) d
 ..i $$checkView1^LVBVIS5(rowid) s Result=0 q
 ..i $$checkView2^LVBVIS5(rowid) s Result=0 q
 .i '$$results^LVBVISTS(rowid) f j=1:1:PLIST d
 ..s ti=$p(PLIST(j),$c(1),2),(result,flag,sr)=""
 ..i Result s result=$p(PLIST(j),$c(1),3),flag=$p(PLIST(j),$c(1),5),sr=$p(PLIST(j),$c(1),22)
 ..i '$d(FORMAT(ti)) q
 .. ; not included in cumulative
 ..i $p(FORMAT(ti),$c(2),20)'="Y" q
 ..i $l(ti),$l(result) d
 ... ; Alternate result entered
 ...i type="P",$l(Alternate),result=$p(Alternate,"-") q
 ... ; non reportable test item
 ...i $l(type),($p(FORMAT(ti),$c(2),15)'="Y")!(sr="Y") q
 ...i $e($p(FORMAT(ti),$c(2),18))="N" d IN
 ...i $e($p(FORMAT(ti),$c(2),18))="M" d IM
 ...i $e($p(FORMAT(ti),$c(2),18))="D" d ID^PRTDR00
 ...i $e($p(FORMAT(ti),$c(2),18))="T" d IT^PRTDR00
 ...i $e($p(FORMAT(ti),$c(2),18))="Y" d IY^PRTDR00
 ...f xcol=col\1:.1:(col+.9) q:'$d(^TMP($zn,$j,"CUM",xcol,ti))  i ^TMP($zn,$j,"CUM",xcol,ti)=(result_$c(1)_flag_$c(1)_sr) s xcol="" q
 ...q:'$l(xcol)  s ^TMP($zn,$j,"CUM",xcol,ti)=result_$c(1)_flag_$c(1)_sr
 ...i '$d(^TMP($zn,$j,"CUM-TC",ti)) d
 ....s order=$p(FORMAT(ti),$c(2),27) i '$l(order) s order=$p(FORMAT(ti),$c(2),2)
 ....s ^TMP($zn,$j,"CUM-TC-ORDER",(order+(xcol#1)))=ti,^TMP($zn,$j,"CUM-TC",ti)=""
 q
IN ; Numeric
IM ; Numeric *1000
 i $l(result),$d(^TTAB("RC",result)) s:$l($p(^(result),"\",1))<12 result=$p(^(result),"\",1) q
 s Flag="" f  q:'$l(result)  q:"<>+-"'[$e(result)  s Flag=Flag_$e(result),result=$e(result,2,$l(result))
 s result=Flag_$j(result,0,$e($p(FORMAT(ti),$c(2),18),2,9))
 q
]]></Routine>
</Export>
