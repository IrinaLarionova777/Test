<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBHIVACESP03" type="INT" languagemode="0" generated="1"><![CDATA[
LVBHIVACESP03 ; LVB Control data access for HIVAC - Sant Pau
 ;
 ; supported calls:
 ;
 ; $$new^LVBHIVACESP03(epis,lab,site,year,update,old)
 ; $$block^LVBHIVACESP03(current,blocks)
 ;
 ; calculate new block number
block(current,blocks,tests1) n (current,blocks,tests1) s current=$g(current),blocks=$g(blocks),tests1=$g(tests1),result="A"
 i $l(current)=0 d  q result
 . s epi=$p(tests1,"||",1)
 . s ts=$p(tests1,"||",2)
 . s cnt=$p(tests1,"||",3)
 . s db="" f  s db=$o(^THIR(epi,db)) q:db=""  i $d(^THIR(epi,db,"TEST",tests1)) q
 . i $l(db)=0 q
 . s result=$o(^THIR(epi,db,"BLOCK",99999),-1)+1
 s seq=$l($p(current,"||",3),"/")+1
 f j=1:1:$l(blocks,",") d
 .i $a(result)>$a($p($p(blocks,",",j),"/",seq)) q
 .s result=$c($a($p($p(blocks,",",j),"/",seq))+1)
 q $p(current,"||",3)_"/"_result
 ; update - Y/N (empty-Y)
new(epis,lab,site,year,update,old) n (epis,lab,site,year,update,old,PLIST)
 s epis=$g(epis),lab=$g(lab),site=$g(site),year=$g(year),update=$g(update),old=$g(old),result="",SQLCODE=100
 i update="" s update="Y"
 i $l(epis) s SQLCODE=0 d
 .i $$select^LVBCTDBL(lab,"Y") q
 .k CTDBL m CTDBL=PLIST
 .i CTDBL(12)="N" d  q
 ..s new=CTDBL(8)
 ..i CTDBL(14)'="Y" s new=new+1,PLIST(8)=new
 ..i CTDBL(14)="Y" d
 ...s stop=0 f j=1:1:$l(new,",") i $p($p(new,",",j),"-")=year s new=$p($p(new,",",j),"-",2)+1,$p(PLIST(8),",",j)=year_"-"_new,stop=1 q
 ...i 'stop d
 ....i PLIST(8)["-" s new=1,PLIST(8)=PLIST(8)_$s($l(PLIST(8)):",",1:"")_year_"-"_new
 ....i PLIST(8)'["-" s new=PLIST(8)+1,PLIST(8)=year_"-"_new
 ..i update'="Y" s result=new_$s(epis=" ":"",1:"A")
 ..i update="Y",'$$update^LVBCTDBL(lab) s result=new_$s(epis=" ":"",1:"A")
 .i CTDBL(12)="Y" d  q
 ..i $$select^LVBCTDBF(lab_"||"_site,"Y") q
 ..s new=PLIST(4)
 ..i CTDBL(14)'="Y" s new=new+1,PLIST(4)=new
 ..i CTDBL(14)="Y" d
 ...s stop=0 f j=1:1:$l(new,",") i $p($p(new,",",j),"-")=year s new=$p($p(new,",",j),"-",2)+1,$p(PLIST(4),",",j)=year_"-"_new,stop=1 q
 ...i 'stop d
 ....i PLIST(4)["-" s new=1,PLIST(4)=PLIST(4)_$s($l(PLIST(4)):",",1:"")_year_"-"_new
 ....i PLIST(4)'["-" s new=PLIST(4)+1,PLIST(4)=year_"-"_new
 ..i update'="Y" s result=new_$s(epis=" ":"",1:"A")
 ..i update="Y",'$$update^LVBCTDBF(lab_"||"_site) s result=new_$s(epis=" ":"",1:"A")
 k PLIST s PLIST(1)=result
 s PLIST=$o(PLIST(""),-1)
 q SQLCODE
]]></Routine>
</Export>
