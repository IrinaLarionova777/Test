<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR2IND02" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR2IND02 ; ML 05/02/2003 ; Doctors Reports printing (single test set - antibiotic panel) - India - Escorts Heart Institute
 ;
 ; supported calls :
 ;
 ; $$print^PRTDR2IND02(vtsRID,type,DH)
 ;
 ; type  : P - print
 ;         V - view from Enquiry
 ;           - view from Entry
 ; DH    : D - Doctor
 ;         H - Hospital
print(vtsRID,type,DH,tc) n (vtsRID,type,DH,tc,tsLM,tsWD) s vtsRID=$g(vtsRID),type=$g(type),DH=$g(DH),tc=$g(tc)
 s test=$p(vtsRID,"||",2),ColWidth=9,ColLeft=40,tsLM=$g(tsLM),tsWD=$g(tsWD)
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .s tsLM=PLIST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 i '$$select^MVBCFLAB(1) d
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 i '$$select^LVBEPVIS($p(vtsRID,"||")) m VISIT=PLIST k PLIST
 ; inpatient format or outpatient format
 s reportformat="" i type="P" s reportformat=$$RepFormat^PRTDR0IND02($g(VISIT(22)))
 i reportformat="OUTPAT" s ColWidth=12,ColLeft=20,tsLM=10,tsWD=80
 i reportformat="INPAT" s ColWidth=12,ColLeft=20,tsLM=5,tsWD=40
 k ^TMP($zn,$j,"ANT")
 ; print an antibiotic panel
 s rowid=vtsRID_"||"_tc
 i '$$select^LVBVISTD(rowid),$l(PLIST(3)) d
 .i '$$select^LVBCTBUG(PLIST(3),"Y") d
 ..f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ..s cnt=$o(^TMP($zn,$j,"ANT",0,""),-1)+1,^TMP($zn,$j,"ANT",0,cnt)=$s($l(PLIST(3))<ColWidth:PLIST(3),1:PLIST(2))
 ..i $$open^LVBVISTA(rowid)
 ..f  q:$$fetch^LVBVISTA(rowid)  d
 ...i $l(PLIST(2)),$l(PLIST(3)) d
 ....i $l(type),PLIST(4)'="Y" q
 ....f j=2:1:6 s $p(^TMP($zn,$j,"ANT",0,cnt,$p(PLIST(2),$c(1))),$c(2),j)=PLIST(j)
 ..i $$close^LVBVISTA()
 .. ; check if there is a reportable antibiotic
 ..i $d(^TMP($zn,$j,"ANT",0,cnt))\10=0 k ^TMP($zn,$j,"ANT",0,cnt)
 ..s (ok,ant)="" f  s ant=$o(^TMP($zn,$j,"ANT",0,cnt,ant)) q:ant=""  d  i ok
 ...i $l(type),$p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),4)'="Y" q
 ...s ok=1
 ..i 'ok k ^TMP($zn,$j,"ANT",0,cnt)
 ; sort antibiotics
 s cnt="" f  s cnt=$o(^TMP($zn,$j,"ANT",0,cnt)) q:cnt=""  d
 .s ant="" f  s ant=$o(^TMP($zn,$j,"ANT",0,cnt,ant)) q:ant=""  d
 ..s seq=$$seldata^LVBCTANT(ant,4)
 ..s ^TMP($zn,$j,"ANT",1,+seq,ant,cnt)=""
 i '$d(^TMP($zn,$j,"ANT",0)) q 0
 f i=1:1:2 d LN^PRTDR00()
 s seq="" f  s seq=$o(^TMP($zn,$j,"ANT",1,seq)) q:seq=""  d
 .s ant="" f  s ant=$o(^TMP($zn,$j,"ANT",1,seq,ant)) q:ant=""  d
 ..s cnt="" f first=0:1 s cnt=$o(^TMP($zn,$j,"ANT",1,seq,ant,cnt)) q:cnt=""  d
 ...i 'first d ADD^PRTDR00(tsLM,$p($p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),2),$c(1),2))
 ... ; non printable/viewable antibiotics
 ...i $l(type),$p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),4)'="Y" q
 ... ; print result
 ...s (code,result)=$p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),3)
 ...s result=$$seldata^LVBCTSN(result,3)
 ...i $p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),4)'="Y" s result=result_" NR"
 ...i $p(^TMP($zn,$j,"ANT",0,cnt,ant),$c(2),3)="S" d ADD^PRTDR00(,"~BOLD")
 ...i $l(result) d ADD^PRTDR00(tsLM+ColLeft+(ColWidth*(cnt-1)),result)
 ...d ADD^PRTDR00(,"~NORMAL")
 ..d LN^PRTDR00()
 k ^TMP($zn,$j,"ANT")
 q 0
]]></Routine>
</Export>
