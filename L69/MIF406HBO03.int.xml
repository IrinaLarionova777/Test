<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF406HBO03" type="INT" languagemode="0" generated="1"><![CDATA[
MIF406HBO03(mi) ; ML 16/7/99 ; ASTM protocol  -  BECTON DICKINSON Bactec 9000 (Bi-Directional) - SQUH
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s can=$c(24),ack=$c(6),eot=$c(4),lf=$c(10),cr=$c(13),nak=$c(21),soh=$c(1),syn=$c(22)
 s (sample,epis,surname,result,date,time,QC,TimeCheck)=""
 s USER="trak",TEST="M025"
 s ITEM("M0012")="",ITEM("M0013")="",ITEM("M0017")=""
 i $$start^MIF000() q
 f count=0:1 d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10
 i R=-1 d  q
 .i count#30=0 d
 ..i '$d(^TMIF(mi,10)) d
 ...s ^TMIF(mi,10,1)="H|\^&|||LABTRAK||||LABTRAK|BACTEC|||1"_cr_lf
 ...s ^TMIF(mi,10,1)=^TMIF(mi,10,1)_"Q|1|ALL^ALL||^^^ALL||||||||P"_cr_lf
 ...s ^TMIF(mi,10,1)=^TMIF(mi,10,1)_"L|1"_cr_lf
 ..s xLine=$o(^TMIF(mi,10,"")) i xLine d Send(xLine)
 . ; check loaded bottles status - check every hour
 .i +$h*(3600*24)+$p($h,",",2)>TimeCheck s TimeCheck=+$h*(3600*24)+$p($h,",",2)+3600 d BottleCheck
 .i count#5 d SCAN
 d trace(R) i $c(R)=syn d
 .d ACK
 .s RECORD="" f  r *R:10 q:$c(R)=eot  q:$c(R)=can  q:R=-1  d
 ..d trace(R)
 ..i $c(R)'=soh q
 ..s bs1="" f j=1:1:2 r *x:1 s bs1=bs1_$c(x)
 ..s bs2="" f j=1:1:2 r *x:1 s bs2=bs2_$c(x)
 ..s record="" f j=1:1:128 r *x:1 s record=record_$c(x)
 ..s chsum="" f j=1:1:2 r *x:1 s chsum=chsum_$c(x)
 ..d trace^MIF000(mi,bs1_bs2_$tr(record,$c(10,13),"  ")_chsum,"H<--M")
 .. ; check record validity
 ..i $$sh($$CHSUM(record))'=chsum d NAK q
 ..d ACK s RECORD=RECORD_record
 .i $c(R)=eot d trace(R) d ACK
 .i $c(R)=can d trace(R) q
 .s RECORD=$tr(RECORD,cr)
 .d trace^MIF000(mi,"RECORD="_RECORD,"H<--M")
 .f jR=1:1:$l(RECORD,lf) s record=$p(RECORD,lf,jR) d
 .. ; header record
 ..i $e(record)="H" d  q
 ...d Last s (sample,epis,surname,result,date,time,QC)=""
 .. ; patient record
 ..i $e(record)="P" d  q
 ...d Last s (sample,epis,surname,result,date,time,QC)=""
 ...s surname=$tr($tr($p(record,"|",6)," "),"^"," ")
 .. ; request record
 ..i $e(record)="Q" d  q
 ...s epis=$tr($p($p(record,"|",3),"^",2)," ")
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=""
 ..i $e(record)="O" s epis=$p(record,"|",3) q
 .. ; result record
 ..i $e(record)="R" d  q
 ... ; result of testing so far + = pos, - = neg, * = neg, still under testing
 ...s res=$tr($p(record,"|",4)," ")
 ...s res=$s(res="BACTEC_POS":"+",res="BACTEC_NEG":"-",1:"*")
 ... ; extract status of bottle - P = prelim status. F = final status
 ...s stat=$tr($p(record,"|",9)," ")
 ... ; extract bottle type and bottle number
 ...s bot=$p($p(record,"|",3),"^",5) q:'$l(bot)
 ...s botnum=$p($p(record,"|",3),"^",4) q:'$l(botnum)
 ...q:$p($g(^TMIF(mi,33,"Res",epis,bot,botnum)),"\",1)="F"
 ... ; allow only bottles with previous negative history to continue
 ...q:$p($g(^TMIF(mi,33,"Res",epis,bot,botnum)),"\",2)="+"
 ...s (DateLoad,Datelo,DateRes,Daters,DateDiff)=""
 ... ; date bottle loaded
 ...s DateLoad=$p(record,"|",12)
 ...i $l(DateLoad) d
 ....s dt=$$intdate^SSUTIL4($e(DateLoad,5,6)_"/"_$e(DateLoad,7,8)_"/"_$e(DateLoad,1,4))
 ....s tm=$$inttime^SSUTIL4($e(DateLoad,9,10)_":"_$e(DateLoad,11,12))+$e(DateLoad,13,14)
 ....s Datelo=dt*24*3600+tm
 ....i $p($g(^TMIF(mi,33,"Epis",epis,bot,botnum)),"\",2)'=Datelo d
 .....i $p($p($g(^TMIF(mi,33,"Epis",epis,bot,botnum)),"\",1),",",1)'=dt d
 ......s x=$p($p($g(^TMIF(mi,33,"Epis",epis,bot,botnum)),"\",1),",",1)
 ......i $l(x) k ^TMIF(mi,33,"Date",x,epis,bot,botnum)
 .....s $p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",1)=dt_","_tm
 .....s $p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",2)=Datelo
 .....s ^TMIF(mi,33,"Date",dt,epis,bot,botnum)=""
 .....i '$d(^TMIF(mi,33,"Res",epis,bot,botnum)) s ^TMIF(mi,33,"Res",epis,bot,botnum)=""
 ...i '$l(DateLoad) s Datelo=$p($g(^TMIF(mi,33,"Epis",epis,bot,botnum)),"\",2)
 ...i '$l(Datelo) q
 ... ; date result determined
 ...s DateRes=$p(record,"|",13)
 ...i $l(DateRes) d
 ....s dt=$$intdate^SSUTIL4($e(DateRes,5,6)_"/"_$e(DateRes,7,8)_"/"_$e(DateRes,1,4))
 ....s tm=$$inttime^SSUTIL4($e(DateRes,9,10)_":"_$e(DateRes,11,12))+$e(DateRes,13,14)
 ....s Daters=dt*24*3600+tm
 ...i '$l(DateRes) s Daters=+$h*24*3600+$p($h,",",2)
 ... ; calculate date difference between date loaded and date of result
 ...s DateDiff=Daters-Datelo,DateDiff=DateDiff\24\3600
 ...i res="-"!(res="*"),DateDiff<7 q
 ... ; ignore already reported 7 day negatives - update only
 ...i DateDiff>7,$p($g(^TMIF(mi,33,"Res",epis,bot,botnum)),"\",3)=8 s ^TMIF(mi,33,"Res",epis,bot,botnum)=stat_"\"_res_"\"_DateDiff q
 ...i DateDiff>7,(res="-"!(res="*")) s DateDiff=7
 ...i DateDiff>7,res="+" s DateDiff=8
 ...s ^TMIF(mi,33,"Res",epis,bot,botnum)=stat_"\"_res_"\"_DateDiff
 ...s result=result_bot_$c(par10)_res_"/"_DateDiff_$c(par11)
 .. ; last record
 ..i $e(record)="L" d Last q
 .i $d(^TMP($zn,$j,"ENQ")) d ORDER
 q
ORDER ; create list of orders if exists
 s (header,epis)="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  d
 .k ^TMP("MIF-SINGLE",$j,mi)
 .d scanone^MIF000(mi,epis)
 .i $d(^TMP("MIF-SINGLE",$j,mi)) d SET(epis)
 .k ^TMP("MIF-SINGLE",$j,mi)
 .k ^TMP($zn,$j,"ENQ",epis)
 i $d(^TMIF(mi,10)) d
 .s line=$o(^TMIF(mi,10,""),-1)
 .s ^TMIF(mi,10,line)=^TMIF(mi,10,line)_"L|1"_cr_lf
 .s (stop,xLine)="" f  s xLine=$o(^TMIF(mi,10,xLine)) q:xLine=""  d  q:stop
 ..d Send(xLine) i $d(^TMIF(mi,10,xLine)) s stop=1
 q
trace(R) d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=eot:"EOT",$c(R)=can:"CAN",$c(R)=soh:"SOH",$c(R)=syn:"SYN",1:"R="_R),"H<--M")
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d
 .d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 .i '$$select^LVBMIRH4(mi,epis_" ",epis) d
 ..k plist m plist=PLIST k PLIST s (cnt,stop)=0
 ..f j=1:1:plist i $l($p(plist(j),$c(1),2)),$l($p(plist(j),$c(1),3))!$l($p(plist(j),$c(1),4)) d  q:stop
 ...i $p(plist(j),$c(1),26)="Y" s stop=1 q
 ...s cnt=cnt+1,PLIST(cnt)=$tr($p(plist(j),$c(1),2,4),$c(1),"\")
 .. ; s flag="A"
 .. ; s x="" f  s x=$o(^TEPI(epis,1,TEST,x)) q:x=""  i $p(^(x),"\",41)="Y" s flag="E" q
 .. ; i $$machsave^LVBVIS4(mi,epis,flag,USER,TEST,,"AG")
 s (sample,epis,surname,result,date,time,QC)=""
 q
BottleCheck ; check result status of bottles
 s epis="" f  s epis=$o(^TMIF(mi,33,"Epis",epis)) q:epis=""  d
 .s (file,bot)="" f  s bot=$o(^TMIF(mi,33,"Epis",epis,bot)) q:bot=""  d
 ..s botnum="" f  s botnum=$o(^TMIF(mi,33,"Epis",epis,bot,botnum)) q:botnum=""  d
 ...s DateLoaded=$p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",2),DateNow=+$h*3600*24+$p($h,",",2)
 ...i DateNow-DateLoaded>(7*24*3600) d  q
 .... ; ignore ones which have been loaded manually
 ....i '$d(^TMIF(mi,33,"Res",epis,bot,botnum)) q
 .... ; ignore previous +ve or final results
 ....i $p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="+"!($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",1)="F") q
 .... ; calculate days since loaded
 ....s DateDiff=DateNow-DateLoaded,DateDiff=DateDiff\24\3600 q:DateDiff>7
 .... ; ignore already reported 7 day negatives
 ....i $p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",3)=7,($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="*"!($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="-")) q
 ....s result=result_bot_$c(par10)_"N7"_$c(par11),episx=epis
 ....i episx?1.99AN,$l(result) d
 .....s file=1
 .....d file^MIF000(mi,sample,episx,surname,result,date,time,QC)
 ....s ^TMIF(mi,33,"Res",epis,bot,botnum)="I\*\7"
 ....s (sample,episx,surname,result,date,time,QC)=""
 ...i DateNow-DateLoaded>(2*24*3600) d  q
 .... ; ignore ones which have been loaded manually
 ....i '$d(^TMIF(mi,33,"Res",epis,bot,botnum)) q
 .... ; ignore previous +ve or final results
 ....i $p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="+"!($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",1)="F") q
 .... ; calculate days since loaded
 ....s DateDiff=DateNow-DateLoaded,DateDiff=DateDiff\24\3600 q:DateDiff>2
 .... ; ignore already reported 2 day negatives
 ....i $p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",3)=2,($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="*"!($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="-")) q
 ....s result=result_bot_$c(par10)_"N2"_$c(par11),episx=epis
 ....i episx?1.99AN,$l(result) d
 .....s file=1
 .....d file^MIF000(mi,sample,episx,surname,result,date,time,QC)
 ....s ^TMIF(mi,33,"Res",epis,bot,botnum)="I\*\2"
 ....s (sample,episx,surname,result,date,time,QC)=""
 .i file,'$$select^LVBMIRH4(mi,epis_" ",epis) d
 ..k plist m plist=PLIST k PLIST s (cnt,stop)=0
 ..f j=1:1:plist i $l($p(plist(j),$c(1),2)),$l($p(plist(j),$c(1),3))!$l($p(plist(j),$c(1),4)) d  q:stop
 ...i $p(plist(j),$c(1),26)="Y" s stop=1 q
 ...s cnt=cnt+1,PLIST(cnt)=$tr($p(plist(j),$c(1),2,4),$c(1),"\")
 .. ; s flag="A"
 .. ; s x="" f  s x=$o(^TEPI(epis,1,TEST,x)) q:x=""  i $p(^(x),"\",41)="Y" s flag="E" q
 .. ; i $$machsave^LVBVIS4(mi,epis,flag,USER,TEST,,"AG")
 ; delete bottles older than 28 days
 s date="" f  s date=$o(^TMIF(mi,33,"Date",date)) q:date=""  q:date>+$h-28  d
 .s epis="" f  s epis=$o(^TMIF(mi,33,"Date",date,epis)) q:epis=""  d
 ..s bot="" f  s bot=$o(^TMIF(mi,33,"Date",date,epis,bot)) q:bot=""  d
 ...s botnum="" f  s botnum=$o(^TMIF(mi,33,"Date",epis,date,bot,botnum)) q:botnum=""  d
 ....k ^TMIF(mi,33,"Epis",epis,bot,botnum),^TMIF(mi,33,"Res",epis,bot,botnum),^TMIF(mi,33,"Date",date,epis,bot,botnum)
 q
SCAN ; send all available episodes
 d scanall^MIF000(mi)
 i $d(^TMP("MIF-SCAN",$j,mi)) d
 .s header=""
 .s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d SET(epis)
 i $d(^TMIF(mi,10)) d
 .s line=$o(^TMIF(mi,10,""),-1)
 .s ^TMIF(mi,10,line)=^TMIF(mi,10,line)_"L|1"_cr_lf
 .s (stop,xLine)="" f  s xLine=$o(^TMIF(mi,10,xLine)) q:xLine=""  d  q:stop
 ..d Send(xLine) i $d(^TMIF(mi,10,xLine)) s stop=1
 q
SET(epis) ; set record to be sent to instrument
 i '$$select^LVBEPVIS(epis,"Y") d
 .k VISIT m VISIT=PLIST
 .f j=1:1:VISIT s VISIT(j)=$p(VISIT(j),$c(1))
 .s cdate=$zd($s($l(VISIT(12)):VISIT(12),1:""),8)
 .s ctime=$tr($zt($s($l(VISIT(13)):VISIT(13),1:""),1),":")
 .s tcx=""
 . ; extract bottle numbers and set loaded date and time
 .s num="" f  s num=$o(^TEPI(epis,1,TEST,num)) q:num=""  d
 .. ; ignore pathologist attention and authorised results
 ..s status=$p(^(num),"\",31) i (status="P")!(status="A") q
 ..s tc="" f  s tc=$o(ITEM(tc)) q:tc=""  d
 ...s botnum=$p($g(^TEPI(epis,1,TEST,num,"DATA",tc)),"\") q:botnum=""
 ...s bot=$e(botnum,3,4) q:bot=""
 ...i " 92 93 94 "'[(" "_bot_" ") q
 ...s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_botnum
 ... ; set date bottle 'effectively' loaded onto instrument using $h and seconds since 0,0 $h
 ...i '$d(^TMIF(mi,33,"Epis",epis,bot,botnum)) d
 ....s $p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",1)=$h,$p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",2)=+$h*(3600*24)+$p($h,",",2)
 ....s ^TMIF(mi,33,"Date",+$h,epis,bot,botnum)="",^TMIF(mi,33,"Res",epis,bot,botnum)=""
 .i $l(tcx) d
 ..i 'header d
 ...s line=$o(^TMIF(mi,10,""),-1)+1,header=1
 ...s ^TMIF(mi,10,line)="H|\^&|||LABTRAK||||LABTRAK|BACTEC|||1"_cr_lf
 ..s line=$o(^TMIF(mi,10,""),-1)
 ..s ^TMIF(mi,10,line)=^TMIF(mi,10,line)_"P|"_header_"||"_$g(VISIT(20))_"||"_$g(VISIT(4))_"^"_$g(VISIT(3))_"||"_$zd($g(VISIT(6)),8)_"|"_$g(VISIT(5))_"|"_cr_lf
 ..s ^TMIF(mi,10,line)=^TMIF(mi,10,line)_"O|1|"_epis_"||"_tcx_"|||"_cdate_ctime_cr_lf
 ..s header=header+1
 q
sh(x) ; shift
 n (x) s x1=x\16+32,x2=x#16+32
 q $c(x2)_$c(x1)
Send(xLine) ; send list of orders if exists
 w syn,*-3 d trace^MIF000(mi,"SYN","H-->M")
 f j=1:1:10 r *R:1 i ($c(R)=ack)!($c(R)=syn) q
 d trace(R)
 i $c(R)=syn q
 i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 s record=^TMIF(mi,10,xLine)
 f bsn=1:1 q:'$l(record)  d
 .s data=$e(record,1,128),record=$e(record,129,$l(record))
 .f j=$l(data)+1:1:128 s data=data_$c(0)
 .i '$$SEND(data,bsn) k ^TMIF(mi,10,xLine)
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str,bsn) ; send string to instrument
 s chsum=$$sh($$CHSUM(str))
 w soh,$$sh(bsn#256),$$sh(255-(bsn#256)),str,chsum,*-3
 d trace^MIF000(mi,$$sh(bsn#256)_$$sh(255-(bsn#256))_str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i R'=-1 q
 d trace(R)
 i $c(R)=ack q 0
 i $c(R)=eot q 1
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 q z#256
NAK ; send 'nak' to instrument
 h 3 w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 q
ACK ; send 'ack' to instrument
 h 3 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 q
]]></Routine>
</Export>
