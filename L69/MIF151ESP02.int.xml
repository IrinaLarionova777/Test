<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF151ESP02" type="INT" languagemode="0" generated="1"><![CDATA[
MIF151ESP02(mi) ; ILab 600 (Bi-Directional)
 ; -------------------------------
 ;  Hospital Municipal de Badalona
 ;  Mc_Fly Apr 17, 2003
 ; -------------------------------
 ; Host query
 ; Message type 4: <STX>Message contents<ETX><BCC><CR><LF>
 ; Instrument number = 1
 ; ID Channel: <Test type> + <Test #>
 ; -------------------------------
 ; It must know the speciment
 ; Workarround: Host-query is always Serum
 k ^TMIF(mi,99)
 k ^TMIF(mi,10)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),cr=$c(13),lf=$c(10)
 i $$start^MIF000() q
 k barcodeans
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main 
 s record=$$read^MIF000(stx,lf) q:'$l(record)
 d trace^MIF000(mi,record,"H<--M")
 ; Here: Message contents<ETX><BCC1><BCC2><CR>
 s body=$e(record,1,$l(record)-4)
 s bcc=$e(record,$l(record)-2,$l(record)-1)
 i $$bcc(body)'=bcc d send("E11") q  ; nak
 s type=$e(body,1)
 i type="B" d barcode q
 i type="R" d ready q
 i type="X" d response q
 i type="D" d result q
 i type="Q" d send("E10") q  ; TODO: Quality control
 i type="C" d send("E10") q  ; TODO: Calibration
 d trace^MIF000(mi,"Unknow type "_type,"ERROR")
 q
barcode 
 d send("E10") ; ack
 s barcodeans=""
 ; Barcode Query
 s epis=$$remleft($e(body,3,20))
 k ^TMP("MIF-SINGLE",$j)
 d scanone^MIF000(mi,epis)
 s wepis=epis_"S" ; Only Serum
 s total=0
 s (tc,test)=""
 f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,wepis,tc)) q:tc=""  d
 . s test=test_tc
 . s total=total+1
 k ^TMP("MIF-SINGLE",$j)
 d savew(epis,test,total,1) ; Serum
 q
ready 
 ; Ready to Receive
 i $d(barcodeans) d  k barcodeans q
 . i barcodeans="" d send("E18") q  ; Nothing to do
 . d send(barcodeans)
 i '$d(^TMIF(mi,10)) k ^TMIF(mi,36,"COUNT") d loadlist^MIF000(mi)
 i '$d(^TMIF(mi,10)) d send("E18") q  ; Nothing to do
 s line=$o(^TMIF(mi,10,""))
 s reg=^TMIF(mi,10,line)
 d send(reg)
 k ^TMIF(mi,10,line)
 q
SAVE(mi) ; create loadlist records
 n (mi)
 s epis=^TMP("MIF000",$j,"LL","EPISODE")
 s epis=$p(epis,$c(1),1)
 s tray=^TMP("MIF000",$j,"LL","TRAY")
 s suffix=^TMP("MIF000",$j,"LL","SUFFIX")
 s specimen=^TMP("MIF000",$j,"LL","SPECIMEN")
 s specimen=$p(specimen,$c(1),1)
 s wsuffix=$s(suffix="S":1,suffix="U":2,1:"?")
 i specimen'=wsuffix q
 d trace^MIF000(mi,"Save "_epis,"H-->M") 
 ; Verify tray
 i '$d(^TMIF(mi,36,"COUNT")) d
 . s disk=+$e(tray)
 . s pos=+$e(tray,2,3)
 . i disk<1!(disk>3) s disk=1
 . i pos<1!(pos>75) s pos=1
 . s ^TMIF(mi,36,"COUNT")=disk_$e("00",1,2-$l(pos))_pos
 s tray=^TMIF(mi,36,"COUNT")
 s disk=$e(tray)
 s pos=$e(tray,2,3)
 s pos=pos+1
 i pos>75 s pos=1,disk=disk+1 i disk>3 s disk=1
 s ^TMIF(mi,36,"COUNT")=disk_$e("00",1,2-$l(pos))_pos
 ; Specimen =1 Serum, 2 Urine
 s specimen=$s(specimen=1:1,specimen=2:2,1:3)
 s total=0
 s (tc,test)=""
 f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  d
 . s test=test_tc
 . s total=total+1
 d savew(epis,test,total,specimen,tray)
 q 0
savew(epis,test,total,specimen,tray) 
 s tray=$g(tray)
 s wdisk=" "
 s wtray="  "
 i tray'="" s wdisk=$e(tray),wtray=$e(tray,2,3),wtray=$e("00",1,2-$l(wtray))_wtray
 s total=$e("000",1,3-$l(total))_total
 d PATDET(epis)
 s urgent=0
 s mess="W"
 s mess=mess_"1" ; Instrument
 s mess=mess_"0000" ; Request #
 s mess=mess_$e(epis_$j("",14),1,14) ; Sample ID
 s mess=mess_urgent ; Routine or Stat
 s mess=mess_$e(epis_$j("",18),1,18) ; Sample barcode
 s mess=mess_$e($zd($h,8),3,8) ; Request date
 s mess=mess_$tr($zt($p($h,",",2),2),":") ; Request time
 s mess=mess_wdisk ; Sample disk #
 s mess=mess_wtray ; Sample position #
 s mess=mess_specimen ; Sample type
 s mess=mess_sex ; Sex
 s mess=mess_"3" ; Cup type
 s mess=mess_"001" ; Dilution ratio
 s mess=mess_"1" ; Automatic rerun flag
 s mess=mess_"1" ; Reflex testing flag
 s mess=mess_"001" ; Doctor code
 s mess=mess_total ; No of test request
 s mess=mess_test
 i $d(barcodeans) s barcodeans=mess q
 s line=$o(^TMIF(mi,10,""),-1)+1
 s ^TMIF(mi,10,line)=mess        
 q
PATDET(epis) ; set patient details record
 s sex=3
 i $$select^LVBEPVIS(epis,"Y")
 i SQLCODE q
 s sex=$s(PLIST(5)="M":1,PLIST(5)="F":2,1:3)
 q
response 
 s response=$e(body,7)
 d trace^MIF000(mi,"response="_response_" (0=ok)","H<--M")
 q
result 
 d send("E10") ; ack
 s (sample,epis,surname,result,date,time,QC)=""
 s epis=$$remleft($e(body,7,20))
 s stype=$e(body,21)
 s sample=epis_$s(stype=1:"S",stype=2:"U",1:"?")
 s tblocks=$e(body,22)
 s block=$e(body,23)
 s ttest=+$e(body,24,25)
 s wtest=$e(body,26,$l(body))
 f n=1:1:ttest d
 . s rtest=$e(wtest,1,22)
 . s wtest=$e(wtest,23,$l(wtest))
 . s testtype=$e(rtest,1)
 . s testnum=$e(rtest,2,4)
 . s testflag=$e(rtest,5)
 . s testquality=$e(rtest,6)
 . s testquantity=$e(rtest,7,14)
 . s testerrors=$e(rtest,15,22)
 . 
 . i testquality'=" " s res=$s(testquality=0:"-",testquality=1:"+-",1:"+"_(testquantity-1))
 . i testquality=" " s res=+$e(testquantity,1,5)_"."_$e(testquantity,6,8)
 . 
 . ; We look some flags only
 . k alarms
 . f z=8:-1:1 d setalarm($e(testerrors,z),z-1)
 . s alarm=""
 . i $d(alarms(8000000)) s alarm=8000000
 . i $d(alarms(10000)) s alarm=10000
 . i $d(alarms(1)) s alarm=1
 .
 . s result=result_testtype_testnum_$c(par10)_res_$c(par10)_alarm_$c(par11)
 i $l(result) d
 . d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 . i $d(^TEPI(epis)) d
 .. s tsgrp="" f  s tsgrp=$o(^TMIF(mi,"TG",tsgrp)) q:tsgrp=""  d
 ... s ts="" f  s ts=$o(^TMIF(mi,"TG",tsgrp,"TS",ts)) q:ts=""  i $e(ts)="U" d
 .... s tscnt=$$getnats^LVBVIS4(epis,ts) i tscnt d
 ..... k PLIST
 ..... s ti="" f j=1:1 s ti=$o(^TMIF(mi,1,epis_" ","DATA",ti)) q:ti=""  d
 ...... s x=$o(^TMIF(mi,1,epis_" ","DATA",ti,""),-1)  
 ...... s PLIST(j)=ti_"\"_$p($g(^TMIF(mi,1,epis_" ","DATA",ti,x)),"\",1)
 ..... s PLIST=$o(PLIST(""),-1)
 ..... i $$machsave^LVBVIS4(par2,epis,"E","",ts,tsgrp,"G",,,,"Y")
 q
setalarm(char,zeros) 
 n num,p
 s num=$a(char)-64
 f p=8,4,2,1 i num'<p s alarms(p*(10**zeros))="",num=num-p
 q
bcc(txt) ; CRC
 n j
 s j=$zcrc(txt,1)
 s j=$zhex(j)
 q $e("00",1,2-$l(j))_j
remleft(txt) ; Remove left spaces
 n n
 f n=$l(txt):-1:0 q:$e(txt,n)'=" "
 q $e(txt,1,n)
remright(txt) ; Remove right spaces
 n n,v
 f n=1:1:$l(txt) q:$e(txt,n)'=" "
 s v=$e(txt,n,$l(txt))
 i v=" " s v=""
 q v
send(str) ; send string to instrument
 n bcc
 s bcc=$$bcc(str)
 w stx,str,etx,bcc,cr,lf,*-3
 d trace^MIF000(mi,str,"H-->M")
 q
]]></Routine>
</Export>
