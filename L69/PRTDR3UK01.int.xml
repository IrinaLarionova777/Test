<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR3UK01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR3UK01 ;IG 27/05/98 ;Doctors Reports printing (cumulative) - UK NN
 ;
 ; supported calls :
 ;
 ; $$print^PRTDR301(vtsRID,type,EpisodeList,DH)
 ;
 ; type          : P - print
 ;                 V - view from Enquiry
 ;                   - view from Entry
print(vtsRID,type,EpisodeList,DH,group,cOrder) n (vtsRID,type,EpisodeList,DH,group,cOrder,dh1,dh2,tsLM,tsWD,COMMENTS,GroupName) 
 s vtsRID=$g(vtsRID),type=$g(type),EpisodeList=$g(EpisodeList),DH=$g(DH),group=$g(group),cOrder=$g(cOrder),dh1=$g(dh1),dh2=$g(dh2)
 s epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2),max=6,ColWidth=9,ColLeft=17,FullYear="N",tsLM=$g(tsLM),tsWD=$g(tsWD)
 ; check for margin changes
 s margin="",%routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 i $l(%routine) s LineRoutine="Margin^"_%routine i $l($t(@LineRoutine)) x "s margin=$$Margin^"_%routine_"()"
 K ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"CUM-TC-ORDER")
 i '$$select^LVBEPVIS(epis,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s range1="(",range2=")",(flaglow,flaghigh,Alternate,Reportable)="" i '$$select^MVBCFLAB(1) d
 .i $l(PLIST(18)) s flaglow=PLIST(18)
 .i $l(PLIST(19)) s flaghigh=PLIST(19)
 .i $l(PLIST(20)) s range1=PLIST(20)
 .i $l(PLIST(21)) s range2=PLIST(21)
 .i $l(PLIST(14)) s max=PLIST(14)
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 .i PLIST(37)="Y" s ColWidth=11,FullYear="Y"
 .i $l(PLIST(68)) s Reportable=PLIST(68)
 .i $l(PLIST(69)) s Alternate=PLIST(69)
 k FORMAT,TS
 s rowid=test_"||"_dh1_"||"_dh2,layout=""
 s layout=$$seldata^LVBCTTSP(rowid,4)
 s layout=$p(layout,$c(1))
 i dh1="D" s lan=$$seldata^LVBCTDR(dh2,46)
 s lan=$p($g(lan),$c(1))
 i '$$getit^LVBCTTSF(,vtsRID,"Y",,,layout,,lan) d
 .f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS($p(vtsRID,"||",2),ti)=""
 i $l(group),group'=" " d
 .i '$$select^LVBCTTWG(group) m CTTWG=PLIST
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ...i '$$getit^LVBCTTSF(,epis_"||"_ts_"||"_tscnt,"Y") d
 ....f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)=""
 .s ts="" f  s ts=$o(^TTAB("GR",group,"TS",ts)) q:ts=""  i '$d(TS(ts)) d
 ..i '$$getit^LVBCTTSF(ts,,"Y") d
 ...f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s:'$d(FORMAT(ti)) FORMAT(ti)=PLIST(j) s TS(ts,ti)=""
 .s ti="" f cnt=1:1 s ti=$o(FORMAT(ti)) q:ti=""  d
 ..i $g(^TTAB("GR",group,"TI",ti)) s $p(FORMAT(ti),$c(2),27)=^(ti)
 ..e  s $p(FORMAT(ti),$c(2),27)=cnt
 ; create list of episodes and results
 k EPTS i $l(EpisodeList) d
 .f col=1:1:$l(EpisodeList,",") s ep=$p(EpisodeList,",",col) i $l(ep) d
 ..s date=$$seldata^LVBEPVIS(ep,12),time=$$seldata^LVBEPVIS(ep,13)
 ..s time=$tr(time," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 k EPTS i '$l(EpisodeList) d
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=VISIT(j)
 .s TestList=test i $l(group),group'=" " s ts="" f  s ts=$o(TS(ts)) q:ts=""  s TestList=TestList_","_ts
 .d CumList^PRTDR00(TestList,epis,visit,cOrder)
 .f col=1:1:PLIST d
 ..s ep=PLIST(col)
 ..s date=$$seldata^LVBEPVIS(ep,12),time=$$seldata^LVBEPVIS(ep,13)
 ..s time=$tr(time," ")
 ..s ^TMP($zn,$j,"CUM",col)=ep_$c(1)_date_$c(1)_time
 ..i $l(group),group'=" " d
 ...s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,ts,cnt)) q:cnt=""  s rowid=ep_"||"_ts_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ..i '$l(group)!(group=" ") d
 ...i ep'=epis s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 ...i ep=epis d
 ....i '$d(EPTS(vtsRID)) s EPTS(vtsRID)="" d TestItems(col,vtsRID) q
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  s rowid=ep_"||"_test_"||"_cnt i '$d(EPTS(rowid)) s EPTS(rowid)="" d TestItems(col,rowid) q
 i '$d(^TMP($zn,$j,"CUM-TC")) d  q 100_$c(1)_EpisodeList
 .k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-EXIST")
 ; print cumulative header
 d ADD^PRTDR00(,"~BOLD","Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM+margin,$e("Date Collected"_$j("",tsWD),1,ColLeft-3)_" :","Y")
 s maxcol=$o(^TMP($zn,$j,"CUM",""),-1) s:maxcol>max maxcol=max
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s xx=$p(^TMP($zn,$j,"CUM",col),$c(1),2)
 .i xx s xx=$$extdate2^SSUTIL4(xx) i FullYear="N" s $p(xx,"/",3)=$e($p(xx,"/",3),3,4)
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,xx,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM+margin,$e("Time Collected"_$j("",tsWD),1,ColLeft-3)_" :","Y")
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s ep=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .s xx=$$seldata^LVBEPVIS(ep,13)
 .s xx=$tr(xx," "),xx=$s($l(xx):$$exttime^SSUTIL4(xx),1:"N/S")
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,xx,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM+margin,$e("Episode"_$j("",tsWD),1,ColLeft-3)_" :","Y")
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 .s xx=$p(^TMP($zn,$j,"CUM",col),$c(1),1)
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,xx,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM+margin,"==============","Y") s delim="",$p(delim,"=",ColWidth)=""
 f col=1:1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,delim,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(,"~NORMAL","Y")
 ; print test set header only for Cum report page
 i $l(EpisodeList) d
 .d ADD^PRTDR00(,"~RES-TSHEADER"),ADD^PRTDR00(,$c(13))
 .i ('$l(group)!(group=" ")),$l(TEST(17)) d
 ..s tsName=""
 ..i '$l(tsName),$l(lan) d
 ...s rowid=TEST(2)_"||"_lan
 ...s tsName=$$seldata^LVBCTTSZ(rowid,3)
 ...s tsName=$p(tsName,$c(1))
 ..i '$l(tsName) s tsName=$s(TEST(17)="*":TEST(3),1:TEST(17))
 ..d LN^PRTDR00(),ADD^PRTDR00(tsLM+margin,tsName),LN^PRTDR00()
 .i $l(group),group'=" ",$l(GroupName) d LN^PRTDR00(),ADD^PRTDR00(tsLM+margin,GroupName),LN^PRTDR00() s GroupName=""
 .d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 ; print start of the group
 i $l($g(CTTWG(11))) d
 .d LN^PRTDR00()
 .f j=1:1:$l(CTTWG(11),"|") d ADD^PRTDR00(tsLM+margin,$p(CTTWG(11),"|",j)),LN^PRTDR00()
 ; print results
 s order="" f  s order=$o(^TMP($zn,$j,"CUM-TC-ORDER",order)) q:order=""  d
 .s ti=^(order)
 .i type="",$p(FORMAT(ti),$c(2),15)'="Y" d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13))
 .i order#1=0 d ADD^PRTDR00(tsLM+margin,$e($tr($p(FORMAT(ti),$c(2),3),":")_$j("",tsWD),1,ColLeft-3)_" :")
 .f col=(1+(order#1)):1:maxcol i $d(^TMP($zn,$j,"CUM",col)) d
 ..s result=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),1),flag=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),2),sr=$p($g(^TMP($zn,$j,"CUM",col,ti)),$c(1),3)
 ..i result="" d
 ...i $d(^TMP($zn,$j,"CUM-EXIST",col,ti)) d  q
 ....i $p(FORMAT(ti),$c(2),8)="M" s result="TF"
 ....e  s result=""
 ...s result="-"
 .. ; print extra info
 ..i flag="L" d
 ...i "MN"[$e($p(FORMAT(ti),$c(2),18)),$l(flaglow) s result=result_" "_flaglow
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ..i flag="H" d
 ...i "MN"[$e($p(FORMAT(ti),$c(2),18)),$l(flaghigh) s result=result_" "_flaghigh
 ...d ADD^PRTDR00(,"~RES-ABNORMAL")
 ..d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth)+margin,result)
 ..i (flag="L")!(flag="H") d
 ...i type="",($p(FORMAT(ti),$c(2),15)'="Y")!(sr="Y") d ADD^PRTDR00(,"~INVISIBLE"),ADD^PRTDR00(,$c(13)) q
 ...d ADD^PRTDR00(,"~NORMAL")
 .i type="",($p(FORMAT(ti),$c(2),15)'="Y")!(sr="Y") d ADD^PRTDR00(,"~NORMAL"),ADD^PRTDR00(,$c(13))
 .d LN^PRTDR00()
 ; print end of the group
 i $l($g(CTTWG(12))) d
 .d LN^PRTDR00()
 .f j=1:1:$l(CTTWG(12),"|") d ADD^PRTDR00(tsLM+margin,$p(CTTWG(12),"|",j)),LN^PRTDR00()
 i $d(^TMP($zn,$j,"CUM-COMMENTS")) d
 .s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 .s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="~BOLD"
 .s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM_"   ",1,3)_"Comments"
 .s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="~NORMAL"
 .s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 .s col="" f  s col=$o(^TMP($zn,$j,"CUM-COMMENTS",col)) q:col=""  d
 ..f j=1:1 q:'$d(^TMP($zn,$j,"CUM-COMMENTS",col,j))  d
 ...f j1=1:1:$l(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^") d
 ....s xx=$p(^TMP($zn,$j,"CUM-COMMENTS",col,j),"^",j1) i type="P" s xx=$tr(xx,$c(16,17,18),"^\|")
 ....s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM_"   ",1,3)_xx
 ...s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 .s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-COMMENTS"),^TMP($zn,$j,"CUM-EXIST")
 d PrintNonCum
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-COMMENTS"),^TMP($zn,$j,"CUM-EXIST"),^TMP($zn,$j,"NON-CUM-TC")
 q 0
 ; collect Test Items information
TestItems(col,rowid,include) n (col,rowid,include,type,FORMAT,vtsRID,Alternate,TS,Reportable,width) s col=$g(col),rowid=$g(rowid),include=$g(include)
 i '$$select^LVBVISTS(rowid,"Y") d
 .m ^TMP($zn,$j,"CUM-EXIST",col)=TS($p(rowid,"||",2))
 .s status=$$status^LVBVIS1(rowid)
 .s Result=0 i status="A" s Result=1
 .e  i vtsRID=rowid s Result=1
 .e  i $l(type),status="E",Reportable["ENTERED" s Result=1
 .e  i $l(type),status="P",Reportable["PATHOLOGIST" s Result=1
 .i '$$results^LVBVISTS(rowid) f j=1:1:PLIST d
 ..s ti=$p(PLIST(j),$c(1),2),(result,flag,sr)=""
 ..i Result s result=$p(PLIST(j),$c(1),3),flag=$p(PLIST(j),$c(1),5),sr=$p(PLIST(j),$c(1),22)
 ..i '$d(FORMAT(ti)) q
 .. ; not included in cumulative
 ..i $p(FORMAT(ti),$c(2),20)'="Y",include="" q
 ..i $l(ti),$l(result) d
 ... ; Alternate result entered
 ...i type="P",$l(Alternate),result=$p(Alternate,"-") q
 ... ; non reportable test item
 ...i $l(type),($p(FORMAT(ti),$c(2),15)'="Y")!(sr="Y") q
 ...i $e($p(FORMAT(ti),$c(2),18))="N" d IN
 ...i $e($p(FORMAT(ti),$c(2),18))="D" d ID^PRTDR00
 ...i $e($p(FORMAT(ti),$c(2),18))="T" d IT^PRTDR00
 ...i $e($p(FORMAT(ti),$c(2),18))="Y" d IY^PRTDR00
 ...i include="" d
 ....f xcol=col\1:.1:(col+.9) q:'$d(^TMP($zn,$j,"CUM",xcol,ti))  i ^TMP($zn,$j,"CUM",xcol,ti)=(result_$c(1)_flag_$c(1)_sr) s xcol="" q
 ....q:'$l(xcol)  s ^TMP($zn,$j,"CUM",xcol,ti)=result_$c(1)_flag_$c(1)_sr
 ....i '$d(^TMP($zn,$j,"CUM-TC",ti)) d
 .....s order=$p(FORMAT(ti),$c(2),27) i '$l(order) s order=$p(FORMAT(ti),$c(2),2)
 .....s ^TMP($zn,$j,"CUM-TC-ORDER",(order+(xcol#1)))=ti,^TMP($zn,$j,"CUM-TC",ti)=""
 ...i include="Y" d
 ....i $l(result) s ^TMP($zn,$j,"NON-CUM-TC",ti)=result_$c(1)_flag
 ....e  m ^TMP($zn,$j,"NON-CUM-TC",ti)=result
 . ; compile test set comments
 .i Result k PLIST i '$$COMMENTS^LVBCOM01(rowid,,type,"N",,"Y","Y",.ExcludeList) i PLIST d
 ..s epis=$p(rowid,"||")
 ..s date=$$seldata^LVBEPVIS(ep,12),time=$$seldata^LVBEPVIS(ep,13)
 ..s time=$tr(time," ")
 ..s time=$s($l(time):$$exttime^SSUTIL4(time),1:"N/S")
 ..s x1=$o(^TMP($zn,$j,"CUM-COMMENTS",col,""),-1)+1
 ..s ^TMP($zn,$j,"CUM-COMMENTS",col,x1)=" "_$$extdate2^SSUTIL4(date)_" "_time_" "_epis
 ..i x1=1 s width=$l(^TMP($zn,$j,"CUM-COMMENTS",col,1))+2
 ..f j1=1:1:PLIST d
 ...s x1=$o(^TMP($zn,$j,"CUM-COMMENTS",col,""),-1)+(j1>1)
 ...s ^TMP($zn,$j,"CUM-COMMENTS",col,x1)=$e($g(^TMP($zn,$j,"CUM-COMMENTS",col,x1))_$j("",width),1,width)_PLIST(j1)
 q
PrintNonCum s start="Y"
 i $l(group),group'=" " d
 .s ts="" f  s ts=$o(TS(ts)) q:ts=""  d
 ..s cnt="" f  s cnt=$o(^TEPI(epis,1,ts,cnt)) q:cnt=""  d
 ...k ^TMP($zn,$j,"NON-CUM-TC") s rowid=epis_"||"_ts_"||"_cnt d TestItems(1,rowid,"Y")
 ...i '$d(^TMP($zn,$j,"NON-CUM-TC")) q
 ...s ti="" f  s ti=$o(TS(ts,ti)) q:ti=""  d
 ....i $p(FORMAT(ti),$c(2),20)="N",$p(FORMAT(ti),$c(2),15)="Y" d
 ..... ; print extra info for numeric fields
 .....s result=$p($g(^TMP($zn,$j,"NON-CUM-TC",ti)),$c(1),1)
 .....i '$l(result) d  q
 ......s i="" f  s i=$o(^TMP($zn,$j,"NON-CUM-TC",ti,i)) q:i=""  s result=^(i) d
 .......i start="Y" s start="" f j=1:1:2 s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 .......i i=1 d
 ........s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM_"   ",1,3)_$p(FORMAT(ti),$c(2),3)
 ........s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 .......s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM_"   ",1,3)_result
 .......s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 .....i $l(result) d  q
 ......i start="Y" s start="" f j=1:1:2 s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 ......s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM_"   ",1,3)_$p(FORMAT(ti),$c(2),3)
 ......s flag=$p($g(^TMP($zn,$j,"NON-CUM-TC",ti)),$c(1),2)
 ......i "MN"[$e($p(FORMAT(ti),$c(2),18)) d
 .......i flag="L" d
 ........i $l(flaglow) s result=result_" "_flaglow
 ........s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="~BOLD"
 .......i flag="H" d
 ........i $l(flaghigh) s result=result_" "_flaghigh
 ........s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="~BOLD"
 ......s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)=$c(13),x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM+40_"   ",1,3)_result
 ......i "MN"[$e($p(FORMAT(ti),$c(2),18)) d
 .......i (flag="L")!(flag="H") s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="~NORMAL"
 ...... ; print units
 ......s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)=$c(13),x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM+50_"   ",1,3)_$p(FORMAT(ti),$c(2),17)
 ...... ; print ranges
 ......s range="" i $p(FORMAT(ti),$c(2),14) d
 .......i $l($p(FORMAT(ti),$c(2),22)),$l($p(FORMAT(ti),$c(2),23)) d
 ........i $e($p(FORMAT(ti),$c(2),22))?1N s $p(FORMAT(ti),$c(2),22)=$j($p(FORMAT(ti),$c(2),22),0,$e($p(FORMAT(ti),$c(2),18),2,4))
 ........i $e($p(FORMAT(ti),$c(2),23))?1N s $p(FORMAT(ti),$c(2),23)=$j($p(FORMAT(ti),$c(2),23),0,$e($p(FORMAT(ti),$c(2),18),2,4))
 ........s range=$j(range1_$p(FORMAT(ti),$c(2),22),$p(FORMAT(ti),$c(2),19)+$l(range1))_"-"_$p(FORMAT(ti),$c(2),23)_range2
 .......i $l($p(FORMAT(ti),$c(2),22)),'$l($p(FORMAT(ti),$c(2),23)) d
 ........i $e($p(FORMAT(ti),$c(2),22))?1N s $p(FORMAT(ti),$c(2),22)=$j($j($p(FORMAT(ti),$c(2),22),0,$e($p(FORMAT(ti),$c(2),18),2,4)),$p(FORMAT(ti),$c(2),19)+$l(range1))
 ........s range=range1_$p(FORMAT(ti),$c(2),22)_range2
 ......i $l(range) s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="?"_$e(tsLM+67_"   ",1,3)_range
 ......s x=$o(COMMENTS(""),-1)+1,COMMENTS(x)="!"
 q
IN ; Numeric
 i $l(result),$d(^TTAB("RC",result)) s:$l($p(^(result),"\",1))<12 result=$p(^(result),"\",1) q
 s Flag="" f  q:'$l(result)  q:"<>+-"'[$e(result)  s Flag=Flag_$e(result),result=$e(result,2,$l(result))
 s result=Flag_$j(result,0,$e($p(FORMAT(ti),$c(2),18),2,9))
 q
]]></Routine>
</Export>
