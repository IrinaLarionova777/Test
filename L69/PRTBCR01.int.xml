<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBCR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBCR01 ; ML 20/10/98 ; BILLING CONSISTENCY
 q
 ; Parameters :  1. From Date
 ;               2. To Date
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 ;n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Billing Consistency Report"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..q:$$select^LVBEPVIS(epis,"Y")  k VISIT m VISIT=PLIST k PLIST
 ..i VISIT(87)="" d
 ...s check="Yes" d BILL^at46(epis,,.check)
 ...i check="Y" d
 ....s ^TMP($zn,$j,"REPORT",epis)=VISIT(68)
 ....s ^TMP($zn,$j,"PAY",VISIT(9))=$g(^TMP($zn,$j,"PAY",VISIT(9)))+1
 ....s episx="" f  s episx=$o(^TDEB(VISIT(20),VISIT(12),0,episx)) q:episx=""  d
 .....i epis'=episx s ^TMP($zn,$j,"REPORT",epis,episx)=""
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis) d
 ...d PRINT^PRTREP00(.PLIST,.N) k PLIST,xxx
 ...s episx="" f  s episx=$o(^TMP($zn,$j,"REPORT",epis,episx)) q:episx=""  d
 ....i '$$select^LVBEPVIS(episx) d
 .....k PLIST(3),PLIST(4),PLIST(20)
 .....d PRINT^PRTREP00(.PLIST,.N) k PLIST,xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s payc="" f  s payc=$o(^TMP($zn,$j,"PAY",payc)) q:payc=""  d
 ..i '$$select^LVBCTPC(payc,"Y") d
 ...d ADDLINE^PRTREP00(REPID,"PRT",PLIST(3)_" ("_PLIST(2)_")  -  "_^TMP($zn,$j,"PAY",payc))
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="BCR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
