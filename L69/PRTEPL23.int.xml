<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPL23" type="INT" languagemode="0" generated="1"><![CDATA[
PRTEPL23 ; ML 18/05/2004 ; Episode Patient Label - Goulburn Valley Health
 q
 ; Parameters :  1. Episode number
 ;               2. Label code
 ;               3. Quantity
 ;               4. Computer Name
 ;               5. Worksheet
 ;               6. Worksheet No.
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY
 i '$l(PAR2) d
 .k ^TMP("WS",$j)
 .s ep="" f  s ep=$o(^TMP($zn,$j,"REPORT",ep)) q:ep=""  d
 ..s label="" f  s label=$o(^TMP($zn,$j,"REPORT",ep,label)) q:label=""  d
 ...m ^TMP("WS",$j,label,ep)=^TMP($zn,$j,"REPORT",ep,label)
 .s label="" f  s label=$o(^TMP("WS",$j,label)) q:label=""  d
 ..s ep="" f  s ep=$o(^TMP("WS",$j,label,ep)) q:ep=""  s cnt=+^(ep) d
 ...d START^PRTREP00(USER,,ep_"\"_label_"\"_cnt,"EPL")
 .k ^TMP("WS",$j)
 i $l(PAR2) d PRINT
CCREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR2_"-"_PAR1,PLIST(9)="M" i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 q
BODY ; create temporary printing file
 ; label and quantity entered
 i $l(PAR1),$l(PAR2),$l(PAR3) d  q
 .i $$open^LVBVISTS(PAR1)
 .f  q:$$fetch^LVBVISTS(PAR1)  d
 ..s ts=$p(PLIST(3),$c(1)),label=""
 ..s label=$$seldata^LVBCTTS(ts,23)
 ..i PAR2=label s ^TMP($zn,$j,"REPORT",PAR1,PAR2,ts)=""
 .i $$close^LVBVISTS()
 .s ^TMP($zn,$j,"REPORT",PAR1,PAR2)=PAR3
 ; print labels for a patient
 i $l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)
 ..f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBWKSRT()
 .i '$l(PAR5) d
 ..i $$open^LVBVISTS(PAR1)
 ..f  q:$$fetch^LVBVISTS(PAR1)  s ts=$p(PLIST(3),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBVISTS()
 i '$l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRE(PAR5_"||"_PAR6)
 ..f  q:$$fetch^LVBWKSRE(PAR5_"||"_PAR6)  s epis=$p(PLIST(2),$c(1)) d
 ...i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)
 ...f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,epis,ts)=$g(^TMP($zn,$j,1,epis,ts))+1
 ...i $$close^LVBWKSRT()
 ..i $$close^LVBWKSRE()
 s epis="" f  s epis=$o(^TMP($zn,$j,1,epis)) q:epis=""  d
 .s ts="" f  s ts=$o(^TMP($zn,$j,1,epis,ts)) q:ts=""  d
 ..s (label,max)=""
 ..s label=$$seldata^LVBCTTS(ts,23),max=$$seldata^LVBCTLB(label,4)
 ..i $l(label),max d
 ...i $l(PAR2),PAR2'=label q
 ...s ^TMP($zn,$j,"REPORT",epis,label,ts)=^TMP($zn,$j,1,epis,ts)
 ...s (cnt,ts1)="" f  s ts1=$o(^TMP($zn,$j,"REPORT",epis,label,ts1)) q:ts1=""  s cnt=cnt+^(ts1)
 ...s xx=cnt\max s:cnt#max xx=xx+1 s ^TMP($zn,$j,"REPORT",epis,label)=xx
 q
PRINT 
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 .k VISIT m VISIT=PLIST s ReqLabel=""
 .s spec="" i $p(VISIT(51),$c(1))'="",'$$select^LVBCTSI($p(VISIT(51),$c(1))) s spec=PLIST(3)
 .s label="" f  s label=$o(^TMP($zn,$j,"REPORT",epis,label)) q:label=""  d
 ..s cnt=^(label),LabelDesc=label,extra="" i '$$select^LVBCTLB(label) s LabelDesc=PLIST(3),extra=PLIST(6)
 ..s cnt=cnt+extra
 ..i label'="REF" d
 ...s (ts,tsDetail)="" f  s ts=$o(^TMP($zn,$j,"REPORT",epis,label,ts)) q:ts=""  s tsDetail=tsDetail_$s(tsDetail="":"",1:" ")_$$seldata^LVBCTTS(ts,4)
 ...k LINE
 ...s LINE(1)=PAR1
 ...i $l(VISIT(3)_","_$e(VISIT(4)))<16 s LINE(2)=VISIT(3)_","_$e(VISIT(4))
 ...e  i $l(VISIT(3))<16 s LINE(2)=VISIT(3)
 ...e  s LINE(2)=$e(VISIT(3),1,14)_"."
 ...s LINE(3)=tsDetail
 ...s LINE(4)=$$extdate2^SSUTIL4(+$h)
 ...f j=1:1:cnt d LABEL
 ..i label="REF" d
 ...s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",epis,label,ts)) q:ts=""  d
 ....s tsDetail=$$seldata^LVBCTTS(ts,4)
 ....k LINE
 ....s LINE(1)=PAR1
 ....i $l(VISIT(3)_","_$e(VISIT(4)))<16 s LINE(2)=VISIT(3)_","_$e(VISIT(4))
 ....e  i $l(VISIT(3))<16 s LINE(2)=VISIT(3)
 ....e  s LINE(2)=$e(VISIT(3),1,14)_"."
 ....s LINE(3)=tsDetail
 ....s LINE(4)=$$extdate2^SSUTIL4(VISIT(12))_" "_$$exttime^SSUTIL4(VISIT(13))
 ....s LINE(5)="GVH Pathology"
 ....d LABEL
 q
LABEL ; print label
 s stx=$c(2),cr=$c(13)
 i $d(LINE) d
 .d ADD^SSPRINT(REPID,"PRT",stx_"L"_cr)
 .d ADD^SSPRINT(REPID,"PRT","H14"_cr)
 .d ADD^SSPRINT(REPID,"PRT","m"_cr)
 .d ADD^SSPRINT(REPID,"PRT","O0000"_cr)
 .i $l($g(LINE(1))) d ADD^SSPRINT(REPID,"PRT","111100001500020"_LINE(1)_cr)
 .i $l($g(LINE(2))) d ADD^SSPRINT(REPID,"PRT","101100001200020"_LINE(2)_cr)
 .i $l($g(LINE(3))) d ADD^SSPRINT(REPID,"PRT","101100000900020"_LINE(3)_cr)
 .i $l($g(LINE(4))) d ADD^SSPRINT(REPID,"PRT","101100000600020"_LINE(4)_cr)
 .i $l($g(LINE(5))) d ADD^SSPRINT(REPID,"PRT","101100000000020"_LINE(5)_cr)
 .d ADD^SSPRINT(REPID,"PRT","E"_cr)
 .d ADD^SSPRINT(REPID,"PRT",stx_"e"_cr)
 .d ADD^SSPRINT(REPID,"PRT",stx_"f290"_cr)
 q
]]></Routine>
</Export>
