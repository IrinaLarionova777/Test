<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF507" type="INT" languagemode="0" generated="1"><![CDATA[
MIF507(mi) ; ML 30/08/2002 ; ABBOTT Cell-Dyn 3700 ; QC - Yes
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),nak=$c(21),can=$c(24),del=$c(127)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000(stx,etx) i '$l(record) q
 d trace^MIF000(mi,record,"H<--M")
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 s type=$p(record,",",1) i type'["   " q
 ; process results record
 s (sample,epis,surname,result,date,time,spec,QC)=""
 s epis=$p(record,",",5),epis=$tr(epis," "),epis=$tr(epis,"""")
 q:epis=""
 s spec=$p(record,",",4) s:spec=1 QC="\"_epis
 s surname=$p(record,",",6),surname=$tr(surname,"""","")
 s date=$p(record,",",8),time=$p(record,",",9)
 s date=$tr(date,"""",""),time=$tr(time,"""","")
 s date=$$intdate^SSUTIL4(date),time=$$inttime^SSUTIL4(time)
 s res="",record=$p(record,",",10,31)
 f j=1:1:22 d
 .s code=$p($p($t(CODE),";",2)," ",j+1),res=$p(record,",",j),res=+$tr(res," ")
 .s result=result_code_$c(par10)_res_$c(par11)
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
CODE ; WBC NEU LYM MONO EOS BASO RBC HGB HCT MCV MCH MCHC RDW PLT MPV PCT PDW NEU% LYM% MONO% EOS% BASO% 
]]></Routine>
</Export>
