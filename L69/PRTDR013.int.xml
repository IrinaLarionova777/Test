<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR013" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR013 ; ML 29/11/99 ; Doctor's Report HEADER - Launceston General Hospital
 q
 ; supported calls
 ;
 ; INIT^PRTDR004
 ; QDR^PRTDR004
 ; Header^PRTDR004(page,par1,par2)
 ; Footer^PRTDR004(page,par1,par2)
 ;
ABScreen() q "L0104"
 ; client specific parameters
INIT s MaxLine=45
 q
 ; print header
Header(page,par1,par2) n (page,par1,par2,VISIT,DOC,HOSP,REPID) s CFSM(8)=$$seldata^MVBCFSM(1,8)
 i '$d(^PR(REPID,"HDR")) d QDR
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s1b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3000h0200VNATA Accredited Laboratory Number : 2972")
 s specimen="",epis=$p(VISIT(2),$c(1))
 ; find copy-to
 k copyto s (cnt,drcnt)=""
 i $$open^LVBVISDC(epis)
 f  q:$$fetch^LVBVISDC(epis)  d
 .i $d(DOC),$p(DOC(2),$c(1))=$p(PLIST(3),$c(1)) q
 .s cnt=cnt+1 i cnt#2 s drcnt=drcnt+1
 .s copyto(drcnt)=$s($d(copyto(drcnt)):copyto(drcnt)_", ",1:"")_PLIST(4)
 i $$close^LVBVISDC()
 ; expand collection date and time / received date and time
 f j=12,13,61,62 s VISIT(j)=$p(VISIT(j),$c(1))
 s colltime="" i $l(VISIT(13)) d
 .i CFSM(8)=VISIT(13) s colltime="N/S" q
 .s colltime=$e(VISIT(13)\3600+100,2,3)_":"_$e(VISIT(13)\60#60+100,2,3)
 s colldate="" i $l(VISIT(12)) s colldate=$$extdate2^SSUTIL4(VISIT(12))
 s rectime="" i $l(VISIT(62)) d
 .i CFSM(8)=VISIT(62) s rectime="N/S" q
 .s rectime=$e(VISIT(62)\3600+100,2,3)_":"_$e(VISIT(62)\60#60+100,2,3)
 s recdate="" i $l(VISIT(61)) s recdate=$$extdate2^SSUTIL4(VISIT(61))
 ; format doctor address
 i $d(DOC) d Address(DOC(9),DOC(10),DOC(11),DOC(12),DOC(13),.dAddress)
 i $d(HOSP) d Address(HOSP(4),HOSP(5),HOSP(6),HOSP(7),HOSP(8),.hAddress)
 ; format patient address
 d Address(VISIT(32),VISIT(33),VISIT(34),VISIT(35),VISIT(36),.pAddress)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s1b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h650V"_$p(VISIT(15),$c(1),2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p14v0s1b4101T")
 s ur=$p($p(VISIT(65),$c(1)),",",2) i '$l(ur) s ur=$p(VISIT(20),$c(1))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3350h650V"_ur)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p13v0s1b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3350h850V"_$p(VISIT(3),$c(1))_" "_$p(VISIT(4),$c(1)))
 ; print patient address
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0s1b4101T")
 f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3350h"_(j*110+950)_"V"_pAddress(j))
 ; print doctor's address
 i $d(DOC) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0s3b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h850V"_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51)))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0s1b4101T")
 .f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h"_(j*110+900)_"V"_dAddress(j))
 ; print ward name
 i $d(HOSP) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0s3b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h850VWard : ")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0s1b4101T")
 .i $l($p(VISIT(22),$c(1),2))>15 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a400h1100V")
 .d ADD^PRTREP00(REPID,"PRT",HOSP(3))
 i $d(copyto) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p9v0s0b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a400h1380VCopy To:")
 .f j=1:1 q:'$d(copyto(j))  d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h"_(j*95+1280)_"V"_copyto(j))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s1b4101T")
 i '$l($p(VISIT(6),$c(1))) s VISIT(6)="Unknown"
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4200h1450V"_VISIT(26)_" "_$s(VISIT(26)=1:"year",1:"years"))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3350h1450V"_$$extdate2^SSUTIL4(VISIT(6)))
 i '$l($p(VISIT(5),$c(1),2)) s $p(VISIT(5),$c(1),2)="Unknown"
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a5000h1450V"_$p(VISIT(5),$c(1),2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1100h1600V"_colldate)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2000h1600V"_colltime)
 i $p(VISIT(16),$c(1))'="" d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3000h1600V"_"External Ref No: "_$p(VISIT(16),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4900h1600V"_$p(VISIT(2),$c(1)))
 ; find if report is partial
 i par2'="XMR" d
 .s (Partial,curRID)=""
 .f  s curRID=$o(^TMP("REPORT",$j,page,"TEST",curRID)) q:curRID=""  i $p(curRID,"||",3) d  q:(Partial="Y")
 ..s status=$$status^LVBVIS1(curRID) i status'="A" s Partial="Y" q
 .i Partial="Y" d
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1600h7350V")
 ..d ADD^PRTREP00(REPID,"PRT","INTERIM REPORT  -  FINAL COPY TO FOLLOW.")
 .i par2="MEET" d
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1600h7350V")
 ..d ADD^PRTREP00(REPID,"PRT","NOT FOR PATIENT FILE - MEETING OR REGISTRY ONLY.")
 d TABS
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h1600V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
 ; print department down side of report (footer)
TABS s (phone,DepName)="" q:par2["-FAX-"
 s rowid="" f  s rowid=$o(^TMP("REPORT",$j,page,"TEST",rowid)) q:rowid=""  d  i $l(DepName) q
 .s dep=$e($p(rowid,"||",2)) i '$$select^LVBCTDEP(dep,"Y") s DepName=PLIST(3)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p18v0s0b5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0p5500h3200v270P"_DepName)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0p0h0V")
 q
 ; calculate date/time
dt(date,time) n (date,time) s date=$g(date),time=$g(time),result=""
 s result=$e(time\3600+100,2,3)_":"_$e(time\60#60+100,2,3)_" "_$$extdate2^SSUTIL4(date)
 q result
 ; print footer
Footer(page,par1,par2) n (page,par1,REPID,VISIT)
 i par1="D" d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p18v1s3b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2000h450VDISCHARGED PATHOLOGY REPORT")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v1s1b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h7600V"_$$dt(+$h,$p($h,",",2)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1600h7600VIssuing of Report Authorised by Dr. Terry Brain, Director of Pathology")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s1b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a5000h7600V"_page_" / "_$o(^TMP("REPORT",$j,""),-1))
 q
QDR s cr="" i '$$select^LVBCTCR(par2,"Y") s cr=$p(PLIST(4),$c(1))
 s QDR="LGH" d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,1))
 i $l($p(cr,"-",2)) d ADD^PRTREP00(REPID,"HDR",$c(27)_"&l"_$p(cr,"-",2)_"H")
 s a=1 f  s a=$o(^SS("QDR",QDR,a)) q:a=""  d ADD^PRTREP00(REPID,"HDR",^(a))
 q
 ; format address
Address(x1,x2,x3,x4,x5,Address) n (x1,x2,x3,x4,x5,Address) k Address s cnt=0 f j=1:1:3 s Address(j)=""
 i $l(x1) s cnt=cnt+1,Address(cnt)=x1
 i $l(x2) s cnt=cnt+1,Address(cnt)=x2
 s cnt=cnt+1
 i $l(x3) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x3
 i $l(x4) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x4
 i $l(x5) d
 .i cnt>1,'$l(Address(cnt)) s cnt=cnt-1
 .s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x5
 q
]]></Routine>
</Export>
