<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTIFC01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTIFC01 ; ML 13/11/98 ; INFECTION CONTROL
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Organism
 ;               4. Patient location
 ;               5. Pathogen Groups
 ;               6. Location Type : Empty - Current
 ;				                    Y     - Original
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Infection Control Report"
 s line=2,xHEADER(line)=""
 i '$l(PAR3) d
 .s line=line+1,xHEADER(line)="All Organisms"
 .s line=line+1,xHEADER(line)=""
 i $l(PAR3) d
 .i PAR3'["," d
 ..s line=line+1,xHEADER(line)="Organism : "_BUG(3)_" ("_PAR3_")"
 ..s line=line+1,xHEADER(line)=""
 .i PAR3["," d
 ..s line=line+1,xHEADER(line)="Organisms : "_PAR3
 ..s line=line+1,xHEADER(line)=""
 i $l(PAR5) d
 .i PAR5'["," d
 ..s line=line+1,xHEADER(line)="Pathogen Group : "_GRP(3)_" ("_PAR5_")"
 ..s line=line+1,xHEADER(line)=""
 .i PAR5["," d
 ..s line=line+1,xHEADER(line)="Pathogen Groups : "_PAR5
 ..s line=line+1,xHEADER(line)=""
 i '$l(PAR4) d
 .s line=line+1,xHEADER(line)="All Patient locations"
 .s line=line+1,xHEADER(line)=""
 i $l(PAR4) d
 .i PAR4'["," d
 ..s line=line+1,xHEADER(line)="Patient location : "_LOC(3)_" ("_PAR4_")"
 ..s line=line+1,xHEADER(line)=""
 .i PAR4["," d
 ..s line=line+1,xHEADER(line)="Patient locations : "_PAR4
 ..s line=line+1,xHEADER(line)=""
 .i PAR6="Y" d
 ..s line=line+1,xHEADER(line)="Search is done by original patient location."
 ..s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 i '$$select^LVBCTBG(PAR5,"Y") k GRP m GRP=PLIST
 i '$$select^LVBCTBUG(PAR3,"Y") k BUG m BUG=PLIST
 i '$$select^LVBCTHOS(PAR4,"Y") k LOC m LOC=PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 i $l(PAR5) d
 .f x=1:1:$l(PAR5,",") i '$$fetchall^LVBCTBGP($p(PAR5,",",x)) d
 ..f y=1:1:PLIST s pat=$p($p(PLIST(y),$c(28),2),$c(1),1) i $l(pat) s PAR3=PAR3_$s($l(PAR3):",",1:"")_pat  
 f j="TS","TC" s ^TMP($zn,$j,j)=""
 s tc="" f  s tc=$o(^TTAB("TC",tc)) q:tc=""  d
 .s format=$p(^TTAB("TC",tc),"\",3) i format="V" d
 ..s ^TMP($zn,$j,"TC")=^TMP($zn,$j,"TC")_$s($l(^TMP($zn,$j,"TC")):",",1:"")_tc
 ..s ts="" f  s ts=$o(^TTAB("TC",tc,1,ts)) q:ts=""  s ^TMP($zn,$j,"TS",ts)=""
 s ts="" f  s ts=$o(^TMP($zn,$j,"TS",ts)) q:ts=""  s ^TMP($zn,$j,"TS")=^TMP($zn,$j,"TS")_$s($l(^TMP($zn,$j,"TS")):",",1:"")_ts
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s pl=$$seldata^LVBEPVIS(epis,22) i PAR6="Y",$$seldata^LVBEPVIS(epis,123)'="" s pl=$$seldata^LVBEPVIS(epis,123)
 ..i $l(PAR4),(","_PAR4_",")'[(","_pl_",") q
 ..s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ...s xti=$s(ts="M005":"M0180",ts="M012":"M0001",ts="M013":"M0001",ts="M014":"M0185",ts="M015":"M0183",ts="M018":"M0210",ts="M030":"M0083",ts="M036":"M0181",1:" ")
 ...i (","_^TMP($zn,$j,"TS")_",")'[(","_ts_",") q
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....s vtsRID=epis_"||"_ts_"||"_tscnt i $$status^LVBVIS1(vtsRID)'="A" q
 ....s tc="" f  s tc=$o(^TEPI(epis,1,ts,tscnt,"DATA",tc)) q:tc=""  s bug=$p(^(tc),"\") i $l(bug) d
 .....i (","_^TMP($zn,$j,"TC")_",")'[(","_tc_",") q
 .....i $l(PAR3),(","_PAR3_",")'[(","_bug_",") q
 .....s st=$p($g(^TEPI(epis,1,ts,tscnt,"DATA",xti)),"\"),stDescr=""
 .....i '$$select^LVBCTTCT(xti_"||"_st) s stDescr=PLIST(9) i '$l(stDescr) s stDescr=$p(PLIST(3),"|")
 .....s mrn=$$mrn^LVBEPVIS(epis),^TMP($zn,$j,"REPORT",mrn,epis,vtsRID,bug)=st_$c(1)_stDescr
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s (Count1,Count2)=""
 .s mrn="" f  s mrn=$o(^TMP($zn,$j,"REPORT",mrn)) q:mrn=""  d
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",mrn,epis)) q:epis=""  d
 ...i $$select^LVBEPVIS(epis) 
 ...s vts="" f  s vts=$o(^TMP($zn,$j,"REPORT",mrn,epis,vts)) q:vts=""  d
 ....s bug="" f  s bug=$o(^TMP($zn,$j,"REPORT",mrn,epis,vts,bug)) q:bug=""  s st=^(bug) d
 .....s xxx(1)=st
 .....s xxx(2)=bug_$c(1)_$$seldata^LVBCTBUG(bug,3)
 .....d PRINT^PRTREP00(.PLIST,.N) k xxx,PLIST
 ...s Count2=Count2+1
 ..s Count1=Count1+1
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?1  Total Episodes : "_Count2)
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","?1  Total Patients : "_Count1)
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="IFC"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
