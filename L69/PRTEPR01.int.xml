<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTEPR01	; IG 19/08/99 ; Episode listing
	q
	; Parameters :  1. Date From
	;               2. Date To
	;               3. Select criteria :    DR - Doctor
	;                                       DG - Doctor group
	;                                       HW - Hospital/Ward
	;                                       PC - Pay code
	;                                       CC - Collection centre
	;                                       CP - Collection personel
	;                                       CO - Company
	;                                       UL - User location
	;                                       MC - Marketing code
	;               4. Value
	;               5. Sort key_1 : DR - Doctor
	;                               DG - Doctor group
	;                               HW - Hospital/Ward
	;                               PC - Pay code
	;                               DT - Date
	;                               CC - Collection centre
	;                               CP - Collection personel
	;                               CO - Company
	;                               UL - User location
	;                               MC - Marketing code
	;               6. Sort key_2
	;               7. Include conning ammount
	;               8. Exclude PEI
	;
CREATE(REPID,PAR,USER,%msql)	; create report
	n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
	k xHEADER d INIT
	s line=1,xHEADER(line)="Episode listing"
	s line=2,xHEADER(line)=""
	i $l(PAR3) d
	.s line=3,xHEADER(line)="Selection criteria : "_$$name(PAR3)_" = "_PAR4
	.s line=4,xHEADER(line)=""
	d HEADER^PRTREP00(.xHEADER,PAR1,PAR2,,,PAR5_"^"_PAR6),BODY,PRINT
	k ^TMP($zn,$j),^TMP("PRTREP00",$j)
	q
INIT	; initialise parameters
	f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
	k xxx f j=1,2 s xxx=$p(PAR10,",",j),par=$s(j=1:PAR5,1:PAR6) d  s $p(PAR10,",",j)=xxx
	.s $p(xxx,$c(2),3)=$$name(par)
	.i par="DR" s $p(xxx,$c(2),4)=35
	.i j=2,'$l(PAR6) s $p(xxx,$c(2),4)=0
	k xxx,xSORT d H^PRTREP00(PAR10,$zn)
	f j="DR","HW","PC","DT","CC","CP","CO","UL","MC" s xSORT(j)=$$name(j)
	s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
	q
name(par)	n (par) s name=""
	i par="DR" s name="Doctor"
	i par="DG" s name="Doctor group"
	i par="HW" s name="Hospital/Ward"
	i par="PC" s name="Pay code"
	i par="DT" s name="Collection Date"
	i par="CC" s name="Collection centre"
	i par="CP" s name="Collection personel"
	i par="CO" s name="Company"
	i par="UL" s name="User location"
	i par="MC" s name="Marketing code"
	q name
BODY	; create temporary printing file
	s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
	.s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
	..i '$$select^LVBEPVIS(epis,"Y") d
	...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
	...k VISIT m VISIT=PLIST
	... ; check selection type
	...s ok=1 d  i 'ok q
	....i PAR3="DR",(","_PAR4_",")'[(","_VISIT(15)_",") s ok=0 q
	....i PAR3="DG",$$select^LVBCTDGD(PAR4_"||"_VISIT(15)) s ok=0 q
	....i PAR3="HW",(","_PAR4_",")'[(","_VISIT(22)_",") s ok=0 q
	....i PAR3="PC",(","_PAR4_",")'[(","_VISIT(9)_",") s ok=0 q
	....i PAR3="CC",(","_PAR4_",")'[(","_VISIT(27)_",") s ok=0 q
	....i PAR3="CP",(","_PAR4_",")'[(","_VISIT(28)_",") s ok=0 q
	....i PAR3="CO",(","_PAR4_",")'[(","_$$epcomp^LVBVIS1(epis)_",") s ok=0 q
	....i PAR3="UL",(","_PAR4_",")'[(","_VISIT(66)_",") s ok=0 q
	....i PAR3="MC" s result=$$seldata^LVBCTDR(VISIT(15),28) i (","_PAR4_",")'[(","_$p(result,$c(1))_",") s ok=0 q
	... ; set sorting key - 1
	...k SORT1 s par=PAR5 d
	....i par="MC" d  q
	.....s x=$$seldata^LVBCTDR(VISIT(15),28) i $l(x) s SORT1(x)=""
	....i par="DG" d  q
	.....s x="" f  s x=$o(^TTAB("DG",x)) q:x=""  i $d(^TTAB("DG",x,$p(VISIT(15),$c(1)))) s SORT1(x)=""
	....s x=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
	....i $l(x) s SORT1(x)=""
	... ; set sorting key - 2
	...k SORT2 s par=PAR6 d
	....i par="MC" d  q
	.....s x=$$seldata^LVBCTDR(VISIT(15),28) i $l(x) s SORT2(x)=""
	....i par="DG" d  q
	.....s x="" f  s x=$o(^TTAB("DG",x)) q:x=""  i $d(^TTAB("DG",x,$p(VISIT(15),$c(1)))) s SORT1(x)=""
	....s x=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
	....i $l(x) s SORT2(x)=""
	...i '$d(SORT1) s SORT1(" ")=""
	...i '$d(SORT2) s SORT2(" ")=""
	...s SORT1="" f  s SORT1=$o(SORT1(SORT1)) q:SORT1=""  d
	....s SORT2="" f  s SORT2=$o(SORT2(SORT2)) q:SORT2=""  d
	.....s ^TMP($zn,$j,"xREPORT",SORT1,SORT2,epis)=""
	.....i PAR7="Y" d
	......s sum=$$price(epis) i sum s ^TMP($zn,$j,"xREPORT",SORT1,SORT2,epis)=sum
	......f j=1:1:$l(VISIT(55),",") s ep=$p(VISIT(55),",",j) i $l(ep) d
	.......s sum=$$price(ep) i sum s ^TMP($zn,$j,"xREPORT",SORT1,SORT2,epis)=$g(^TMP($zn,$j,"xREPORT",SORT1,SORT2,epis))+sum
	q
price(epis)	n (epis,PLIST,PAR8) s epis=$g(epis),(mrn,pc,init,xInit)=""
	s mrn=$$seldata^LVBEPVIS(epis,20),pc=$$seldata^LVBEPVIS(epis,9),init=$$seldata^LVBEPVIS(epis,54)
	i '$l(pc)!'$l(mrn) q ""
	i PAR8'="Y",'$$init^LVBQUOTE(init,pc) s xInit=$g(PLIST(1))
	k PLIST s ts="" f j1=1:1 s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
	.s (j2,tscnt)="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
	..s rowid=epis_"||"_ts_"||"_tscnt
	..s type=$$seldata^LVBCTTS($$seldata^LVBVISTS(rowid,3),15)
	..s supress=$$seldata^LVBVISTS(rowid,37)
	.. ; no charge test set
	..i type="N" q
	.. ; supress billing
	..i supress="Y" q
	..s j2=j2+1
	.i j2 s PLIST(j1)=ts_"\"_j2,PLIST=j1
	i '$$quote^LVBQUOTE(pc,"N",xInit)
	s result="" f j=1:1:PLIST s result=result+$p(PLIST(j),"\",2)
	q result
SORT(par,value)	n (par,value) s result="" i value'=" " d
	.i par="DR",'$$select^LVBCTDR(value,"Y") s result=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))_"\"_PLIST(2)_" - "_PLIST(18)
	.i par="DG",'$$select^LVBCTDG(value,"Y") s result=PLIST(3)
	.i par="HW",'$$select^LVBCTHOS(value,"Y") s result=PLIST(3)
	.i par="PC",'$$select^LVBCTPC(value,"Y") s result=PLIST(3)
	.i par="DT" s result=$$extdate2^SSUTIL4(value)
	.i par="CC",'$$select^LVBCTCC(value,"Y") s result=PLIST(3)
	.i par="CP",'$$select^LVBCTPER(value,"Y") s result=PLIST(3)
	.i par="CO",'$$select^LVBCTCO(value,"Y") s result=PLIST(3)
	.i par="UL",'$$select^LVBCTUSL(value,"Y") s result=PLIST(3)
	.i par="MC",'$$select^LVBCTMC(value,"Y") s result=PLIST(3)
	q result
PRINT	; print report
	; sort in Alphabetical order
	i $d(^TMP($zn,$j,"xREPORT")) d
	.s SORT1="" f  s SORT1=$o(^TMP($zn,$j,"xREPORT",SORT1)) q:SORT1=""  d
	..s x1=SORT1 i PAR5'="DT" s x1=$$SORT(PAR5,SORT1) i '$l(x1) s x1=" "
	..s SORT2="" f  s SORT2=$o(^TMP($zn,$j,"xREPORT",SORT1,SORT2)) q:SORT2=""  d
	...s x2=SORT2 i PAR6'="DT" s x2=$$SORT(PAR6,SORT2) i '$l(x2) s x2=" "
	...m ^TMP($zn,$j,"REPORT",x1,x2)=^TMP($zn,$j,"xREPORT",SORT1,SORT2)
	i $d(^TMP($zn,$j,"REPORT")) d
	.s (total1(3),total1(4),SORT1)="" f  s SORT1=$o(^TMP($zn,$j,"REPORT",SORT1)) q:SORT1=""  d
	..s xxx(1)=SORT1 i PAR5="DT" s xxx(1)=$$SORT(PAR5,SORT1)
	..i xxx(1)["\" s xxx(1)=$e($p(xxx(1),"\",2)_$j("",10),1,10)_$p(xxx(1),"\",1)
	..s (total2(3),total2(4),SORT2)="" f  s SORT2=$o(^TMP($zn,$j,"REPORT",SORT1,SORT2)) q:SORT2=""  d
	...s xxx(2)=SORT2 i PAR6="DT" s xxx(2)=$$SORT(PAR6,SORT2)
	...i xxx(2)["\" s xxx(2)=$e($p(xxx(2),"\",2)_$j("",10),1,10)_$p(xxx(2),"\",1)
	...s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",SORT1,SORT2,epis)) q:epis=""  d
	....s xxx(3)=^(epis)
	....s (x,xxx(4))="" f  s x=$o(^TEPI(epis,7,x)) q:x=""  s xxx(4)=xxx(4)+^(x)
	....f j=3,4 s total1(j)=total1(j)+xxx(j),total2(j)=total2(j)+xxx(j)
	....i '$$select^LVBEPVIS(epis) d PRINT^PRTREP00(.PLIST,.N) k PLIST,xxx
	...d ADDLINE^PRTREP00(REPID,"PRT","!")
	...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
	...d ADDLINE^PRTREP00(REPID,"PRT","!")
	..d ADDLINE^PRTREP00(REPID,"PRT","!")
	..s found=0 f j=1:1 q:'$d(N(j,1))  i $$UPPER^SSUTIL4(N(j,3))["AMOUNT" s found=1 q
	..i found d
	...s xxx(1)="Total",xxx(3)=total2(3),xxx(4)=total2(4) d PRINT^PRTREP00(,.N) k xxx
	...d ADDLINE^PRTREP00(REPID,"PRT","!")
	..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
	..d ADDLINE^PRTREP00(REPID,"PRT","!")
	.d ADDLINE^PRTREP00(REPID,"PRT","!")
	.s found=0 f j=1:1 q:'$d(N(j,1))  i $$UPPER^SSUTIL4(N(j,3))["AMOUNT" s found=1 q
	.i found d
	..s xxx(1)="Total",xxx(3)=total1(3),xxx(4)=total1(4) d PRINT^PRTREP00(,.N) k xxx
	..d ADDLINE^PRTREP00(REPID,"PRT","!")
	.d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
	.d ADDLINE^PRTREP00(REPID,"PRT","!")
	s report="EPR"
	s EndLine=$$seldata^LVBCTCLR(report,9)
	i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
	q
]]></Routine>
</Export>
