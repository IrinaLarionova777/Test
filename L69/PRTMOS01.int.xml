<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTMOS01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTMOS01 ; IG 19/08/99 ; MicroOrganism Statistics
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Select criteria :    DR - Doctor
 ;                                       DG - Doctor group
 ;                                       HW - Hospital/Ward
 ;                                       PC - Pay code
 ;                                       CC - Collection centre
 ;                                       CP - Collection personel
 ;                                       CO - Company
 ;                                       UL - User location
 ;                                       MC - Marketing code
 ;                                       OR - Organism
 ;               4. Value
 ;               5. Sort key_1 : DR - Doctor
 ;                               HW - Hospital/Ward
 ;                               PC - Pay code
 ;                               DT - Date
 ;                               CC - Collection centre
 ;                               CP - Collection personel
 ;                               CO - Company
 ;                               UL - User location
 ;                               MC - Marketing code
 ;               6. Sort key_2
 ;               7. Type :       AG - Age group
 ;                               SN - Sensitivities
 ;                                  - Count only
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Micro Organism Statistics"
 s line=2,xHEADER(line)=""
 s selection=$s(PAR3="DR":"Doctor",PAR3="DG":"Doctor Group",PAR3="HW":"Hospital/Ward",PAR3="PC":"Pay code",PAR3="CC":"Collection Centre",PAR3="CP":"Collection Personnel",1:"")
 s:selection="" selection=$s(PAR3="CO":"Company",PAR3="UL":"User Location",PAR3="MC":"Marketing Code",1:"Organism")
 s line=3,xHEADER(line)="Selection Criteria : "_selection
 s line=4,xHEADER(line)=""
 s line=5,xHEADER(line)="Value : "_PAR4
 s line=6,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2,,,PAR5_"^"_PAR6),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xxx f j=1:1:22 s xxx=$p(PAR10,",",j),par=$s(j=1:PAR5,j=2:PAR6,1:"") d  s $p(PAR10,",",j)=xxx
 . ; use organism/antibiotic field length from code tables
 .i j=3!(j=4) s $p(xxx,$c(2),1)="xxx",$p(xxx,$c(2),2)=j q
 .s $p(xxx,$c(2),1)="xxx",$p(xxx,$c(2),2)=j,$p(xxx,$c(2),4)=$s(j<5:15,1:5)
 .i par="DR" s $p(xxx,$c(2),3)="Doctor"
 .i par="HW" s $p(xxx,$c(2),3)="Hospital/Ward"
 .i par="PC" s $p(xxx,$c(2),3)="Pay code"
 .i par="DT" s $p(xxx,$c(2),3)="Collection Date"
 .i par="CC" s $p(xxx,$c(2),3)="Collection centre"
 .i par="CP" s $p(xxx,$c(2),3)="Collection personel"
 .i par="CO" s $p(xxx,$c(2),3)="Company"
 .i par="UL" s $p(xxx,$c(2),3)="User location"
 .i par="MC" s $p(xxx,$c(2),3)="Marketing code"
 .i j=1,'$l(PAR5) s $p(xxx,$c(2),4)=0
 .i j=2,'$l(PAR6) s $p(xxx,$c(2),4)=0
 .i j=4,PAR7'="SN" s $p(xxx,$c(2),3)="",$p(xxx,$c(2),4)=0
 .i j=6,PAR7="SN" s $p(xxx,$c(2),3)="Resistant",$p(xxx,$c(2),4)=9
 .i j=7,PAR7="SN" s $p(xxx,$c(2),3)="Sensitive",$p(xxx,$c(2),4)=9
 .i j=8,PAR7="SN" s $p(xxx,$c(2),3)="Intermediate",$p(xxx,$c(2),4)=12
 .i j=9,PAR7="SN" s $p(xxx,$c(2),3)="% Sensitive",$p(xxx,$c(2),4)=9
 .i j=6,PAR7="AG" s $p(xxx,$c(2),3)="<1 Month"
 .i j=7,PAR7="AG" s $p(xxx,$c(2),3)="<6 Month"
 .i j=8,PAR7="AG" s $p(xxx,$c(2),3)="<1 Year"
 .i j=9,PAR7="AG" s $p(xxx,$c(2),3)="<2 Years"
 .i j=10,PAR7="AG" s $p(xxx,$c(2),3)="<6 Years"
 .i j=11,PAR7="AG" s $p(xxx,$c(2),3)="<11 Years"
 .i j=12,PAR7="AG" s $p(xxx,$c(2),3)="<16 Years"
 .i j=13,PAR7="AG" s $p(xxx,$c(2),3)="<21 Years"
 .i j=14,PAR7="AG" s $p(xxx,$c(2),3)="<31 Years"
 .i j=15,PAR7="AG" s $p(xxx,$c(2),3)="<41 Years"
 .i j=16,PAR7="AG" s $p(xxx,$c(2),3)="<51 Years"
 .i j=17,PAR7="AG" s $p(xxx,$c(2),3)="<61 Years"
 .i j=18,PAR7="AG" s $p(xxx,$c(2),3)="<71 Years"
 .i j=19,PAR7="AG" s $p(xxx,$c(2),3)="<81 Years"
 .i j=20,PAR7="AG" s $p(xxx,$c(2),3)="<91 Years"
 .i j=21,PAR7="AG" s $p(xxx,$c(2),3)="<101 Years"
 .i j=22,PAR7="AG" s $p(xxx,$c(2),3)="The rest"
 i PAR7="" s PAR10=$p(PAR10,",",1,5)
 i PAR7="SN" s PAR10=$p(PAR10,",",1,9)
 k xxx,xSORT d H^PRTREP00(PAR10,$zn)
 s xSORT("DR")="Doctor"
 s xSORT("HW")="Hospital/Ward"
 s xSORT("PC")="Pay code"
 s xSORT("DT")="Date"
 s xSORT("CC")="Collection centre"
 s xSORT("CP")="Collection personel"
 s xSORT("CO")="Company"
 s xSORT("UL")="User location"
 s xSORT("MC")="Marketing code"
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 f j="TS","TC" s ^TMP($zn,$j,j)=""
 s tc="" f  s tc=$o(^TTAB("TC",tc)) q:tc=""  d
 .s format=$p(^TTAB("TC",tc),"\",3) i format="V" d
 ..s ^TMP($zn,$j,"TC")=^TMP($zn,$j,"TC")_$s($l(^TMP($zn,$j,"TC")):",",1:"")_tc
 ..s ts="" f  s ts=$o(^TTAB("TC",tc,1,ts)) q:ts=""  s ^TMP($zn,$j,"TS",ts)=""
 s ts="" f  s ts=$o(^TMP($zn,$j,"TS",ts)) q:ts=""  s ^TMP($zn,$j,"TS")=^TMP($zn,$j,"TS")_$s($l(^TMP($zn,$j,"TS")):",",1:"")_ts
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis,"Y") d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k VISIT m VISIT=PLIST
 ... ; check selection type
 ...s ok=1 d  i 'ok q
 ....i PAR3="DR",(","_VISIT(15)_",")'[(","_PAR4_",") s ok=0 q
 ....i PAR3="DG",$$select^LVBCTDGD(PAR4_"||"_VISIT(15)) s ok=0 q
 ....i PAR3="HW",(","_VISIT(22)_",")'[(","_PAR4_",") s ok=0 q
 ....i PAR3="PC",(","_VISIT(9)_",")'[(","_PAR4_",") s ok=0 q
 ....i PAR3="CC",(","_VISIT(27)_",")'[(","_PAR4_",") s ok=0 q
 ....i PAR3="CP",(","_VISIT(28)_",")'[(","_PAR4_",") s ok=0 q
 ....i PAR3="CO",(","_$$epcomp^LVBVIS1(epis)_",")'[(","_PAR4_",") s ok=0 q
 ....i PAR3="UL",(","_VISIT(66)_",")'[(","_PAR4_",") s ok=0 q
 ....i PAR3="MC" d  i (","_$p(result,$c(1))_",")'[(","_PAR4_",") s ok=0 q
 .....s result="" s result=$$seldata^LVBCTDR(VISIT(15),28)
 ...s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ....i (","_^TMP($zn,$j,"TS")_",")'[(","_ts_",") q
 ....s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 .....s vtsRID=epis_"||"_ts_"||"_tscnt i $$status^LVBVIS1(vtsRID)'="A" q
 .....s tc="" f  s tc=$o(^TEPI(epis,1,ts,tscnt,"DATA",tc)) q:tc=""  s bug=$p(^(tc),"\") i $l(bug) d
 ......i (","_^TMP($zn,$j,"TC")_",")'[(","_tc_",") q
 ......i PAR3="OR",(","_PAR4_",")'[(","_bug_",") q
 ...... ; set sorting key - 1
 ......s SORT1="" s par=PAR5 d
 .......i par="MC" s SORT1=$$seldata^LVBCTDR(VISIT(15),28) q
 .......s SORT1=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
 ...... ; set sorting key - 2
 ......s SORT2="" s par=PAR6 d
 .......i par="MC" s SORT2=$$seldata^LVBCTDR(VISIT(15),28) q
 .......s SORT2=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
 ......s:'$l(SORT1) SORT1=" " s:'$l(SORT2) SORT2=" "
 ......d SetTotal(bug,"",5,1,SORT1,SORT2)
 ......i PAR7="AG" d  q
 .......i $e(VISIT(26))="." s par=VISIT(26)*100,piece=$s(par<31:6,par<182:7,1:8)
 .......i $e(VISIT(26))'="." s par=VISIT(26),piece=$s(par<2:9,par<6:10,par<11:11,par<16:12,par<21:13,par<31:14,par<41:15,par<51:16,par<61:17,par<71:18,par<81:19,par<91:20,par<101:21,1:22)
 .......d SetTotal(bug,"",piece,1,SORT1,SORT2)
 ......i PAR7="SN" d  q
 .......s ant="" f  s ant=$o(^TEPI(epis,1,ts,tscnt,"DATA",tc,"ANT",ant)) q:ant=""  s res=$p(^(ant),"\",1) i $l(res) d
 ........d SetTotal(bug,ant,5,1,SORT1,SORT2)
 ........s type=$$seldata^LVBCTSN(res,4)
 ........s type=$p(type,$c(1)),piece=$s(type="R":6,type="S":7,1:8)
 ........d SetTotal(bug,ant,piece,1,SORT1,SORT2)
 q
SORT(par,value) n (par,value) s result="" i value'=" " d
 .i par="DR",'$$select^LVBCTDR(value,"Y") s result=PLIST(3)_", "_$s($l(PLIST(26)):PLIST(26)_" ",1:"")_" "_PLIST(4)
 .i par="HW",'$$select^LVBCTHOS(value,"Y") s result=PLIST(3)
 .i par="PC",'$$select^LVBCTPC(value,"Y") s result=PLIST(3)
 .i par="DT" s result=$$extdate2^SSUTIL4(value)
 .i par="CC",'$$select^LVBCTCC(value,"Y") s result=PLIST(3)
 .i par="CP",'$$select^LVBCTPER(value,"Y") s result=PLIST(3)
 .i par="CO",'$$select^LVBCTCO(value,"Y") s result=PLIST(3)
 .i par="UL",'$$select^LVBCTUSL(value,"Y") s result=PLIST(3)
 .i par="MC",'$$select^LVBCTMC(value,"Y") s result=PLIST(3)
 q result
 ; set totals
SetTotal(bug,ant,piece,tot,SORT1,SORT2) n (bug,ant,piece,tot,SORT1,SORT2)
 i $l(ant) d
 .s $p(^TMP($zn,$j,"REPORT",bug,ant),"\",piece)=$p($g(^TMP($zn,$j,"REPORT",bug,ant)),"\",piece)+tot
 .s $p(^TMP($zn,$j,"REPORT"," ",SORT1,bug,ant),"\",piece)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1,bug,ant)),"\",piece)+tot
 .s $p(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug,ant),"\",piece)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug,ant)),"\",piece)+tot
 i '$l(ant) d
 .s $p(^TMP($zn,$j,"REPORT",bug),"\",piece)=$p($g(^TMP($zn,$j,"REPORT",bug)),"\",piece)+tot
 .s $p(^TMP($zn,$j,"REPORT"," ",SORT1,bug),"\",piece)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1,bug)),"\",piece)+tot
 .s $p(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug),"\",piece)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug)),"\",piece)+tot
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s SORT1="" f  s SORT1=$o(^TMP($zn,$j,"REPORT"," ",SORT1)) q:SORT1=""  d
 ..s xxx(1)=$$SORT(PAR5,SORT1)
 ..s SORT2="" f  s SORT2=$o(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2)) q:SORT2=""  d
 ...s xxx(2)=$$SORT(PAR6,SORT2)
 ...s bug="" f  s bug=$o(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug)) q:bug=""  d
 ....s xxx(3)=$$seldata^LVBCTBUG(bug,3)
 ....i PAR7="" d
 .....s xxx(5)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug)),"\",5)
 .....d PRINT^PRTREP00(,.N) k xxx
 ....i PAR7="AG" d
 .....f j=5:1:22 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug)),"\",j)
 .....d PRINT^PRTREP00(,.N) k xxx
 ....i PAR7="SN" d
 .....s ant="" f  s ant=$o(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug,ant)) q:ant=""  d
 ......s xxx(4)=$$seldata^LVBCTANT(ant,3)
 ......f j=5:1:8 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1," ",SORT2,bug,ant)),"\",j)
 ......i xxx(5) s xxx(9)=xxx(7)/xxx(5)*100
 ......d PRINT^PRTREP00(,.N) k xxx
 ..i $l(PAR6) d
 ...s xxx(2)="Total for "_$$SORT(PAR5,SORT1)
 ...s bug="" f  s bug=$o(^TMP($zn,$j,"REPORT"," ",SORT1,bug)) q:bug=""  i bug'=" " d
 ....s xxx(3)=$$seldata^LVBCTBUG(bug,3)
 ....i PAR7="" d
 .....s xxx(5)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1,bug)),"\",5)
 .....d PRINT^PRTREP00(,.N) k xxx
 ....i PAR7="AG" d
 .....f j=5:1:22 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1,bug)),"\",j)
 .....d PRINT^PRTREP00(,.N) k xxx
 ....i PAR7="SN" d
 .....s ant="" f  s ant=$o(^TMP($zn,$j,"REPORT"," ",SORT1,bug,ant)) q:ant=""  d
 ......s xxx(4)=$$seldata^LVBCTANT(ant,3)
 ......f j=5:1:8 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT"," ",SORT1,bug,ant)),"\",j)
 ......i xxx(5) s xxx(9)=xxx(7)/xxx(5)*100
 ......d PRINT^PRTREP00(,.N) k xxx
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 .i $l(PAR5) d
 ..s xxx(1)="Grand Total"
 ..s bug="" f  s bug=$o(^TMP($zn,$j,"REPORT",bug)) q:bug=""  i bug'=" " d
 ...s xxx(3)=$$seldata^LVBCTBUG(bug,3)
 ...i PAR7="" d
 ....s xxx(5)=$p($g(^TMP($zn,$j,"REPORT",bug)),"\",5)
 ....d PRINT^PRTREP00(,.N) k xxx
 ...i PAR7="AG" d
 ....f j=5:1:22 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT",bug)),"\",j)
 ....d PRINT^PRTREP00(,.N) k xxx
 ...i PAR7="SN" d
 ....s ant="" f  s ant=$o(^TMP($zn,$j,"REPORT",bug,ant)) q:ant=""  d
 .....s xxx(4)=$$seldata^LVBCTANT(ant,3)
 .....f j=5:1:8 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT",bug,ant)),"\",j)
 .....i xxx(5) s xxx(9)=xxx(7)/xxx(5)*100
 .....d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="MOS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
