<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="SSUTIL5" type="INT" languagemode="0" generated="1"><![CDATA[
SSUTIL5 ; Restore patches
 ; Error log
Error i $ze["ENDOFFILE" g END
 d BACK^%ETN
 q
restore(dir) n (dir) s dir=$g(dir) k ^TMP($zn,$j)
 s sep=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=sep s dir=dir_sep
 s mdir=$p(dir,sep,1,$l(dir,sep)-1)_sep_"completed"_sep
 s ^TMP($zn,$j)=dir
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_"*.* /B > ""patches.txt""")
 s $zt="Error^"_$zn
 s file="patches.txt"
 o file:("RS"::$c(10,13))
 f  u file r x s xFile=$p(x,sep,$l(x,sep)),prefix=$p($p(xFile,"."),"_",1,4) i $l(prefix,"_")=4 s ^TMP($zn,$j," ",prefix,xFile)=""
END c file
 s prefix="" f  s prefix=$o(^TMP($zn,$j," ",prefix)) q:prefix=""  d
 . ; import rouitnes
 . i $$RI(dir_prefix_"_src.ro")
 . ; import class definitions and SS global
 . i $$GIGEN(dir_prefix)
 . ; move files into completed directory
 .s file="" f  s file=$o(^TMP($zn,$j," ",prefix,file)) q:file=""  d
 ..i '$zf(-1,"copy "_dir_file_" "_mdir_file),$zf(-1,"del "_dir_file)
 d ^LVBCOM80
 k ^TMP($zn,$j)
 q
 ; Delete the class definition
DeleteClass(class) n (class) s class=$g(class)
 k ^oddDEF(class),^oddCOM(class),^oddMAP(class)
 s sqlpkg=$p(class,".",1),sqltbl=$p(class,".",2) s:sqlpkg="User" sqlpkg="SQLUser"
 k ^oddEXTR(class),^oddSQL(sqlpkg,sqltbl)
 k ^rINDEXCLASS($zcvt(class,"U"))
 k ^rINDEXSQL("TABLE",sqlpkg,sqltbl),^rINDEXSQL("rv",$zcvt(sqlpkg,"U"),$zcvt(sqltbl,"U"))
 ; this is only loaded up from source file
 k ^oddDEP(class)
 q 0
 ; restore source routines
RI(filename) n (filename) s filename=$g(filename)
 i '##class(%Library.File).Exists(filename) q 100
 w !!,"Importing routines sourcecode.."
 S IO=filename
 s IOPAR="(""RS"")"
 S BACKUP=0,WANTCOM=0,WANTSYN=0,OVLAY=1
 S external=1
 s DSMRS=0,SETLANG=0
 S SETOK=""
 s OK=1,%R=0
 ;
 O IO
 U IO R HEAD,DAT
 S MWHENCE=$P(HEAD,"^"),EXTS=$P(HEAD,"^",2),DESC=$P($P(HEAD,"^",3,999),"^~Format=",1)
 F ii=0:0 D RNAM^%RI Q:ROU=""  I '$G(MCODE),ROU'="*" D SAVE^%RI,RDIR^%RI
 C IO
 ;
 w !,"Import of routines source code complete..."
 q 0
 ; Load the list of globals in GIF format
 ; Wrapper for ugly %GIF interface
 ; really GIF but VB calls the old GIGEN line label
GIGEN(filename) n (filename) s filename=$g(filename)
 l +^ZPATCH,+^SS
 s THISIO=$I
 s prefix=filename
 f fil=prefix_".gogen",prefix_"_src.gogen" d
 . k ^ZPATCH
 . o fil:("RV"):0 e  w !,"File not found '"_fil_"' in GIGEN^"_$zn q
 . w !,"Importing globals from ",fil
 . s IO=fil,IOPAR="UR",IOT="RMS",glomerge=1
 . s success=$$entry^%GIF
 . i 'success w !,"Global import failed ",$g(success)
 . i success w !,"Import of globals complete..."
 . i $d(IO) c IO
 . u THISIO
 . s cname="" f  s cname=$o(^ZPATCH("DEF",cname)) q:cname=""  i $l(cname),$$DeleteClass(cname)
 . m ^oddDEF=^ZPATCH("DEF")
 . m ^oddCOM=^ZPATCH("COM")
 . m ^oddMAP=^ZPATCH("MAP")
 . m ^oddSQL=^ZPATCH("SQL")
 . m ^oddEXTR=^ZPATCH("EXTR")
 . m ^oddDEP=^ZPATCH("DEP")
 . m ^rINDEXSQL=^ZPATCH("IDXSQL")
 . m ^rINDEXCLASS=^ZPATCH("IDXCLS")
 . k ^ZPATCH
 f fil=prefix_"_ss.gogen" d
 . o fil:("RV"):0 e  w !,"File not found '"_fil_"' in GIGEN^"_$zn q
 . k ^SS
 . w !,"Importing globals from ",fil
 . s IO=fil,IOPAR="UR",IOT="RMS",glomerge=1
 . s success=$$entry^%GIF
 . i 'success w !,"Global import failed ",$g(success)
 . i success w !,"Import of globals complete..."
 . i $d(IO) c IO
 . u THISIO
 l -^ZPATCH,-^SS
 q 0
]]></Routine>
</Export>
