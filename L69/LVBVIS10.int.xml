<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBVIS10" type="INT" languagemode="0" generated="1"><![CDATA[
LVBVIS10 ; IG 2/6/98 ; Visit selection routines
 ;
 ; supported calls:
 ;
 ; $$open^LVBVIS10(type,p1,p2,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetch^LVBVIS10(type,p1,p2,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetchbuffer^LVBVIS10(type,p1,p2,p3,p4,p5,p6,p7,p8,p9)
 ; $$close^LVBVIS9()
 ; $$update^LVBVIS10(type)
 ;
 ; save printed data
update(type) d pre("Y") 
 f j=1:1:4 s @("x"_j)=$g(PLIST(j))
 s x5=$p(PLIST(5),"||",1)
 s x6=$p(PLIST(5),"||",2)
 s x7=$p(PLIST(5),"||",3)
 s x4=$tr(x4,$c(16),"^")
 i $l(PLIST(11)) s $p(^TRPT("WORD",type,x1,x2,x3,x4,x5,x6,x7),"\",3)=PLIST(11)
 i '$l(PLIST(11)),$l(x1),$l(x2),$l(x3),$l(x4),$l(x5),$l(x6),$l(x7),$d(^TRPT("WORD",type,x1,x2,x3,x4,x5,x6,x7)) d
 .s ^TRPT("WORD",1-type,x1,x2,x3,x4,x5,x6,x7)=$tr($h,",","\")
 .k ^TRPT("WORD",type,x1,x2,x3,x4,x5,x6,x7)
 .i type=0 d
 ..i '$$selskip^LVBCTTS(x6) s x6=PLIST(4)
 ..d events^LVBCOM01(x5,"DR","Word-"_x6)
 q 0
 ; type - 0-To Be Printed
 ;        1-Printed
open(type,p1,p2,p3,p4,p5,p6,p7,p8,p9) n (type) s type=$g(type)
 k ^TMP($zn,$j,type) s ^TMP($zn,$j,type)=""
 q 0
 ; PLIST - 1-Type : C/U/F
 ;         2-Courier run/User
 ;         3-Receiver type : H - Hospital
 ;                           D - Doctor
 ;         4-Receiver
 ;         5-vtsRID
 ;         6-MRN
 ;         7-Date
 ;         8-Time
 ;         9-Printer
 ;        10-Bin
 ;        11-Error type
 ;        12-Destination
 ;        13-No of Copies
fetch(type,p1,p2,p3,p4,p5,p6,p7,p8,p9) n (type,PLIST) s type=$g(type),SQLCODE=100
 k PLIST f j=1:1:7 s @("x"_j)=$p($g(^TMP($zn,$j,"LIST",type)),"\",j)
 f  d:$l(x1)  q:'SQLCODE  s x1=$o(^TRPT("WORD",type,x1)) q:x1=""  s (x2,x3,x4,x5,x6,x7)=""
 .f  d:$l(x2)  q:'SQLCODE  s x2=$o(^TRPT("WORD",type,x1,x2)) q:x2=""  s (x3,x4,x5,x6,x7)=""
 ..f  d:$l(x3)  q:'SQLCODE  s x3=$o(^TRPT("WORD",type,x1,x2,x3)) q:x3=""  s (x4,x5,x6,x7)=""
 ...f  d:$l(x4)  q:'SQLCODE  s x4=$o(^TRPT("WORD",type,x1,x2,x3,x4)) q:x4=""  s (x5,x6,x7)=""
 ....f  d:$l(x5)  q:'SQLCODE  s x5=$o(^TRPT("WORD",type,x1,x2,x3,x4,x5)) q:x5=""  s (x6,x7)=""
 .....f  d:$l(x6)  q:'SQLCODE  s x6=$o(^TRPT("WORD",type,x1,x2,x3,x4,x5,x6)) q:x6=""  s x7=""
 ......f  s x7=$o(^TRPT("WORD",type,x1,x2,x3,x4,x5,x6,x7)) q:x7=""  d  q
 .......s SQLCODE=0,xx=^(x7)
 .......s PLIST(1)=x1,PLIST(2)=x2,PLIST(3)=x3,PLIST(4)=$tr(x4,"^",$c(16)),PLIST(5)=x5_"||"_x6_"||"_x7
 .......s PLIST(6)=$$mrn^LVBEPVIS(x5),PLIST(7)=$p(xx,"\",1),PLIST(8)=$p(xx,"\",2),PLIST(11)=$p(xx,"\",3)
 ....... ; find printer and bin
 .......i x1="U" d
 ........s x="Y" d DEST^PRTREP00("","DRP",x1_"\"_x2,x2,"",.x)
 ........s PLIST(9)="" i x s PLIST(9)=$p(x,$c(1),2),PLIST(13)=$p(x,$c(1),1)
 ........s PLIST(10)=""
 ........s PLIST(12)=PLIST(9)
 ........d copies(x3,x4,x1,.copies) i copies>PLIST(13) s PLIST(13)=copies
 .......i x1="C" d
 ........s x="Y" d DEST^PRTREP00("","DRP",x1_"\"_x2,"","",.x)
 ........s PLIST(9)="" i x s PLIST(9)=$p(x,$c(1),2),PLIST(13)=$p(x,$c(1),1)
 ........s PLIST(10)=$p($p($g(^TTAB("CR",x2)),"\",2),"-",3)
 ........s PLIST(12)=PLIST(9)
 ........d copies(x3,x4,x1,.copies) i copies>PLIST(13) s PLIST(13)=copies
 .......i x1="F" d
 ........s PLIST(9)=""
 ........s PLIST(10)=""
 ........s PLIST(12)=$p(x2,"-",3,4)
 ........s PLIST(13)=1
 ........d copies(x3,x4,x1,.copies) i copies>PLIST(13) s PLIST(13)=copies
 .......i $l(PLIST(9)) s PLIST(9)=$p($g(^TTAB("CTDS",PLIST(9),1)),"|PRN|",2)
 s PLIST=$o(PLIST(""),-1)
 i 'SQLCODE d adjust
 f j=1:1:7 s $p(^TMP($zn,$j,"LIST",type),"\",j)=@("x"_j)
 q SQLCODE
 ; Fetch all data
fetchbuffer(type,p1,p2,p3,p4,p5,p6,p7,p8,p9) s type=$g(type) k ^TMP($zn,$j,1)
 s max=$s($g(PLIST(1)):PLIST(1),1:100) i max>100 s max=100
 f xxx=1:1:max s SQLCODE=$$fetch(type) q:SQLCODE  d
 .s cnt=$o(^TMP($zn,$j,1,""),-1)+1 f j=1:1:PLIST s $p(^TMP($zn,$j,1,cnt),$c(28),j)=$g(PLIST(j))
 k PLIST m PLIST=^TMP($zn,$j,1) k ^TMP($zn,$j,1)
 s PLIST=$o(PLIST(""),-1)
 q SQLCODE
close() k ^TMP($zn,$j)
 q 0
copies(xDH1,xDH2,xDH3,copies) n (xDH1,xDH2,xDH3,copies)
 i xDH1="D",'$$select^LVBCTDR(xDH2) s copies=$s(xDH3="F":PLIST(39),1:PLIST(14))
 i xDH1="H",'$$select^LVBCTHOS(xDH2) s copies=$s(xDH3="F":PLIST(20),1:PLIST(11))
 q
 ; Adjust certain fields
pre(xUpdate) d PreSQL^SSUTIL4()
 q
adjust N plist,I,J,SQLCODE,X d PostSQL^SSUTIL4("")
 I $G(PLIST(1))="C",$G(PLIST(2))'="" D
 .K plist M plist=PLIST K PLIST
 .S SQLCODE=$$select^LVBCTCR(plist(2),"Y")
 .S X="" I 'SQLCODE S X=PLIST(2)_$C(1)_PLIST(3)
 .K PLIST M PLIST=plist S PLIST(2)=X K plist
 I $G(PLIST(1))="U",$G(PLIST(2))'="" D
 .k plist m plist=PLIST k PLIST
 .s SQLCODE=$$selrow^MVBSSUSR(plist(2),"Y")
 .s X="" i 'SQLCODE s X=PLIST(2)_$c(1)_PLIST(3)
 .k PLIST m PLIST=plist s PLIST(2)=X k plist
 i PLIST(3)="D",$G(PLIST(4))'="",$e(PLIST(4),1,3)'="xxx" D
 .K plist M plist=PLIST K PLIST
 .S SQLCODE=$$select^LVBCTDR(plist(4),"Y")
 .S X="" I 'SQLCODE S X=PLIST(2)_$C(1)_$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 .K PLIST M PLIST=plist S PLIST(4)=X K plist
 I PLIST(3)="H",$G(PLIST(4))'="",$e(PLIST(4),1,3)'="xxx" D
 .K plist M plist=PLIST K PLIST
 .S SQLCODE=$$select^LVBCTHOS(plist(4),"Y")
 .S X="" I 'SQLCODE S X=PLIST(2)_$C(1)_PLIST(3)
 .K PLIST M PLIST=plist S PLIST(4)=X K plist
 I $G(PLIST(5))'="" D
 .K plist M plist=PLIST K PLIST
 .S SQLCODE=$$select^LVBEPVIS($p(plist(5),"||"),"Y")
 .S X=plist(5) I 'SQLCODE S $p(X,$c(1),2)=$$NameFormat^LVBCOM03("PT",$p(PLIST(7),$c(1),2),PLIST(4),PLIST(3),PLIST(99),PLIST(100),PLIST(101))
 .K PLIST M PLIST=plist S PLIST(5)=X K plist
 .
 .K plist M plist=PLIST K PLIST
 .S SQLCODE=$$selskip^LVBCTTS($p(plist(5),"||",2))
 .S X=plist(5) I 'SQLCODE S $p(X,$c(1),3)=PLIST(4)
 .K PLIST M PLIST=plist S PLIST(5)=X K plist
 q
]]></Routine>
</Export>
