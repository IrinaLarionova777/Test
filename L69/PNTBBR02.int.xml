<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PNTBBR02" type="INT" languagemode="0"><![CDATA[
PNTBBR02 ; X-Match Units Revert to stock  ; Compiled July 7, 2000 11:33:16 for Cache for Windows
 q
print(ROWID,%IN,MSG) 
 n (ROWID,%IN,MSG)
 k ^PR(+ROWID,"PRT")
 s %="\"
 s site=$P(MSG,%,1),datefrom=$P(MSG,%,2),dateto=$p(MSG,%,3)
 s user=%IN
 s $p(LINE,"-",91)="",page=0
 d tab
 d initial k ^TMP("MVB1",$j)
 s site1="" f  s site1=$o(^TMP("MVB",$j,site1)) q:site1=""  d
 .s pack="" f  s pack=$o(^TMP("MVB",$j,site1,pack)) q:pack=""  d
 ..s chl="" f  s chl=$o(^TMP("MVB",$j,site1,pack,chl)) q:chl=""  d
 ...s chlx=chl-1 i $$select^BVBPKTRN(pack_"||"_chlx) q
 ...s epis=PLIST(11),err=$$select^LVBEPVIS(epis,"Y"),name=$g(PLIST(3))_" "_$g(PLIST(4))
 ...s ^TMP("MVB1",$j,site1,name,pack,chl)=""
 s site1="" f  s site1=$o(^TMP("MVB1",$j,site1)) q:site1=""  d
 .d head1
 .s name="" f  s name=$o(^TMP("MVB1",$j,site1,name)) q:name=""  d
 ..k str
 ..s pack="" f  s pack=$o(^TMP("MVB1",$j,site1,name,pack)) q:pack=""  d
 ...q:$$select^BVBPACK1(pack)
 ...s packid=PLIST(2),datecoll=PLIST(3),expdate=PLIST(18),labno=PLIST(13),bgrp=$p($g(^CT("BG",+PLIST(10))),"^",1),prod=$p($g(^CT("PR",+PLIST(8))),"^")
 ...s chl="" f  s chl=$o(^TMP("MVB1",$j,site1,name,pack,chl)) q:chl=""  d
 ....s chlx=chl-1 i $$select^BVBPKTRN(pack_"||"_chlx) q
 ....s epis=PLIST(11),err=$$select^LVBEPVIS(epis,"Y"),name=$$NameFormat^LVBCOM03("PT","",$g(PLIST(4)),$g(PLIST(4)))
 ....s str(packid,prod)=packid_","_prod_","_bgrp_","_$$extdate^SSUTIL4(datecoll)_","_$$extdate^SSUTIL4(expdate)_","_epis_","_name
 ..s packid="" f  s packid=$o(str(packid)) q:packid=""  d
 ...s prod="" f  s prod=$o(str(packid,prod)) q:prod=""  d
 ....s str=str(packid,prod)
 ....d ADD^SSPRINT(ROWID,"PRT","!")
 ....d ADD^SSPRINT(ROWID,"PRT","?3  | "_$$tab1(str))
 ....d ADD^SSPRINT(ROWID,"PRT","|")
 k ^TMP("MVB",$j),^TMP("MVB1",$j)
 q
 ;
 ;name of field,length of field
tab ;Unit No,15;Product,10;Group,9;Date Coll.,12;Date Exp.,12;Lab No,14;Patient Name,15;
 k tab n a s $p(space," ",70)="",a=$p($t(tab),";",2,99),tab1="",tab2=""
 f jj=1:1:$l(a,";") d
 .s tab=jj
 .s a1=$p(a,";",jj) q:a1=""
 .s a2=$p(a1,","),a3=$p(a1,",",2)
 .s tab1=tab1_$e(a2_space,1,a3),tab(jj)=a3
 .s tab2=tab2_a2_","
 q
 ;
tab1(str) 
 s $p(space," ",70)=""
 s str1="" f j=1:1:tab s str1=str1_$e($p(str,",",j)_space,1,$g(tab(j)))         
 q str1
head1 ;Print Page Header
 s page=page+1
 d ADD^SSPRINT(ROWID,"PRT","#")
 d ADD^SSPRINT(ROWID,"PRT","!")
 d head0
 d ADD^SSPRINT(ROWID,"PRT","?3  Units Revert to Stock  Page "_page)
 d ADD^SSPRINT(ROWID,"PRT","!")
 d ADD^SSPRINT(ROWID,"PRT","?3  From "_$$extdate^SSUTIL4(datefrom))
 d ADD^SSPRINT(ROWID,"PRT","?27  To "_$$extdate^SSUTIL4(dateto))
 d ADD^SSPRINT(ROWID,"PRT","!")
 d ADD^SSPRINT(ROWID,"PRT","?3  Site "_site1_" "_$p($g(^TTAB("USLOC",site1)),"\"))
 d ADD^SSPRINT(ROWID,"PRT","!")
 d ADD^SSPRINT(ROWID,"PRT","?3  "_LINE)
 d ADD^SSPRINT(ROWID,"PRT","!")
 d ADD^SSPRINT(ROWID,"PRT","?3  |"_$$tab1(tab2))
 d ADD^SSPRINT(ROWID,"PRT"," |")
 d ADD^SSPRINT(ROWID,"PRT","!")
 d ADD^SSPRINT(ROWID,"PRT","?3  "_LINE)
 q
head0 ;Print Common Page Header
 d ADD^SSPRINT(ROWID,"PRT","!")
 d ADD^SSPRINT(ROWID,"PRT","?3  "_$p(^CF("SM",1),"^",6))
 d ADD^SSPRINT(ROWID,"PRT","?50 "_$$extdate^SSUTIL4(+$h)_" "_$$exttime^SSUTIL4($p($h,",",2)))
 d ADD^SSPRINT(ROWID,"PRT","!")
 q
initial ;initial sorting
 k ^TMP("MVB",$j)
 s date=datefrom-.1 f  s date=$o(^BB("PK",0,"Date",date)) q:date=""  q:date>dateto  d
 .s trans="" f  s trans=$o(^BB("PK",0,"Date",date,trans)) q:trans=""  d
 ..;only return / fail xmatch
 ..i " RV RT XF "'[(" "_$p($g(^CT("TRANS",trans)),"^")_" ") q
 ..s pack="" f  s pack=$o(^BB("PK",0,"Date",date,trans,pack)) q:pack=""  d
 ...q:'$d(^BB("PK",pack))  s s=^(pack),loc=$p(s,"^",10),site1=$p($g(^CT("LO",+loc)),"^",3)
 ...i site'="",site'=site1 q
 ...s:site1="" site1=" "
 ...s chl="" f  s chl=$o(^BB("PK",0,"Date",date,trans,pack,chl)) q:chl=""  s ^TMP("MVB",$j,site1,pack,chl)=""
 q
]]></Routine>
</Export>
