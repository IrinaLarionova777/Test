<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF455" type="INT" languagemode="0" generated="1"><![CDATA[
MIF455(mi)	; IG 4/6/98 ; ASTM protocol  -  DiaMed (Bi-Directional)
	s mi=$g(mi) i '$l(mi) q
	k ^TMIF(mi,10) s $zt="err^MIF000"
	i $$select^LVBMIMP(mi) q
	f j=1:1:PLIST s @("par"_j)=PLIST(j)
	s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),lf=$c(10),cr=$c(13),nak=$c(21),etb=$c(23)
	s (sample,epis,surname,result,date,time,QC)=""
	i $$start^MIF000() q
	f  d Main i $$stop^MIF000() q
	c par4
	q
Main	r *R:10
	i $c(R)=enq d
	.d trace^MIF000(mi,"ENQ","H<--M")
	.d ACK
	.k ^TMP($zn,$j,"records") s recordOLD=""
	.f  r *R:10 q:$c(R)=eot  q:R=-1  d
	..i $c(R)'=stx q
	..s record=$$read^MIF000("",lf) q:'$l(record)
	..s record=$e(record,1,$l(record)-1)
	..d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
	.. ; check record validity
	..s et=$s(record[etx:etx,1:etb),chsum=$p(record,et,2),chrecord=$p(record,et,1)_et
	..i $$CHSUM(chrecord)'=chsum d  q
	...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
	..d ACK
	..s record=recordOLD_$p($e(record,2,$l(record)),et,1)
	..i et=etb s recordOLD=$p(record,cr,$l(record,cr)),record=$p(record,cr,1,$l(record,cr)-1)
	..i et=etx s recordOLD=""
	..f j=1:1:$l(record,cr) s x=$p(record,cr,j) i $l(x) s cnt=$o(^TMP($zn,$j,"records",""),-1)+1,^TMP($zn,$j,"records",cnt)=x
	.d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
	.s cnt="" f  s cnt=$o(^TMP($zn,$j,"records",cnt)) q:cnt=""  d
	..s record=^(cnt)
	.. ; query record
	..i $e(record)="Q" d  q
	...n (record) f j=1:1:$l($p(record,"|",3),"\") s epis=$tr($p($p($p(record,"|",3),"\",j),"^",2)," ") d
	....i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="" q
	....s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
	....i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix q
	.. ; header record
	..i $e(record)="H" d Last q
	.. ; order record
	..i $e(record)="O" d  q
	...s epis=$tr($p($p(record,"|",3),"^",1)," ")
	.. ; result record
	..i $e(record)="R" d  q
	...s x1=$p(record,"|",5),x2=$p(record,"|",4)
	...f j=1:1:$l(x1,"^") s xx1=$p(x1,"^",j),xx2=$p(x2,"^",j) i $l(xx1),$l(xx2) s result=result_xx1_$c(par10)_xx2_$c(par11)
	... ; date and time
	...i $l($tr($p(record,"|",13)," ")) d
	....s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
	....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
	.. ; last record
	..i $e(record)="L" d Last q
	.k ^TMP($zn,$j,"records")
	.d ORDER
	q
Last	; file result if exists
	i $l(result),$l(epis) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
	s (sample,epis,surname,result,date,time,QC)=""
	q
ORDER	; create list of orders if exists
	s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  s suffix=$p(^(epis),"|") d
	.k ^TMP("MIF-SINGLE",$j,mi)
	.d scanone^MIF000(mi,epis,,,suffix)
	.s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="H|\^&|||LABTRAK|||||||P|1|"
	.d PATDET(epis)
	.s tc="" f N=1:1 s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d
	..s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_N_"|"_epis_suffix_"||"_tc_"|||||||N||||||||||||||Q"
	.s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|N"
	.k ^TMP("MIF-SINGLE",$j,mi)
	.k ^TMP($zn,$j,"ENQ",epis)
	.d Send
	q
PATDET(epis)	; set patient details record
	k PLIST s sex="U" i '$$select^LVBEPVIS(epis,"Y") d
	.f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
	.s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
	s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||"_epis_suffix_"||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"|||"_sex_"|||||"_$g(PLIST(15))
	q
Send	; send list of orders if exists
	w enq,*-3 f j=1:1:10 r *R:1 i ($c(R)=ack)!($c(R)=enq) q
	i $c(R)=ack d
	.s x="" f FRAME=1:1 s x=$o(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
	.i x="" k ^TMIF(mi,10)
	w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
	q
SEND(str)	; send string to instrument
	s str=str_cr_etx,chsum=$$CHSUM(str)
	w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
	f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
	i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
	i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
	d trace^MIF000(mi,R,"H<--M")
	q 2
CHSUM(x)	; calculate check sum
	n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
	s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
	q z
ACK	; send 'ack' to instrument
	w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
	q
]]></Routine>
</Export>
