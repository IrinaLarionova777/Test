<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBHL7OUT01" type="INT" languagemode="0" generated="1"><![CDATA[
LVBHL7OUT01 ; ML 29/11/01 ; Create HL7 Format File - TRAK
 ;
 ; supported calls:
 ;
 ; create^LVBHL7OUT01
 ;
create n
 i $$select^MVBCFSM(1) q 100
 s labname=PLIST(9)
 k CFLAB i '$$select^MVBCFLAB(1) m CFLAB=PLIST
 ; environment : T - Test
 ;               P - Production/Live
 s type="HL7"
 s environ=$p($g(CFLAB(45)),$c(1)) i environ="L" s environ="P"
 l +^TMPclean k ^TMP($zn,$j)
 l +^TEDI("OUT",type)
 s dh="" f  s dh=$o(^TEDI("OUT",type,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TEDI("OUT",type,dh,client)) q:client=""  d
 ..s doc="" f  s doc=$o(^TEDI("OUT",type,dh,client,doc)) q:doc=""  d
 ...s epis="" f  s epis=$o(^TEDI("OUT",type,dh,client,doc,epis)) q:epis=""  d
 ....s table="" f  s table=$o(^TEDI("OUT",type,dh,client,doc,epis,table)) q:table=""  d
 .....s vtsRID="" f  s vtsRID=$o(^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID)) q:vtsRID=""  d
 ......m ^TMP($zn,$j,dh,client,doc,epis,table,vtsRID)=^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID)
 ......k ^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID)
 l -^TEDI("OUT",type)
 ; check confidentiality
 s dh="" f  s dh=$o(^TMP($zn,$j,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TMP($zn,$j,dh,client)) q:client=""  d
 ..s doc="" f  s doc=$o(^TMP($zn,$j,dh,client,doc)) q:doc=""  d
 ...s epis="" f  s epis=$o(^TMP($zn,$j,dh,client,doc,epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 ....s vtsRID="" f  s vtsRID=$o(^TMP($zn,$j,dh,client,doc,epis,46,vtsRID)) q:vtsRID=""  d
 .....s conf=$$conf^LVBVISTS(vtsRID)
 .....i " Y P "[(" "_conf_" "),CFLAB(131)'="Y" k ^TMP($zn,$j,dh,client,doc,epis,46,vtsRID)
 ; check Visit Action
 s table=46,dh="" f  s dh=$o(^TMP($zn,$j,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TMP($zn,$j,dh,client)) q:client=""  d
 ..s doc="" f  s doc=$o(^TMP($zn,$j,dh,client,doc)) q:doc=""  d
 ...s ediType=$s(dh="DR":$$seldata^LVBCTDR(doc,35),dh="HOSP":$$seldata^LVBCTHOS(doc,22),1:"")
 ...i ediType="" s ediType=CFLAB(54)
 ...s epis="" f  s epis=$o(^TMP($zn,$j,dh,client,doc,epis)) q:epis=""  s stop="" d
 ....i 'stop,$$seldata^LVBCTVA($$seldata^LVBEPVIS(epis,25),5)="Y" s stop=1
 ....i 'stop d
 .....i $$open^LVBVISVA(epis)
 .....s stop="" f  q:$$fetch^LVBVISVA(epis)  i $p(PLIST(2),$c(1),4)="Y" s stop=1 q
 .....i $$close^LVBVISVA()
 ....i 'stop,ediType="F" d
 .....s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d  q:stop
 ......s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d  q:stop
 .......i '$$printable^LVBCTTS(ts,epis_"||"_ts_"||"_tscnt) q
 .......i $$status^LVBVIS1(epis_"||"_ts_"||"_tscnt)'="A" s stop=1
 ....i stop d
 .....s vtsRID="" f  s vtsRID=$o(^TMP($zn,$j,dh,client,doc,epis,table,vtsRID)) q:vtsRID=""  d
 ......s seq="" f  s seq=$o(^TMP($zn,$j,dh,client,doc,epis,table,vtsRID,seq)) q:seq=""  d
 .......m ^TEDI("OUT",type,dh,client,doc,epis,table,vtsRID,seq)=^TMP($zn,$j,dh,client,doc,epis,table,vtsRID,seq)
 .......k ^TMP($zn,$j,dh,client,doc,epis,table,vtsRID,seq)
 s dh="" f  s dh=$o(^TMP($zn,$j,dh)) q:dh=""  d
 .s client="" f  s client=$o(^TMP($zn,$j,dh,client)) q:client=""  d
 ..s fileno=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,""),-1)+1
 ..k ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno)
 ..s doc="" f  s doc=$o(^TMP($zn,$j,dh,client,doc)) q:doc=""  d
 ...k toDOC,HOSP
 ... ; ignore uncoded dr's
 ...i dh="DR" d
 ....i doc'["NC-",'$$select^LVBCTDR(doc,"Y") m toDOC=PLIST q
 ....i doc["NC-" s (toDOC(2),toDOC(3))=$p(doc,"-",2),(toDOC(4),toDOC(18),toDOC(26))=""
 ...i dh="HOSP",'$$select^LVBCTHOS(doc,"Y") m HOSP=PLIST
 ...s epis="" f  s epis=$o(^TMP($zn,$j,dh,client,doc,epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 ....s vtsRID="" f  s vtsRID=$o(^TMP($zn,$j,dh,client,doc,epis,46,vtsRID)) q:vtsRID=""  d
 .....s seq95="" f  s seq95=$o(^TMP($zn,$j,dh,client,doc,epis,46,vtsRID,seq95)) q:seq95=""  d
 ......s status=$p(^(seq95),"\",3),test=$p(vtsRID,"||",2)
 ......d SingleTest(vtsRID,status)
 ......s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"EPIS",epis,test)=""
 ..i $d(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ")) d
 ...s xfile=$g(^TEDI("OUT","HL7",dh,client))+1,^TEDI("OUT","HL7",dh,client)=xfile
 ...l +^TEDI("OUT-DATA","HL7",client,xfile)
 ...m ^TEDI("OUT-DATA","HL7",client,xfile)=^TMP($zn,$j,"OUT-DATA","HL7",client,fileno)
 ...l -^TEDI("OUT-DATA","HL7",client,xfile)
 ..k ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno)
 k ^TMP($zn,$j) l -^TMPclean
 q
dt(date,time) n (date,time) s date=$g(date),time=$g(time),result=""
 i date?5N s result=$tr($zd(date,3),"-","") i $l(time),time?1.5N s result=result_$tr($zt(time,2),":")
 q result
 ; status : Add    - Pending/Entered Test
 ;          Auth   - Authorised Test
 ;          DeAuth - Deauthorised test
 ;          Del    - Deleted Test (Pending/Resulted)
 ;          Update - Updated
 ;          Print  - Retransmit
 ;
SingleTest(vtsRID,status) ; build packet strings
 s uniseq=$$new() k OBR,OBX,bug
 k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 k VISTS i '$$select^LVBVISTS(vtsRID) m VISTS=PLIST
 k CTTS i '$$select^LVBCTTS(test,"Y") m CTTS=PLIST
 k DOC i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") m DOC=PLIST
 i '$d(VISIT)!'$d(VISTS)!'$d(CTTS)!'$d(DOC) q
 k REPORT s report=$p(CTTS(21),$c(1)) i '$$select^LVBCTRG(report,"Y") m REPORT=PLIST
 i (report=" ")!'$d(REPORT) s REPORT(4)=0,REPORT(5)="Y"  
 ; extract copy-to doctors
 s (copyto,copycnt)="" i $l(VISIT(22)) s copyto="^"_$p(VISIT(22),$c(1),2)_"^^^^",copycnt=1
 s x="" f  s x=$o(^TEPI(epis,5,x)) q:x=""  d  q:copycnt=5
 .s copy=$p(^TEPI(epis,5,x),"\",1) i $l(copy),'$$select^LVBCTDR(copy,"Y") d  q
 ..s copyto=copyto_$s($l(copyto):"~",1:"")_PLIST(18)_"^"_PLIST(3)_"^"_PLIST(4)_"^^^"_PLIST(26)
 ..s copycnt=copycnt+1
 .s copy=$p(^TEPI(epis,5,x),"\",15) i $l(copy),'$$select^LVBCTHOS(copy,"Y") d  q
 ..s copyto=copyto_$s($l(copyto):"~",1:"")_"^"_PLIST(3)_"^^^^"
 ..s copycnt=copycnt+1
 .s copy=$p(^TEPI(epis,5,x),"\",2) i $l(copy) d
 ..s copyto=copyto_$s($l(copyto):"~",1:"")_"^"_copy_"^^^^"
 ..s copycnt=copycnt+1
 ; check if updated test has to be sent away
 i status="Update" s vq="",stop=1 d  i stop g SingleEnd
 .i $$open^LVBVISTQ(vtsRID,"VQ")
 .f  q:$$fetch^LVBVISTQ(vtsRID,"VQ")  i $p(PLIST(9),$c(1))="" s vq=$p(PLIST(10),$c(1))
 .i $$close^LVBVISTQ()
 .i $l(vq),'$$select^LVBCTVQ(vq),PLIST(6)="Y" s stop=0
 s msh(3)=labname
 s msh(6)=$$dt(+$h,$p($h,",",2))
 s msh(9)=uniseq
 s msh(10)=environ
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="MSH|^~\&|LABTRAK|"_msh(3)_"|PATH_RESULTS||"_msh(6)_"||ORU^R01|"_msh(9)_"|"_msh(10)_"|2.3|||AL|NE"
 s pid(2)=$p(VISIT(20),$c(1))_"^^^^MRLT"
 s pid(3)=$p(VISIT(20),$c(1))
 i dh="DR" s pid(4)=$p(VISIT(16),$c(1)) i pid(4)="" s pid(4)=$p(VISIT(20),$c(1))
 i dh="HOSP" s pid(4)=$p(VISIT(65),",",2) i pid(4)="" s pid(4)=$p(VISIT(20),$c(1))
 s pid(5)=$p(VISIT(3),$c(1))_"^"_$p(VISIT(4),$c(1))_"^^^"_$p(VISIT(7),$c(1),2)
 s pid(7)=$$dt(VISIT(6))
 s pid(8)=$p(VISIT(5),$c(1))
 s pid(11)=$p(VISIT(32),$c(1))_"^"_$p(VISIT(33),$c(1))_"^"_$p(VISIT(34),$c(1))_"^^"_$p(VISIT(36),$c(1))
 s pid(13)=$p(VISIT(43),$c(1))
 s pid(14)=$p(VISIT(44),$c(1))
 s pid(19)=$p(VISIT(8),$c(1))
 s pid(27)=$p(VISIT(80),$c(1))
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="PID||"_pid(2)_"|"_pid(3)_"|"_pid(4)_"|"_pid(5)_"||"_pid(7)_"|"_pid(8)_"|||"_pid(11)_"||"_pid(13)_"|"_pid(14)_"|||||"_pid(19)_"||||||||"_pid(27)
 s pv1(2)="*"
 s pv1(8)=DOC(18)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)
 i dh="DR" s pv1(9)=toDOC(18)_"^"_toDOC(3)_"^"_toDOC(4)_"^"_toDOC(49)_"^"_toDOC(50)_"^"_toDOC(26)
 i dh="HOSP" s pv1(9)=HOSP(2)_"^"_HOSP(3)
 s pv1(19)=$p(VISIT(65),",",1)
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="PV1||"_pv1(2)_"||||||"_pv1(8)_"|"_pv1(9)_"||||||||||"_pv1(19)
 ; build pending/deleted/deauthorised test packet
 i '((status="Auth")!(status="Update")!(status="Print")) d  g SingleEnd
 . ; Pending = P, Deleted = X, Deauthorised = D
 .s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1,OBR=$g(OBR)+1 k OBX
 .s obr(1)=OBR
 .s obr(2)=$tr($g(VISTS(38)),"|"," ")
 .s obr(3)=$tr(vtsRID,"|"," ")_"^PLS"
 .s obr(4)=CTTS(2)_"^"_CTTS(3)_"^L" i $l(CTTS(34)) s obr(4)=obr(4)_"^"_CTTS(34)_"^"_$s($l(CTTS(36)):CTTS(36),1:CTTS(35))_"^"_CFLAB(122)
 .s obr(7)=$$dt(VISIT(12),+VISIT(13))
 .s obr(11)=$p($g(VISTS(58)),$c(1))
 .s obr(12)=CTTS(22)
 .s obr(14)=$$dt(VISIT(61),+VISIT(62))
 .s obr(16)=DOC(18)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)
 .s obr(20)=copyto
 .s obr(22)=$$dt(VISTS(7),+VISTS(8))
 .s obr(25)=$s(status="Add":"P",status="Del":"X",1:"D")
 .s obr(27)="^^^"_$$dt(VISIT(14),+VISIT(111))_"^^"_$$priority^LVBVISTS(vtsRID)_"^^^^^^"
 .s obr(28)=copyto
 .s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="OBR|"_obr(1)_"|"_obr(2)_"|"_obr(3)_"|"_obr(4)_"|||"_obr(7)_"||||"_obr(11)_"|"_obr(12)_"||"_obr(14)_"||"_obr(16)_"||||"_obr(20)_"||"_obr(22)_"|||"_obr(25)_"||"_obr(27)_"|"_obr(28)
 ; check confidentiality
 s conf=$$conf^LVBVISTS(vtsRID)
 ; build authorised result packet
 i " Y P "[(" "_conf_" ") k PLIST s PLIST=1,PLIST(1)=vtsRID_"||T"_$c(1)_"T"_$c(1)_"Confidential result. Please call pathology on <>" m visres=PLIST
 e  k visres i '$$results^LVBVISTS(vtsRID) m visres=PLIST
 k PLIST
 ; Updated = P, Authorised = A
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1,OBR=$g(OBR)+1 k OBX
 s obr(1)=OBR
 s obr(2)=$tr(VISTS(38),"|"," ")
 s obr(3)=$tr(vtsRID,"|"," ")_"^PLS"
 s obr(4)=CTTS(2)_"^"_CTTS(3)_"^L" i $l(CTTS(34)) s obr(4)=obr(4)_"^"_CTTS(34)_"^"_$s($l(CTTS(36)):CTTS(36),1:CTTS(35))_"^"_CFLAB(122)
 s obr(7)=$$dt(VISIT(12),+VISIT(13))
 s obr(11)=$p(VISTS(58),$c(1))
 s obr(12)=CTTS(22)
 s obr(14)=$$dt(VISIT(61),+VISIT(62))
 s obr(16)=DOC(18)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)
 s obr(20)=copyto
 s obr(22)=$$dt(VISTS(7),+VISTS(8))
 s obr(25)=$s(status="Update":"P",1:"A")
 s obr(27)="^^^"_$$dt(VISIT(14),+VISIT(111))_"^^"_$$priority^LVBVISTS(vtsRID)_"^^^^^^"
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="OBR|"_obr(1)_"|"_obr(2)_"|"_obr(3)_"|"_obr(4)_"|||"_obr(7)_"||||"_obr(11)_"|"_obr(12)_"||"_obr(14)_"||"_obr(16)_"||||"_obr(20)_"||"_obr(22)_"|||"_obr(25)_"||"_obr(27)
 k PLIST i '$$COMMENTS^LVBCOM01(vtsRID,,"P") m COMM=PLIST d
 .k xxx
 .s j1="" f  s j1=$o(COMM(j1)) q:j1=""  s text=COMM(j1) d  s COMM(j1)=text
 ..f j2=1:1 q:$l(text,"^")=1  s text=$p(text,"^",1)_$p(text,"^",3)
 .s j1="" f  s j1=$o(COMM(j1)) q:j1=""  f j2=1:1:$l(COMM(j1),$c(16)) s xxx(j1)=$g(xxx(j1))_$s($l($g(xxx(j1))):"\S\",1:"")_$p(COMM(j1),$c(16),j2)
 s (nte(3),j1)="" f  s j1=$o(xxx(j1)) q:j1=""  s nte(3)=nte(3)_$s($l(nte(3)):"~",1:"")_xxx(j1)
 i $l(VISTS(58)) s nte(3)=nte(3)_$s($l(nte(3)):"~",1:"")_$p(VISTS(58),$c(1),2)
 i $l(nte(3)) d
 .s nte(3)=$tr(nte(3),$c(18),"!")
 .s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1
 .s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="NTE|1||"_nte(3)
 s i="" f  s i=$o(visres(i)) q:i=""  s tc=$p(visres(i),$c(1),2) i '$$select^LVBCTTC(tc,"Y") d
 .k CTTC m CTTC=PLIST k PLIST
 .s res=$p(visres(i),$c(1),3)
 .i $l(res) d
 ..s format="TX"
 ..i " N M "[(" "_$e(CTTC(5))_" ") s format="NM" i '$$select^LVBCTRC(res,"Y") s res=$tr(PLIST(3),"|","~"),format="TX"
 ..i " S B1 A "[(" "_CTTC(5)_" "),'$$select^LVBCTTCT(tc_"||"_res) d
 ...s res=$tr(PLIST(3),"|","~"),xx2=$s($p(visres(i),$c(1),5)="N":"N",1:"A")
 ...i $e(res,1,5)'="\.br\" s res="\.br\"_res
 ..i " D Z11 Z12 Z14 Z15 "[(" "_CTTC(5)_" ") s res=$$extdate2^SSUTIL4(res),format="ST"
 ..i " B2 "[(" "_CTTC(5)_" "),'$$select^LVBBBBG(res) s res=PLIST(2)_"^"_$tr(PLIST(3),"|","~"),format="CE"
 ..i " V "[(" "_CTTC(5)_" "),'$$select^LVBCTBUG(res) s res=PLIST(2)_"^"_$tr(PLIST(3),"|","~"),format="CE",bug(0,tc)=""
 ..s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1,OBX=$g(OBX)+1 i $d(bug(0,tc)) s bug(0,tc)=OBX,bug(1,OBX)=tc
 ..s obx(1)=OBX
 ..s obx(2)=format
 ..s obx(3)=CTTC(2)_"^"_CTTC(3)_"^L" i $l(CFLAB(123)),$l(CTTC(36)) s obx(3)=obx(3)_"^"_CTTC(36)_"^"_$s($l(CTTC(38)):CTTC(38),1:CTTC(37))_"^"_CFLAB(123)
 ..s obx(5)=$tr(res,$c(18),"!")
 ..s (obx(6),obx(7),obx(8))="" i format="NM" d
 ...s obx(6)=$tr(CTTC(4),$c(16),"^")
 ...s xx=$$ranges^LVBVIS1(tc,VISIT(26),$p(VISIT(5),$c(1)),,"0",$s($l(VISTS(7)):VISTS(7),1:+$h),VISIT(98),VISIT(22),,VISIT(64)_$c(1)_$p(VISIT(60),$c(1))_$c(1)_$p(VISIT(167),$c(1)))
 ...s obx(7)=$p(xx,$c(1))_$s($l($p(xx,$c(1),2)):"-"_$p(xx,$c(1),2),1:"")
 ...s obx(8)=$s($p(visres(i),$c(1),7)="L":"--",$p(visres(i),$c(1),7)="H":"++",$p(visres(i),$c(1),5)="L":"-",$p(visres(i),$c(1),5)="H":"+",1:"")
 ..s obx(11)=$s(VISTS(33)="A":"F",1:"")
 ..s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="OBX|"_obx(1)_"|"_obx(2)_"|"_obx(3)_"||"_obx(5)_"|"_obx(6)_"|"_obx(7)_"|"_obx(8)_"|||"_obx(11)
 k SingRes,CumRes,AllRes,CompRes
 s x=$$test^LVBCOM07(CTTS(2),$e(dh),doc),report=$p(x,$c(1))
 k REPORT s REPORT(4)=0 i '$$select^LVBCTRG(report,"Y") m REPORT=PLIST
 ; compile non cumulative formatted result
 s report="" i " 0 2 "[(" "_REPORT(4)_" "),'$$SINGLE^PRTDR00(vtsRID,"E-HL7","") d
 .d adjust(.PLIST) i PLIST d
 ..f j=1:1:PLIST s PLIST(j+.5)="!"
 ..m SingRes=PLIST k PLIST s report="Y"
 ; compile cumulative format
 s format=""
 i $p(CTTS(7),$c(1))'="D" s format=$s($p(CTTS(7),$c(1))="F":"C",CTTS(7)="Y":"C",1:"")
 i $p(CTTS(7),$c(1))="D" s format=$s((dh="DR")&($g(toDOC(34))'="N"):"C",(dh="HOSP")&($g(HOSP(15))'="N"):"C",1:"")
 i format="C" d
 .k ^TMP($zn,$j,"EpisodeList")
 .s visit="" f j=1:1:VISIT s $p(visit,$c(28),j)=$g(VISIT(j))
 .s visit=$tr(visit,"""","'")
 .d CumList^PRTDR00($p(vtsRID,"||",2),$p(vtsRID,"||",1),visit,$s(REPORT(4)=1:"Y",REPORT(4)=4:"Y",1:""))
 .m ^TMP($zn,$j,"EpisodeList")=PLIST k PLIST
 .s (col,EpisodeList)="" f  s col=$o(^TMP($zn,$j,"EpisodeList",col)) q:col=""  s EpisodeList=$s($l(EpisodeList):EpisodeList_",",1:"")_^(col)
 .k ^TMP($zn,$j,"EpisodeList")
 .i '$$SINGLE^PRTDR00(vtsRID,"E-HL7","C",EpisodeList) d
 ..d adjust(.PLIST) i PLIST f j=1:1:PLIST s PLIST(j+.5)="!",report="Y"
 ..d adjust(.HEADER) i HEADER f j=1:1:HEADER s HEADER(j+.5)="!",report="Y"
 ..i PLIST m CumRes=PLIST k PLIST
 i report'="Y",'$$SINGLE^PRTDR00(vtsRID,"E-HL7","") d
 .d adjust(.PLIST) i PLIST d
 ..f j=1:1:PLIST s PLIST(j+.5)="!"
 ..m SingRes=PLIST k PLIST
 ; merge single result and cumulative results together
 s cnt="" f  s cnt=$o(SingRes(cnt)) q:cnt=""  s AllRes(cnt)=SingRes(cnt)
 i $d(SingRes) f j=1:1:2 s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)="!"
 s cnt="" f  s cnt=$o(HEADER(cnt)) q:cnt=""  s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)=HEADER(cnt)
 s cnt="" f  s cnt=$o(CumRes(cnt)) q:cnt=""  s cntx=$o(AllRes(""),-1)+1,AllRes(cntx)=CumRes(cnt)
 ; compile for sending
 s (cnt,count,text,textx)="",string="~",i="" f  s i=$o(AllRes(i)) q:i=""  d
 .i $e(AllRes(i))="~" q
 .s text=AllRes(i) i text=$c(13) q
 .i ($e(text)="!")!($e(text)="#") d  q
 ..i textx'="" s cnt=cnt+1,CompRes(cnt)=textx
 ..s cnt=cnt+1,CompRes(cnt)="!",textx=$e($tr(text,"~","^"),2,$l(text))
 .s text=$tr(text,"~","^")
 .s len=$l(text) f x=1:1 q:$e(text,x)'=" "
 .f j=x:1:len s $e(textx,j)=$e(text,j)
 s string="\.br\" f j=1:1 q:'$d(CompRes(j))  d
 .i CompRes(j)="!" s string=string_"\.br\" q
 .s string=string_$$translate^LVBCOM01(CompRes(j),"\","\E\")
 s str="" f j=1:1:$l(string,"^") s str=str_$p(string,"^",j)_"\S\"
 s string=$p(str,"\S\",1,$l(str,"\S\")-1)
 s string=$$translate^LVBCOM01(string,"|","\F\")
 s string=$$translate^LVBCOM01(string,"^","\S\")
 s string=$$translate^LVBCOM01(string,"&","\T\")
 s string=$$translate^LVBCOM01(string,"~","\R\")
 s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1,OBX=$g(OBX)+1
 s obx(1)=OBX
 s obx(2)="FT"
 s obx(5)=string
 s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="OBX|"_obx(1)_"|"_obx(2)_"|||"_obx(5)
 ; file antibiotics
 f j=1:1:3 s @("a"_j)=$p(vtsRID,"||",j)
 s bugSEQ="" f  s bugSEQ=$o(bug(1,bugSEQ)) q:bugSEQ=""  s bugTC=bug(1,bugSEQ) i $l(bugTC) d
 .s (zz,ant)="" f  s ant=$o(^TEPI(a1,1,a2,a3,"DATA",bugTC,"ANT",ant)) q:ant=""  s xx=^(ant) i $p(xx,"\",2)="Y" s zz="Y" q
 .i zz="Y" d
 ..s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1,OBR=$g(OBR)+1 k OBX
 ..s obr(1)=OBR
 ..s obr(2)=$tr(VISTS(38),"|"," ")
 ..s obr(3)=$tr(vtsRID,"|"," ")_"^PLS"
 ..s obr(4)=CTTS(2)_"^"_CTTS(3)_"^L" i $l(CTTS(34)) s obr(4)=obr(4)_"^"_CTTS(34)_"^"_$s($l(CTTS(36)):CTTS(36),1:CTTS(35))_"^"_CFLAB(122)
 ..s obr(7)=$$dt(VISIT(12),+VISIT(13))
 ..s obr(11)=$p(VISTS(58),$c(1))
 ..s obr(12)=CTTS(22)
 ..s obr(14)=$$dt(VISIT(61),+VISIT(62))
 ..s obr(16)=DOC(18)_"^"_DOC(3)_"^"_DOC(4)_"^"_DOC(49)_"^"_DOC(50)_"^"_DOC(26)
 ..s obr(20)=copyto
 ..s obr(22)=$$dt(VISTS(7),+VISTS(8))
 ..s obr(25)=$s(status="Update":"P",1:"A")
 ..s obr(26)=bugTC
 ..s obr(27)="^^^"_$$dt(VISIT(14),+VISIT(111))_"^^"_$$priority^LVBVISTS(vtsRID)_"^^^^^^"
 ..s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="OBR|"_obr(1)_"|"_obr(2)_"|"_obr(3)_"|"_obr(4)_"|||"_obr(7)_"||||"_obr(11)_"|"_obr(12)_"||"_obr(14)_"||"_obr(16)_"||||"_obr(20)_"||"_obr(22)_"|||"_obr(25)_"|"_obr(26)_"|"_obr(27)
 ..s ant="" f  s ant=$o(^TEPI(a1,1,a2,a3,"DATA",bugTC,"ANT",ant)) q:ant=""  s xx=^(ant) i $p(xx,"\",2)="Y" d
 ...k CTANT i '$$select^LVBCTANT(ant,"Y") m CTANT=PLIST
 ...s seq=$o(^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",""),-1)+1,OBX=$g(OBX)+1
 ...s obx(1)=OBX
 ...s obx(2)="ST"
 ...s obx(3)=CTANT(2)_"^"_CTANT(3)_"^L"
 ...s (obx(5),obx(8))=$p(xx,"\",1) i $l(obx(8)),'$$select^LVBCTSN(obx(8)) s obx(5)=PLIST(3)
 ...s obx(11)=$s(VISTS(33)="A":"F",1:"")
 ...s ^TMP($zn,$j,"OUT-DATA","HL7",client,fileno,"SEQ",seq)="OBX|"_obx(1)_"|"_obx(2)_"|"_obx(3)_"||"_obx(5)_"|||"_obx(8)_"|||"_obx(11)
SingleEnd q
 ; create new sequence number for MSH record
new() l +^CF(dh_"-SEQ")
 s num=$g(^CF(dh_"-SEQ"))+1,^CF(dh_"-SEQ")=num
 l -^CF(dh_"-SEQ")
 q num
 ; adjust into one line
adjust(ARRAY) k array s x2=1
 s x1="" f  s x1=$o(ARRAY(x1)) q:x1=""  d
 .i ARRAY(x1)=$c(13) q
 .i ARRAY(x1)="!" s x2=x2+1 q
 .i $e(ARRAY(x1))="~" q
 .i $e(ARRAY(x1))="?" d  q
 ..s tab=+$e(ARRAY(x1),2,4),ARRAY(x1)=$e(ARRAY(x1),5,$l(ARRAY(x1)))
 ..i tab>$l($g(array(x2))) s array(x2)=$e($g(array(x2))_$j("",tab),1,tab)
 ..i $l(ARRAY(x1)) s array(x2)=$g(array(x2))_ARRAY(x1)
 .i $l(ARRAY(x1)) s array(x2)=$g(array(x2))_ARRAY(x1)
 k ARRAY m ARRAY=array s ARRAY=$o(ARRAY(""),-1)
 f j=1:1:ARRAY i '$d(ARRAY(j)) s ARRAY(j)=""
 q
]]></Routine>
</Export>
