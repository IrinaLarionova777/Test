<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LHOSPINT19" type="INT" languagemode="0" generated="1"><![CDATA[
LHOSPINT19	; IG 12/07/98 ; Hospital Interface - Medlab
	Q
	; delete end spaces
space(x)	
	f  q:'$l(x)  q:$e(x,$l(x))'=" "  s x=$e(x,1,$l(x)-1)
	f  q:'$l(x)  q:$e(x)'=" "  s x=$e(x,2,$l(x))
	q x
	; Import results
RESULT(HOSP)	N (HOSP) k ^TMP($zn,$j)
	i $$select^LVBCTRL(HOSP) q
	s NAME=PLIST(3)
	s dir=PLIST(7),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
	s dir=dir_"*.pit"
	; MS-DOS based system
	i $zf(-1,"DIR "_dir_" *.pit /B > ""results.txt""")
	; UNIX based system
	;i $zf(-1,"ls "_dir_" *.pit > ""results.txt""")
	s $zt="Error1^"_$zn
	s FILE="results.txt"
	o FILE:("RS"::$c(10,13))
	f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1	c FILE
END2	s $zt="Error2^"_$zn
	s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
	.s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
	.o xFILE:("RS"::$c(10,13)):1 e  q
	.s Error="" f  u xFILE r MESS s seq=$o(^TMP($zn,$j,xxFILE,"IN",""),-1)+1,^TMP($zn,$j,xxFILE,"IN",seq)=MESS
	s $zt=""
	s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
	.s (lab,seq)="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=^(seq) d
	..i $e(xx,1,3)="110" k result s cnt=0,x=$tr($e(xx,27,99)," "),epis=$p(x,"-",1),lab=""
	..i $e(xx,1,3)="111" s lab=$tr($e(xx,27,99)," ")
	..i $e(xx,1,3)="201" s specimen=$e(xx,5,$l(xx))
	..i $e(xx,1,3)="205" s comment="" d
	...s syn=$tr($p($p($$UPPER^SSUTIL4($e(xx,27,$l(xx))),"-"),".")," ")
	...i syn["COMMENT" s comment="Y" q
	...i $l(syn) s ^TMP($zn,$j,xFile,"OUT",lab,"SYN",syn)=""
	..i $e(xx,1,3)="301",$l(lab) d
	...s cnt=cnt+1,result(cnt)=$e(xx,5,$l(xx)),res="" k dd2
	...s prev=0 f j=2:1:$l(result(cnt),"~")-1 d
	....s (DD,dd1)=$p(result(cnt),"~",j),(stop,dd2(j))=""
	....f  q:'$l(dd1)  d  q:stop
	.....i $e(dd1,1,4)="SBLD" s dd1=$e(dd1,5,$l(dd1)),prev=1 q
	.....i $e(dd1,1,4)="EBLD" s dd1=$e(dd1,5,$l(dd1)),prev=1 q
	.....i " SUND EUND SBLK EBLK DFLT "[(" "_$e(dd1,1,4)_" ") s dd1=$e(dd1,5,$l(dd1)),prev=1 q
	.....i $e(dd1,1,4)="FG04" s dd1=$e(dd1,5,$l(dd1)),prev=1 q
	.....i $e(dd1,1,4)?1"FG"2N s dd1=$e(dd1,5,$l(dd1)),prev=1 q
	.....i $e(dd1,1,4)?1"BG"2N s dd1=$e(dd1,5,$l(dd1)),prev=1 q
	.....s stop=1,dd2(j)=$s(prev:"",1:"~")_DD
	...s result(cnt)=$tr(result(cnt),"~",$c(1))
	...f j=2:1:$l(result(cnt),$c(1))-1 s $p(result(cnt),$c(1),j)=dd2(j)
	...s result(cnt)=$tr(result(cnt),$c(1))
	..i ($e(xx,1,3)="309")!($e(xx,1,3)="390"),$l(lab),$d(result) d
	...i '$d(^TMP($zn,$j,xFile,"OUT",lab,"T")) d
	....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)="This test has been performed at "_NAME
	....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)="Lab Reference: "_lab
	....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)=""
	....i $l(specimen) d
	.....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)=specimen
	.....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)=""
	...i comment="Y" s comment="" d
	....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)=""
	....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)="Comments :"
	....s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)=""
	...f j=1:1:cnt s x=$o(^TMP($zn,$j,xFile,"OUT",lab,"T",""),-1)+1,^TMP($zn,$j,xFile,"OUT",lab,"T",x)=result(j)
	...s ^TMP($zn,$j,xFile,"OUT",lab)=epis
	...k result s lab=""
	.k ^TMP($zn,$j,xFile,"out")
	.s lab="" f  s lab=$o(^TMP($zn,$j,xFile,"OUT",lab)) q:lab=""  d
	..s epis=$g(^TMP($zn,$j,xFile,"OUT",lab)) i $l(epis) d
	...s (vts,syn)="" f  s syn=$o(^TMP($zn,$j,xFile,"OUT",lab,"SYN",syn)) q:syn=""  d  q:$l(vts)
	....s ts="" f  s ts=$o(^TTAB("SY",syn,"TS",ts)) q:ts=""  d
	.....i $$seldata^LVBCTTS(ts,31)'="Y" q
	.....s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  s vts=epis_"||"_ts_"||"_tscnt
	...i $l(vts) m ^TMP($zn,$j,xFile,"out",vts)=^TMP($zn,$j,xFile,"OUT",lab)
	.k ^TMP($zn,$j,xFile,"OUT")
	.m ^TMP($zn,$j,xFile,"OUT")=^TMP($zn,$j,xFile,"out")
	.k ^TMP($zn,$j,xFile,"out")
	.s vts="" f  s vts=$o(^TMP($zn,$j,xFile,"OUT",vts)) q:vts=""  d
	..s (text,j)="" f i=1:1 s j=$o(^TMP($zn,$j,xFile,"OUT",vts,"T",j)) q:j=""  s $p(text,"|",i)=^(j)
	..s epis=$p(vts,"||",1),ts=$p(vts,"||",2),tscnt=$p(vts,"||",3)
	..i $$vtsLock^SSLOCK("IM",epis,vts,"trak")=-1 d
	...i $$status^LVBVIS1(vts)="A" d
	....i $$delall^LVBVISTQ(vts)
	....i '$$select^LVBVISTS(vts,"Y") s (PLIST(7),PLIST(8),PLIST(9))="" i $$update^LVBVISTS(vts)
	...i $d(^TEPI(epis,1,ts,tscnt,"DATA","T")) d
	....k xx m xx=^TEPI(epis,1,ts,tscnt,"DATA","T","REM")
	....s max=0,xx=""
	....f j=1:1 q:'$d(xx(j))  s xx=xx_$s($l(xx):"|",1:"")_xx(j) i max<$l(xx(j)) s max=$l(xx(j))
	....f j=1:1:$l(text,"|") i max<$l($p(text,"|",j)) s max=$l($p(text,"|",j))
	....s line="",$p(line,"-",max+1)=""
	....s text=$j("",max-22\2)_"--- Amended Result ---"_"||"_text_"||"_line_"|"_xx
	...k PLIST
	...s PLIST(1)="DATA"_$c(1)_"T"_$c(1)_text
	...s PLIST=$o(PLIST(""),-1)
	...i $$TScheck^LVBVIS7(vts,"A","","B")
	...s (VQ,ok)="" f j=1:1:PLIST d  i ok="N" q
	....i $e($p(PLIST(j),$c(1)))="E" s ok="N" q
	....i $e($p(PLIST(j),$c(1)),1,2)="VQ" s VQ=$p(PLIST(j),$c(1),2)
	...i $$delall^LVBVISTQ(vts)
	...k PLIST s PLIST(1)="T\"_text
	...s PLIST=$o(PLIST(""),-1)
	...s err=$$save^LVBVIS4(vts,"A","trak","N")
	...s ^TMPimport(xFile,"index",vts)=$g(^TMPimport(xFile,"index",vts))_" err"
	...i 'err d
	....i '$$select^LVBVISTS(vts,"Y") s PLIST(29)=$$seldata^LVBEPVIS(epis,66) i $$update^LVBVISTS(vts)
	....k PLIST
	....s PLIST=$o(PLIST(""),-1)
	....i $$TScheck^LVBVIS7(vts,"A","","A")
	....f j=1:1:PLIST d
	.....s val=$p($p(PLIST(j),$c(1),2),$c(2))
	.....i $e($p(PLIST(j),$c(1)),1,2)="VQ",$$VQ^LVBCOM13(vts,val)
	.....i $e($p(PLIST(j),$c(1)),1,2)="TA",$$TAR^LVBCOM13($p(vts,"||"),,val)
	.....i $e($p(PLIST(j),$c(1)),1,2)="SA",$$SA^LVBCOM13($p(vts,"||"),val)
	.....i $e($p(PLIST(j),$c(1)),1,2)="SF",$$SF^LVBCOM13(vts,$p(PLIST(j),$c(1),6),val)
	.....i $e($p(PLIST(j),$c(1)),1,3)="ACT",$$ACT^LVBCOM13($p(vts,"||"),$p(val,"-",1),$p(val,"-",2))
	..i $$vtsLockClr^SSLOCK("IM",epis,vts)
	s (param,xFile)="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  s param=param_$s($l(param):",",1:"")_xFile
	i $l(param),$$START^PRTREP00("","",HOSP_"\"_param,"RLI")
	k ^TMP($zn,$j)
	q
	; Error log
Error1	i $ze["ENDOFFILE" g END1
	d BACK^%ETN
	q
	; Error log
Error2	i $ze'["ENDOFFILE" d BACK^%ETN q
	c xFILE k ^TMP($zn,$j," ",xxFILE)
	; Move the file
	s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
	; MS-DOS based system
	i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
	; UNIX based system
	;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
	g END2
]]></Routine>
</Export>
