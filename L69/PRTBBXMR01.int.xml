<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBXMR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBXMR01 ; IG 01/05/99 ; BloodBank XMatch report
 q
 ; Parameters :  1. vtsRID
 ;               2. Event : U-Update
 ;                           -Authorize
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j),^TMP("PRTDR00",$j),^TMP("REPORT",$j)
 k xHEADER d INIT,BODY,PRINT
 k ^TMP($zn,$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
BODY ; create report
 s vtsRID=PAR1,epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2)
 k VISTE i '$$getall^LVBVISTE(vtsRID) d
 .f j=1:1:PLIST d
 ..i PAR2="U",$p($p($p(PLIST(j),$c(2),4),$c(28),12),$c(1))'="Y" q
 ..s tr=$p($p($p(PLIST(j),$c(2),4),$c(28),5),$c(1)) i $l(tr) s x=$o(VISTE(tr,""),-1)+1,VISTE(tr,x)=PLIST(j)
 i '$d(VISTE) q
 k VISTD i '$$results^LVBVISTS(vtsRID,,,,"Y") m VISTD=PLIST
 k VISTS i '$$select^LVBVISTS(vtsRID) m VISTS=PLIST
 k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 ; get doctor information
 s doc=$p(VISIT(15),$c(1)) k DOC i '$$select^LVBCTDR(doc,"Y") m DOC=PLIST
 s dep="",tsLM=0,tsWD=80
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s tsWD=80 i '$$select^MVBCFLAB(1) d
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 s tr="" f  s tr=$o(VISTE(tr)) q:tr=""  d BODY1(tr)
 q
 ; tr : XM1 - Direct Issue
 ;      XM2 - Group Specific Issue
 ;      DI  - Emergency Issue
 ;      I   - Issue
 ;      II  - Incompatible Issue
BODY1(tr) s tr=$g(tr)
 d ADD^PRTDR00(,$c(27)_"(s7B"),LN^PRTDR00()
 s header=""
 i " XM1 XM2 DI II "[(" "_$p(tr,$c(1))_" ") s header=$p(tr,$c(1),2)_" Report"
 e  i " I "[(" "_$p(tr,$c(1))_" ") s header="Compatibility Report"
 d ADD^PRTDR00(tsLM+20,header),ADD^PRTDR00(,$c(13))
 s line="",$p(line,"_",$l(TEST(17))+1)="" d ADD^PRTDR00(tsLM+20,line),LN^PRTDR00()
 d ADD^PRTDR00(,$c(27)_"(s0B"),LN^PRTDR00()
 i $l(VISIT(69)) d ADD^PRTDR00(tsLM,"GROUP: "_$p(VISIT(69),$c(1),2))
 d LN^PRTDR00()
 i tr'="XM1" d ADD^PRTDR00(tsLM,"ANTIBODY SCREEN: "_$$abs())
 f J=1:1:3 d LN^PRTDR00()
 i tr="XM2" d
 .d ADD^PRTDR00(tsLM,"EMERGENCY BLOOD ISSUE. The units listed below are issued as"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"Group Specific only as requested by Medical Officers to meet a"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"clinical emergency. Full crossmatching procedures will continue"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"and final Blood Transfusion Sheet and Compatibility Labels will"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"be issued on completion."),LN^PRTDR00()
 d ADD^PRTDR00(,"~BOLD"),LN^PRTDR00()
 d ADD^PRTDR00(tsLM,"Pack ID")
 d ADD^PRTDR00(tsLM+10,"Group")
 d ADD^PRTDR00(tsLM+30,"Product")
 d ADD^PRTDR00(tsLM+45,"SIGNATURE")
 d ADD^PRTDR00(tsLM+55,"SIGNATURE")
 d ADD^PRTDR00(tsLM+65,"Hold Date")
 d ADD^PRTDR00(tsLM+77,"Hold Time")
 d ADD^PRTDR00(,$c(13))
 d ADD^PRTDR00(tsLM,"________")
 d ADD^PRTDR00(tsLM+10,"_____")
 d ADD^PRTDR00(tsLM+30,"______")
 d ADD^PRTDR00(tsLM+45,"_________")
 d ADD^PRTDR00(tsLM+55,"_________")
 d ADD^PRTDR00(tsLM+65,"__________")
 d ADD^PRTDR00(tsLM+77,"__________")
 d ADD^PRTDR00(,"~NORMAL"),LN^PRTDR00()
 s cnt="" f  s cnt=$o(VISTE(tr,cnt)) q:cnt=""  s data=$p(VISTE(tr,cnt),$c(2),4) d
 .s x1=$p(data,$c(28),2)_"||"_$p(data,$c(28),3) i '$$select^LVBBBP(x1) d
 ..k PACK m PACK=PLIST
 ..d ADD^PRTDR00(tsLM,PACK(2))
 ..d ADD^PRTDR00(tsLM+10,$p(PACK(9),$c(1),2))
 ..d ADD^PRTDR00(tsLM+30,$p(PACK(8),$c(1),2))
 ..d ADD^PRTDR00(tsLM+65,$$extdate2^SSUTIL4($p($p(data,$c(28),8),$c(1),1)))
 ..d ADD^PRTDR00(tsLM+77,$$exttime^SSUTIL4($p($p(data,$c(28),9),$c(1),1)))
 ..d LN^PRTDR00()
 i tr'="DI" d
 .d ADD^PRTDR00(,$c(27)_"(s7B"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM+15,"UNITS ARE HELD ON CROSSMATCH FOR 48 HOURS ONLY")
 .d ADD^PRTDR00(,$c(27)_"(s0B"),LN^PRTDR00()
 .s line="",$p(line,"-",tsWD-tsLM)="" d ADD^PRTDR00(tsLM,line),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"If a serious (Haemolytic) Transfusion Reaction is a possibility: Please "),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"collect and send blood samples, urine and donor units to the laboratory"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"as soon as possible. Refer to Laboratory Handbook"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM,"Note: ")
 .d ADD^PRTDR00(tsLM+6,"- Administer through a standard giving set with an integral"),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM+8,"in-line 140-170 micron filter."),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM+6,"- Do not exceed 37oC when warming blood."),LN^PRTDR00()
 .d ADD^PRTDR00(tsLM+6,"- Infusion time of each unit should not exceed 4 hours"),LN^PRTDR00()
 f j=1:1:3 d LN^PRTDR00()
 s user=$p(VISTS(9),$c(1),2)_"  on "_$$extdate2^SSUTIL4(VISTS(7))_"  at "_$$exttime^SSUTIL4(VISTS(8))
 i tr'="XM2" d ADD^PRTDR00(tsLM,"Authorised by "_user),LN^PRTDR00()
 i tr="XM2" d ADD^PRTDR00(tsLM,"Issued by "_user),LN^PRTDR00()
 ; create page for printing
 k PLIST m PLIST=^TMP("PRTDR00",$j,"RESULT") s PLIST=$o(PLIST(""),-1)
 k ^TMP("PRTDR00",$j)
 s page=$o(^TMP("REPORT",$j,""),-1)+1,^TMP("REPORT",$j,page,"TEST",vtsRID)=""
 f jLine=1:1:PLIST s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1,^TMP("REPORT",$j,page,"TEXT",x)=PLIST(jLine)
 q
PRINT ; print report
 ; xPAR1 - U-User
 ;         C-Courier
 ; xPAR2 - User RowID/Courier
 s (xPAR1,xPAR2)=""
 s page="" f  s page=$o(^TMP("REPORT",$j,page)) q:page=""  d  k ^TMP("REPORT",$j,page)
 . ; print page Header
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 .i $l(%routine) x "d Header^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 . ; print report
 .f j=1:1 q:'$d(^TMP("REPORT",$j,page,"TEXT",j))  s xx=^(j) d ADDLINE^PRTREP00(REPID,"PRT",xx)
 . ; print page Footer
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 .i $l(%routine) x "d Footer^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 q
 ; check if antibody screen detected
abs() n (VISTD) s result="",item="X0015"
 f j=1:1:VISTD s ti=$p(VISTD(j),$c(1),2) i ti=item s result=$p(VISTD(j),$c(1),3)
 s result=$s(result="D":"Possible atypical red cell antibody detected",1:"No atypical red cell antibodies detected")
 q result
]]></Routine>
</Export>
