<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV17" type="INT" languagemode="0"><![CDATA[
PRTINV17 ; ML 26/10/00 ; PRIVATE INVOICES - Russell, de Wit, Tiley
 q
 ; Parameters :  1.Payment Code
 ;               2.Batch Number
 ;               3.Episode Number
 ;               4.Sequence Number
 ;               5.Reminder
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i 'PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)=PAR1_"-"_PAR2 i $$update^LVBPRPR(REPID)
 i PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)="REM-"_PAR5 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
BODY ; create temporary printing file
 ; print single patient
 i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
 .m VISIT=PLIST k PLIST s epis=PAR3
 .s pc=$p(VISIT(57),"||",1),batch=$p(VISIT(57),"||",2)
 .k INVOICE i '$$select^LVBINVB(pc_"||"_batch) m INVOICE=PLIST
 .s COMP(10)=$$seldata^LVBCTCO(INVOICE(6),10)
 .s seq=$p($g(^TMCB(pc,batch,epis)),"\",2)
 .s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq_"\"_$p(VISIT(9),$c(1))_"\"_COMP(10)
 .d ITEM
 ; print batch
 i $l(PAR1),$l(PAR2) d
 .k INVOICE i '$$select^LVBINVB(PAR1_"||"_PAR2) m INVOICE=PLIST
 .s COMP(10)=$$seldata^LVBCTCO(INVOICE(6),10)
 .s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
 ..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
 ..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
 ..i $l(PAR4),PAR4'=seq q
 ..i '$$select^LVBEPVIS(epis,"Y") d  q
 ...m VISIT=PLIST k PLIST
 ...s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq_"\"_$p(VISIT(9),$c(1))_"\"_COMP(10)
 ...d ITEM 
 ; check credits
 s (credit,epis)="" f cnt=1:1 s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
 .s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 .s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ..s rowid=deb_"||"_date_"||"_ep
 ..i $$open^LVBDEBVT(rowid)
 ..f  q:$$fetch^LVBDEBVT(rowid)  d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k TRAN m TRAN=PLIST
 ...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) d
 ....s credit=credit-PLIST(14)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) d
 ....s credit=credit-PLIST(16)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) d
 ....s credit=credit-PLIST(9)
 ..i $$close^LVBDEBVT()
 .i credit s ^TMP($zn,$j,"EPIS",epis,"CREDIT")=credit,credit=""
 ; set events
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
 .s invoice=$p($p(^(epis),"\",2),"||")_"-"_$p($p(^(epis),"\",2),"||",2)_"-"_$p($p(^(epis),"\",2),"||",3)
 .i '$l(PAR5) d events^LVBCOM01(epis,"ACC","Original invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 .i 'PAR5 d events^LVBCOM01(epis,"ACC","Duplicate invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 .d events^LVBCOM01(epis,"ACC","Reminder "_PAR5_$s($l(USER):" printed by "_USER,1:""))
 q
PRINT ; print report
 d QDR,PATH
 s max=15,(line,nextpg)=""
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis)  d
 ..f j=1:1:PLIST i '((j=7)!(j=22)) s PLIST(j)=$p(PLIST(j),$c(1))
 ..m VISIT=PLIST k PLIST
 .k PAYC i '$$select^LVBCTPC(VISIT(9)) m PAYC=PLIST k PLIST
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101))
 .f  q:$e(name)'=" "  s name=$e(name,2,99)
 .s (line,nextpg,item)=""
 .s compabn=$p(^TMP($zn,$j,"EPIS",epis),"\",4)
 .s payable=$g(^TMP($zn,$j,"EPIS",epis,"TOT"))-$g(^TMP($zn,$j,"EPIS",epis,"CREDIT"))
 . ; extract collection centre licence number
 .s lcc="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1)),"Y") s lcc=$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))
 .d ADDRESS,PHEAD
 .d ADD^PRTREP00(REPID,"PRT","?2  "_$$extdate2^SSUTIL4(VISIT(12)))
 .d ADD^PRTREP00(REPID,"PRT","?15 "_epis_$s($g(DOC(38))="Y":" (sd)",1:""))
 .s item="" f  s item=$o(^TMP($zn,$j,"EPIS",epis,"ITEM",item)) q:item=""  d
 ..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2),Rule3=$p(det,"\",3)
 .. ; check item desc length
 ..s x="" s xdesc=desc k desc s desc=xdesc
 ..i $l(desc)<35 s desc(1)=desc
 ..i $l(desc)>34 d
 ...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>34 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>34
 ..d ADD^PRTREP00(REPID,"PRT","?30 "_item)
 ..d ADD^PRTREP00(REPID,"PRT","?38 "_desc(1)) i '$d(desc(2)),Rule3="Y",$l(desc(1))<19 d ADD^PRTREP00(REPID,"PRT","?58 Rule 3 Exemption")
 ..d ADD^PRTREP00(REPID,"PRT","?81 "_$j(amt,7,2)),LN s line=line+1
 ..i '$d(desc(2)),Rule3="Y",$l(desc(1))>19 d ADD^PRTREP00(REPID,"PRT","?38 Rule 3 Exemption"),LN s line=line+1
 ..i '$d(desc(2)) d NewPage
 ..f i=2:1 q:'$d(desc(i))  d
 ...d ADD^PRTREP00(REPID,"PRT","?38 "_desc(i)) i '$d(desc(i+1)),Rule3="Y",$l(desc(i))<19 d ADD^PRTREP00(REPID,"PRT","?58 Rule 3 Exemption")
 ...d LN s line=line+1 d NewPage
 ...i '$d(desc(i+1)),Rule3="Y",$l(desc(i))>19 d ADD^PRTREP00(REPID,"PRT","?38 Rule 3 Exemption"),LN s line=line+1
 .i VISIT(45)="Y" d
 ..d ADD^PRTREP00(REPID,"PRT","?38 All items are Rule 3 Exempt"),LN s line=line+1 d NewPage
 .f i=1:1:14-line d LN
 .i $l($p(VISIT(22),$c(1))) d
 ..i '$l(PAYC(10)) d LN
 ..d ADD^PRTREP00(REPID,"PRT","?3  ")
 ..d ADD^PRTREP00(REPID,"PRT","~BOLD"),ADD^PRTREP00(REPID,"PRT","~ITALIC")
 ..d ADD^PRTREP00(REPID,"PRT","* Services provided as an Admitted Patient")
 ..d ADD^PRTREP00(REPID,"PRT"," in "_$p(VISIT(22),$c(1),2))
 ..d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 ..d ADD^PRTREP00(REPID,"PRT",$c(13))
 .d LN i $l(PAYC(10)) d
 ..d ADD^PRTREP00(REPID,"PRT","?3  ")
 ..d ADD^PRTREP00(REPID,"PRT","~BOLD"),ADD^PRTREP00(REPID,"PRT","~ITALIC")
 ..d ADD^PRTREP00(REPID,"PRT",PAYC(10))
 ..d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 ..d ADD^PRTREP00(REPID,"PRT",$c(13))
 .s tot=$g(^TMP($zn,$j,"EPIS",epis,"TOT")),credit=$g(^TMP($zn,$j,"EPIS",epis,"CREDIT")),GST=$g(^TMP($zn,$j,"EPIS",epis,"GST"))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h3250V"),ADD^PRTREP00(REPID,"PRT",$c(27)_"%1BSP1;PW0.15;PU;PR7600,0;PD;PR-1200,0"),ADD^PRTREP00(REPID,"PRT",$c(27)_"%1A")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4148T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4150h3350VTotal Fee:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T"),ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?81 $"_$j(tot,7,2))
 .i amount,PAR5 d
 ..i PAR5=1 d
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b4148T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h3480V")
 ...d ADD^PRTREP00(REPID,"PRT","Our records show that this account is outstanding.")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h3590V")
 ...d ADD^PRTREP00(REPID,"PRT","If payment has been forwarded, please disregard this reminder.")
 ..i PAR5=2 d
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b4148T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h3480V")
 ...d ADD^PRTREP00(REPID,"PRT","This account is now overdue. Payment is required within 14 days. Please")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h3590V")
 ...d ADD^PRTREP00(REPID,"PRT","call us if you require assistance.")
 ..i PAR5=3 d
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b4148T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h3480V")
 ...d ADD^PRTREP00(REPID,"PRT","This account is now overdue. Payment is required immediately or further")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h3590V")
 ...d ADD^PRTREP00(REPID,"PRT","action may commence. Please call us if you have any queries.")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4148T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3950h3480VGST Included:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T"),ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?81 $"_$j(GST,7,2))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4148T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4160h3590VReceived:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T"),ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?81 $"_$j(credit,7,2))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4148T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4000h3700VBalance Due:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T"),ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?81 $"_$j(payable,7,2))
 ; force last page feed
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"E")
 q
PHEAD ; print patient details
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h0V")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p9v0s0b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h220VProvider: "_pathdr)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h220V"_compabn)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h330VProvider No: "_pathprv)
 i $l(lcc) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a100h440VSCP: "_lcc)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
 d ADD^PRTREP00(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h700V"_name),ADD^PRTREP00(REPID,"PRT","~NORMAL"),LN
 f i=1:1:4 i $l(ADDRESS(i)) d ADD^PRTREP00(REPID,"PRT","?15 "_ADDRESS(i)) d LN
 d DOC
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4600h380V"_$zd(+$h,2))
 s episdet=$g(^TMP($zn,$j,"EPIS",epis)),batch=$p(episdet,"\",2),payc=$p(episdet,"\",3)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4600h500V"_$p(batch,"||")_" "_$p(batch,"||",2)_" "_$p(batch,"||",3))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3950h700V"_drdet)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3950h820V"_drprov)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3950h940V"_$zd(VISIT(14),2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3950h1060V"_VISIT(8))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3950h1180V"_VISIT(23))
 f i=1:1:3 d LN
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det,GST)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 .i $p(det,"\",4)="GST" s GST=$p(det,"\")
 .s ^TMP($zn,$j,"EPIS",epis,"ITEM",item)=det
 s ^TMP($zn,$j,"EPIS",epis,"TOT")=amount
 s ^TMP($zn,$j,"EPIS",epis,"GST")=GST
 q
DOC ; extract referring doctor details
 s (drdet,drprov)=""
 i '$$select^LVBCTDR(VISIT(15),"Y") s drdet=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51)),drprov=PLIST(18)
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
NewPage ; check if new page is required
 i line>15,($o(^TMP($zn,$j,"EPIS",epis,"ITEM",item))'=""!($o(^TMP($zn,$j,"EPIS",epis))'="")) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4800h4950VCont. Over..")
 .s nextpg=1 d PHEAD s (line,nextpg)=""
 q
PATH ; extract pathology doctor details
 s (pathdr,pathprv)=""
 i '$$select^LVBINVB(PAR1_"||"_PAR2) s pathdr=$p(PLIST(10),$c(1),2),pathprv=PLIST(5)
 q
LN ; add line to report
 d ADD^PRTREP00(REPID,"PRT","!")
 q
QDR ; set header
 s QDR="RDWT-INV",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
 q
]]></Routine>
</Export>
