<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBAT126" type="INT" languagemode="0" generated="1"><![CDATA[
LVBAT126 ; IG 24/07/96 ; Receiving Accounts payments - BPAY - for APP
 ;
 ; supported calls :
 ;
 ; $$start^LVBAT126(type,p1,user)
 ;
 ; 
 ; type : BPAY
start(type,p1,user) n (type,user) s type=$g(type),user=$g(user) k ^TMP($zn,$j),^TMP("AT1",$j)
 d Log^LVBAT201(type,"Start Processing <"_type_">...")
 d Log^LVBAT201(type," ")
 k CFLAB i '$$select^MVBCFLAB(1) m CFLAB=PLIST
 i $$select^LVBCTPCT(type) q 100
 s dir=$tr(PLIST(6),$c(17),"\"),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.*"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""edi.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""edi.txt""")
 s $zt="Error1^"_$zn
 s FILE="edi.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  u xFILE r MESS d
 ..i $$field(MESS,4)'="BPAYData" q
 ..s seq=$o(^TMP($zn,$j,xxFILE,""),-1)+1,^TMP($zn,$j,xxFILE,seq)=MESS
 s $zt=""
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .s (amount,seq)="" f cnt=0:1 s seq=$o(^TMP($zn,$j,xFile,seq)) q:seq=""  s amount=amount+$$field(^(seq),9)
 .k PLIST
 .s PLIST(2)=type_$$new^LVBCRBT(type)
 .s PLIST(3)="U"
 .s PLIST(4)=amount
 .s PLIST(5)=cnt
 .s PLIST(6)=+$h
 .s PLIST=$o(PLIST(""),-1)
 .f j=1:1:PLIST s PLIST(j)=$g(PLIST(j))
 .s err=$$insert^LVBCRBT()
 .i err d Log^LVBAT201(type,"Error <"_err_"> Creating Credit Batch <"_CRBatch_">...") q
 .i 'err s CRBatch=PLIST(1) d
 ..d Log^LVBAT201(type,"Created Credit Batch <"_CRBatch_">...")
 ..s seq="" f line=1:1 s seq=$o(^TMP($zn,$j,xFile,seq)) q:seq=""  d
 ...s ediUN=$e($$field(^(seq),7),4,999)
 ...s amount=+$$field(^(seq),9)
 ...s ediReceipt=$$field(^(seq),7)
 ...s $p(^TMP($zn,$j,xFile,seq," "),"\",5)=ediReceipt
 ...s $p(^TMP($zn,$j,xFile,seq," "),"\",6)=ediUN
 ...i $$select^LVBCTBC($e(ediReceipt,1,3)) d  q
 ....d Log^LVBAT201(type,"Error <Bank code-"_$e(ediReceipt,1,3)_"> Creating Batch-Line <"_CRBatch_"-"_line_">...")
 ....s $p(^TMP($zn,$j,xFile,seq," "),"\",2)=$e(ediReceipt,1,3)
 ...k PLIST
 ...s PLIST(2)=line
 ...s PLIST(7)=amount
 ...s xx=$$invoice^LVBINVBP(ediUN)
 ...i '$l(xx) s $p(^TMP($zn,$j,xFile,seq," "),"\",3)=ediUN d  q
 ....d Log^LVBAT201(type,"Error <Invoice-"_ediUN_"> Creating Batch-Line <"_CRBatch_"-"_line_">...")
 ...s PLIST(10)=$p(xx,"||",1)_"-"_$p(xx,"||",2)_"-"_$p(xx,"||",3)
 ...s PLIST(17)=ediReceipt
 ...s PLIST(18)=ediUN
 ...s PLIST=$o(PLIST(""),-1)
 ...f j=1:1:PLIST s PLIST(j)=$g(PLIST(j))
 ...s err=$$insert^LVBCRBL(CRBatch)
 ...i err d Log^LVBAT201(type,"Error <"_err_"> Creating Credit Batch-Line <"_CRBatch_"-"_line_">...") q
 ...i 'err s CRBLine=PLIST(1) d
 ....s $p(^TMP($zn,$j,xFile,seq," "),"\",1)=CRBLine
 .... ; file payment
 ....k PLIST
 ....s PLIST(2)=1
 ....s PLIST(3)="EDI"
 ....s PLIST(4)=amount
 ....s PLIST(5)=$e(ediReceipt,1,3)
 ....s PLIST(7)=$e(ediReceipt,4,99)
 ....s PLIST=$o(PLIST(""),-1)
 ....f j=1:1:PLIST s PLIST(j)=$g(PLIST(j))
 ....s err=$$insert^LVBCRBLA(CRBLine)
 ....i err d Log^LVBAT201(type,"Error <"_err_"> Creating Credit Batch-Line payment <"_CRBatch_"-"_line_">...") q
 ....i 'err d
 ..... ; check line amount with patients amount
 .....f j=1,2 s @("x"_j)=$p(CRBLine,"||",j)
 .....s (am,x)="" f  s x=$o(^TBAT(x1,x2,1,x)) q:x=""  s am=am+^(x)
 .....i am>amount s dif=am-amount d
 ...... ; adjust patients amount with BPAY amount
 ......s x="" f  s x=$o(^TBAT(x1,x2,1,x)) q:x=""  d  q:'dif
 .......f j=10,18 s @("a"_j)=$p(^TEPI(x),"\",j)
 .......s xx="" i $l(a10),$l(a18) s xx=$p(^TDEB(a18,a10,0,x),"\",1)
 .......i xx<dif s $p(^TBAT(x1,x2,1,x),"\",1)=0,dif=dif-xx
 .......e  s $p(^TBAT(x1,x2,1,x),"\",1)=xx-dif,dif=""
 ..s ^TMP($zn,$j,xFile)=CRBatch
 ; print reports
 s (xFile,list)="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  i $l($g(^(xFile))) s list=list_$s($l(list):",",1:"")_^(xFile)
 i $l(list),$$START^PRTREP00(user,"",list,"UPB")
 m ^TMP("AT1",$j)=^TMP($zn,$j) i $$START^PRTREP00(user,"","","AT1-2")
 k ^TMP($zn,$j),^TMP("AT1",$j)
 d Log^LVBAT201(type," ")
 d Log^LVBAT201(type,"Finish Processing <"_type_">...")
 q 0
field(mess,field) n (mess,field) s mess=$g(mess),field=$g(field)
 i field s result=$tr($p(mess,",",field)," ")
 q result
 ; Error log
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
 ; Error log
Error2 i $ze'["ENDOFFILE" d BACK^%ETN q
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
]]></Routine>
</Export>
