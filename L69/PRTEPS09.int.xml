<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPS09" type="INT" languagemode="0"><![CDATA[
PRTEPS09 ; IG 19/08/99 ; Episode statistics - Quinn
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Select criteria :    DR - Doctor
 ;                                       DG - Doctor group
 ;                                       HW - Hospital/Ward
 ;                                       PC - Pay code
 ;                                       CC - Collection centre
 ;                                       CP - Collection personel
 ;                                       CO - Company
 ;                                       UL - User location
 ;                                       MC - Marketing code
 ;               4. Value
 ;               5. Sort key_1 : DR - Doctor
 ;                               DG - Doctor group
 ;                               HW - Hospital/Ward
 ;                               PC - Pay code
 ;                               DT - Date
 ;                               CC - Collection centre
 ;                               CP - Collection personel
 ;                               CO - Company
 ;                               UL - User location
 ;                               MC - Marketing code
 ;               6. Sort key_2
 ;               7. Include Accounting data
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Episode Billing Statistics"
 s line=2,xHEADER(line)=""
 s start=PAR1,finish=PAR2
 s line=3,xHEADER(line)="From "_$$extdate2^SSUTIL4(start)_" to "_$$extdate2^SSUTIL4(finish)
 d
 .i $p($$extdate2^SSUTIL4(PAR1),"/",2)=$p($$extdate2^SSUTIL4(PAR2),"/",2)
 .i  i $p($$extdate2^SSUTIL4(PAR1-1),"/",2)'=$p($$extdate2^SSUTIL4(PAR1),"/",2)
 .i  i $p($$extdate2^SSUTIL4(PAR2+1),"/",2)'=$p($$extdate2^SSUTIL4(PAR2),"/",2)
 .e  s start=(PAR1-1)-(PAR2-PAR1),finish=PAR1-1 q
 .s finish=PAR1-1,start=finish f  q:$p($$extdate2^SSUTIL4(start-1),"/",2)-$p($$extdate2^SSUTIL4(finish),"/",2)  s start=start-1
 s line=4,xHEADER(line)="Previous data : From "_$$extdate2^SSUTIL4(start)_" to "_$$extdate2^SSUTIL4(finish)
 s line=5,xHEADER(line)=""
 i $l(PAR3) d
 .s line=6,xHEADER(line)="Selection criteria : "_$$name(PAR3)_" = "_PAR4
 .s line=7,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,,,,,PAR5_"^"_PAR6),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xxx f j=1,2 s xxx=$p(PAR10,",",j),par=$s(j=1:PAR5,1:PAR6) d  s $p(PAR10,",",j)=xxx
 .s $p(xxx,$c(2),3)=$$name(par)
 .i par="DR" s $p(xxx,$c(2),4)=35
 .i j=2,'$l(PAR6) s $p(xxx,$c(2),4)=0
 i PAR7'="Y" s PAR10=$p(PAR10,",",1,3)
 k xxx,xSORT d H^PRTREP00(PAR10,$zn)
 f j="DR","HW","PC","DT","CC","CP","CO","UL","MC" s xSORT(j)=$$name(j)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
name(par) n (par) s name=""
 i par="DR" s name="Doctor"
 i par="DG" s name="Doctor group"
 i par="HW" s name="Hospital/Ward"
 i par="PC" s name="Pay code"
 i par="DT" s name="Collection Date"
 i par="CC" s name="Collection centre"
 i par="CP" s name="Collection personel"
 i par="CO" s name="Company"
 i par="UL" s name="User location"
 i par="MC" s name="Marketing code"
 q name
BODY ; create temporary printing file
 d
 . ; current period
 .s period=0,start=PAR1,finish=PAR2 d PatientData
 . ; previous period
 .s period=20
 .i $p($$extdate2^SSUTIL4(PAR1),"/",2)=$p($$extdate2^SSUTIL4(PAR2),"/",2)
 .i  i $p($$extdate2^SSUTIL4(PAR1-1),"/",2)'=$p($$extdate2^SSUTIL4(PAR1),"/",2)
 .i  i $p($$extdate2^SSUTIL4(PAR2+1),"/",2)'=$p($$extdate2^SSUTIL4(PAR2),"/",2)
 .e  s start=(PAR1-1)-(PAR2-PAR1),finish=PAR1-1 d PatientData q
 .s finish=PAR1-1,start=finish f  q:$p($$extdate2^SSUTIL4(start-1),"/",2)-$p($$extdate2^SSUTIL4(finish),"/",2)  s start=start-1
 .d PatientData
 ; include accounting data
 i PAR7="Y" d
 . ; current period
 .s period=0,start=PAR1,finish=PAR2 d AccountingData
 . ; previous period
 .s period=20
 .i $p($$extdate2^SSUTIL4(PAR1),"/",2)=$p($$extdate2^SSUTIL4(PAR2),"/",2)
 .i  i $p($$extdate2^SSUTIL4(PAR1-1),"/",2)'=$p($$extdate2^SSUTIL4(PAR1),"/",2)
 .i  i $p($$extdate2^SSUTIL4(PAR2+1),"/",2)'=$p($$extdate2^SSUTIL4(PAR2),"/",2)
 .e  s start=(PAR1-1)-(PAR2-PAR1),finish=PAR1-1 d AccountingData q
 .s finish=PAR1-1,start=finish f  q:$p($$extdate2^SSUTIL4(start-1),"/",2)-$p($$extdate2^SSUTIL4(finish),"/",2)  s start=start-1
 .d AccountingData
 i PAR5="DG",PAR6="DR" d
 .k ^TMP($zn,$j,"xREPORT"," ")
 .s dg="" f  s dg=$o(^TTAB("DG",dg)) q:dg=""  d
 ..s dr="" f  s dr=$o(^TTAB("DG",dg,dr)) q:dr=""  d
 ...s ^TMP($zn,$j,"xREPORT",dg)=$g(^TMP($zn,$j,"xREPORT",dg))
 ...s ^TMP($zn,$j,"xREPORT",dg,dr)=$g(^TMP($zn,$j,"xREPORT",dg,dr))
 i PAR5="DR",PAR3="DG",$l(PAR4) d
 .k ^TMP($zn,$j,"xREPORT"," ")
 .s dr="" f  s dr=$o(^TTAB("DG",PAR4,dr)) q:dr=""  d
 ..s ^TMP($zn,$j,"xREPORT",PAR4)=$g(^TMP($zn,$j,"xREPORT",PAR4))
 ..s ^TMP($zn,$j,"xREPORT",PAR4,dr)=$g(^TMP($zn,$j,"xREPORT",PAR4,dr))
 q
Episode(epis) k VISIT,SORT1,SORT2
 i '$$select^LVBEPVIS(epis,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 . ; check selection type
 .s ok=1 d  i 'ok q
 ..i PAR3="DR",(","_PAR4_",")'[(","_VISIT(15)_",") s ok=0 q
 ..i PAR3="DG",$$select^LVBCTDGD(PAR4_"||"_VISIT(15)) s ok=0 q
 ..i PAR3="HW",(","_PAR4_",")'[(","_VISIT(22)_",") s ok=0 q
 ..i PAR3="PC",(","_PAR4_",")'[(","_VISIT(9)_",") s ok=0 q
 ..i PAR3="CC",(","_PAR4_",")'[(","_VISIT(27)_",") s ok=0 q
 ..i PAR3="CP",(","_PAR4_",")'[(","_VISIT(28)_",") s ok=0 q
 ..i PAR3="CO",(","_PAR4_",")'[(","_$$epcomp^LVBVIS1(epis)_",") s ok=0 q
 ..i PAR3="UL",(","_PAR4_",")'[(","_VISIT(66)_",") s ok=0 q
 ..i PAR3="MC" d  i (","_PAR4_",")'[(","_$p(result,$c(1))_",") s ok=0 q
 ...s result="" s result=$$seldata^LVBCTDR(VISIT(15),28)
 . ; set sorting key - 1
 .s par=PAR5 d
 ..i par="MC" d  q
 ...s x=$$seldata^LVBCTDR(VISIT(15),28)
 ...s x=$p(x,$c(1)) i $l(x) s SORT1(x)=""
 ..i par="DG" d  q
 ...s x="" i $l(VISIT(15)) f  s x=$o(^TTAB("DG",x)) q:x=""  i $d(^TTAB("DG",x,$p(VISIT(15),$c(1)))) s SORT1(x)=""
 ..s x=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
 ..i $l(x) s SORT1(x)=""
 . ; set sorting key - 2
 .s par=PAR6 d
 ..i par="MC" d  q
 ...s x=$$seldata^LVBCTDR(VISIT(15),28)
 ...s x=$p(x,$c(1)) i $l(x) s SORT2(x)=""
 ..i par="DG" d  q
 ...s x="" i $l(VISIT(15)) f  s x=$o(^TTAB("DG",x)) q:x=""  i $d(^TTAB("DG",x,$p(VISIT(15),$c(1)))) s SORT1(x)=""
 ..s x=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
 ..i $l(x) s SORT2(x)=""
 .i '$d(SORT1) s SORT1(" ")=""
 .i '$d(SORT2) s SORT2(" ")=""
 q $d(VISIT)
PatientData 
 s date=start-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>finish  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..d Episode(epis)
 ..s SORT1="" f  s SORT1=$o(SORT1(SORT1)) q:SORT1=""  d
 ...s SORT2="" f  s SORT2=$o(SORT2(SORT2)) q:SORT2=""  d
 ....d SetTotal(3+period,1,SORT1,SORT2)
 .... ; check all No-Priced reasons
 ....d
 ..... ; No charge TestSets
 .....s (ok,ts)="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d  i ok q
 ......s x=$$seldata^LVBCTTS(ts,15)
 ......i x'="N" s ok=1
 .....i 'ok d SetTotal(10+period,1,SORT1,SORT2) q
 ..... ; Visit Action suppress billing
 .....i '$$select^LVBCTVA(VISIT(25),"Y"),PLIST(4)="Y" d SetTotal(8+period,1,SORT1,SORT2) q
 ..... ; Debtor Action suppress billing
 .....i '$$select^LVBDEBVI(VISIT(20)_"||"_VISIT(12)_"||"_epis),'$$select^LVBCTDA(PLIST(8),"Y"),PLIST(4)="Y" d SetTotal(9+period,1,SORT1,SORT2) q
 q
AccountingData 
 ; get invoices
 s pc="" f  s pc=$o(^TMCB(pc)) q:pc=""  d
 .s inv="" f  s inv=$o(^TMCB(pc,inv)) q:inv=""  d
 ..s date=$p(^(inv),"\") i (date<start)!(date>finish) q
 ..s epis="" f  s epis=$o(^TMCB(pc,inv,epis)) q:epis=""  d
 ...d Episode(epis)
 ...s SORT1="" f  s SORT1=$o(SORT1(SORT1)) q:SORT1=""  d
 ....s SORT2="" f  s SORT2=$o(SORT2(SORT2)) q:SORT2=""  d
 .....s sum=$p($g(^TMCB(pc,inv,epis)),"\",1)
 ..... ; check for coned episodes
 .....s incl="" i $l(VISIT(55)) s incl=$l(VISIT(55),",")
 .....d SetTotal(4+period,incl+1,SORT1,SORT2)
 .....d SetTotal(5+period,sum,SORT1,SORT2)
 .....d SetTotal(12+period,sum,SORT1,SORT2)
 .....i incl d SetTotal(6+period,incl,SORT1,SORT2)
 ; get rebills
 s date=start-1 f  s date=$o(^TRBL(date)) q:date=""  q:date>finish  d
 .s epis="" f  s epis=$o(^TRBL(date,epis)) q:epis=""  d
 ..s pc=$p(^(epis),"\",1),inv=$p(^(epis),"\",5)
 ..i $l(pc),$l(inv) d
 ...d Episode(epis)
 ...s SORT1="" f  s SORT1=$o(SORT1(SORT1)) q:SORT1=""  d
 ....s SORT2="" f  s SORT2=$o(SORT2(SORT2)) q:SORT2=""  d
 .....s sum=$p($g(^TMCB(pc,inv,epis)),"\",1)
 ..... ; check for coned episodes
 .....s incl="" i $l(VISIT(55)) s incl=$l(VISIT(55),",")
 .....d SetTotal(4+period,-(incl+1),SORT1,SORT2)
 .....d SetTotal(11+period,sum,SORT1,SORT2)
 .....d SetTotal(12+period,-sum,SORT1,SORT2)
 .....i incl d SetTotal(6+period,-incl,SORT1,SORT2)
 q
SORT(par,value) n (par,value) s result="" i value'=" " d
 .i par="DR",'$$select^LVBCTDR(value,"Y") s result=PLIST(3)_"\"_$e(PLIST(18)_$j("",10),1,10)_$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3))
 .i par="DG",'$$select^LVBCTDG(value,"Y") s result=PLIST(3)
 .i par="HW",'$$select^LVBCTHOS(value,"Y") s result=PLIST(3)
 .i par="PC",'$$select^LVBCTPC(value,"Y") s result=PLIST(3)
 .i par="DT" s result=$$extdate2^SSUTIL4(value)
 .i par="CC",'$$select^LVBCTCC(value,"Y") s result=PLIST(3)
 .i par="CP",'$$select^LVBCTPER(value,"Y") s result=PLIST(3)
 .i par="CO",'$$select^LVBCTCO(value,"Y") s result=PLIST(3)
 .i par="UL",'$$select^LVBCTUSL(value,"Y") s result=PLIST(3)
 .i par="MC",'$$select^LVBCTMC(value,"Y") s result=PLIST(3)
 q result
 ; set totals
SetTotal(piece,tot,SORT1,SORT2) n (piece,tot,SORT1,SORT2)
 s $p(^TMP($zn,$j,"xREPORT"),"\",piece)=$p($g(^TMP($zn,$j,"xREPORT")),"\",piece)+tot
 s $p(^TMP($zn,$j,"xREPORT",SORT1),"\",piece)=$p($g(^TMP($zn,$j,"xREPORT",SORT1)),"\",piece)+tot
 s $p(^TMP($zn,$j,"xREPORT",SORT1,SORT2),"\",piece)=$p($g(^TMP($zn,$j,"xREPORT",SORT1,SORT2)),"\",piece)+tot
 q
PRINT ; print report
 ; sort in Alphabetical order
 i $d(^TMP($zn,$j,"xREPORT")) d
 .s SORT1="" f  s SORT1=$o(^TMP($zn,$j,"xREPORT",SORT1)) q:SORT1=""  d
 ..s x1=SORT1 i PAR5'="DT" s x1=$$SORT(PAR5,SORT1) i '$l(x1) s x1=" "
 ..s SORT2="" f  s SORT2=$o(^TMP($zn,$j,"xREPORT",SORT1,SORT2)) q:SORT2=""  d
 ...s x2=SORT2 i PAR6'="DT" s x2=$$SORT(PAR6,SORT2) i '$l(x2) s x2=" "
 ...f j=1:1:$l(^TMP($zn,$j,"xREPORT",SORT1,SORT2),"\") s $p(^TMP($zn,$j,"REPORT",x1,x2),"\",j)=$p($g(^TMP($zn,$j,"REPORT",x1,x2)),"\",j)+$p(^TMP($zn,$j,"xREPORT",SORT1,SORT2),"\",j)
 ..f j=1:1:$l(^TMP($zn,$j,"xREPORT",SORT1),"\") s $p(^TMP($zn,$j,"REPORT",x1),"\",j)=$p($g(^TMP($zn,$j,"REPORT",x1)),"\",j)+$p(^TMP($zn,$j,"xREPORT",SORT1),"\",j)
 .f j=1:1:$l(^TMP($zn,$j,"xREPORT"),"\") s $p(^TMP($zn,$j,"REPORT"),"\",j)=$p($g(^TMP($zn,$j,"REPORT")),"\",j)+$p(^TMP($zn,$j,"xREPORT"),"\",j)
 i $d(^TMP($zn,$j,"REPORT")) d
 .s SORT1="" f  s SORT1=$o(^TMP($zn,$j,"REPORT",SORT1)) q:SORT1=""  d
 ..s xxx(1)=SORT1 i PAR5="DT" s xxx(1)=$$SORT(PAR5,SORT1)
 ..i xxx(1)["\" s xxx(1)=$p(xxx(1),"\",2)
 ..s SORT2="" f  s SORT2=$o(^TMP($zn,$j,"REPORT",SORT1,SORT2)) q:SORT2=""  d
 ...s xxx(2)=SORT2 i PAR6="DT" s xxx(2)=$$SORT(PAR6,SORT2)
 ...i xxx(2)["\" s xxx(2)=$p(xxx(2),"\",2)
 ...f j=3:1:32 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT",SORT1,SORT2)),"\",j) i 'xxx(j) s xxx(j)=""
 ...f j=4,24 i xxx(j) s xxx(j+3)=$j(xxx(j+8)/xxx(j),0,2)
 ...d PRINT^PRTREP00(,.N) k xxx
 ..i $l(PAR6) d
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 ...s xxx(1)="Total for "_$s(PAR5="DT":$$SORT(PAR5,SORT1),1:SORT1)
 ...f j=3:1:32 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT",SORT1)),"\",j) i 'xxx(j) s xxx(j)=""
 ...f j=4,24 i xxx(j) s xxx(j+3)=$j(xxx(j+8)/xxx(j),0,2)
 ...d PRINT^PRTREP00(,.N) k xxx
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s xxx(1)="Grand Total"
 .f j=3:1:32 s xxx(j)=$p($g(^TMP($zn,$j,"REPORT")),"\",j) i 'xxx(j) s xxx(j)=""
 .f j=4,24 i xxx(j) s xxx(j+3)=$j(xxx(j+8)/xxx(j),0,2)
 .d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="EPS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
