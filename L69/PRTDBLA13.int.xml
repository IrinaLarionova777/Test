<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDBLA13" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDBLA13 ; IG 02/09/98 ; Day Book Labels - Launceston
 Q
 ; Parameters :  1.Laboratory
 ;               2.Site
 ;               3.Date from
 ;               4.Date to
 ;               5.List episodes/accesion numbers
 ;               6.Order RowID
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($ZN,$J)
 D INIT,BODY,PRINT
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=$g(TYPE),PLIST(9)="M" I $$update^LVBPRPR(REPID)
 K ^TMP($ZN,$J),^TMP("PRTREP00",$J)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
 ; check if visit/accession should print labels
CHECK(XX) n (XX,PAR3,PAR4) s XX=$g(XX)
 ; check if Histo or Cyto or Micro only
 i '(($e($p(XX,"||",2))="C")!($e($p(XX,"||",2))="H")!($e($p(XX,"||",2))="M")) q
 ; check all blocks
 i $$open^LVBHIVAB(XX)
 f  q:$$fetch^LVBHIVAB(XX)  d
 .f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 .k BLOCK m BLOCK=PLIST
 . ; check all records
 .i $$open^LVBHIVAO(BLOCK(1))
 .f  q:$$fetch^LVBHIVAO(BLOCK(1))  d
 ..f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ..i $l(PAR3),PLIST(8)<PAR3 q
 ..i $l(PAR4),PLIST(8)>PAR4 q
 ..i PLIST(7)'="Y" s ^TMP($zn,$j,"LIST",PLIST(1))=""
 .i $$close^LVBHIVAO()
 i $$close^LVBHIVAB()
 Q
 ; create list of labels to print
BODY k ^TMP($zn,$j)
 ; create list of labels for a laboratory
 i '$l(PAR5) d
 .s x1="" f  s x1=$o(^THIRi("PRINT-LABEL",x1)) q:x1=""  d
 ..i $$select^LVBEPVIS(x1,"Y") q
 ..f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ..k VISIT m VISIT=PLIST
 ..s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 ... ; check laboratory
 ...i $l(PAR1),$e(x2,1)'=PAR1 q
 ... ; check site
 ...i $l(PAR2),$e(x2,2)'=PAR2 q
 ...D CHECK(x1_"||"_x2)
 ; create list of labels for an episodes
 i $l(PAR5) d
 .i '$l(PAR6) d
 ..f j=1:1:$l(PAR5,"|") s x1=$p($p(PAR5,"|",j),"~",1),x2=$p($p(PAR5,"|",j),"~",2) i $l(x1),$l(x2) d
 ...i x2'="*",$d(^THIRi("PRINT-LABEL",x1,x2)) D CHECK(x1_"||"_x2)
 ...i x2="*" s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 .... ; check laboratory
 ....i $l(PAR1),$e(x2,1)'=PAR1 q
 .... ; check site
 ....i $l(PAR2),$e(x2,2)'=PAR2 q
 ....D CHECK(x1_"||"_x2)
 .i $l(PAR6) f j=1:1:$l(PAR6,",") i $l($p(PAR6,",",j)) s ^TMP($zn,$j,"LIST",$p(PAR6,",",j))=""
 ; Sort
 s xROWID="" f  s xROWID=$o(^TMP($zn,$j,"LIST",xROWID)) q:xROWID=""  d
 .i $e($p(xROWID,"||",2))="M" s SORT=xROWID
 .e  s SORT=$p(xROWID,"||",2)
 .s ^TMP($zn,$j,"REPORT",SORT,xROWID)=""
 Q
 ; print all labels
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s SORT="" f  s SORT=$o(^TMP($zn,$j,"REPORT",SORT)) q:SORT=""  d
 ..s xROWID="" f  s xROWID=$o(^TMP($zn,$j,"REPORT",SORT,xROWID)) q:xROWID=""  d
 ...s TYPE=""
 ... ; print Cytology
 ...i $e($p(xROWID,"||",2))="C" s TYPE="C-"_PAR2
 ... ; print Histology
 ...i $e($p(xROWID,"||",2))="H" s TYPE="H-"_PAR2
 ...i " C H "[(" "_$e(TYPE)_" ") d
 .... ; order information
 ....i $$select^LVBHIVAO(xROWID) q
 ....f j=1:1:4,6:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k ORDER m ORDER=PLIST
 .... ; update record that it had been printed already
 ....f j=1:1:9 i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....s PLIST(7)="Y" i $$update^LVBHIVAO(xROWID)
 .... ; visit information
 ....i $$select^LVBEPVIS($p(xROWID,"||"),"Y") q
 ....f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k VISIT m VISIT=PLIST
 .... ; accession number information
 ....i $$select^LVBHIVAC($p(xROWID,"||",1,2)) q
 ....f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k ACCESS m ACCESS=PLIST
 .... ; start build lines
 ....s LINE(1)=$p(xROWID,"||",1)
 ....s intials="" f j=1:1:$l(VISIT(4)," ") s intials=intials_$s($l(intials):" ",1:"")_$e($p(VISIT(4)," ",j))
 ....s LINE(2)=VISIT(2)_" - B"_$p($p(ORDER(1),"||",3),$c(1))_" "_"L"_ORDER(10)
 ....s LINE(3)=VISIT(3)_" "_intials
 ....s CutUpDate=$$extdate2^SSUTIL4(ORDER(8)),CutUpDate=$p(CutUpDate,"/",1,2)
 ....s LINE(4)=ACCESS(3)_" "_$p(ORDER(5),$c(1),1)_" "_CutUpDate
 ....s LINE(5)=$p(xROWID,"||",2)
 ... ; print created lines
 ...i " C H "[(" "_$e(TYPE)_" ") d
 ....d ADD^SSPRINT(REPID,"PRT","^XA")
 ....d ADD^SSPRINT(REPID,"PRT","^LH10,30")
 ....d ADD^SSPRINT(REPID,"PRT","^FO5,15^ADN,12^FDLGH^FS")
 ....d ADD^SSPRINT(REPID,"PRT","^FO45,2^BY1,2,40^B3N,N,40,N,N^FD"_LINE(1)_"^FS")
 ....d ADD^SSPRINT(REPID,"PRT","^FO5,45^ADN,12^FD"_LINE(2)_"^FS")
 ....d ADD^SSPRINT(REPID,"PRT","^FO5,65^ADN,12^FD"_LINE(3)_"^FS")
 ....d ADD^SSPRINT(REPID,"PRT","^FO5,85^ADN,12^FD"_LINE(4)_"^FS")
 ....d ADD^SSPRINT(REPID,"PRT","^FO5,105^ADN,30,12^FD"_LINE(5)_"^FS")
 ....d ADD^SSPRINT(REPID,"PRT","^XZ")
 q
]]></Routine>
</Export>
