<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTREM01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTREM01 ; IG 07/10/98 ; Reminders and Summons list
 Q
 ; Parameters :  1.Parameter
 ;
check() s ok=100 l +^TMP("REPORT",$zn):1
 i  l -^TMP("REPORT",$zn) s ok=0
 q ok
create f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 i $$select^MVBCFLAB(1) q
 f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 k CFLAB m CFLAB=PLIST
 f jj=1:1:$l(CFLAB(57),",") i $p(PAR1,",",jj)="Y" d
 .i $$open^LVBCTPC(,"Y")
 .f  q:$$fetch^LVBCTPC(,"Y")  d
 ..f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ..i $l($p(PLIST(29),",",jj)) d
 ...f next=1:1:$l(CFLAB(57),",")-jj i $l($p(PLIST(29),",",jj+next)) q
 ...s last="" f jx=jj-1:-1:1 i $l($p(PLIST(29),",",jx)) s last=$p(PLIST(29),",",jx) q
 ...s xx1=$e($p(PLIST(29),",",jj)),xx2=+$e($p(PLIST(29),",",jj),2)
 ...s ^TMP($zn,$j,0,xx1,xx2,PLIST(2))=PLIST(28)_"^"_$p(CFLAB(57),",",jj)_"^"_$p(CFLAB(57),",",jj+next)_"^"_last
 .i $$close^LVBCTPC()
 ; create TAGE master
 s epis="" f  s epis=$o(^TAGE(epis)) q:epis=""  s xx=^(epis) d
 .s master=$$seldata^LVBEPVIS(epis,87) i '$l(master) s master=epis
 .i '$d(^TMP($zn,$j,"MASTER",master)) d
 ..s invb=$$seldata^LVBEPVIS(epis,57),date=$$seldata^LVBINVB(invb,4)
 ..s $p(^TMP($zn,$j,"MASTER",master),"\",2)=$p(date,$c(1),1)
 .s $p(^TMP($zn,$j,"MASTER",master),"\",1)=$p(^TMP($zn,$j,"MASTER",master),"\",1)+$p(xx,"\",1)
 ; sort TAGE
 s epis="" f  s epis=$o(^TMP($zn,$j,"MASTER",epis)) q:epis=""  d
 .s amt=$p(^(epis),"\",1),date=$p(^(epis),"\",2) i amt>0,$l(date) d
 ..s batch=$$seldata^LVBEPVIS(epis,57),reminder=$$seldata^LVBEPVIS(epis,59),dcol=$$seldata^LVBEPVIS(epis,12),deb=$$seldata^LVBEPVIS(epis,20)
 ..s rowid=deb_"||"_dcol_"||"_epis,(start,extra)=""
 ..s extra=$$seldata^LVBDEBVI(rowid,6),start=$$seldata^LVBDEBVI(rowid,11)
 ..i start+extra'>$h s pc=$p(batch,"||",1),batch=$p(batch,"||",2) i $l(pc),$l(batch) s ^TMP($zn,$j,9,pc,date+extra,epis)=batch_"\"_reminder
 ; go through reminders
 s itype="" f  s itype=$o(^TMP($zn,$j,0,"R",itype)) q:itype=""  d
 .s pc="" f  s pc=$o(^TMP($zn,$j,0,"R",itype,pc)) q:pc=""  d
 ..s pctype=$p(^(pc),"^",1),min=$p(^(pc),"^",2),max=$p(^(pc),"^",3),last=$p(^(pc),"^",4)
 ..s date=$h-min,stop="" f  s date=$o(^TMP($zn,$j,9,pc,date),-1) q:date=""  d  q:stop
 ... ;  i $l(max),date<($h-max) s stop=1 q
 ...s epis="" f  s epis=$o(^TMP($zn,$j,9,pc,date,epis)) q:epis=""  d
 ....s batch=$p(^(epis),"\",1),reminder=$p(^(epis),"\",2)
 .... ; Summons list already printed
 ....i $p(reminder,"-",1)="S" q
 .... ; reminder already printed
 ....i $tr(reminder,"-")'=last q
 .... ; group billing
 ....i $l(batch),pctype="G" s ^TMP($zn,$j,"RG",pc,batch)=itype,^TMP($zn,$j,"RG",pc,batch,epis)=itype q
 .... ; invoices
 ....i $l(batch),pctype="I" d  q
 .....s rowid=pc_"||"_batch_"||"_epis,seq=""
 .....s seq=$$seldata^LVBINVBP(rowid,4)
 .....i $l(seq) s ^TMP($zn,$j,"RI",pc,batch,$p(seq,$c(1)))=itype_"\"_epis
 ; go through Summons listing
 s itype=0 i $d(^TMP($zn,$j,0,"S",itype)) d
 .s pc="" f  s pc=$o(^TMP($zn,$j,0,"S",itype,pc)) q:pc=""  d
 ..s pctype=$p(^(pc),"^",1),min=$p(^(pc),"^",2),max=$p(^(pc),"^",3),last=$p(^(pc),"^",4)
 ..s date=$h-min,stop="" f  s date=$o(^TMP($zn,$j,9,pc,date),-1) q:date=""  d  q:stop
 ... ; i $l(max),date<($h-max) s stop=1 q
 ...s epis="" f  s epis=$o(^TMP($zn,$j,9,pc,date,epis)) q:epis=""  d
 ....s batch=$p(^(epis),"\",1),reminder=$p(^(epis),"\",2)
 .... ; Summons list already printed
 ....i $p(reminder,"-",1)="S" q
 .... ; last reminder has not been printed yet
 ....i $tr(reminder,"-")'=last q
 .... ; group billing
 ....i $l(batch),pctype="G" s ^TMP($zn,$j,"SG",pc,batch)=itype,^TMP($zn,$j,"SG",pc,batch,epis)=itype_"\"_epis q
 .... ; invoices
 ....i $l(batch),pctype="I" d  q
 .....s rowid=pc_"||"_batch_"||"_epis,seq=""
 .....s seq=$$seldata^LVBINVBP(rowid,4)
 .....i $l(seq) s ^TMP($zn,$j,"SI",pc,batch,$p(seq,$c(1)))=itype_"\"_epis
 q
CREATE(REPID,PAR,USER,%msql) ; Create report
 l +^TMP("REPORT",$zn)
 k ^TMP($zn,$j),^TMP("SUMMONS-LIST",$j) d create
 ; print reminders for invoices
 i $d(^TMP($zn,$j,"RI")) d
 .s pc="" f  s pc=$o(^TMP($zn,$j,"RI",pc)) q:pc=""  d
 ..k PAYCODE i '$$select^LVBCTPC(pc,"Y") m PAYCODE=PLIST
 ..s batch="" f  s batch=$o(^TMP($zn,$j,"RI",pc,batch)) q:batch=""  d
 ...s seq="" f  s seq=$o(^TMP($zn,$j,"RI",pc,batch,seq)) q:seq=""  d
 ....s check=1 d  i 'check k ^TMP($zn,$j,"RI",pc,batch,seq) q
 .....s ep="" f  s ep=$o(^TMCB(pc,batch,ep)) q:ep=""  i $p(^(ep),"\",2)=seq d  i 'check q
 ......s dcol=$$seldata^LVBEPVIS(ep,12),deb=$$seldata^LVBEPVIS(ep,20)
 ......s rowid=deb_"||"_dcol_"||"_ep,(start,extra)=""
 ......s extra=$$seldata^LVBDEBVI(rowid,6),start=$$seldata^LVBDEBVI(rowid,11)
 ......i start+extra>$h s check=0
 ....s itype=$p(^TMP($zn,$j,"RI",pc,batch,seq),"\",1)
 ....s PARAM=pc_"\"_batch_"\\"_seq_"\"_itype i $$START^PRTREP00(USER,"",PARAM,"INV")
 ....s epis="" f  s epis=$o(^TMCB(pc,batch,epis)) q:epis=""  i seq=$p(^(epis),"\",2) d
 .....i $$LOCK^SSLOCK("Patient",epis,,,,1)
 .....i '$$select^LVBEPVIS(epis) s PLIST(59)="R-"_itype i $$update^LVBEPVIS(epis)
 .....i $$LOCKCLR^SSLOCK("Patient",epis)
 ..... ; set Account fee
 .....i PAYCODE(33)=("R"_itype) d AccountFee(epis,deb)
 . ; Print Reminder Summary
 .k ^TMP("REMINDER",$j) m ^TMP("REMINDER",$j)=^TMP($zn,$j,"RI")
 .i $$START^PRTREP00(USER,"","1","CBR")
 .k ^TMP("REMINDER",$j)
 ; print reminders for group billing
 i $d(^TMP($zn,$j,"RG")) d
 .s pc="" f  s pc=$o(^TMP($zn,$j,"RG",pc)) q:pc=""  d
 ..s batch="" f  s batch=$o(^TMP($zn,$j,"RG",pc,batch)) q:batch=""  d
 ...s itype=^(batch)
 ...s PARAM=pc_"\"_batch_"\"_itype i $$START^PRTREP00(USER,"",PARAM,"GRB")
 ...s epis="" f  s epis=$o(^TMP($zn,$j,"RG",pc,batch,epis)) q:epis=""  d
 ....i $$LOCK^SSLOCK("Patient",epis,,,,1)
 ....i '$$select^LVBEPVIS(epis) s PLIST(59)="R-"_itype i $$update^LVBEPVIS(epis)
 ....i $$LOCKCLR^SSLOCK("Patient",epis)
 . ; Print Reminder Summary
 .k ^TMP("REMINDER",$j) m ^TMP("REMINDER",$j)=^TMP($zn,$j,"RG")
 .i $$START^PRTREP00(USER,"","2","CBR")
 .k ^TMP("REMINDER",$j)
 ; print Summons list for invoices/group billing
 i $d(^TMP($zn,$j,"SI"))!$d(^TMP($zn,$j,"SG")) d
 .f TYPE="SI","SG" s pc="" f  s pc=$o(^TMP($zn,$j,TYPE,pc)) q:pc=""  d
 ..k PAYCODE i '$$select^LVBCTPC(pc,"Y") m PAYCODE=PLIST
 ..s batch="" f  s batch=$o(^TMP($zn,$j,TYPE,pc,batch)) q:batch=""  d
 ...s seq="" f  s seq=$o(^TMP($zn,$j,TYPE,pc,batch,seq)) q:seq=""  d
 ....s found="",check=1 d  i 'check k ^TMP($zn,$j,TYPE,pc,batch,seq) q
 .....s ep="" f  s ep=$o(^TMCB(pc,batch,ep)) q:ep=""  d  i 'check q
 ......i TYPE="SG",$$invoice^LVBEPVIS(ep)'=(pc_"||"_batch) q
 ......i TYPE="SI" d  q
 .......i $p(^(ep),"\",2)'=seq q
 .......i $$invoice^LVBEPVIS(ep)'=(pc_"||"_batch_"||"_seq) q
 .......s dcol=$$seldata^LVBEPVIS(ep,12),deb=$$seldata^LVBEPVIS(ep,20)
 .......s rowid=$p(deb,$c(1))_"||"_$p(dcol,$c(1))_"||"_ep,(start,extra)=""
 .......s extra=$$seldata^LVBDEBVI(rowid,6),start=$$seldata^LVBDEBVI(rowid,11)
 .......i start+extra>$h s check=0
 ....s itype=$p(^TMP($zn,$j,TYPE,pc,batch,seq),"\",1)
 ....i TYPE="SI",CFLAB(117) s stop=0 d  i stop q
 .....s (sum,epis)="" f  s epis=$o(^TMCB(pc,batch,epis)) q:epis=""  i seq=$p(^(epis),"\",2) s sum=sum+$g(^TAGE(epis))
 .....i sum'>CFLAB(117) s stop=1 d
 ......s epis="" f  s epis=$o(^TMCB(pc,batch,epis)) q:epis=""  i seq=$p(^(epis),"\",2),$g(^TAGE(epis)) d
 .......k DEB i '$$select^LVBDEB(deb) m DEB=PLIST
 .......f j=2:1:20 s PLIST(j)=""
 .......s PLIST(2)=$$epcomp^LVBVIS1(epis)
 .......s PLIST(3)=CFLAB(119)
 .......s PLIST(4)=+$h
 .......s PLIST(5)=epis
 .......s PLIST(7)=DEB(3)
 .......s (PLIST(9),PLIST(15))=+$g(^TAGE(epis))
 .......s PLIST(11)=CFLAB(118)
 .......s PLIST(16)=DEB(2)
 .......s PLIST=$o(PLIST(""),-1)
 .......i $$insert^LVBJOU()
 ....s epis="" f  s epis=$o(^TMCB(pc,batch,epis)) q:epis=""  d
 .....i TYPE="SI",seq'=$p(^(epis),"\",2) q
 .....i $$LOCK^SSLOCK("Patient",epis,,,,1)
 .....i '$$select^LVBEPVIS(epis) s PLIST(59)="S-"_(+$h) i $$update^LVBEPVIS(epis)
 .....i $$LOCKCLR^SSLOCK("Patient",epis)
 ..... ; set Bad Debtor listing
 .....s ^TBDL(0,epis)=""
 ..... ; set Account fee
 .....i 'found,TYPE="SI",PAYCODE(33)="D" d AccountFee(epis,deb)
 . ; print Summons list
 .i $$START^PRTREP00(USER,"",+$h,"SL")
 . ; Print Reminder Summary
 .i $$START^PRTREP00(USER,"","3\"_(+$h),"CBR")
 k ^TMP($zn,$j)
 l -^TMP("REPORT",$zn)
 q
AccountFee(epis,deb,ReBill,amount) n (epis,deb,ReBill,amount,PAYCODE,CFLAB,found)
 s epis=$g(epis),deb=$g(deb),ReBill=$g(ReBill),amount=$g(amount),found=$g(found)
 i '$d(CFLAB),'$$select^MVBCFLAB(1) d
 .f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 .k CFLAB m CFLAB=PLIST
 i $g(PAYCODE(33))="I",$l(PAYCODE(46)) d
 .k DEB i '$$select^LVBDEB(deb) m DEB=PLIST
 .f j=2:1:20 s PLIST(j)=""
 .s PLIST(2)=$$epcomp^LVBVIS1(epis)
 .s PLIST(3)=PAYCODE(46)
 .s PLIST(4)=+$h
 .s PLIST(5)=epis
 .s PLIST(7)=DEB(3)
 .i $l(amount) d
 ..s PLIST(9)=$p(amount,"\")
 ..s PLIST(15)=$p(amount,"\",3)
 ..s PLIST(17)=$p(amount,"\",4)
 ..s PLIST(18)=$p(amount,"\",5)
 .i '$l(amount),$l(PAYCODE(20)) d
 ..s Master=epis
 ..s %routine=$p($g(^TTAB("REPORT-GENERIC","PRICING")),"\",1) i '$l(%routine) q
 ..x "s xxx=$$amount^"_%routine_"("""_PAYCODE(20)_""","""_PAYCODE(2)_""","""_(+$h)_""")"
 ..s $p(xxx,"\",1)=$p(xxx,"\",1)*$s(ReBill="Y":-1,1:1)
 ..i $p(xxx,"\",2)'="Y" s (PLIST(9),PLIST(15))=$p(xxx,"\",1)
 ..i $p(xxx,"\",2)="Y" d
 ...s date=$o(^TTAB("BI",PAYCODE(2),"GST",$h+1),-1) i $l(date) d
 ....f j=1,3 s @("x"_j)=$p(^TTAB("BI",PAYCODE(2),"GST",date),"\",j)
 ....i $l(x1),$l(x3) s PLIST(18)=+$j($p(xxx,"\",1)*x3/100,0,2),PLIST(17)=$p(xxx,"\",1),PLIST(9)=PLIST(17)+PLIST(18) q
 ....s (PLIST(9),PLIST(15))=$p(xxx,"\",1)
 .s PLIST(16)=DEB(2)
 .s PLIST=$o(PLIST(""),-1)
 .i ReBill="Y" s ok=0 d  q:'ok
 ..s (stop,x1)="" f  s x1=$o(^TDEB(deb,x1)) q:x1=""  d  q:stop
 ...s x2="" f  s x2=$o(^TDEB(deb,x1,0,epis,"JOURNAL",x2),-1) q:x2=""  d  q:stop
 ....i $p(x2,"||")=PAYCODE(46) d
 .....s stop=1,xx=PLIST(9) n PLIST
 .....i '$$select^LVBJOU(x2),PLIST(9)+xx=0 s ok=1
 .i $$insert^LVBJOU()
 i $g(PAYCODE(33))'="I",$l(PAYCODE(16)),$l(PAYCODE(20)) d
 .k DEB i '$$select^LVBDEB(deb) m DEB=PLIST
 .f j=2:1:20 s PLIST(j)=""
 .s PLIST(2)=$$epcomp^LVBVIS1(epis)
 .s PLIST(3)=PAYCODE(16)
 .s PLIST(4)=+$h
 .s PLIST(5)=epis
 .s PLIST(7)=DEB(3)
 .s Master=epis
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","PRICING")),"\",1) i '$l(%routine) q
 .x "s xxx=$$amount^"_%routine_"("""_PAYCODE(20)_""","""_PAYCODE(2)_""","""_(+$h)_""")"
 .s $p(xxx,"\",1)=$p(xxx,"\",1)*$s(ReBill="Y":-1,1:1)
 .i $p(xxx,"\",2)'="Y" s (PLIST(9),PLIST(15))=$p(xxx,"\",1)
 .i $p(xxx,"\",2)="Y" d
 ..s date=$o(^TTAB("BI",PAYCODE(2),"GST",$h+1),-1) i $l(date) d
 ...f j=1,3 s @("x"_j)=$p(^TTAB("BI",PAYCODE(2),"GST",date),"\",j)
 ...i $l(x1),$l(x3) s PLIST(18)=+$j($p(xxx,"\",1)*x3/100,0,2),PLIST(17)=$p(xxx,"\",1),PLIST(9)=PLIST(17)+PLIST(18) q
 ...s (PLIST(9),PLIST(15))=$p(xxx,"\",1)
 .s PLIST(16)=DEB(2)
 .s PLIST=$o(PLIST(""),-1)
 .i ReBill="Y" s ok=0 d  q:'ok
 ..s (stop,x1)="" f  s x1=$o(^TDEB(deb,x1)) q:x1=""  d  q:stop
 ...s x2="" f  s x2=$o(^TDEB(deb,x1,0,epis,"JOURNAL",x2),-1) q:x2=""  d  q:stop
 ....i $p(x2,"||")=PAYCODE(16) d
 .....s stop=1,xx=PLIST(9) n PLIST
 .....i '$$select^LVBJOU(x2),PLIST(9)+xx=0 s ok=1
 .i '$$insert^LVBJOU() s found=1 i CFLAB(134) d
 ..k DEB i '$$select^LVBDEB(deb) m DEB=PLIST
 ..f j=2:1:20 s PLIST(j)=""
 ..s PLIST(2)=$$epcomp^LVBVIS1(epis)
 ..s PLIST(3)=CFLAB(135)
 ..s PLIST(4)=+$h
 ..s PLIST(5)=epis
 ..s PLIST(7)=DEB(3)
 ..s (PLIST(9),PLIST(15))=$j($$invAmount(epis)*CFLAB(134)/100,0,2)*$s(ReBill="Y":-1,1:1)
 ..s PLIST(16)=DEB(2)
 ..s PLIST=$o(PLIST(""),-1)
 ..i ReBill="Y" s ok=0 d  q:'ok
 ...s (stop,x1)="" f  s x1=$o(^TDEB(deb,x1)) q:x1=""  d  q:stop
 ....s x2="" f  s x2=$o(^TDEB(deb,x1,0,epis,"JOURNAL",x2),-1) q:x2=""  d  q:stop
 .....i $p(x2,"||")=CFLAB(135) d
 ......s stop=1,xx=PLIST(9) n PLIST
 ......i '$$select^LVBJOU(x2),PLIST(9)+xx=0 s ok=1
 ..i '$$insert^LVBJOU()
 q
invAmount(epis) n (epis) s epis=$g(epis),sum=0
 s inv=$$seldata^LVBEPVIS(epis,57)
 i $l(inv) d
 .f j=1,2 s @("inv"_j)=$p(inv,"||",j)
 .i $l(inv1),$l(inv2),$d(^TMCB(inv1,inv2,epis)) d
 ..s seq=$p(^TMCB(inv1,inv2,epis),"\",2)
 ..s ep="" f  s ep=$o(^TMCB(inv1,inv2,ep)) q:ep=""  i seq=$p(^(ep),"\",2) d
 ...s dcol=$$seldata^LVBEPVIS(ep,12),deb=$$seldata^LVBEPVIS(ep,20)
 ...s sum=sum+$p($g(^TDEB(deb,dcol,0,ep)),"\")
 q sum
]]></Routine>
</Export>
