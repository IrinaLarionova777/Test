<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTTIST01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTTIST01 ; ML 22/12/98 ; TEST ITEM STATISTICS
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Department
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Test Item Statistics"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Department : "_$s($l(PAR3):DEP(3),1:"All Departments")
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 i $l(PAR3) i '$$select^LVBCTDEP(PAR3,"Y") k DEP m DEP=PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s ts=PAR3 f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ...i $l(PAR3),($e(ts)'=PAR3) q
 ...s dept=$e(ts) s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....s tc="" f  s tc=$o(^TEPI(epis,1,ts,tscnt,"DATA",tc)) q:tc=""  d
 .....s ^TMP($zn,$j,"REPORT",dept,tc,ts)=$g(^TMP($zn,$j,"REPORT",dept,tc,ts))+1
 .....s ^TMP($zn,$j,"REPORT",dept,tc)=$g(^TMP($zn,$j,"REPORT",dept,tc))+1
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s dept="" f  s dept=$o(^TMP($zn,$j,"REPORT",dept)) q:dept=""  d
 ..i '$l(PAR3) d
 ...i '$$select^LVBCTDEP(dept) m DEP=PLIST k PLIST
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ...d ADDLINE^PRTREP00(REPID,"PRT","~ITALIC")
 ...d ADDLINE^PRTREP00(REPID,"PRT","?5  "_dept_" - "_DEP(3))
 ...d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...k DEP
 ..s tc="" f  s tc=$o(^TMP($zn,$j,"REPORT",dept,tc)) q:tc=""  d
 ...s xxx(1)=tc,xxx(3)=^(tc) i '$$select^LVBCTTC(tc,"Y") s xxx(2)=$p(PLIST(3),$c(1))
 ...s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",dept,tc,ts)) q:ts=""  d
 ....s xxx(4)=ts,xxx(6)=^(ts) i '$$select^LVBCTTS(ts,"Y") s xxx(5)=$p(PLIST(3),$c(1))
 ....d PRINT^PRTREP00(,.N) k xxx
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="TIST"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
