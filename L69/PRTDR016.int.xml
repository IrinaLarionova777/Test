<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR016" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR016 ; ML 28/1/2000 ; Doctor's Report HEADER - Independent Diagnostic Services
 q
 ; supported calls
 ;
 ; INIT^PRTDR016
 ; QDR^PRTDR016
 ; Header^PRTDR016(page,par1,par2)
 ; Footer^PRTDR016(page,par1,par2)
 ;
 ; check stationery size
CR(CR) n (CR) s xCR=CR,result="A4" i xCR["-FAX-" s xCR=$p(CR,"-FAX-",1)
 i '$$select^LVBCTCR(xCR),$e($p(PLIST(4),$c(1)),1,2)="A5" s result="A5" 
 q result
 ; client specific parameters
INIT i $e($$CR(PAR2),1,2)="A5" s MaxLine=23
 e  s MaxLine=47
 q
 ; print header
Header(page,par1,par2) n (page,par1,par2,VISIT,DOC,HOSP,REPID)
 d ADD^PRTREP00(REPID,"PRT","#"),QDR
 s (copyto,copyfrom,specimen)="",epis=$p(VISIT(2),$c(1))
 ; find copy-to only for DR's copies
 i $d(DOC),$p(VISIT(15),$c(1))=$p(DOC(2),$c(1)) d
 .i $$open^LVBVISDC(epis)
 .f  q:$$fetch^LVBVISDC(epis)  s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 .i $$close^LVBVISDC()
 ; find copy-from
 i $d(DOC),$p(VISIT(15),$c(1))'=$p(DOC(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 i $d(HOSP),$p(VISIT(22),$c(1))'=$p(HOSP(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 i '$l($p(VISIT(6),$c(1)))!(VISIT(26)=500) s dob="Unknown"
 e  s dob=$$dt($p(VISIT(6),$c(1)))
 ; expand collection date and time / received date and time
 f j=12,13,61,62 s VISIT(j)=$p(VISIT(j),$c(1))
 s colltime="" i $l(VISIT(13)) s colltime=$e(VISIT(13)\3600+100,2,3)_":"_$e(VISIT(13)\60#60+100,2,3)
 s colldate="" i $l(VISIT(12)) s colldate=$$dt(VISIT(12))
 s rectime="" i $l(VISIT(62)) s rectime=$e(VISIT(62)\3600+100,2,3)_":"_$e(VISIT(62)\60#60+100,2,3)
 s recdate="" i $l(VISIT(61)) s recdate=$$dt(VISIT(61))
 ; format doctor address
 i $d(DOC) d Address(DOC(9),DOC(10),DOC(11),DOC(12),DOC(13),.dAddress)
 i $d(HOSP) d Address(HOSP(4),HOSP(5),HOSP(6),HOSP(7),HOSP(8),.hAddress)
 ; format patient address
 d Address(VISIT(32),VISIT(33),VISIT(34),VISIT(35),VISIT(36),.pAddress)
 ; extract tests requested
 k ReqTS s cnt="" m xVISIT(68)=VISIT(68)
 f  s cnt=$g(cnt)+1 d  q:'$l(xVISIT(68))
 .s x="" i $l(xVISIT(68))>45 s x=$l($e(xVISIT(68),1,45)," ")-1,ReqTS(cnt)=$p(xVISIT(68)," ",1,x),xVISIT(68)=$e(xVISIT(68),$l(ReqTS(cnt))+1,$l(xVISIT(68))) q
 .i $l(xVISIT(68))<45 s ReqTS(cnt)=xVISIT(68),xVISIT(68)=$e(xVISIT(68),$l(ReqTS(cnt))+1,$l(xVISIT(68)))
 .i $l(xVISIT(68))=45 s ReqTS(cnt)=xVISIT(68),xVISIT(68)=$e(xVISIT(68),$l(ReqTS(cnt))+1,$l(xVISIT(68)))
 ; get tests printing on this page
 s (TestOnPage,curRID)=""
 f  s curRID=$o(^TMP("REPORT",$j,page,"TEST",curRID)) q:curRID=""  i $p(curRID,"||",3) d
 .i '$$select^LVBCTTS($p(curRID,"||",2),"Y") m TEST=PLIST k PLIST d
 ..i $g(TEST(4))'="" s TestOnPage=TestOnPage_$s($l(TestOnPage):", ",1:"")_TEST(4) q
 ..i $g(TEST(5))'="" s TestOnPage=TestOnPage_$s($l(TestOnPage):", ",1:"")_TEST(5)
 ; get tests to follow
 s ttf=$$TTF(VISIT(2))
 s xCR=$$CR(par2)
 i $e(xCR,2)'="A5" d Header4
 q
 ; patient details - A4 format
Header4 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4900h7710V"_page_" of "_$o(^TMP("REPORT",$j,""),-1))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a80h7700V"_$$dt(VISIT(12))_"    "_VISIT(3)_", "_VISIT(4))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 i $l(TestOnPage) s TestOnPage=TestOnPage_$s(ttf'="":"   ("_ttf_" to follow)",1:"")
 i $l(TestOnPage)>100 s TestOnPage=$e(TestOnPage,1,100)_" ..."
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a560h7800V"_TestOnPage)
 ; print doctor details
 i $d(DOC) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h650V"_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51)))
 .f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h"_(j*110+650)_"V"_dAddress(j))
 i $d(HOSP) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h650V"_HOSP(3))
 .f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h"_(j*110+650)_"V"_hAddress(j))
 ; print copy to and referring dr
 i $l(copyto) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a80h1300VCopy To :")
 .i $l(copyto)>31 s copyto=$e(copyto,1,29)_".."
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h1300V"_copyto)
 i $l(copyfrom) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a80h1300VReferral :")
 .i $l(copyfrom)>31 s copyfrom=$e(copyfrom,1,29)_".."
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h1300V"_copyfrom)
 ; print test requested
 f j=1:1:3 i $d(ReqTS(j)) d
 .f  q:$e(ReqTS(j))'=" "  s ReqTS(j)=$e(ReqTS(j),2,99)
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p7v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a700h"_(j*85+1350)_"V"_ReqTS(j))
 ; print patient details
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3450h650V"_$p(VISIT(3),$c(1))_", "_$p(VISIT(4),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 f j=1:1:3 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3450h"_(j*110+650)_"V"_pAddress(j))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s"_$s(par1="F":"3b",1:"0b")_"5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3450h1150V"_dob)
 i '$l($p(VISIT(5),$c(1),2)) s $p(VISIT(5),$c(1),2)="Unknown"
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4200h1150V"_$p(VISIT(5),$c(1),2)_" "_$s(VISIT(26)'=500:VISIT(26),1:"")_" "_$s(VISIT(26)=1:"year",VISIT(26)'=500:"years",1:""))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3450h1300V"_VISIT(43))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h1450V"_VISIT(2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h1600V"_$p(VISIT(16),$c(1)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s"_$s(par1="F":"3b",1:"0b")_"4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3450h1450V"_colldate)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3800h1450V"_colltime)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3450h1600V"_$$dt(+$h))
 ; copy, final and interim report tabs
 s (ShadeLen,TabDesc)=""
 i VISIT(29)'="F" s TabDesc="INTERIM REPORT",ShadeLen=1600
 i VISIT(29)="F" s TabDesc="FINAL REPORT"
 i $l(copyfrom) s TabDesc="COPY REPORT"
 i $l(copyfrom),(VISIT(29)'="F") s ShadeLen=1300
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s3b5T")
 f i=1:1:$l(TabDesc) s char=$e(TabDesc,i) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s(char="I":5415,char="M":5392,1:5400)_"h"_(i*110+2000)_"V"_char)
 i $l(ShadeLen) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a5300h2000V"),ADD^PRTREP00(REPID,"PRT",$c(27)_"*c285h"_ShadeLen_"v15g2P")
 ; clinical history
 i page=1 d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p13h0s"_$s(par1="F":"3b",1:"1b")_"5T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a270h1800V"_"CLINICAL NOTES")
 .i $l(VISIT(53)) s cl1=$p(VISIT(53),"|",1),cl2="" d
 ..f  q:$l(cl1)<80  s cl2=$p(cl1," ",$l(cl1," "))_$s($l(cl2):" ",1:"")_cl2,cl1=$p(cl1," ",1,$l(cl1," ")-1)
 ..i $l(cl2)>75 s cl2=$e(cl2,1,73)_".."
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p13h0s"_$s(par1="F":"3b",1:"0b")_"5T")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1110h1800V"_cl1),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1110h1900V"_cl2)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p13h0s"_$s(par1="F":"3b",1:"0b")_"5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h1850V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
 ; calculate date
dt(date) n (date) s date=$g(date),result=""
 s result=$$extdate2^SSUTIL4(date),result=$p(result,"/",1,2)_"/"_$e(result,$l(result)-1,$l(result))
 q result
 ; print footer
Footer(page,par1,par2) 
 q
QDR i $p(VISIT(29),$c(1))="F" s QDR="IDS2"
 i ($p(VISIT(29),$c(1))'="F")!(par1="F") s QDR="IDS"
 d ADD^PRTREP00(REPID,"PRT",^SS("QDR",QDR,1))
 i QDR="IDS2" d ADD^PRTREP00(REPID,"PRT"," !R! PSRC 1; EXIT;")
 i QDR="IDS" d ADD^PRTREP00(REPID,"PRT"," !R! PSRC 2; EXIT;")
 s x=1 f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP",(par2'["-FAX-") d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"PRT",^SS("QDR",bitmap,bit))
 .i $p(^SS("QDR",QDR,x),"-",1)'="BITMAP" d ADD^PRTREP00(REPID,"PRT",^SS("QDR",QDR,x))
 q
 ; Tests to Follow
TTF(epis) n (epis) s epis=$g(epis),result=""
 s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 . ; do not look at empty tests
 .i '$d(^TTAB("TS",ts,0)) q
 . ; do not look at non printable tests
 .s (ok,x)="" f  s x=$o(^TTAB("TS",ts,0,x)) q:x=""  i $p(^(x),"\",8)'="",$p(^(x),"\",13)="Y" s ok=1 q
 .i ok s cnt="" f  s cnt=$o(^TEPI(epis,1,ts,cnt)) q:cnt=""  d
 ..s rowid=epis_"||"_ts_"||"_cnt,status=""
 ..s status=$$seldata^LVBVISTS(rowid,31),test=$$seldata^LVBCTTS($$seldata^LVBVISTS(rowid,3),4)
 ..i $p(status,$c(1))'="Y" s result=result_$s($l(result):", ",1:"")_test
 q result
 ; format address
Address(x1,x2,x3,x4,x5,Address) n (x1,x2,x3,x4,x5,Address) k Address s cnt=0 f j=1:1:3 s Address(j)=""
 i $l(x1) s cnt=cnt+1,Address(cnt)=x1
 i $l(x2) s cnt=cnt+1,Address(cnt)=x2
 s cnt=cnt+1
 i $l(x3) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x3
 i $l(x4) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x4
 i $l(x5) d
 .i cnt>1,'$l(Address(cnt)) s cnt=cnt-1
 .s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):", ",1:"")_x5
 q
]]></Routine>
</Export>
