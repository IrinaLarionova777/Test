<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDRSUM01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDRSUM01 ; ML 16/3/98 ; DAILY RESULTS SUMMARY - Trak
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Site
 ;               4. Department
 ;               5. Group of Tests
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 d ADDLINE^PRTREP00(REPID,"HDR","~BOLD")
 s text="Daily Results Summary" s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s text="Site : " i PAR3'="" d
 .i PAR3="*" s text=text_"All Sites"
 .i PAR3'="" s text=text_site
 .s tab=$$TAB(text)
 .d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 .f i=1:1:2 d HDLN
 s text="Department : " i PAR4'="" d
 .i PAR4="*" s text=text_"All Departments"
 .i PAR4'="",$l(PAR4)>0 s text=text_dept
 .s tab=$$TAB(text)
 .d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 .f i=1:1:2 d HDLN
 s text="Test Set Group : " i PAR5'="" d
 .s text=text_tsgrp
 .s tab=$$TAB(text)
 .d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 .f i=1:1:2 d HDLN
 s text="From "_$$extdate2^SSUTIL4(PAR1)_" to "_$$extdate2^SSUTIL4(PAR2) s tab=$$TAB(text)
 d ADDLINE^PRTREP00(REPID,"HDR","?"_tab_text)
 f i=1:1:2 d HDLN
 s prt="" f i="Name:1","MRN:30","DOB:45","Sex:57","Location:62","Specimen:82" d PRT(i)
 d ADDLINE^PRTREP00(REPID,"HDR",prt),HDLN
 d ADDLINE^PRTREP00(REPID,"HDR","?1  "_und),HDLN
 d ADDLINE^PRTREP00(REPID,"HDR","~NORMAL")
 d BODY,PRINT
 d ADDLINE^PRTREP00(REPID,"PRT","~FONTSIZE=10")
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
TAB(text) ; calculate header position
 s textlen=130-$l(text),tab=textlen\2_"   "
 q $e(tab,1,3)
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s (site,dept,tsgrp)=""
 s:PAR3="" PAR3="*"
 i PAR3'="*",'$$select^LVBCTUSL(PAR3,"Y") s site=$p(PLIST(3),$c(1)) k PLIST
 i PAR4'="*",'$$select^LVBCTDEP(PAR4,"Y") s dept=$p(PLIST(3),$c(1)) k PLIST
 i PAR5'="",'$$select^LVBCTTWG(PAR5,"Y") s tsgrp=$p(PLIST(3),$c(1)) k PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)="",$p(und,"=",130)="",$p(und1,"-",130)=""
 q
BODY ; create temporary printing file
 k ts i $l(PAR5),PAR5'="*" q:$$getall^LVBCTTWT(PAR5)  m grpts=PLIST k PLIST d
 .f i=1:1:grpts s ts=$p(grpts(i),$c(2),3) i $l(ts) s ts(ts)=""
 s date=PAR1-1 f  s date=$o(^TEPIi("AUTHORISED",date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TEPIi("AUTHORISED",date,epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis,"Y") d
 ...k EPVIS m EPVIS=PLIST
 ...i $l(PAR3),PAR3'="*",$p(EPVIS(66),$c(1))'=PAR3 q
 ...s ts="" f  s ts=$o(^TEPIi("AUTHORISED",date,epis,ts)) q:ts=""  d
 ....i $l(PAR4),PAR4'="*",$e(ts)'=PAR4 q
 ....i $l(PAR5),PAR5'="*",'$d(ts(ts)) q
 ....s nn="" f  s nn=$o(^TEPIi("AUTHORISED",date,epis,ts,nn)) q:nn=""  d
 .....s:'$l(EPVIS(3)) EPVIS(3)=" "
 .....s:'$l(EPVIS(20)) EPVIS(20)=" "
 .....s ^TMP($zn,$j,EPVIS(3),EPVIS(20),epis,ts,nn)=""
 q
PRINT ; print report
 d LN
 s surn="" f  s surn=$o(^TMP($zn,$j,surn)) q:surn=""  d
 .s mrn="" f  s mrn=$o(^TMP($zn,$j,surn,mrn)) q:mrn=""  d
 ..s epis="" f start=0:1 s epis=$o(^TMP($zn,$j,surn,mrn,epis)) q:epis=""  d
 ...s spec="" i '$$getall^LVBVISSP(epis) f j=1:1:PLIST s $p(spec,",",j)=$p(PLIST(j),$c(2),4)
 ...i '$$select^LVBEPVIS(epis) k EPVIS m EPVIS=PLIST
 ... ; print first line for MRN
 ...i 'start d
 ....s prt=$e($$NameFormat^LVBCOM03("PT","",EPVIS(4),EPVIS(3),EPVIS(99),EPVIS(100),EPVIS(101)),1,28)
 ....f i=$p(EPVIS(20),$c(1))_":29",$$extdate2^SSUTIL4(EPVIS(6))_":44",$p(EPVIS(5),$c(1))_":56",$p(EPVIS(22),$c(1),2)_":61",spec_":81" d PRT(i)
 ....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 ... ; print first line for Episode
 ...s prt=$e(epis_$j("",10),1,10)
 ...s prt=prt_"  Rec.Date "_$$extdate2^SSUTIL4(EPVIS(61))_" Rec.Time "_$$exttime^SSUTIL4(EPVIS(62))
 ...s prt=prt_"  Col.Date "_$$extdate2^SSUTIL4(EPVIS(12))_" Col.Time "_$$exttime^SSUTIL4(EPVIS(13))
 ...s prt=prt_"  ("_$p(EPVIS(17),$c(1))_")"
 ...s prt=prt_"  Doctor "_$p(EPVIS(15),$c(1),2)
 ...d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 ...s ts="" f  s ts=$o(^TMP($zn,$j,surn,mrn,epis,ts)) q:ts=""  d
 ....s nn="" f  s nn=$o(^TMP($zn,$j,surn,mrn,epis,ts,nn)) q:nn=""  d
 .....s vtsRID=epis_"||"_ts_"||"_nn q:$$results^LVBVISTS(vtsRID,,,,"Y")  m visres=PLIST k PLIST
 .....s prt=$j("",12) f i=1:1:visres s resstr=$p(visres(i),$c(2)),tc=$p(resstr,$c(1),2),res=$p(resstr,$c(1),3) i $l(res) d
 ......q:$$select^LVBCTTC(tc,"Y")  s tcnm=$p(PLIST(3),$c(1)),type=$p(PLIST(5),$c(1))
 ...... ; check for date format
 ......i type="D",$l(res) s res=$$extdate2^SSUTIL4(res)
 ...... ; check for culture/antibiotic format
 ......i type="V",$l(res),$d(^TTAB("BUG",res)) s res=$p(^TTAB("BUG",res),"\",1)
 ......s tstprt=prt_" "_tcnm_" "_res
 ......i $l(tstprt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 ......s prt=prt_" "_tcnm_" "_res i type="V" d
 .......s ant="" f  s ant=$o(^TEPI(epis,1,ts,nn,"DATA",tc,"ANT",ant)) q:ant=""  d
 ........s z=^(ant) q:$p(z,"\",2)'="Y"
 ........s antres=$p(z,"\",1),tstprt=prt_" "_$s($g(^TTAB("ANT",ant))'="":$p(^TTAB("ANT",ant),"\",1),1:ant)_" "_antres
 ........i $l(tstprt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 ........s prt=prt_" "_$s($g(^TTAB("ANT",ant))'="":$p(^TTAB("ANT",ant),"\",1),1:ant)_" "_antres
 .....i $l($tr(prt," ")) d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .....s bbp1="" f  s bbp1=$o(^TEPI(epis,1,ts,nn,"BBP",bbp1)) q:bbp1=""  d
 ......s bbp2="" f  s bbp2=$o(^TEPI(epis,1,ts,nn,"BBP",bbp1,bbp2)) q:bbp2=""  d
 .......s z=^(bbp2)
 .......s prt=prt_" Pack="_bbp1 i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($p(z,"\",1)) s prt=prt_" Compatibility="_$p(z,"\",1) i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($p(z,"\",2)) s prt=prt_" Transaction="_$p(z,"\",2) i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($p(z,"\",3)) s prt=prt_" Status_After="_$p(z,"\",3) i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($p(z,"\",4)) s prt=prt_" Phases="_$p(z,"\",4) i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($p(z,"\",5)) s prt=prt_" Hold_Date="_$$extdate2^SSUTIL4($p(z,"\",5)) i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($p(z,"\",6)) s prt=prt_" Hold_Time="_$$exttime^SSUTIL4($p(z,"\",6)) i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($p(z,"\",8)) s prt=prt_" Location="_$p(z,"\",8) i $l(prt)>125 d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .......i $l($tr(prt," ")) d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN s prt=$j("",12)
 .....d ADDLINE^PRTREP00(REPID,"PRT","?1  "_prt),LN
 .....k PLIST i $$COMMENTS^LVBCOM01(vtsRID)
 .....f j=1:1:$g(PLIST) d ADDLINE^PRTREP00(REPID,"PRT","?1  "_$s(j=1:"Comments: ",1:$j("",10))_"   "_PLIST(j)),LN
 ..d ADDLINE^PRTREP00(REPID,"PRT",und1),LN
 q
PRT(det) ; create line to print
 s text=$p(det,":",1),tab=$p(det,":",2),prt=prt_$j(" ",tab-$l(prt))_text
 q
HDLN ; add line to header
 d ADDLINE^PRTREP00(REPID,"HDR","!")
 q
LN ; add line to report
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 q
]]></Routine>
</Export>
