<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCLNUK01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTCLNUK01 ; IG 07/10/98 ; Lithium Report
 q
 ; Parameters :  1. Start Date
 ;               2. End Date
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Lithium Report"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
CREND k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s sort="" i '$$select^LVBEPVIS(epis,"Y") s sort=$$ALPHAUP^SSUTIL4(PLIST(3))
 ..i '$l(sort) s sort=" "
 ..f ts="C106","C1008" i $d(^TEPI(epis,1,ts)) d
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....i ts="C106",$d(^TEPI(epis,1,ts,tscnt,"DATA","C215"))#10=0 q
 ....i ts="C1008",$d(^TEPI(epis,1,ts,tscnt,"DATA","C0018"))#10=0 q
 ....s ^TMP($zn,$j,"REPORT",sort,epis_"||"_ts_"||"_tscnt)=PLIST(20)
 .. ; check if U&E or Thyroid has been performed
 ..f ts="C098","C089" i $d(^TEPI(epis,1,ts)) d
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....i $p(^(tscnt),"\",31)="A" s $p(^TMP($zn,$j,"UE-THYROID",PLIST(20)),"\",$s(ts="C098":1,1:2))="Y"
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s sort="" f  s sort=$o(^TMP($zn,$j,"REPORT",sort)) q:sort=""  d
 ..s vts="" f  s vts=$o(^TMP($zn,$j,"REPORT",sort,vts)) q:vts=""  d
 ...s epis=$p(vts,"||",1),ts=$p(vts,"||",2),tscnt=$p(vts,"||",3) i '$$select^LVBEPVIS(epis) d
 ....k EPVIS m EPVIS=PLIST
 ....i ts="C106" s xxx(1)=$p(^TEPI(epis,1,ts,tscnt,"DATA","C215"),"\",1)
 ....i ts="C1008" s xxx(1)=$p(^TEPI(epis,1,ts,tscnt,"DATA","C0018"),"\",1)
 ....s mrn=^TMP($zn,$j,"REPORT",sort,vts)
 ....s (xxx(2),xxx(3))="NO"
 ....i $d(^TMP($zn,$j,"UE-THYROID",mrn)) d
 .....s x=^TMP($zn,$j,"UE-THYROID",mrn)
 .....s xxx(2)=$s($p(x,"\",1)="Y":"YES",1:"NO"),xxx(3)=$s($p(x,"\",2)="Y":"YES",1:"NO")
 ....s xxx(4)=ts
 ....i '$$select^LVBCTDR($p(EPVIS(15),$c(1)),"Y") d
 .....s xxx(5)=$p(PLIST(18),$c(1))
 .....s xxx(6)=$p(PLIST(15),$c(1))
 ....d PRINT^PRTREP00(.EPVIS,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="CLNUK01"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" d PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
