<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTEPL25" type="INT" languagemode="0"><![CDATA[
PRTEPL25 ; ML 18/05/2004 ; Episode Patient Label - SCIP
 q
 ; Parameters :  1. Episode number
 ;               2. Label code
 ;               3. Quantity
 ;               4. Computer Name
 ;               5. Worksheet
 ;               6. Worksheet No.
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY
 i '$l(PAR2) d
 .k ^TMP("WS",$j) m ^TMP("WS",$j)=^TMP($zn,$j,"REPORT")
 .s label="" f  s label=$o(^TMP("WS",$j,label)) q:label=""  d
 ..d START^PRTREP00(USER,,PAR1_"\"_label,"EPL")
 .k ^TMP("WS",$j)
 i $l(PAR2) d PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR2_"-"_PAR1,PLIST(9)="M" i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 q
BODY ; create temporary printing file
 ; label and quantity entered
 i $l(PAR1),$l(PAR2),$l(PAR3) s ^TMP($zn,$j,"REPORT",PAR1,PAR2)=PAR3 q
 ; print labels for a patient
 i $l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)
 ..f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_PAR1)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBWKSRT()
 .i '$l(PAR5) d
 ..i $$open^LVBVISTS(PAR1)
 ..f  q:$$fetch^LVBVISTS(PAR1)  s ts=$p(PLIST(3),$c(1)),^TMP($zn,$j,1,PAR1,ts)=$g(^TMP($zn,$j,1,PAR1,ts))+1
 ..i $$close^LVBVISTS()
 i '$l(PAR1) d
 .i $l(PAR5),$l(PAR6) d
 ..i $$open^LVBWKSRE(PAR5_"||"_PAR6)
 ..f  q:$$fetch^LVBWKSRE(PAR5_"||"_PAR6)  s epis=$p(PLIST(2),$c(1)) d
 ...i $$open^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)
 ...f  q:$$fetch^LVBWKSRT(PAR5_"||"_PAR6_"||"_epis)  s ts=$p(PLIST(2),$c(1)),^TMP($zn,$j,1,epis,ts)=$g(^TMP($zn,$j,1,epis,ts))+1
 ...i $$close^LVBWKSRT()
 ..i $$close^LVBWKSRE()
 s epis="" f  s epis=$o(^TMP($zn,$j,1,epis)) q:epis=""  d
 .s ts="" f  s ts=$o(^TMP($zn,$j,1,epis,ts)) q:ts=""  d
 ..s (label,max)=""
 ..s label=$$seldata^LVBCTTS(ts,23),max=$$seldata^LVBCTLB(label,4)
 ..i $l(label),max d
 ...i $l(PAR2),PAR2'=label q
 ...s ^TMP($zn,$j,"REPORT",epis,label,ts)=^TMP($zn,$j,1,epis,ts)
 ...s (cnt,ts1)="" f  s ts1=$o(^TMP($zn,$j,"REPORT",epis,label,ts1)) q:ts1=""  s cnt=cnt+^(ts1)
 ...s xx=cnt\max s:cnt#max xx=xx+1 s ^TMP($zn,$j,"REPORT",epis,label)=xx
 q
PRINT 
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  i '$$select^LVBEPVIS(epis,"Y") d
 .k VISIT m VISIT=PLIST s ReqLabel=""
 .s spec="" i $p(VISIT(51),$c(1))'="",'$$select^LVBCTSI($p(VISIT(51),$c(1))) s spec=PLIST(3)
 .s label="" f  s label=$o(^TMP($zn,$j,"REPORT",epis,label)) q:label=""  d
 ..s cnt=^(label),LabelDesc=label,extra="" i '$$select^LVBCTLB(label) s LabelDesc=PLIST(3),extra=PLIST(6)
 ..s cnt=cnt+extra
 ..i label="REF" d
 ...s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",epis,label,ts)) q:ts=""  d
 ....s tsDetail=$p($g(^TMP($zn,$j,2,ts)),"|")
 ....k LINE
 ....i $l(VISIT(3)_","_$e(VISIT(4)))<16 s LINE(1)=VISIT(3)_","_$e(VISIT(4))
 ....e  i $l(VISIT(3))<16 s LINE(1)=VISIT(3)
 ....e  s LINE(1)=$e(VISIT(3),1,14)_"."
 ....s LINE(2)=$$extdate2^SSUTIL4(VISIT(6))
 ....s LINE(3)="SCI Pathology"
 ....s LINE(4)=tsDetail
 ....s LINE(5)=PAR1
 ....d LABEL
 q
LABEL ; print label
 s stx=$c(2),cr=$c(13)
 i $d(LINE) d
 .s LF=$c(10),LABnum=1
 .d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
 .d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_LABnum_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_LABnum_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","Q184,56"_LF)
 .d ADD^SSPRINT(REPID,"PRT","A580,0,0,2,1,1,N,"""_LINE(1)_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","A580,30,0,4,1,1,N,"""_LINE(2)_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","A580,60,0,2,1,1,N,"""_LINE(3)_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","A580,85,0,2,1,1,N,"""_LINE(4)_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","A580,115,0,4,1,1,N,"""_LINE(5)_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","FE"_LF)
 .d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_LABnum_""""_LF)
 .d ADD^SSPRINT(REPID,"PRT","P1"_LF)
 q
]]></Routine>
</Export>
