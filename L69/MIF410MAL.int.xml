<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF410MAL" type="INT" languagemode="0" generated="1"><![CDATA[
MIF410MAL(mi)      ; ML 9/4/99 ; ASTM protocol  -  AVL Omni Models 1 - 9
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4)
 s lf=$c(10),cr=$c(13),(result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main 
 s record=$$read^MIF000("",cr) Q:record=""  d trace^MIF000(mi,record,"H<--M")
 ; header record
 i $e(record)="H" d  q
 .d Last s (sample,epis,surname,result,date,time,QC)=""
 ; patient record
 i $e(record)="P" d  q
 .s epis=$tr($p($p(record,"|",4),"^",1)," ")
 .s surname=$tr($tr($p(record,"|",6)," "),"^"," ")
 .s:surname="$$$" surname=""
 ; order record
 i $e(record)="O" d  q
 .s:epis="" epis=$tr($p($p(record,"|",3),"^",1)," ")
 .s QC=$tr($p(record,"|",12)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
 .s cup=$p($p(record,"|",4),"^",3)
 .i QC="Q" s QC=cup_"\"_epis
 ; result record
 i $e(record)="R" d  q
 .s x1=$tr($p($p(record,"|",3),"^",4)," "),x2=$tr($p(record,"|",4)," "),x3=$tr($p(record,"|",7)," ")
 .i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 . ; date and time
 .i $l($tr($p(record,"|",13)," ")) d
 ..s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
 ..s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)
 ..s result=result_"DATE"_$c(par10)_$$extdate2^SSUTIL4(date)_$c(par11)
 ..s result=result_"TIME"_$c(par10)_$$exttime^SSUTIL4(time)_$c(par11)
 ; last record
 i $e(record)="L" d Last q
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)=""
 q
ORDER ; create list of orders if exists
 s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  d
 .d scanone^MIF000(mi,epis)
 .d PATDET(epis)
 .s (tc,tcx)=""
 .i $d(^TMP("MIF-SINGLE",$j,mi,epis)) f N=1:1 s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  d
 ..q:tcx=$e(tc,1,$l(tc)-1)  s tcx=$e(tc,1,$l(tc)-1)
 ..d SET(epis,tcx)
 .i '$d(^TMP("MIF-SINGLE",$j,mi,epis)) d SET(epis,"")
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 .k ^TMP($zn,$j,"ENQ",epis)
 .d Send k ^TMIF(mi,10)
 q
SAVE(mi) ; create loadlist records
 s epis=^TMIF("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s sample=^TMIF("MIF000",$j,"LL","SAMPLE")
 d PATDET(epis)
 s (tc,tcx)="" f  s tc=$o(^TMIF("MIF000",$j,"LL","TEST",tc)) q:tc=""  d
 .q:tcx=$e(tc,1,$l(tc)-1)  s tcx=$e(tc,1,$l(tc)-1)
 .d SET(epis,tcx)
 q 0
PATDET(epis)    ; set patient details record
 i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"|||"_sex_"|||||"_$g(PLIST(15))
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||^||||||||"
 q
SET(epis,tcx)   ; set record to be sent to instrument
 i $l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_N_"|"_epis_"||^^^"_tcx_"|||||||N||||||||||||||Q"
 i '$l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="Q|1|^"_epis_"||^^^ALL||||||||X"
 q
Send ; send list of orders if exists
 w enq,*-3 f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 i $c(R)=enq q
 i $c(R)'=ack w eot,*-3 q
 q:$$SEND("1H|\^&|||LABTRAK|||||||P|1")
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str)       ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 0
 d trace^MIF000(mi,R,"H<--M")
 q 1
CHSUM(x)        ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
NEXTTRAY(tray)  
 q tray
]]></Routine>
</Export>
