<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF148" type="INT" languagemode="0" generated="1"><![CDATA[
MIF148(mi) ; ML 29/6/98 ; TRINITY BIOTECH AMAX (Bi-directional) ; QC - Yes
         s mi=$g(mi) i '$l(mi) q
         k ^TMIF(mi,10) s $zt="err^MIF000"
         i $$select^LVBMIMP(mi) q
         f j=1:1:PLIST s @("par"_j)=PLIST(j)
         s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4)
         s lf=$c(10),cr=$c(13),crlf=$c(13,10),dle=$c(16),nak=$c(21),(result,epis)=""
         i $$start^MIF000() q
         f  d Main i $$stop^MIF000() q
         c par4
         q
Main     s char=$$read^MIF000(,lf)
         ; start of results transmission
         i $e(char)=dle d
         .d trace^MIF000(mi,"DLE","H<--M")
         .d ACK
         .f  r *R:10 q:$c(R)=eot  q:R=-1  d
         ..i $c(R)'=stx q
         ..s record=$$read^MIF000("",lf) q:'$l(record)
         ..s record=$e(record,1,$l(record)-1)
         ..d trace^MIF000(mi,record,"H<--M")
         .. ; check record validity
         ..s chsum=$e($p(record,etx,2),1,2),chrecord=$p(record,etx,1)
         ..i $$CHSUM(chrecord)'=chsum d  q
         ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
         ..d ACK
         .. ; query record
         .. ; i $e(record)="Q" d  q
         ...s machid=$e(record,2,6)
         ...s epis=$tr($e(record,7,26)," ")
         ...s ^TMP($zn,$j,"ENQ",epis)=machid
         .. ; order record
         ..i $e(record)="O" d  q
         ...s epis=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
         ...s cup=$p($p(record,"|",4),"^",3)
         ... ; remove punctuation from QC id
         ...i QC="Q" s epis=$tr(epis,"-,.<>?/':;{[}]+-=)(*&^%$#@!|\ ",""),QC=cup_"\"_epis
         .. ; result record
         ..i $e(record)="R" d  q
         ...s (sample,epis,surname,result,date,time,QC)=""
         ...s epis=$tr($e(record,7,26)," ")
         ...s x1=$tr($e(record,27,34)," "),x2=$tr($e(record,35,39)," ")
         ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par11)
         ...i epis?1.99AN,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
         .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
         .d ORDER
         q
SCAN     ; check all episodes for tests to be performed
         d scanall^MIF000(mi) q:'$d(^TMP("MIF-SCAN",$j,mi))
         s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
         .d PATDET(epis)
         .s mode="SCAN" d SET(epis,mode)
         .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
         .d Send
         .k ^TMP("MIF-SCAN",$j,mi,epis)
         q
ORDER    ; create list of orders if exists
         s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  d
         .d scanone^MIF000(mi,epis)
         .d PATDET(epis)
         .s mode="QUERY" d SET(epis,mode)
         .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
         .d Send
         .k ^TMP("MIF-SINGLE",$j,mi,epis),^TMP($zn,$j,"ENQ",epis)
         q
SAVE(mi) ; create loadlist records
         s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),lf=$c(10),cr=$c(13),nak=$c(21)
         s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
         s sample=^TMP("MIF000",$j,"LL","SAMPLE")
         d PATDET(epis)
         s mode="LOAD" d SET(epis,mode)
         s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
         d Send
         q 0
PATDET(epis) ; set patient details record
         i '$$select^LVBEPVIS(epis,"Y") d  q
         .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
         .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
         .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"|||"_sex_"|||||"_$g(PLIST(15))
         s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||^||||||||"
         q
SET(epis,mode) ; set record to be sent to instrument
         s tcx=""
         ; process a single episode scan
         i mode="QUERY",$d(^TMP("MIF-SINGLE",$j,mi,epis)) d
         .k EpisTC s tc="" f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis,tc)) q:tc=""  s EpisTC($e(tc,1,$l(tc)-1))=""
         .s tc="" f  s tc=$o(EpisTC(tc)) q:tc=""  d
         ..s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_tc,^TMIF(mi,20,epis,"TI-done",tc)=""
         ; process an all episode scan
         i mode="SCAN",$d(^TMP("MIF-SCAN",$j,mi,epis)) d
         .s tc="" f  s tc=$o(^TMP("MIF-SCAN",$j,mi,epis,tc)) q:tc=""  i '$d(^TMIF(mi,20,epis,"TI-done",$e(tc,1,$l(tc)-1))) d
         ..s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_$e(tc,1,$l(tc)-1),^TMIF(mi,20,epis,"TI-done",$e(tc,1,$l(tc)-1))=""
         ; process a load list episode scan
         i mode="LOAD" d
         .k ^TMIF(mi,20,epis,"TI-done")
         .k EpisTC s tc="" f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  s EpisTC($e(tc,1,$l(tc)-1))=""
         .s tc="" f  s tc=$o(EpisTC(tc)) q:tc=""  d
         ..s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^"_tc,^TMIF(mi,20,epis,"TI-done",tc)=""
         i $l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_"||"_tcx_"|||||||N||||||||||||||"_$s(mode="QUERY":"Q",1:"O")
         i '$l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="Q|1|^"_epis_"||^^^ALL||||||||X"
         q
Send     ; send list of orders if exists
         w enq,crlf,*-3 d trace^MIF000(mi,"ENQ","H-->M")
         f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
         d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=nak:"NAK",1:R),"H<--M")
         i $c(R)=enq q
         i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
         q:$$SEND("HCS120")
         s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
         i x="" k ^TMIF(mi,10)
         w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
         q
SEND(str) ; send string to instrument
         s str=str_cr_etx,chsum=$$CHSUM(str)
         w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
         f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
         i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
         i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
         d trace^MIF000(mi,R,"H<--M")
         q 2
CHSUM(x) ; calculate check sum
         n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
         s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
         q z
ACK      ; send 'ack' to instrument
         w ack,cr,lf,*-3
         d trace^MIF000(mi,"ACK","H-->M")
         q
NEXTTRAY(tray) 
         q tray
]]></Routine>
</Export>
