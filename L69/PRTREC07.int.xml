<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTREC07" type="INT" languagemode="0"><![CDATA[
PRTREC07 ; ML 23/12/99 ; RECEIPTS - Universal Diagnostics
 q
 ; Parameters :  1. Receipt Number
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) S PLIST(12)=$g(bs)_"-"_PAR1
 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
 ; create temporary printing file
BODY s epis="" i '$$select^LVBRCPT(PAR1) d
 .f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 .k REC m REC=PLIST
 .i '$$select^LVBCRBL(REC(8)) d
 ..f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ..k crLINE m crLINE=PLIST
 .. ; get single episodes
 ..i $l(crLINE(3)),crLINE(7) i '$l(epis) s epis=crLINE(3)
 .. ; get episode from multiple episodes
 ..i $$open^LVBCRBLB(REC(8))
 ..i '$$fetch^LVBCRBLB(REC(8)) s epis=$p(PLIST(2),$c(1))
 ..i $$close^LVBCRBLB()
 ; update receipt
 i REC(3)="" d
 .s REC(3)=+$h,REC(4)=$p($h,",",2)
 .k PLIST m PLIST=REC i $$update^LVBRCPT(PAR1) 
 i $l(epis),'$$select^LVBEPVIS(epis,"Y") d
 .s ^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW")=crLINE(7)
 .s ^TMP($zn,$j,"REPORT",epis)=""
 .s credit="" i $l(PLIST(87)) s epis=PLIST(87)
 .s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 .s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ..s rowid=deb_"||"_date_"||"_ep
 ..i $$open^LVBDEBVT(rowid)
 ..f  q:$$fetch^LVBDEBVT(rowid)  d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k TRAN m TRAN=PLIST
 ...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) s credit=credit-PLIST(14)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) s credit=credit-PLIST(16)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) s credit=credit-PLIST(9)
 ..i $$close^LVBDEBVT()
 .i credit s ^TMP($zn,$j,"REPORT",epis,"CREDIT-TOTAL")=credit
 Q
PRINT ; print report
 s max=10
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
 .d DOC,PATH
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101))
 .d ITEM,ADDRESS,PHEAD
 .s (line,nextpg,item)=""
 .f  s item=$o(^TMP($zn,$j,"REPORT",epis,"ITEM",item)) q:item=""  d
 ..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2)
 .. ; check item desc length
 ..s x="" s xdesc=desc k desc s desc=xdesc
 ..i $l(desc)<23 s desc(1)=desc
 ..i $l(desc)>22 d
 ...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>22 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>22
 ..d ADD^PRTREP00(REPID,"PRT","?10 "_item),ADD^PRTREP00(REPID,"PRT","?35 "_desc(1)),ADD^PRTREP00(REPID,"PRT","?60 "_$j(amt,7,2)),LN s line=line+1
 ..f i=2:1 q:'$d(desc(i))  d ADD^PRTREP00(REPID,"PRT","?38 "_desc(i)),LN s line=line+1
 ..i line=16,$o(^TMP($zn,$j,"REPORT",epis,"ITEM",item))'="" d
 ...d LN,ADD^PRTREP00(REPID,"PRT","?61 Continued over..")
 ...s nextpg=1 d PHEAD s (line,nextpg)=""
 . ; print LCC after billing items
 .i $l(VISIT(18)),$l(VISIT(27)) d
 ..i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1))) d
 ...d ADD^SSPRINT(REPID,"PRT","?5  SCP : "_$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))),LN  s line=line+1
 .f i=1:1:3 d LN
 .s current=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW"))
 .d ADD^PRTREP00(REPID,"PRT","?46 Amount Paid : ")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD")
 .d ADD^PRTREP00(REPID,"PRT",$j(current,7,2))
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 ; print provider details
 i $l(pathdr) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2500h3300V"_pathdr_" Provider No : "_pathprv)
 q
PHEAD ; print patient details
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&l1O")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a200h220V"_$$NameFormat^LVBCOM03("PT","",VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101)))
 f j=1:1:3 i $l(ADDRESS(j)) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a200h"_(j*90+230)_"V"_ADDRESS(j))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1500h280VService Date : "_$s(VISIT(61)'="":$$extdate2^SSUTIL4(VISIT(61)),1:""))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1500h380VReference    : "_VISIT(2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h220V"_drdet)
 f j=1:1:3 i $l(dAddress(j)) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3500h"_(j*90+230)_"V"_dAddress(j))
 d ADD^PRTREP00(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2000h850V***** TAX RECEIPT *****")
 f i=1:1:3 d LN
 d ADD^PRTREP00(REPID,"PRT","?10 ITEM NO")
 d ADD^PRTREP00(REPID,"PRT","?35 DESCRIPTION")
 d ADD^PRTREP00(REPID,"PRT","?61 AMOUNT")
 d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 f i=1:1:2 d LN
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 .s ^TMP($zn,$j,"REPORT",epis,"ITEM",item)=det
 s ^TMP($zn,$j,"REPORT",epis,"TOT")=amount
 q
DOC ; extract referring doctor details
 s drdet=""
 q:$$select^LVBCTDR(VISIT(15))  m DOC=PLIST k PLIST
 s drdet=$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51))_"   "_DOC(18)
 k dAddress s cnt=0 f j=1:1:3 s dAddress(j)=""
 i $l(DOC(9)) s cnt=cnt+1,dAddress(cnt)=DOC(9)
 i $l(DOC(10)) s cnt=cnt+1,dAddress(cnt)=DOC(10)
 s cnt=cnt+1
 i $l(DOC(11)) s dAddress(cnt)=dAddress(cnt)_$s($l(dAddress(cnt)):",",1:"")_DOC(11)
 i $l(DOC(12)) s dAddress(cnt)=dAddress(cnt)_$s($l(dAddress(cnt)):",",1:"")_DOC(12)
 i $l(DOC(13)) d
 .i cnt>1,'$l(dAddress(cnt)) s cnt=cnt-1
 .s dAddress(cnt)=dAddress(cnt)_$s($l(dAddress(cnt)):",",1:"")_DOC(13)
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
PATH ; extract pathology doctor details
 s (pathdr,pathprv)=""
 i '$$select^LVBINVB(VISIT(57)) s pathdr=$p(PLIST(10),$c(1),2),pathprv=PLIST(5)
 q
LN ; add line to report
 d ADD^PRTREP00(REPID,"PRT","!")
 q
]]></Routine>
</Export>
