<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LHOSPINT23" type="INT" languagemode="0" generated="1"><![CDATA[
LHOSPINT23 ; Hospital Interface - GVH
 q
 ; Import results
RESULT(HOSP) N (HOSP) k ^TMP($zn,$j)
 i $$select^LVBCTRL(HOSP) q
 s dir=PLIST(7),x=$s(dir["\":"\",1:"/") i $e(dir,$l(dir))'=x s dir=dir_x
 s dir=dir_"*.*"
 ; MS-DOS based system
 i $zf(-1,"DIR "_dir_" /B > ""results.txt""")
 ; UNIX based system
 ;i $zf(-1,"ls "_dir_" > ""results.txt""")
 s $zt="Error1^"_$zn
 s FILE="results.txt"
 o FILE:("RS"::$c(10,13))
 f  u FILE r x s xFILE=$p(x," ",$l(x," ")) i xFILE[".",$l(xFILE) s ^TMP($zn,$j," ",xFILE)=""
END1 c FILE
END2 s $zt="Error2^"_$zn
 s xxFILE=$o(^TMP($zn,$j," ","")) i $l(xxFILE) d
 .s x=$s(dir["\":"\",1:"/"),xFILE=$p(dir,x,1,$l(dir,x)-1)_x_xxFILE
 .o xFILE:("RS"::$c(10,13)):1 e  q
 .s Error="" f  u xFILE r MESS s seq=$o(^TMP($zn,$j,xxFILE,"IN",""),-1)+1,^TMP($zn,$j,xxFILE,"IN",seq)=MESS
 s $zt=""
 s xFile="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  d
 .i $$UPPER^SSUTIL4($p(xFile,".",2))="PIT" d
 ..s (vts,seq)="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=^(seq) d
 ...i $e(xx,1,3)="100" s name1=$$UPPER^SSUTIL4($p($tr($e(xx,27,99)," "),",",1)),name2=$$UPPER^SSUTIL4($p($tr($e(xx,27,99)," "),",",2))
 ...i $e(xx,1,3)="104" s dob=$$intdate^SSUTIL4($e(xx,41,42)_"/"_$e(xx,38,39)_"/"_$e(xx,44,47))
 ...i $e(xx,1,3)="104" s sex=$e(xx,69)
 ...i $e(xx,1,3)="110" d
 ....k result s cnt=0
 ....s x=$tr($e(xx,27,99)," "),vts=$p(x,"-",1)_"||"_$p(x,"-",2)_"||"_$p(x,"-",3)
 .... ; only file results in Word/RTF test set
 ....s test=$p(vts,"||",2) i '$$select^LVBCTTS(test,"Y"),'((PLIST(31)="Y")!(PLIST(54)="Y")) s vts=""
 ...i $e(xx,1,3)="301",$l(vts) d
 ....s cnt=cnt+1,result(cnt)=$e(xx,5,$l(xx))
 ....f j="SBLD","EBLD","SUND","EUND","SBLK","EBLK","DFLT" s jj="~"_j_"~" d
 .....f  q:result(cnt)'[jj  s result(cnt)=$p(result(cnt),jj,1)_$p(result(cnt),jj,2,999)
 ....f jj="~FG","~BG" d
 .....f  q:result(cnt)'[jj  s result(cnt)=$p(result(cnt),jj,1)_$e($p(result(cnt),jj,2,999),4,999)
 ...i ($e(xx,1,3)="309")!($e(xx,1,3)="390"),$l(vts),$d(result) d
 ....m ^TMP($zn,$j,xFile,"OUT",vts,"T")=result
 ....k result s vts=""
 .i $$UPPER^SSUTIL4($p(xFile,".",2))="ORU" d
 ..s cnt=0
 ..s seq="" f  s seq=$o(^TMP($zn,$j,xFile,"IN",seq)) q:seq=""  s xx=^(seq) d
 ...i $p(xx,"|")="OBR" s cnt=cnt+1,^TMP($zn,$j,xFile,"IN-1",cnt,0)=xx
 ...i $p(xx,"|")="OBX",$d(^TMP($zn,$j,xFile,"IN-1",cnt)) s x=$o(^TMP($zn,$j,xFile,"IN-1",cnt,""),-1)+1,^TMP($zn,$j,xFile,"IN-1",cnt,x)=xx
 ..s seq1="" f  s seq1=$o(^TMP($zn,$j,xFile,"IN-1",seq1)) q:seq1=""  d
 ...s (epis,ts,tscnt,vts)=""
 ...s seq2="" f  s seq2=$o(^TMP($zn,$j,xFile,"IN-1",seq1,seq2)) q:seq2=""  s xx=^(seq2) d
 ....i $p(xx,"|")="OBR" d
 .....s epis=$p($p($p(xx,"|",4),"^"),"-") i $l(epis) s ts=$p($p($p(xx,"|",4),"^"),"-",2) i $l(ts) s tscnt=$p($p($p(xx,"|",4),"^"),"-",3)+1
 .....i $l(epis),$l(ts),$l(tscnt) s vts=epis_"||"_ts_"||"_tscnt
 .....i $$select^LVBVISTS(vts,"Y") s vts=""
 ..... ; only file results in RTF test set
 ..... i '$$select^LVBCTTS(ts,"Y"),'((PLIST(31)="Y")!(PLIST(54)="Y")) s vts=""
 ....i $l(vts),$p(xx,"|")="OBX",$p(xx,"|",3)="FT" s result=$p(xx,"|",6) d
 .....f j1=1:1:$l(result,"\.br\") d
 ......s xx=$p(result,"\.br\",j1)
 ......f j2="S" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(16)_$p(xx,x2,2,$l(xx,x2))
 ......f j2="F" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$c(17)_$p(xx,x2,2,$l(xx,x2))
 ......f j2="T" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"&"_$p(xx,x2,2,$l(xx,x2))
 ......f j2="R" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_"~"_$p(xx,x2,2,$l(xx,x2))
 ......f j2="H","N","E" s x2="\"_j2_"\" i xx[x2 f j3=1:1:$l(xx,x2)-1 s xx=$p(xx,x2)_$p(xx,x2,2,$l(xx,x2))
 ......s result(j1)=xx
 .....m ^TMP($zn,$j,xFile,"OUT",vts,"T")=result
 .....k result s vts=""
 .s vts="" f  s vts=$o(^TMP($zn,$j,xFile,"OUT",vts)) q:vts=""  d
 ..i $$vtsLock^SSLOCK("IM",epis,vts,"trak")=-1 d
 ...i $$status^LVBVIS1(vts)'="A" d  q
 ....s text=""
 ....s cnt="" f  s cnt=$o(^TMP($zn,$j,xFile,"OUT",vts,"T",cnt)) q:cnt=""  s text=text_$s($l(text):"|",1:"")_^(cnt)
 ....k PLIST s PLIST(1)="T\"_text
 ....s PLIST=$o(PLIST(""),-1)
 .... ; patient check
 ....s ok="N",epis=$p(vts,"||") d
 .... ; .n (ok,epis,dob,sex,name1,name2)
 .... ; .i '$$select^LVBEPVIS(epis,"Y") d
 .... ; ..i dob=PLIST(6),sex=PLIST(5),name1=$$UPPER^SSUTIL4(PLIST(3)),name2=$$UPPER^SSUTIL4(PLIST(4)) s ok="Y" q
 .... ; ..i dob=PLIST(6),sex=PLIST(5),name1=$$UPPER^SSUTIL4(PLIST(4)),name2=$$UPPER^SSUTIL4(PLIST(3)) s ok="Y" q
 .... ; i ok="Y",$$save^LVBVIS4(vts,"A","TRAK","N")
 .... ; i ok="N",$$save^LVBVIS4(vts,"A"_$c(2)_"PIT","TRAK","N")
 ....s err=$$save^LVBVIS4(vts,"A"_$c(2)_"PIT","TRAK","N")
 ....s ^TMPimport(xFile,"index",vts)=$g(^TMPimport(xFile,"index",vts))_" err"
 ....i 'err d
 .....i '$$select^LVBVISTS(vts,"Y") s PLIST(29)=$$seldata^LVBEPVIS(epis,66) i $$update^LVBVISTS(vts)
 .....k PLIST
 .....s PLIST=$o(PLIST(""),-1)
 .....i $$TScheck^LVBVIS7(vts,"A","","A")
 .....f j=1:1:PLIST d
 ......s val=$p($p(PLIST(j),$c(1),2),$c(2))
 ......i $e($p(PLIST(j),$c(1)),1,2)="VQ",$$VQ^LVBCOM13(vts,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="TA",$$TAR^LVBCOM13($p(vts,"||"),,val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SA",$$SA^LVBCOM13($p(vts,"||"),val)
 ......i $e($p(PLIST(j),$c(1)),1,2)="SF",$$SF^LVBCOM13(vts,$p(PLIST(j),$c(1),6),val)
 ......i $e($p(PLIST(j),$c(1)),1,3)="ACT",$$ACT^LVBCOM13($p(vts,"||"),$p(val,"-",1),$p(val,"-",2))
 ..i $$vtsLockClr^SSLOCK("IM",epis,vts)
 s (param,xFile)="" f  s xFile=$o(^TMP($zn,$j,xFile)) q:xFile=""  s param=param_$s($l(param):",",1:"")_xFile
 i $l(param),$$START^PRTREP00("","",HOSP_"\"_param,"RLI")
 k ^TMP($zn,$j)
 q
 ; Error log
Error1 i $ze["ENDOFFILE" g END1
 d BACK^%ETN
 q
 ; Error log
Error2 i $ze'["ENDOFFILE" d BACK^%ETN q
 c xFILE k ^TMP($zn,$j," ",xxFILE)
 ; Move the file
 s x=$s(dir["\":"\",1:"/"),xFILEN=x_$p(xFILE,x,$l(xFILE,x))
 ; MS-DOS based system
 i '$zf(-1,"copy "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN),$zf(-1,"del "_xFILE)
 ; UNIX based system
 ;i $zf(-1,"mv "_xFILE_" "_$p(dir,x,1,$l(dir,x)-1)_x_$s(Error:"bad",1:"process")_xFILEN)
 g END2
START(HOSP) 
 n (HOSP) k ^TMP($zn,$j)
 ; Echuca hospital
 i HOSP="E" d
 .i $$select^LVBHOS(HOSP) q
 .s file=PLIST(5) i file="" q
 .s $zt="ERROR1^"_$zn
 .o file:"WAS":0 e  q
 .u file w !,"End_Of_File",! c file
 .o file::0 e  q
 .f  u file r MESSAGE q:MESSAGE="End_Of_File"  i $l(MESSAGE) d
 ..s HospMRN=$p(MESSAGE,$c(9),2)
 ..s HospEpis=$p(MESSAGE,$c(9),3)
 ..i $$DDS^LVBVIS21(HospMRN,HospEpis)
 .c file
 ; GVH hospital
 i HOSP'="E" d
 .s msgn="" f jRecord=1:1:50 s msgn=$o(^THOS(HOSP,"IN",msgn)) q:msgn=""  d
 ..s xx=^(msgn)
 ..S XX=$O(^THOS(HOSP,"DONE",""),-1)+1
 ..s ^THOS(HOSP,"DONE",XX)=^THOS(HOSP,"IN",msgn) k ^THOS(HOSP,"IN",msgn)
 ..k msg f j=1:1:$l(xx,$c(13)) s line=$p(xx,$c(13),j),type=$p(line,"|") i $l(type) s msg(type)=line
 ..s msgtype=$p($g(msg("MSH")),"|",9)
 ..i $p(msgtype,"^")'="ADT" d log("ERROR: No ADT message ("_msgn_") type "_msgtype) q
 ..s msgtype=$p(msgtype,"^",2)
 ..i $l($t(@msgtype)) d init,@msgtype q
 ..d log("ERROR: Unknow message ("_msgn_") type "_msgtype)
 q
init n (msg,msgn,msgtype,HOSP,xxx,oldmrn,exist,DATA)
 d getdata
 d log("Processing message "_msgn_" "_msgtype)
 d log("mrn="_xxx(2)_" epi="_xxx(3))
 ; check if admission already exists
 k DATA
 s exist="" i $l(xxx(2)),$l(xxx(3)) s exist="N" i '$$select^LVBHOSP(HOSP_"||"_xxx(2)_"||"_xxx(3)) s exist="Y" m DATA=PLIST
 q
update d log("Update "_msgtype_" "_HOSP_"||"_xxx(2)_"||"_xxx(3))
 k PLIST m PLIST=DATA f j=1:1 q:'$d(xxx(j))  i $l(xxx(j)) s PLIST(j)=xxx(j)
 s PLIST=$o(PLIST(""),-1)
 s err=$$update^LVBHOSP(HOSP_"||"_xxx(2)_"||"_xxx(3)) i err d log("ERROR: Return "_err) q
 d tepi(HOSP_"||"_xxx(2)_"||"_xxx(3),$g(oldmrn))
 q
insert k PLIST f j=1:1 q:'$d(xxx(j))  i $l(xxx(j)) s PLIST(j)=xxx(j)
 s PLIST=$o(PLIST(""),-1)
 d log("Insert "_msgtype_" "_HOSP_"||"_xxx(2)_"||"_xxx(3))
 s err=$$insert^LVBHOSP(HOSP) i err d log("ERROR: Return "_err)
 i '$$new^LVBEPVIS("X") s xxx=PLIST(1)
 k PLIST
 s PLIST(2)=xxx
 s PLIST(3)=$g(xxx(5))
 s PLIST(4)=$g(xxx(4))
 s PLIST(5)=$g(xxx(8))
 s PLIST(6)=$g(xxx(7))
 s PLIST(7)=$g(xxx(6))
 s PLIST(8)=$g(xxx(24))
 s PLIST(9)=$g(xxx(38))
 s PLIST(12)=+$h
 s PLIST(15)=$g(xxx(17))
 s PLIST(20)=$g(xxx(2))
 s PLIST(22)=$g(xxx(15))
 s PLIST(32)=$g(xxx(9))
 s PLIST(33)=$g(xxx(10))
 s PLIST(34)=$g(xxx(11))
 s PLIST(35)=$g(xxx(31))
 s PLIST(36)=$g(xxx(12))
 s PLIST(43)=$g(xxx(13))
 s PLIST(44)=$g(xxx(32))
 s PLIST(47)=$g(xxx(21))
 s PLIST(51)=$g(xxx(34))
 s PLIST(60)=$g(xxx(33))
 s PLIST(65)=$g(xxx(3))_","_$g(xxx(2))
 s PLIST(66)=$g(xxx(28))
 s PLIST(73)=$g(xxx(35))
 s PLIST(86)=0
 s PLIST(88)=$g(xxx(37))
 s PLIST(89)=$g(xxx(36))
 s err=$$save^LVBEPVIS(PLIST(2)) i err d log("ERROR in TEPI : Return "_err)
 i $l($g(oldMRN)) d tepi(HOSP_"||"_xxx(2)_"||"_xxx(3),$g(oldmrn))
 q
delete d log("Delete "_msgtype_" "_HOSP_"||"_xxx(2)_"||"_xxx(3))
 s err=$$delete^LVBHOSP(HOSP_"||"_xxx(2)_"||"_xxx(3)) i err d log("ERROR: Return "_err)
 q
A01 ; Inpatient Admission
A28 ; PMI Add
 n (msgtype,HOSP,xxx,exist,DATA)
 i exist="Y" d update
 i exist="N" d insert
 q
A03 ; Inpatient Discharge/Attend a Clinic
 n (msgtype,HOSP,xxx,exist,DATA)
 i exist="Y" d delete
 i " I E "[(" "_$g(xxx(14))_" "),$$DDS^LVBVIS21(,,$g(xxx(2)),$g(xxx(21)),$g(xxx(121)),,,"Y")
 q
A11 ; Inpatient Admission Delete
 n (msgtype,HOSP,xxx,exist,DATA)
 i exist="Y" d delete
 q
A02 ; Ward Transfer/Bed Swap
A08 ; Inpatient Admission Modify/Inpatient Discharge Modify
A45 ; Episode Move
A31 ; Alias Add/Modify/Delete/Alternate Address/Next of Kin Add/Modify/Delete/Medical Alerts/Allergies/PMI Modify
 n (msgtype,HOSP,xxx,exist,DATA)
 i $l(xxx(2)) s epis="" f  s epis=$o(^THOS(HOSP,"PATIENT",xxx(2),epis)) q:epis=""  d
 .d log("Select "_HOSP_"||"_xxx(2)_"||"_epis)
 .k PLIST s err=$$select^LVBHOSP(HOSP_"||"_xxx(2)_"||"_epis) i err d log("ERROR: Return "_err) q
 .m DATA=PLIST s xxx(3)=epis d update
 i exist="N" d insert
 q
A12 ; Ward Transfer Delete
A13 ; Inpatient Admission Delete/Inpatient Discharge Delete/Cancel Attendance
A16 ; Pending Discharge Add/Pending Discharge Modify
A21 ; Discharge to Leave/Cancel Return from Leave
A22 ; Cancel Discharge to Leave/Return from Leave
A25 ; Pending Discharge Delete
 q
A30 ; Patient Merge
A40 ;
 n (msgtype,HOSP,xxx,exist,oldmrn)
 i $l(oldmrn) d
 .i $d(^THOS(HOSP,"PATIENT",oldmrn)) d  q
 ..s epis="" f  s epis=$o(^THOS(HOSP,"PATIENT",oldmrn,epis)) q:epis=""  d
 ... ; check if admission already exists
 ...s exist="" i $l(xxx(2)),$l(epis) s exist="N" i '$$select^LVBHOSP(HOSP_"||"_xxx(2)_"||"_epis) s exist="Y"
 ...d log("Select "_HOSP_"||"_oldmrn_"||"_epis)
 ...k PLIST s err=$$select^LVBHOSP(HOSP_"||"_oldmrn_"||"_epis) i err d log("ERROR: Return "_err) q
 ...i exist="Y" m DATA=PLIST k DATA(1) s xxx(3)=epis d update s xxx(2)=oldmrn,xxx(3)=epis d delete
 ...i exist="N" k DATA d insert s xxx(2)=oldmrn,xxx(3)=epis d delete
 .i '$d(^THOS(HOSP,"PATIENT",oldmrn)) d  q
 ..s epis="PMI"  d
 ... ; check if admission already exists
 ...s exist="" i $l(xxx(2)),$l(epis) s exist="N" i '$$select^LVBHOSP(HOSP_"||"_xxx(2)_"||"_epis) s exist="Y"
 ...d log("Select "_HOSP_"||"_xxx(2)_"||"_epis)
 ...k PLIST s err=$$select^LVBHOSP(HOSP_"||"_xxx(2)_"||"_epis) i err d log("ERROR: Return "_err) q
 ...i exist="Y" m DATA=PLIST k DATA(1) s xxx(3)=epis d update
 ...i exist="N" k DATA d insert
 q
log(txt) n (txt,HOSP)
 s d=$p($h,","),n=$o(^THOS(HOSP,"log",d,""),-1)+1
 s ^THOS(HOSP,"log",d,n)=$zt($p($h,",",2))_" "_txt
 q
getdata k xxx f j=1:1:40 s xxx(j)=""
 s xx=$g(msg("PID"))
 s xxx(4)=$p($p(xx,"|",6),"^",2)                                         ; given name
 i $l($p($p(xx,"|",6),"^",3)) s xxx(4)=xxx(4)_" "_$p($p(xx,"|",6),"^",3) ; given name
 s xxx(5)=$p($p(xx,"|",6),"^")                                           ; surname
 s xxx(6)=$p($p(xx,"|",6),"^",5)                         ; title
 s x=$p(xx,"|",8) i $l(x) s xxx(7)=$zdh(x,8)             ; dob
 s xxx(8)=$p(xx,"|",9)                                   ; sex
 i $l(xxx(8)) d
 .n (HOSP,xxx)
 .s X="" i '$$select^LVBHOSCS(HOSP_"||"_xxx(8)) s X=PLIST(3)
 .s xxx(8)=X
 s xxx(9)=$p($p(xx,"|",12),"^")                          ; address 1
 s xxx(10)=$p($p(xx,"|",12),"^",2)                       ; address 2
 s xxx(11)=$p($p(xx,"|",12),"^",3)                       ; suburb
 s xxx(12)=$p($p(xx,"|",12),"^",5)                       ; post code
 s xxx(13)=$p($p(xx,"|",14),"^")                         ; home phone
 s xxx(25)=HOSP
 s xxx(32)=$p($p(xx,"|",15),"^")                         ; work phone
 s x1=$p(xx,"|",4) f j=1:1:$l(x1,"~") s x2=$p(x1,"~",j) d
 .i $p(x2,"^",5)="MR" s xxx(2)=+$p(x2,"^")               ; hospital MRN
 .i $p(x2,"^",5)="MC" d
 ..s xxx(24)=$e($tr($p(x2,"^")," "),1,10)                ; medicare
 ..s $p(xxx(23),"|",108)=$e($tr($p(x2,"^")," "),11)      ; medicare ref
 .i $p(x2,"^",5)="DVA" s xxx(36)=$p(x2,"^")              ; dva
 .i $p(x2,"^",5)="PEN" s xxx(37)=$p(x2,"^")              ; pensioner
 s xx=$g(msg("PV1"))
 s xxx(3)=$p(xx,"|",20),xxx(3)=$s($l(xxx(3)):+xxx(3),1:"PMI") ; hospital episode
 s xxx(14)=$p($p(xx,"|",3),"^")                          ; class
 s xxx(15)=$p($p(xx,"|",4),"^")                          ; ward
 i $l(xxx(15)) d
 .n (HOSP,xxx)
 .s X="" i '$$select^LVBHOSCL(HOSP_"||"_xxx(15)) s X=PLIST(3)
 .s xxx(15)=X
 s xxx(16)=$p(xx,"|",5)                                  ; episode type
 s xxx(17)=$p($p(xx,"|",9),"^")                          ; reffering doctor
 i $l(xxx(17)) d
 .n (HOSP,xxx)
 .s X="" i '$$select^LVBHOSCD(HOSP_"||"_xxx(17)) s X=PLIST(3)
 .s xxx(17)=X
 s xxx(18)=$p($p(xx,"|",10),"^")                         ; consulting doctor
 i $l(xxx(18)) d
 .n (HOSP,xxx)
 .s X="" i '$$select^LVBHOSCD(HOSP_"||"_xxx(18)) s X=PLIST(3)
 .s xxx(18)=X
 s xxx(19)=$p(xx,"|",11)                                 ; service
 s xxx(20)=$p($p(xx,"|",18),"^")                         ; admitting doctor
 i $l(xxx(20)) d
 .n (HOSP,xxx)
 .s X="" i '$$select^LVBHOSCD(HOSP_"||"_xxx(20)) s X=PLIST(3)
 .s xxx(20)=X
 s x=$e($p(xx,"|",45),1,8) i $l(x) s xxx(21)=$zdh(x,8)   ; admission date
 s x=$e($p(xx,"|",45),9,10)_":"_$e($p(xx,"|",45),11,12)
 i $l(x) s xxx(22)=$zth(x)                               ; admission time
 s x=$e($p(xx,"|",46),1,8) i $l(x) s xxx(121)=$zdh(x,8)  ; discharge date
 s oldmrn=$p($p($g(msg("MRG")),"|",2),"^") i $l(oldmrn) s oldmrn=+oldmrn
 f j=1:1:40 s xxx(j)=$tr(xxx(j),"""")
 q
tepi(hROWID,oldMRN) n (hROWID,oldMRN) s hROWID=$g(hROWID),oldMRN=$g(oldMRN)
 i '$$select^LVBHOSP(hROWID) d
 .k HOSP m HOSP=PLIST
 .i '$l(oldMRN) d
 ..s (deb,hospMRN)=$p(hROWID,"||",2)
 ..s epis="" f  s epis=$o(^TDEBi(deb,"HOSPITAL",hospMRN,epis)) q:epis=""  d
 ...i $e(epis)="X",'$$select^LVBEPVIS(epis,"Y") d  i $$update^LVBEPVIS(epis)
 ....f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ....i $l($g(HOSP(5))) s PLIST(3)=HOSP(5)
 ....i $l($g(HOSP(4))) s PLIST(4)=HOSP(4)
 ....i $l($g(HOSP(8))) s PLIST(5)=HOSP(8)
 ....i $l($g(HOSP(7))) s PLIST(6)=HOSP(7)
 ....i $l($g(HOSP(6))) s PLIST(7)=HOSP(6)
 ....i $l($g(HOSP(24))) s PLIST(8)=HOSP(24)
 ....i $l($g(HOSP(38))) s PLIST(9)=HOSP(38)
 ....i $l($g(HOSP(17))) s PLIST(15)=HOSP(17)
 ....i $l($g(HOSP(15))) s PLIST(22)=HOSP(15)
 ....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(32)=$g(HOSP(9))
 ....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(33)=$g(HOSP(10))
 ....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(34)=$g(HOSP(11))
 ....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(35)=$g(HOSP(31))
 ....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(36)=$g(HOSP(12))
 ....i $l($g(HOSP(13))) s PLIST(43)=HOSP(13)
 ....i $l($g(HOSP(32))) s PLIST(44)=HOSP(32)
 ....i $l($g(HOSP(21))) s PLIST(47)=HOSP(21)
 ....i $l($g(HOSP(34))) s PLIST(51)=HOSP(34)
 ....i $l($g(HOSP(33))) s PLIST(60)=HOSP(33)
 ....i $l($g(HOSP(28))) s PLIST(66)=HOSP(28)
 ....i $l($g(HOSP(35))) s PLIST(73)=HOSP(35)
 ....i $l($g(HOSP(37))) s PLIST(88)=HOSP(37)
 ....i $l($g(HOSP(36))) s PLIST(89)=HOSP(36)
 .... ; add All Extra info
 ....i $l($g(HOSP(23))) f j=3:1:$l(HOSP(23),"|") i $l($p(HOSP(23),"|",j)) s PLIST(j)=$p(HOSP(23),"|",j)
 .i $l(oldMRN) d
 ..s deb=oldMRN,newMRN=$p(hROWID,"||",2)
 ..s xd="" f  s xd=$o(^TDEB(deb,xd)) q:xd=""  d
 ...s epis="" f  s epis=$o(^TDEB(deb,xd,0,epis)) q:epis=""  d
 ....i '$$select^LVBEPVIS(epis,"Y") d  i $$update^LVBEPVIS(epis)
 .....f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .....s PLIST(20)=newMRN
 .....s PLIST(65)=$g(HOSP(3))_","_newMRN
 .....i $l($g(HOSP(5))) s PLIST(3)=HOSP(5)
 .....i $l($g(HOSP(4))) s PLIST(4)=HOSP(4)
 .....i $l($g(HOSP(8))) s PLIST(5)=HOSP(8)
 .....i $l($g(HOSP(7))) s PLIST(6)=HOSP(7)
 .....i $l($g(HOSP(6))) s PLIST(7)=HOSP(6)
 .....i $l($g(HOSP(24))) s PLIST(8)=HOSP(24)
 .....i $l($g(HOSP(38))) s PLIST(9)=HOSP(38)
 .....i $l($g(HOSP(17))) s PLIST(15)=HOSP(17)
 .....i $l($g(HOSP(15))) s PLIST(22)=HOSP(15)
 .....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(32)=$g(HOSP(9))
 .....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(33)=$g(HOSP(10))
 .....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(34)=$g(HOSP(11))
 .....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(35)=$g(HOSP(31))
 .....i $l($g(HOSP(9)))!$l($g(HOSP(10)))!$l($g(HOSP(11)))!$l($g(HOSP(31)))!$l($g(HOSP(12))) s PLIST(36)=$g(HOSP(12))
 .....i $l($g(HOSP(13))) s PLIST(43)=HOSP(13)
 .....i $l($g(HOSP(32))) s PLIST(44)=HOSP(32)
 .....i $l($g(HOSP(21))) s PLIST(47)=HOSP(21)
 .....i $l($g(HOSP(34))) s PLIST(51)=HOSP(34)
 .....i $l($g(HOSP(33))) s PLIST(60)=HOSP(33)
 .....i $l($g(HOSP(28))) s PLIST(66)=HOSP(28)
 .....i $l($g(HOSP(35))) s PLIST(73)=HOSP(35)
 .....i $l($g(HOSP(37))) s PLIST(88)=HOSP(37)
 .....i $l($g(HOSP(36))) s PLIST(89)=HOSP(36)
 ..... ; add All Extra info
 .....i $l($g(HOSP(23))) f j=3:1:$l(HOSP(23),"|") i $l($p(HOSP(23),"|",j)) s PLIST(j)=$p(HOSP(23),"|",j)
 ..s kill="Y"
 ..s x="" f  s x=$o(^TDEB(oldMRN,x)) q:x=""  i x?5N,$d(^TDEB(oldMRN,x,0)) s kill="N" q
 ..i kill="Y" d
 ... ; copy old debtor number notes
 ...s x=0 f  s x=$o(^TDEB(oldMRN,"ACC",x)) q:x=""  s line=^(x) d
 ....s x1=$o(^TDEB(newMRN,"ACC",""),-1)+1,^TDEB(newMRN,"ACC",x1)=line,^TDEB(newMRN,"ACC",0)=x1
 ... ; copy old debtor number notes
 ...s x=0 f  s x=$o(^TDEB(oldMRN,"HIS",x)) q:x=""  s line=^(x) d
 ....s x1=$o(^TDEB(newMRN,"HIS",""),-1)+1,^TDEB(newMRN,"HIS",x1)=line,^TDEB(newMRN,"HIS",0)=x1
 ...i $$delete^LVBDEB(oldMRN)
 q
]]></Routine>
</Export>
