<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF204UK01" type="INT" languagemode="0" generated="1"><![CDATA[
MIF204UK01(mi) ; ML 25/6/98 ; DADE Sysmex K1000
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000(stx,etx) q:'$l(record)
 d trace^MIF000(mi,record,"H<--M")
 s (sample,epis,surname,result,date,time,QC)=""
 i $e(record,3)="U" d A1
 i $e(record,3)="C" d A2
 i epis'="",result'="" d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
A1 ; process results record
 s epis=$e(record,11,22),epis=$tr(epis," "),epis=+epis,jj=0
 s res=$e(record,30,999),res1=""
 f j=1:5:86 s jj=jj+1 i $e(res,j)'="*" d C05(5)
 q
C05(jk) ; extract results
 s code=$p($t(C00)," ",jj+2),flag=$e($e(res,j,j+jk-1),jk) i 'flag s flag=""
 s res1=$tr($e(res,j,j+jk-2)," ")
 s result=result_code_$c(par10)_res1_$c(par10)_flag_$c(par11)
 q
A2 ; process QC record
 s epis=$e(record,15,15),epis=$tr(epis," "),QC=1
 s res=$e(record,18,119),res1=""
 f j=1:4:77 i $e(res,j)'="*" d
 .s code=$p($t(C01)," ",j\4+2),res1=$tr($e(res,j,j+3)," ")
 .s result=result_code_$c(par10)_res1_$c(par11)
 q
C00 ; WBC RBC HGB HCT MCV MCH MCHC PLT LY% x LY# x NE# RDW x x x
C01 ; 
 q
]]></Routine>
</Export>
