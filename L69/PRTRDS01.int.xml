<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTRDS01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTRDS01 ; ML 22/12/98 ; REPORTING DOCTOR STATISTICS
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. List of Test Sets
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Reporting Doctor Statistics"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Test Sets : "_$s($l(PAR3):TEST,1:"All")
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s TEST="" i $l(PAR3) f j=1:1:$l(PAR3,",") s test=$p(PAR3,",",j) d
 .i '$$selskip^LVBCTTS(test) s TEST=TEST_$s($l(TEST):",",1:"")_PLIST(4)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ...i $l(PAR3),(","_PAR3_",")'[(","_ts_",") q
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....s vtsRID=epis_"||"_ts_"||"_tscnt
 ....i '$$select^LVBVISTS(vtsRID) k VTS m VTS=PLIST i VTS(33)="A" d
 .....s user=$p(VTS(10),$c(1))
 .....i '$l(user) s user=$p(VTS(9),$c(1)) i '$$gettype^MVBSSUSR(user),PLIST(1)'="P" s user=""
 .....i $l(user) s ^TMP($zn,$j,"REPORT",user,ts)=$g(^TMP($zn,$j,"REPORT",user,ts))+1
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s user="" f  s user=$o(^TMP($zn,$j,"REPORT",user)) q:user=""  d
 ..i '$$selrow^MVBSSUSR(user) s xxx(1)=PLIST(3)_" ("_user_")"
 ..s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",user,ts)) q:ts=""  d
 ...s xxx(3)=^(ts) i '$$select^LVBCTTS(ts,"Y") s xxx(2)=PLIST(3)
 ...d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="RDS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
