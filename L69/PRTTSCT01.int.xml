<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTTSCT01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTTSCT01 ; ML 2/11/98 ; TEST SET COMPLETION TIME
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Site
 ;               4. Department
 ;               5. Test Set WorkGroup
 ;               6. Test Set
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Test Set Completion Time"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s($l(PAR3):SITE(3),1:"All")
 s line=4,xHEADER(line)=""
 i $l(PAR4) d
 .s line=line+1,xHEADER(line)="Department : "_DEP(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR5) d
 .s line=line+1,xHEADER(line)="Test WorkGroup : "_TWG(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR6) d
 .s line=line+1,xHEADER(line)="Test Set : "_TS(3)
 .s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR3_"-"_PAR4 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 i $l(PAR3),'$$select^LVBCTUSL(PAR3,"Y") k SITE m SITE=PLIST
 i $l(PAR4),'$$select^LVBCTDEP(PAR4,"Y") k DEP m DEP=PLIST
 i $l(PAR5),'$$select^LVBCTTWG(PAR5,"Y") k TWG m TWG=PLIST
 i $l(PAR6),'$$select^LVBCTTS(PAR6,"Y") k TS m TS=PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 i $l(PAR5) d
 .i $$open^LVBCTTWT(PAR5,"Y")
 .f  q:$$fetch^LVBCTTWT(PAR5,"Y")  s ^TMP($zn,$j,"TWG",PLIST(2))=""
 .i $$close^LVBCTTWT()
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s USite=$$seldata^LVBEPVIS(epis,66)
 ..s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ...i $l(PAR4),PAR4'=$e(ts) q
 ...i $l(PAR5),'$d(^TMP($zn,$j,"TWG",ts)) q
 ...i $l(PAR6),PAR6'=ts q
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ....s vtsRID=epis_"||"_ts_"||"_tscnt i '$$select^LVBVISTS(vtsRID,"Y"),PLIST(33)="A" d
 .....s site=$s($l(PLIST(29)):PLIST(29),1:USite)
 .....i $l(PAR3),PAR3'=site q
 .....i '$l(PAR3),site="" s site=" "
 .....s time=(PLIST(7)*24*3600+PLIST(8))-(PLIST(24)*24*3600+PLIST(25))
 .....s $p(^TMP($zn,$j,"REPORT",site,ts),"\",1)=$p($g(^TMP($zn,$j,"REPORT",site,ts)),"\",1)+time
 .....s $p(^TMP($zn,$j,"REPORT",site,ts),"\",2)=$p($g(^TMP($zn,$j,"REPORT",site,ts)),"\",2)+1
 .....s ^TMP($zn,$j,"REPORT",site,ts,0,time,vtsRID)=""
 ; find median
 s site="" f  s site=$o(^TMP($zn,$j,"REPORT",site)) q:site=""  d
 .s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",site,ts)) q:ts=""  d
 ..s (cnt,x1)="" f  s x1=$o(^TMP($zn,$j,"REPORT",site,ts,0,x1)) q:x1=""  d
 ...s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",site,ts,0,x1,x2)) q:x2=""  d
 ....s cnt=cnt+1
 ....s ^TMP($zn,$j,"REPORT",site,ts,1,cnt)=x1_"\"_x2
 ..s cnt=$s(cnt#2:cnt\2,1:cnt\2-1)
 ..s x="" f j=1:1:cnt s x=$o(^TMP($zn,$j,"REPORT",site,ts,1,x)) q:x=""  d
 ...s x1=$p(^(x),"\",1),x2=$p(^(x),"\",2)
 ...k ^TMP($zn,$j,"REPORT",site,ts,0,x1,x2)
 ...k ^TMP($zn,$j,"REPORT",site,ts,1,x)
 ..s x="" f j=1:1:cnt s x=$o(^TMP($zn,$j,"REPORT",site,ts,1,x),-1) q:x=""  d
 ...s x1=$p(^(x),"\",1),x2=$p(^(x),"\",2)
 ...k ^TMP($zn,$j,"REPORT",site,ts,0,x1,x2)
 ...k ^TMP($zn,$j,"REPORT",site,ts,1,x)
 ..s (cnt,time,x1)="" f  s x1=$o(^TMP($zn,$j,"REPORT",site,ts,0,x1)) q:x1=""  d
 ...s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",site,ts,0,x1,x2)) q:x2=""  s time=time+x1,cnt=cnt+1
 ..i cnt s $p(^TMP($zn,$j,"REPORT",site,ts),"\",3)=time/cnt
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s site="" f  s site=$o(^TMP($zn,$j,"REPORT",site)) q:site=""  d
 ..i '$$select^LVBCTUSL(site,"Y") s xxx(1)=PLIST(3)_" ("_site_")"
 ..s ts="" f  s ts=$o(^TMP($zn,$j,"REPORT",site,ts)) q:ts=""  d
 ...s time=$p(^(ts),"\"),xxx(4)=$p(^(ts),"\",2),xxx(5)=$p(^(ts),"\",3)
 ...i $l(xxx(4)) s xxx(3)=$j(time/xxx(4),0,1)
 ...f j=3,5 s xxx(j)=$j(xxx(j)/3600,0,1)
 ...i '$$select^LVBCTTS(ts,"Y") s xxx(2)=PLIST(3)_" ("_ts_")"
 ...d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="TSCT",EndLine=$$seldata^LVBCTCLR(report,9) i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
