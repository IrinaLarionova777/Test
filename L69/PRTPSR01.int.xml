<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTPSR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTPSR01 ; ML 7/10/99 ; PRODUCT vs SOURCE OF BLOOD UNIT
 q
 ; Parameters :  1. Site
 ;               2. Date From
 ;               3. Date To
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Product vs Source of Blood Unit"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s(PAR1="":"All Sites",1:$p(^TTAB("USLOC",PAR1),"^"))
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s date=PAR2-1 f  s date=$o(^BB("PK",0,"Date",date)) q:date=""  q:date>PAR3  d
 .s trans="" f  s trans=$o(^BB("PK",0,"Date",date,trans)) q:trans=""  d
 .. ; check for packs with a transaction of receive
 ..q:$p($g(^CT("TRANS",trans)),"^")'="R"
 ..s pack="" f  s pack=$o(^BB("PK",0,"Date",date,trans,pack)) q:pack=""  d
 ...q:$$select^BVBPACK1(pack)  k packdet m packdet=PLIST k PLIST
 ...s suppl=$p(packdet(4),$c(1)),prod=$p(packdet(8),$c(1)),loc=$p(packdet(12),$c(1))
 ...q:'prod  s site=$p($g(^CT("LO",+loc)),"^",3)
 ...i PAR1'="",site'=PAR1 q
 ...s:site="" site=" "
 ...s ^TMP($zn,$j,prod,site,suppl)=$g(^TMP($zn,$j,prod,site,suppl))+1
 q
PRINT ; print report
 k xxx f i=1:1:10 s xxx(i)=""
 s prod="" f  s prod=$o(^TMP($zn,$j,prod)) q:prod=""  d
 .s xxx(1)=$p($g(^CT("PR",prod)),"^",2)
 .s site="" f  s site=$o(^TMP($zn,$j,prod,site)) q:site=""  d
 ..s xxx(2)=$p($p($g(^TTAB("USLOC",site)),"^"),"\")
 ..s suppl="" f  s suppl=$o(^TMP($zn,$j,prod,site,suppl)) q:suppl=""  d
 ...s xxx(3)=$p($g(^CT("SU",suppl)),"^",2)
 ...s xxx(4)=^TMP($zn,$j,prod,site,suppl),tot=$g(tot)+xxx(4)
 ...d PRINT^PRTREP00(,.N)
 ...k xxx f i=1:1:10 s xxx(i)=""
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .i $o(^TMP($zn,$j,prod))'="" d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 s report="PVS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" d PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
