<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF432" type="INT" languagemode="0" generated="1"><![CDATA[
MIF432(mi) ; ML 29/6/98 ; ASTM protocol  -  ABBOTT Architect i2000 (Bi-Directional) ; QC - Yes
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4)
 s lf=$c(10),cr=$c(13),nak=$c(21),(result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  d  q
 .s CNT=0
 .i " L "[(" "_par9_" ") d loadlist^MIF000(mi) d:$d(^TMIF(mi,10)) Send q
 .i " BU "[(" "_par9_" ") d SCAN
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK
 .f  r *R:10 q:$c(R)=eot  q:R=-1  d
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",lf) q:'$l(record)
 ..s record=$e(record,1,$l(record)-1)
 ..d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
 .. ; check record validity
 ..s chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
 ..i $$CHSUM(chrecord)'=chsum d  q
 ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 ..d ACK
 .. ; query record
 ..i $e(record,2)="Q" d  q
 ...d Last
 ...s epis=$p($p(record,"|",3),"^",2)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="" q
 ...s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix q
 .. ; patient record
 ..i $e(record,2)="P" d  q
 ...d Last
 ...s surname=$tr($tr($p(record,"|",6)," "),"^"," "),surname=$p(surname,cr,1)
 .. ; order record
 ..i $e(record,2)="O" d  q
 ...d Last
 ...s epis=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
 ...s cup=$p($p(record,"|",4),"^",3)
 ... ; remove punctuation from QC id
 ...i QC="Q" s epis=$tr(epis,"-,.<>?/':;{[}]+-=)(*&^%$#@!|\ ",""),QC=cup_"\"_epis
 .. ; result record
 ..i $e(record,2)="R" d  q
 ...s x1=$tr($p($p(record,"|",3),"^",4)," ")_$tr($p($p(record,"|",3),"^",11)," "),x2=$tr($p(record,"|",4)," "),x3=$tr($p(record,"|",7)," ")
 ...i $l(x1),$l(x2) s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 ... ; date and time
 ...i $l($tr($p(record,"|",13)," ")) d
 ....s date=$$intdate^SSUTIL4($e($p(record,"|",13),5,6)_"/"_$e($p(record,"|",13),7,8)_"/"_$e($p(record,"|",13),1,4))
 ....s time=($e($p(record,"|",13),9,10)*3600)+($e($p(record,"|",13),11,12)*60)+$e($p(record,"|",13),13,14)
 .. ; last record
 ..i $e(record,2)="L" d Last q
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 .d ORDER
 q
Last ; file result if exist
 i $l(epis),$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)=""
 q
SCAN ; check all episodes for tests to be performed
 d scanall^MIF000(mi) q:'$d(^TMP("MIF-SCAN",$j,mi))
 s epis="" f  s epis=$o(^TMP("MIF-SCAN",$j,mi,epis)) q:epis=""  d
 .d PATDET(epis)
 .s mode="SCAN" d SET(epis,mode)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 .d Send
 .k ^TMP("MIF-SCAN",$j,mi,epis)
 q
 ; create list of orders if exists
ORDER s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  s suffix=$p(^(epis),"|") d
 .k ^TMP("MIF-SINGLE",$j,mi,epis)
 .d scanone^MIF000(mi,epis,,,suffix)
 .s mode="QUERY" d SET(epis,mode,suffix)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 .d Send
 .k ^TMP("MIF-SINGLE",$j,mi,epis),^TMP($zn,$j,"ENQ",epis)
 q
 ; create loadlist records
SAVE(mi) s CNT=$g(CNT)+1
 s line=$o(^TMIF(mi,10,""),-1) i line,^TMIF(mi,10,line)="L|1|F" k ^TMIF(mi,10,line)
 s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s sample=^TMP("MIF000",$j,"LL","SAMPLE")
 s mode="LOAD" d SET(epis,mode)
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 q 0
 ; set patient details record
PATDET(epis,CNT) s CNT=$g(CNT) i 'CNT s CNT=1
 i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 .s name="" i $l($g(PLIST(3)))!$l($g(PLIST(4))) s name=$e($g(PLIST(3)),1,20)_"^"_$e($g(PLIST(4)),1,20)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|"_CNT_"||||"_name_"|||"_sex_"|||||"_$g(PLIST(15))
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|"_CNT_"|"
 q
 ; set record to be sent to instrument
SET(epis,mode,suffix) s suffix=$g(suffix),CNT=$g(CNT)
 k tcx
 ; process a single episode scan
 i mode="QUERY",$d(^TMP("MIF-SINGLE",$j,mi,epis_suffix)) d
 .k test
 .s tc="" f  s tc=$o(^TMP("MIF-SINGLE",$j,mi,epis_suffix,tc)) q:tc=""  s test($e(tc,1,$l(tc)-1))="" 
 .s tc="" f j=1:1 s tc=$o(test(tc)) q:tc=""  s tcx(j\15)=$g(tcx(j\15)),tcx(j\15)=tcx(j\15)_$s($l(tcx(j\15)):"\",1:"")_"^^^"_tc
 ; process an all episode scan
 i mode="SCAN",$d(^TMP("MIF-SCAN",$j,mi,epis_suffix)) d
 .k test
 .s tc="" f  s tc=$o(^TMP("MIF-SCAN",$j,mi,epis_suffix,tc)) q:tc=""  s test($e(tc,1,$l(tc)-1))="" 
 .s tc="" f j=1:1 s tc=$o(test(tc)) q:tc=""  s tcx(j\15)=$g(tcx(j\15)),tcx(j\15)=tcx(j\15)_$s($l(tcx(j\15)):"\",1:"")_"^^^"_tc
 ; process a load list episode scan
 i mode="LOAD" d
 .k test
 .s tc="" f  s tc=$o(^TMP("MIF000",$j,"LL","TEST",tc)) q:tc=""  s test($e(tc,1,$l(tc)-1))="" 
 .s tc="" f j=1:1 s tc=$o(test(tc)) q:tc=""  s tcx(j\15)=$g(tcx(j\15)),tcx(j\15)=tcx(j\15)_$s($l(tcx(j\15)):"\",1:"")_"^^^"_tc
 i $d(tcx) d
 .d PATDET(epis,CNT)
 .s j="" f cnt=1:1 s j=$o(tcx(j)) q:j=""  d
 ..s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|"_cnt_"|"_epis_suffix_"||"_tcx(j)_"|||||||A||||||||||||||"_$s(mode="LOAD":"O",mode="SCAN":"O",1:"Q")
 i '$d(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="Q|1|^"_epis_suffix_"||^^^ALL||||||||X"
 q
Send ; send list of orders if exists
 w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M")
 f j=1:1:15 r *R:1 i $c(R)=ack!($c(R)=enq!($c(R)=nak)) q
 d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=nak:"NAK",1:R),"H<--M")
 i $c(R)=enq q
 i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 q:$$SEND("1H|\^&|||LABTRAK|||||||P|1")
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
NEXTTRAY(tray) 
 q tray
]]></Routine>
</Export>
