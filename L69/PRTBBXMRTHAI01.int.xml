<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBXMRTHAI01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBXMRTHAI01 ; IG 01/05/99 ; BloodBank XMatch report
 q
 ; Parameters :  1. vtsRID
 ;               2. Event : U-Update
 ;                           -Authorize
 ;               3. User
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j),^TMP("PRTDR00",$j),^TMP("REPORT",$j)
 k xHEADER d INIT,BODY
 k ^TMP($zn,$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
BODY ; create report
 s vtsRID=PAR1,epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2)
 k VISTE i '$$getall^LVBVISTE(vtsRID) d
 .f j=1:1:PLIST d
 ..i $p($p($p(PLIST(j),$c(2),4),$c(28),12),$c(1))'="Y" q
 ..s tr=$p($p($p(PLIST(j),$c(2),4),$c(28),5),$c(1)) i $l(tr) s tr=$s(tr="CI":"CI",1:" "),x=$o(VISTE(tr,""),-1)+1,VISTE(tr,x)=$p(PLIST(j),$c(2),4)
 i '$d(VISTE) q
 k VISTD i '$$results^LVBVISTS(vtsRID,,,,"Y") m VISTD=PLIST
 k VISTS i '$$select^LVBVISTS(vtsRID) m VISTS=PLIST
 k VISIT i '$$select^LVBEPVIS(epis) m VISIT=PLIST
 ; get doctor information
 s doc=$p(VISIT(15),$c(1)) k DOC i '$$select^LVBCTDR(doc,"Y") m DOC=PLIST
 s dep="",tsLM=0,tsWD=80
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 s tsWD=80 i '$$select^MVBCFLAB(1) d
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 s tr="" f  s tr=$o(VISTE(tr)) q:tr=""  d BODY1(tr),PRINT
 q
 ; tr : CI  - Direct Issue
 ;      XM  - X-Match
 ;      I   - Issue
 ;      MXM - Minor X-Match
BODY1(tr) s tr=$g(tr)
 ; print xmatch grid report
 s $p(LINE,"_",85)="",TEXT="",UL=LINE,cntline="" 
 s header=$s(tr="CI":"Direct Issue",1:"Cross Match Report")
 d LN^PRTDR00()
 d ADD^PRTDR00(tsLM+$s(PAR1="DI":40,1:30),$c(27)_"-1"_header_$c(27)_"-0"),LN^PRTDR00()
 i $l(VISIT(69)) d ADD^PRTDR00(tsLM,"Group: "_$p(VISIT(69),$c(1),2))
 f j=1:1:VISTD i $p(VISTD(j),$c(1),2)="B0016" d ADD^PRTDR00(tsLM+60,"G/M : "_$p(VISTD(j),$c(1),3)) q
 d LN^PRTDR00()
 f j=1:1:2 d LN^PRTDR00()
 ; switch to Condensed
 d ADD^PRTDR00(,$c(15))
 ; print pack details
 d LN^PRTDR00()
 d ADD^PRTDR00(10,"+---------------------------------------------------------------------------------------------------------------------------------+"),LN^PRTDR00()
 d ADD^PRTDR00(10,"|           Product           |    Bag Number    |      Bag Group      |  Transfusion  |    Hold Date    | Compatibility | Status |"),LN^PRTDR00()
 d ADD^PRTDR00(10,"|                             |                  |                     | Reaction(Y/N) |                 |               |        |"),LN^PRTDR00()
 d ADD^PRTDR00(10,"|-----------------------------+------------------+---------------------+---------------+-----------------+---------------+--------|"),LN^PRTDR00()
 s ind="" f  s ind=$o(VISTE(tr,ind)) q:ind=""  d
 .f j=1:1:$l(VISTE(tr,ind),$c(28)) s @("x"_j)=$p(VISTE(tr,ind),$c(28),j)
 .k PACK i '$$select^LVBBBP($p(x2,$c(1))_"||"_$p(x3,$c(1))) m PACK=PLIST
 .d ADD^PRTDR00(10,"|"_$e($p(PACK(8),$c(1),2),1,29))
 .d ADD^PRTDR00(40,"|"_$p(PACK(2),$c(1)))
 .d ADD^PRTDR00(59,"|"_$p(PACK(9),$c(1),2))
 .d ADD^PRTDR00(81,"|")
 .d ADD^PRTDR00(97,"|"_$$extdate2^SSUTIL4(x8)_" "_$$exttime^SSUTIL4(x9))
 .d ADD^PRTDR00(115,"|"_$s(x4="C":"Compatible",x4="A":"Acceptable",x4="I":"Incompatible",1:""))
 .d ADD^PRTDR00(131,"| "_$s($p(x5,$c(1))="CI":"CI",$p(x5,$c(1))="I":"I",$p(x5,$c(1))="MXM":"MXM",1:"IP"))
 .d ADD^PRTDR00(140,"|"),LN^PRTDR00()
 .d
 ..i $o(VISTE(tr,ind))'="" d ADD^PRTDR00(10,"|-----------------------------+------------------+---------------------+---------------+-----------------+---------------+--------|"),LN^PRTDR00() q
 ..d ADD^PRTDR00(10,"+---------------------------------------------------------------------------------------------------------------------------------+"),LN^PRTDR00()
 ; back to Normal
 d ADD^PRTDR00(,$c(18)),LN^PRTDR00()
 ; print transfusion messages
 d LN^PRTDR00()
 ; create page for printing
 k PLIST m PLIST=^TMP("PRTDR00",$j,"RESULT") s PLIST=$o(PLIST(""),-1)
 d
 .n PAR1,PAR2
 .s PAR1="C",PAR2="XMR"
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1) i $l(%routine) x "d INIT^"_%routine
 k ^TMP("PRTDR00",$j)
 s page=1,^TMP("REPORT",$j,page,"TEST",vtsRID)=""
 s PageLine="" f jLine=1:1:PLIST d
 .i PLIST(jLine)="!" s PageLine=PageLine+1 i PageLine>MaxLine s PageLine="",page=page+1
 .s x=$o(^TMP("REPORT",$j,page,"TEXT",""),-1)+1,^TMP("REPORT",$j,page,"TEXT",x)=PLIST(jLine)
 ; save patient events
 d events^LVBCOM01(epis,"XMR","Report type "_tr_"-"_$g(REPID))
 q
PRINT ; print report
 ; xPAR1 - U-User
 ;         C-Courier
 ; xPAR2 - User RowID/Courier
 s xPAR1="C",xPAR2="XMR"
 s page="" f  s page=$o(^TMP("REPORT",$j,page)) q:page=""  d  k ^TMP("REPORT",$j,page)
 . ; print page Header
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 .i $l(%routine) x "d Header^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 . ; print report
 .f j=1:1 q:'$d(^TMP("REPORT",$j,page,"TEXT",j))  s xx=^(j) d ADDLINE^PRTREP00(REPID,"PRT",xx)
 . ; print page Footer
 .s vtsRID=PAR1,user=$s(PAR2="":PAR3,1:"")
 .s %routine=$p($g(^TTAB("REPORT-GENERIC","DREP0")),"\",1)
 .i $l(%routine) x "d Footer^"_%routine_"("""_page_""","""_xPAR1_""","""_xPAR2_""")"
 q
]]></Routine>
</Export>
