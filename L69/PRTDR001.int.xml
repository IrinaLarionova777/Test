<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR001" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR001 ; IG 23/03/99 ; Doctor's Report HEADER - TrakSystems
 q
 ; supported calls
 ;
 ; INIT^PRTDR001
 ; QDR^PRTDR001
 ; Header^PRTDR001(page,par1,par2)
 ; Footer^PRTDR001(page,par1,par2)
 ; ABScreen^PRTDR001()
 ;
ABScreen() q "X0015"
 ; client specific parameters
INIT s MaxLine=40 i $e($$CR(PAR2),1,2)="A5" s MaxLine=17
 q
CR(CR) n (CR) s xCR=CR,result="A4" i xCR["-FAX-" s xCR=$p(CR,"-FAX-",1)
 i '$$select^LVBCTCR(xCR),$e($p(PLIST(4),$c(1)),1,2)="A5" s result="A5" 
 q result
 ; print header
Header(page,par1,par2) n (page,par1,par2,VISIT,DOC,HOSP,REPID)
 d ADD^PRTREP00(REPID,"PRT","#")
 s (copyto,copyfrom,specimen)="",epis=$p(VISIT(2),$c(1))
 ; find copy-to
 i $d(DOC),$p(VISIT(15),$c(1))=$p(DOC(2),$c(1)) d
 .i $$open^LVBVISDC(epis)
 .f  q:$$fetch^LVBVISDC(epis)  s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 .i $$close^LVBVISDC()
 ; find copy-to
 i $d(HOSP),$p(VISIT(22),$c(1))=$p(HOSP(2),$c(1)) d
 .i $$open^LVBVISDC(epis)
 .f  q:$$fetch^LVBVISDC(epis)  s copyto=copyto_$s($l(copyto):", ",1:"")_$p(PLIST(4),$c(1))
 .i $$close^LVBVISDC()
 ; find copy-from
 i $d(DOC),$p(VISIT(15),$c(1))'=$p(DOC(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 i $d(HOSP),$p(VISIT(22),$c(1))'=$p(HOSP(2),$c(1)) d
 .i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") s copyfrom=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 ; format Doctor's address
 i $d(DOC) d Address(DOC(9),DOC(10),DOC(11),DOC(12),DOC(13),.dAddress)
 i $d(HOSP) d Address(HOSP(4),HOSP(5),HOSP(6),HOSP(7),HOSP(8),.hAddress)
 ; format Patient's address
 d Address(VISIT(32),VISIT(33),VISIT(34),VISIT(35),VISIT(36),.pAddress)
 s dob="Unknown" i $l(VISIT(6)) s dob=$$extdate2^SSUTIL4(VISIT(6))
 s sex="Unknown" i $l(VISIT(5)) s sex=$p(VISIT(5),$c(1),2)
 s age="Unknown" i $l(VISIT(26)) s age=$p(VISIT(26),$c(1))
 s cl1=$p(VISIT(53),"|",1),cl2=$p(VISIT(53),"|",2)
 i $l(cl1)>60 s N=$l($e(cl1,1,60)," ")-1,cl1=$p(cl1," ",1,N),cl2=$p($p(VISIT(53),"|",1)," ",N+1,99)_$s($l(cl2):" "_cl2,1:"")
 i $l(cl2)>60 s N=$l($e(cl2,1,60)," ")-1,cl2=$p(cl2," ",1,N)
 s xCR=$$CR(par2)
 i $e(xCR,2)=4 d Header4
 i $e(xCR,2)=5 d Header5
 q
Header4 i '$d(^PR(REPID,"HDR")) d QDR("WPS")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v2b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1100h930V"_$$NameFormat^LVBCOM03("PT","",VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101)))
 i $d(DOC) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3690h930V"_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51)))
 i $d(HOSP) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3690h930V"_HOSP(3))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0b4148T")
 f j=1:1:3 d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1100h"_(1050+((j-1)*120))_"V"_pAddress(j))
 .i $d(DOC) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3690h"_(1050+((j-1)*120))_"V"_dAddress(j))
 .i $d(HOSP) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3690h"_(1050+((j-1)*120))_"V"_hAddress(j))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1100h1450V"_dob)
 i $l(copyto) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v1b4148T"_$c(27)_"(s1S")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3000h1550VCopy to :")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0b4148T"_$c(27)_"(s0S")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3600h1550V"_copyto)
 i $l(copyfrom) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v1b4148T"_$c(27)_"(s1S")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2840h1550VRequested by :")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0b4148T"_$c(27)_"(s0S")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3600h1550V"_copyfrom)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1100h1700V"_cl1)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h1800V"_cl2)
 s ST1=VISIT(68),ST2=""
 i $l(ST1)>25 s N=$l($e(ST1,1,25)," ")-1,ST1=$p(ST1," ",1,N),ST2=$p(VISIT(68)," ",N+1,99)
 i $l(ST2)>25 s N=$l($e(ST2,1,25)," ")-1,ST2=$p(ST2," ",1,N)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1250h2000V"_ST1)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1250h2100V"_ST2)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3600h2000V"_$$extdate2^SSUTIL4(VISIT(12)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h2000V"_$$datechk(VISIT(2)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3600h2100V"_VISIT(2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4700h2100V"_VISIT(16))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h2400V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
Header5 i '$d(^PR(REPID,"HDR")) d QDR("WPS1")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p9v2b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h175V"_$$NameFormat^LVBCOM03("PT","",VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101)))
 i $d(DOC) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h175V"_$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3),DOC(49),DOC(50),DOC(51)))
 i $d(HOSP) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h175V"_HOSP(3))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0b4148T")
 f j=1,2 d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h"_(275+((j-1)*100))_"V"_pAddress(j))
 .i $d(DOC) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h"_(275+((j-1)*100))_"V"_dAddress(j))
 .i $d(HOSP) d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h"_(275+((j-1)*100))_"V"_hAddress(j))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h475V"_dob)
 i $l(copyto) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v1b4148T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3400h475VCopy to :")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0b4148T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h475V"_copyto)
 i $l(copyfrom) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v1b4148T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3400h475VReferred by :")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0b4148T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h475V"_copyfrom)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a880h575V"_cl1)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a880h675V"_cl2)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a900h3150V"_VISIT(68))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4800h575V"_$$extdate2^SSUTIL4(VISIT(12)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4800h675V"_$$datechk(VISIT(2)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3780h575V"_VISIT(2))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3780h675V"_VISIT(16))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v2b4148T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4300h3150V"_$$NameFormat^LVBCOM03("PT","",VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h680V")
 d ADD^PRTREP00(REPID,"PRT","!")
 q
 ; Tests to Follow
TTF(epis) n (epis) s epis=$g(epis),result=""
 s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 . ; do not look at empty tests
 .i '$d(^TTAB("TS",ts,0)) q
 . ; do not look at non printable tests
 .s (ok,x)="" f  s x=$o(^TTAB("TS",ts,0,x)) q:x=""  i $p(^(x),"\",8)'="",$p(^(x),"\",13)="Y" s ok=1 q
 .i ok s cnt="" f  s cnt=$o(^TEPI(epis,1,ts,cnt)) q:cnt=""  d
 ..s rowid=epis_"||"_ts_"||"_cnt,status=""
 ..s status=$$seldata^LVBVISTS(rowid,31),test=$$seldata^LVBCTTS($$seldata^LVBVISTS(rowid,3),4)
 ..i $p(status,$c(1))'="Y" s result=result_$s($l(result):" ",1:"")_test
 q result
 ; print orginal reported date and time
datechk(epis) n (epis) s epis=$g(epis),(date,time,result)=""
 i $$open^LVBVISTS(epis,,"Y")
 f  q:$$fetch^LVBVISTS(epis,,"Y")  i $l(PLIST(7)) d
 .i '$l(date) s date=PLIST(7),time=PLIST(8) q
 .i PLIST(7)>date s date=PLIST(7),time=PLIST(8) q
 .i PLIST(7)=date,PLIST(8)>time s date=PLIST(7),time=PLIST(8) q
 i $$close^LVBVISTS()
 i $l(date) s result=$$dt(date,time)
 q result
dt(date,time) n (date,time) s date=$g(date),time=$g(time),result=""
 s result=$$extdate2^SSUTIL4(date)_"  "_$e(time\3600+100,2,3)_":"_$e(time\60#60+100,2,3)
 q result
 ; print footer
Footer(page,par1,par2) n (page,par1,par2,REPID,VISIT)
 s xCR=$$CR(par2)
 i $o(^TMP("REPORT",$j,page))="" d
 .i $p(VISIT(29),$c(1))="F" d
 ..i $e(xCR,2)=4 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1800h7480V")
 ..i $e(xCR,2)=5 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2000h3000V")
 ..d ADD^PRTREP00(REPID,"PRT","**** FINAL REPORT. Please File ****")
 .i $p(VISIT(29),$c(1))'="F" d
 ..i $e(xCR,2)=4 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1800h7400V")
 ..i $e(xCR,2)=5 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2000h2920V")
 ..d ADD^PRTREP00(REPID,"PRT","**** INTERIM REPORT. Please File ****")
 ..d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0b4148T")
 ..s TTF=$$TTF(VISIT(2)) i $l(TTF) d
 ...i $e(xCR,2)=4 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a300h7480V")
 ...i $e(xCR,2)=5 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2000h3000V")
 ...d ADD^PRTREP00(REPID,"PRT",TTF_" : To Follow")
 i $o(^TMP("REPORT",$j,page))'="" d
 .i $e(xCR,2)=4 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3700h7480V")
 .i $e(xCR,2)=5 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2300h3000V")
 .d ADD^PRTREP00(REPID,"PRT","Continued Over...")
 q
QDR(QDR) s x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d ADD^PRTREP00(REPID,"HDR",^(x))
 q
Address(x1,x2,x3,x4,x5,Address) n (x1,x2,x3,x4,x5,Address) k Address s cnt=0 f j=1:1:3 s Address(j)=""
 i $l(x1) s cnt=cnt+1,Address(cnt)=x1
 i $l(x2) s cnt=cnt+1,Address(cnt)=x2
 s cnt=cnt+1
 i $l(x3) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):",",1:"")_x3
 i $l(x4) s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):",",1:"")_x4
 i $l(x5) d
 .i cnt>1,'$l(Address(cnt)) s cnt=cnt-1
 .s Address(cnt)=Address(cnt)_$s($l(Address(cnt)):",",1:"")_x5
 q
]]></Routine>
</Export>
