<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBVIS0" type="INT" languagemode="0" generated="1"><![CDATA[
LVBVIS0 ; IG 6/5/98 ; MVB Control data access for EP_VisitNumber
 ;
 ; supported calls:
 ;
 ; $$open^LVBVIS0(user,param,sort,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetch^LVBVIS0(user,param,sort,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetchbuffer^LVBVIS0(user,param,sort,p3,p4,p5,p6,p7,p8,p9)
 ; $$fetchall^LVBVIS0(user,param,sort,p3,p4,p5,p6,p7,p8,p9)
 ; $$close^LVBVIS0()
 ; $$select^LVBVIS0(epis1,epis2,param)
 ; $$alias^LVBVIS0(mrn)
 ;
alias(mrn) n (mrn,PLIST) s mrn=$g(mrn) k ^TMP($zn,$j)
 s x1="" f  s x1=$o(^TDEBi(mrn,"ALIAS",x1)) q:x1=""  d
 .s x2="" f  s x2=$o(^TDEBi(mrn,"ALIAS",x1,x2)) q:x2=""  d
 ..s x3="" f  s x3=$o(^TDEBi(mrn,"ALIAS",x1,x2,x3)) q:x3=""  d
 ...s name="" i '$$select^LVBEPVIS(x3,"Y") s name=$$NameFormat^LVBCOM03("PT",$p(PLIST(7),$c(1),2),PLIST(4),PLIST(3),PLIST(99),PLIST(100),PLIST(101))
 ...i '$l(name) s name=" "
 ...s ^TMP($zn,$j,0,name,x3)=""
 k PLIST s x1="" f  s x1=$o(^TMP($zn,$j,0,x1)) q:x1=""  d
 .s (str,x2)="" f  s x2=$o(^TMP($zn,$j,0,x1,x2)) q:x2=""  s str=str_$s($l(str):",",1:"")_x2
 .s cnt=$o(PLIST(""),-1)+1,PLIST(cnt)=x1_$c(2)_x1_$c(2,2)_x1_$c(28)_str
 s PLIST=$o(PLIST(""),-1)
 k ^TMP($zn,$j)
 q 0
 ; param : 1 - Accept Hospital MRN as HRN (Y/N)
 ;         2 - MRN to be accepted as HRN
 ;         3 - MRN to be accepted as MRN
select(epis1,epis2,param,epis3,data) n (epis1,epis2,param,epis3,data,PLIST) k ^TMP($zn,$j)
 s epis1=$g(epis1),epis2=$g(epis2),param=$g(param),epis3=$g(epis3),data=$g(data)
 k CFLAB i '$$select^MVBCFLAB(1) m CFLAB=PLIST
 k PLIST i '$$select^LVBEPVIS(epis1) m ^TMP($zn,$j,"epis1")=PLIST
 k PLIST i '$$select^LVBEPVIS(epis2) m ^TMP($zn,$j,"epis2")=PLIST
 k HOSP i '$$select^LVBHOSP(epis3) m HOSP=PLIST
 i $l(data),'$d(HOSP) f j=1:1:$l(data,"^") s HOSP(j)=$p(data,"^",j)
 i $d(HOSP) d
 .i $l($g(HOSP(5))) s ^TMP($zn,$j,"epis3",3)=HOSP(5)
 .i $l($g(HOSP(4))) s ^TMP($zn,$j,"epis3",4)=HOSP(4)
 .i $l($g(HOSP(8))) s ^TMP($zn,$j,"epis3",5)=HOSP(8)
 .i $l($g(HOSP(7))) s ^TMP($zn,$j,"epis3",6)=HOSP(7)
 .i $l($g(HOSP(6))) s ^TMP($zn,$j,"epis3",7)=HOSP(6)
 .i $l($g(HOSP(24))) s ^TMP($zn,$j,"epis3",8)=HOSP(24)
 .i $l($g(HOSP(38))) s ^TMP($zn,$j,"epis3",9)=HOSP(38)
 .i $l($g(HOSP(17))) s ^TMP($zn,$j,"epis3",15)=HOSP(17)
 .i $l($g(HOSP(1))),HOSP(1)'["||" s ^TMP($zn,$j,"epis3",20)=HOSP(1)
 .i $l($g(HOSP(15))) s ^TMP($zn,$j,"epis3",22)=HOSP(15)
 .i $l($g(HOSP(9))) s ^TMP($zn,$j,"epis3",32)=HOSP(9)
 .i $l($g(HOSP(10))) s ^TMP($zn,$j,"epis3",33)=HOSP(10)
 .i $l($g(HOSP(11))) s ^TMP($zn,$j,"epis3",34)=HOSP(11)
 .i $l($g(HOSP(31))) s ^TMP($zn,$j,"epis3",35)=HOSP(31)
 .i $l($g(HOSP(12))) s ^TMP($zn,$j,"epis3",36)=HOSP(12)
 .i $l($g(HOSP(13))) s ^TMP($zn,$j,"epis3",43)=HOSP(13)
 .i $l($g(HOSP(32))) s ^TMP($zn,$j,"epis3",44)=HOSP(32)
 .i $l($g(HOSP(21))) s ^TMP($zn,$j,"epis3",47)=HOSP(21)
 .i $l($g(HOSP(34))) s ^TMP($zn,$j,"epis3",51)=HOSP(34)
 .i $l($g(HOSP(33))) s ^TMP($zn,$j,"epis3",60)=HOSP(33)
 .i $l($g(HOSP(3)))!$l($g(HOSP(2))) s ^TMP($zn,$j,"epis3",65)=$g(HOSP(3))_","_$g(HOSP(2))
 .i $l($g(HOSP(3)))!$l($g(HOSP(2))) s ^TMP($zn,$j,"epis3",67)=$g(HOSP(2))
 .i $l($g(HOSP(28))) s ^TMP($zn,$j,"epis3",66)=HOSP(28)
 .i $l($g(HOSP(35))) s ^TMP($zn,$j,"epis3",73)=HOSP(35)
 .i $l($g(HOSP(37))) s ^TMP($zn,$j,"epis3",88)=HOSP(37)
 .i $l($g(HOSP(36))) s ^TMP($zn,$j,"epis3",89)=HOSP(36)
 .i $l($g(HOSP(39))) s ^TMP($zn,$j,"epis3",99)=HOSP(39)
 .i $l($g(HOSP(40))) s ^TMP($zn,$j,"epis3",100)=HOSP(40)
 .i $l($g(HOSP(41))) s ^TMP($zn,$j,"epis3",101)=HOSP(41)
 .i $l($g(HOSP(43))) s ^TMP($zn,$j,"epis3",108)=HOSP(43)
 .i $l($g(HOSP(44))) s ^TMP($zn,$j,"epis3",106)=HOSP(44)
 .i $l($g(HOSP(45))) s ^TMP($zn,$j,"epis3",107)=HOSP(45)
 . ; add All Extra info
 .i $l($g(HOSP(23))) f j=3:1:$l(HOSP(23),"|") i $l($p(HOSP(23),"|",j)) s ^TMP($zn,$j,"epis4",j)=$p(HOSP(23),"|",j)
 s PatFields=""
 s $p(PatFields,",",3)="<PAT_S_NAME>"
 s $p(PatFields,",",4)="<PAT_G_NAME>"
 s $p(PatFields,",",5)="<PAT_SEX>"
 s $p(PatFields,",",6)="<PAT_DOB>"
 s $p(PatFields,",",7)="<PAT_TITLE>"
 s $p(PatFields,",",8)="<MEDICARE_NO>"
 s $p(PatFields,",",9)="<PAY_CODE>"
 s $p(PatFields,",",15)="<DOC_CODE>"
 s $p(PatFields,",",16)="<EX_UR_NO>"
 s $p(PatFields,",",18)="<INIT_CODE>"
 s $p(PatFields,",",22)="<HOS_LOC>"
 s $p(PatFields,",",23)="<PAY_CODE_REF>"
 s $p(PatFields,",",25)="<VIS_ACT_CODE>"
 s $p(PatFields,",",27)="<CTR_CODE>"
 s $p(PatFields,",",28)="<COLL_BY>"
 s $p(PatFields,",",32)="<PAT_ADDR>"
 s $p(PatFields,",",33)="<PAT_ADDR>"
 s $p(PatFields,",",34)="<PAT_SUBURB>"
 s $p(PatFields,",",35)="<PAT_STATE>"
 s $p(PatFields,",",36)="<PAT_PCODE>"
 s $p(PatFields,",",37)="<BILL_ADDR>"
 s $p(PatFields,",",38)="<BILL_ADDR>"
 s $p(PatFields,",",39)="<BILL_SUB>"
 s $p(PatFields,",",40)="<BILL_STATE>"
 s $p(PatFields,",",41)="<BILL_PCODE>"
 s $p(PatFields,",",42)="<BILL_NAME>"
 s $p(PatFields,",",43)="<PAT_H_PH>"
 s $p(PatFields,",",44)="<PAT_W_PH>"
 s $p(PatFields,",",47)="<HOS_A_DATE>"
 s $p(PatFields,",",48)="<EXTRA_1>"
 s $p(PatFields,",",49)="<EXTRA_2>"
 s $p(PatFields,",",51)="<PAT_SPEC>"
 s $p(PatFields,",",60)="<PAT_ETHNIC>"
 s $p(PatFields,",",63)="<PAT_PREG>"
 s $p(PatFields,",",64)="<PAT_WEEKS>"
 s $p(PatFields,",",66)="<USR_LOC>"
 s $p(PatFields,",",73)="<HOS_O_DATE>"
 s $p(PatFields,",",74)="<BILL_LOC>"
 s $p(PatFields,",",75)="<FAX_DOC>"
 f j=3:1:10 s $p(PatFields,",",73+j)="<EXTRA_"_j_">"
 s $p(PatFields,",",84)="<PAT_CONFID>"
 s $p(PatFields,",",85)="<DOC_COPIES>"
 s $p(PatFields,",",88)="<PENS_NO>"
 s $p(PatFields,",",89)="<DVA_NO>"
 s $p(PatFields,",",99)="<EXTRANAME_1>"
 s $p(PatFields,",",100)="<EXTRANAME_2>"
 s $p(PatFields,",",101)="<EXTRANAME_3>"
 s $p(PatFields,",",105)="<DEAD>"
 s $p(PatFields,",",106)="<HEALTHFUND_TABLE>"
 s $p(PatFields,",",107)="<HEALTHFUND_NO>"
 s $p(PatFields,",",108)="<MEDICARE_REF>"
 s $p(PatFields,",",109)="<OUTBREAK_CASE>"
 f j=11:1:15 s $p(PatFields,",",103+j)="<EXTRA_"_j_">"
 s $p(PatFields,",",125)="<REQUEST_TYPE>"
 s $p(PatFields,",",126)="<NATIONAL_ID>"
 s $p(PatFields,",",127)="<NATIONAL_ID_TYPE>"
 s $p(PatFields,",",128)="<ANIMAL_BREED>"
 f j=1:1:28 s $p(PatFields,",",130+j)="<PC_EXTRA_"_j_">"
 s $p(PatFields,",",162)="<DISCOUNT>"
 f j=3:1:9,15,20,22,32:1:36,43,44,47,51,60,65:1:67,73,88,89,99:1:101,106:1:108 d
 .i '$l($g(^TMP($zn,$j,"epis1",j))),$l($g(^TMP($zn,$j,"epis3",j))) s ^TMP($zn,$j,"epis1",j)=^TMP($zn,$j,"epis3",j)
 ; retrieve extra fields
 s j="" f  s j=$o(^TMP($zn,$j,"epis4",j)) q:j=""  i '$l($g(^TMP($zn,$j,"epis1",j))) s ^TMP($zn,$j,"epis1",j)=^TMP($zn,$j,"epis4",j)
 f j=3:1:9,15,16,18,22,23,25,27,28,32:1:44,47:1:49,51,60,63,64,66,73:1:85,88,89,99:1:101,105:1:109,114:1:118,125:1:128,131:1:158,162 d
 .i (","_CFLAB(74)_",")'[(","_$p(PatFields,",",j)_",") q
 .i '$l($g(^TMP($zn,$j,"epis1",j))),$l($g(^TMP($zn,$j,"epis2",j))) s ^TMP($zn,$j,"epis1",j)=^TMP($zn,$j,"epis2",j)
 i '$l($g(^TMP($zn,$j,"epis1",2))) f j=1,2 s ^TMP($zn,$j,"epis1",j)=epis1
 i '$l($g(^TMP($zn,$j,"epis1",20))) f j=20 s ^TMP($zn,$j,"epis1",j)=$g(^TMP($zn,$j,"epis2",j))
 ; check Doctor's Defaults
 s xx=$g(^TMP($zn,$j,"epis1",15)) i $l(xx),'$$select^LVBCTDR(xx,"Y") d
 . ; Payment code
 .i $l(PLIST(17)),'$l($g(^TMP($zn,$j,"epis1",9))) s ^TMP($zn,$j,"epis1",9)=PLIST(17)
 . ; Priority
 .i $l(PLIST(29)),'$l($g(^TMP($zn,$j,"epis1",17))) s ^TMP($zn,$j,"epis1",17)=PLIST(29)
 . ; Initiation code
 .i $l(PLIST(42)),'$l($g(^TMP($zn,$j,"epis1",18))) s ^TMP($zn,$j,"epis1",18)=PLIST(42)
 . ; Collection Centre
 .i $l(PLIST(43)),'$l($g(^TMP($zn,$j,"epis1",27))) s ^TMP($zn,$j,"epis1",27)=PLIST(43)
 . ; Billing name
 .i $l(PLIST(47)),'$l($g(^TMP($zn,$j,"epis1",42))) s ^TMP($zn,$j,"epis1",42)=PLIST(47)
 . ; Billing location
 .i $l(PLIST(48)),'$l($g(^TMP($zn,$j,"epis1",74))) s ^TMP($zn,$j,"epis1",74)=PLIST(48)
 ; check Billing address
 s xx=$g(^TMP($zn,$j,"epis1",42)) i $l(xx) d
 .f j=37:1:41 i $l($g(^TMP($zn,$j,"epis1",j))) s xx="" q
 .i $l(xx),'$$select^LVBCTAD(xx,"Y") f j=4:1:8 s ^TMP($zn,$j,"epis1",33+j)=PLIST(j)
 ; check LabConfig Defaults
 d
 . ; Date of Collection
 .i CFLAB(8)="Y",'$l($g(^TMP($zn,$j,"epis1",12))) s ^TMP($zn,$j,"epis1",12)=+$h
 . ; Fasting
 .i $l(CFLAB(3)),'$l($g(^TMP($zn,$j,"epis1",19))) s ^TMP($zn,$j,"epis1",19)=CFLAB(3)
 . ; Initiation code
 .i $l(CFLAB(4)),'$l($g(^TMP($zn,$j,"epis1",18))) s ^TMP($zn,$j,"epis1",18)=CFLAB(4)
 . ; Collected by
 .i $l(CFLAB(5)),'$l($g(^TMP($zn,$j,"epis1",28))) s ^TMP($zn,$j,"epis1",28)=CFLAB(5)
 . ; Priority
 .i $l(CFLAB(2)),'$l($g(^TMP($zn,$j,"epis1",17))) s ^TMP($zn,$j,"epis1",17)=CFLAB(2)
 . ; Date of Receipt
 .i CFLAB(6)="Y",'$l($g(^TMP($zn,$j,"epis1",61))) s ^TMP($zn,$j,"epis1",61)=+$h
 . ; Date of Request
 .i CFLAB(7)="Y",'$l($g(^TMP($zn,$j,"epis1",14))) s ^TMP($zn,$j,"epis1",14)=+$h
 i $p(param,"\",1)="Y",$l($p(param,"\",2)) s $p(^TMP($zn,$j,"epis1",65),",",2)=$p(param,"\",2)
 i $p(param,"\",1)="Y",$l(epis3) s $p(^TMP($zn,$j,"epis1",65),",",1)=$p(epis3,"||",3)
 i '$l($g(^TMP($zn,$j,"epis1",20))),$l($p(param,"\",3)) s ^TMP($zn,$j,"epis1",20)=$p(param,"\",3)
 k PLIST m PLIST=^TMP($zn,$j,"epis1")
 s PLIST=$o(PLIST(""),-1)
 k ^TMP($zn,$j)
 ; get client specific init parameters
 s cc=$p($g(^CF("SM",1)),"^",28) i $l(cc) s LineRoutine="select^"_$zn_cc i $l($t(@LineRoutine)) x "d "_LineRoutine
 q 0
 ; param : 1-03.Surname
 ;         2-03.Given Name
 ;         3-  .Sex
 ;         4-05.DOB
 ;         5-  .MRN
 ;         6-10.Doctor
 ;         7-46.Doctor reference
 ;         8-42.Date From
 ;         9-42.Date To
 ;        10-  .Test Set
 ;        11-44.Hospital Location
 ;        12-  .Medicare No
 ;        13-  .VA No
 ;        14-  .Pensioner No
 ;        15-  .Last Only (Y/N)
 ;        16-  .Soundex   (Y/N)
 ;        17-47.Post Code
 ;        18-  .Patient Status
 ;        19-  .User Site
 ;        20-  .Episode
 ;        21-  .Worksheet Name
 ;        22-  .Worksheet No
 ;        23-  .Department
 ;        24-  .List of Episodes by $c(1)
 ;        25-  .Donor ID
 ;        26-  .DonorSearch (Y/N)
 ;        27-  .List of Hospital_Episodes by $c(1)
 ;        28-  .Search ALL names
 ;        29-03.Extra name 1
 ;        30-03.Extra name 2
 ;        31-03.Extra name 3
 ;        32-  .QC case
 ;        33-  .Study case
 ;        34-  .DB Accession number
 ;        35-  .Aerobic bottle
 ;        36-  .Anaerobic bottle
 ;        37-  .Natioanal ID type_"|"_National ID
 ;        38-  .Request type
 ;        39-  .ToView
 ;        40-  .ToRead
open(user,param,sort,p3,p4,p5,p6,p7,p8,p9) s user=$g(user),param=$g(param),sort=$g(sort)
 k x,^TMP($zn,$j,"open"),^TMP($zn,$j,"fetch"),^TMP($zn,$j,"buffer"),^TMP($zn,$j,"order"),^TMP($zn,$j,"sort"),^TMP($zn,$j,"SORT")
 i $p(param,"\",5)="{New}" s $p(param,"\",5)=""
 f j=1:1:40 s x(j)=$p(param,"\",j)
 i $l(x(5)),'$d(^TDEB(x(5))),'$$selcode^LVBDEBHN(x(5)) s x(5)=$p(PLIST(1),"||")
 i $l(x(10)),'$d(^TTAB("TS",x(10))) s x(10)=$$select^LVBCTSY(x(10),"TS",x(19))
 f j=1,2,7,12,13,14,17,20,29,30,31,34 s x(j)=$$ALPHAUP^SSUTIL4(x(j))
 f j=35,36 s x(j)=$$UPPER^SSUTIL4(x(j))
 f j=37 s x(j)=$$ALPHAUP^SSUTIL4($p(x(j),"|",1))_"|"_$$ALPHAUP^SSUTIL4($p(x(j),"|",2))
 i x(37)="|" s x(37)=""
 s j="" f  s j=$o(x(j)) q:j=""  s ^TMP($zn,$j,"open",j)=x(j)
 i '$$getall^MVBSSPE() f j=1:1:PLIST s xx=$p($p(PLIST(j),$c(2),4),$c(28),1) i $l(xx) s ^TMP($zn,$j,"order",xx)=$p($p(PLIST(j),$c(2),4),$c(28),6)
 f j=1,2,29:1:31 s ^TMP($zn,$j,"sort",j)=3
 f j=8,9 s ^TMP($zn,$j,"sort",j)=42
 s ^TMP($zn,$j,"sort",6)=10,^TMP($zn,$j,"sort",7)=46
 s ^TMP($zn,$j,"sort",11)=44,^TMP($zn,$j,"sort",17)=47,^TMP($zn,$j,"sort",4)=5
 ; find search order
 s order(1)=99999 i $l(x(8)),$l(x(9)) s xx=$g(^TMP($zn,$j,"sort",8)) i $l(xx) s order(1)=$g(^TMP($zn,$j,"order",xx))
 s order(2)=99999 i $l(x(6)) s xx=$g(^TMP($zn,$j,"sort",6)) i $l(xx) s order(2)=$g(^TMP($zn,$j,"order",xx))
 s order(3)=99999 i $l(x(7)) s xx=$g(^TMP($zn,$j,"sort",7)) i $l(xx) s order(3)=$g(^TMP($zn,$j,"order",xx))
 s order(4)=99999 i $l(x(1))!$l(x(2))!$l(x(29))!$l(x(30))!$l(x(31)) s xx=$g(^TMP($zn,$j,"sort",1)) i $l(xx) s order(4)=$g(^TMP($zn,$j,"order",xx))
 s order(5)=99999 i $l(x(4)) s xx=$g(^TMP($zn,$j,"sort",4)) i $l(xx) s order(5)=$g(^TMP($zn,$j,"order",xx))
 s order(6)=99999 i $l(x(11)) s xx=$g(^TMP($zn,$j,"sort",11)) i $l(xx) s order(6)=$g(^TMP($zn,$j,"order",xx))
 s order(7)=99999 i $l(x(17)) s xx=$g(^TMP($zn,$j,"sort",17)) i $l(xx) s order(7)=$g(^TMP($zn,$j,"order",xx))
 k ^TMP($zn,$j,"sort") f j=1:1:7 s x=$s($l(order(j)):order(j),1:99999),^TMP($zn,$j,"sort",x,j)=""
 ; sort in Reverse order
 i sort="Y" d
 .s sort="" f  q:$$fetch(user,param,sort)  d
 ..s xSort=(100000-PLIST(12))_" "_$e(100000+100000-PLIST(13),2,6)
 ..s ^TMP($zn,$j,"SORT",xSort,PLIST(2))=""
 .k ^TMP($zn,$j,"fetch")
 q 0
 ;
 ; PreDefined sorting order
fetch00 f j=1,2 s y(j)=$g(y(j))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TMP($zn,$j,"SORT",y(1))),y(2)="" q:y(1)=""
 .f  s y(2)=$o(^TMP($zn,$j,"SORT",y(1),y(2))) q:y(2)=""  s epis=y(2) q
 q
 ; List of Hospital_Episodes
fetch01 f j=1 s y(j)=$g(y(j))+1
 i $l($p(x(27),$c(1),y(1))) d
 .f j=1:1:3 s @("z"_j)=$p($p(x(27),$c(1),y(1)),"||",j)
 .i $d(^THOS(z1,"PATIENT",z2,z3)) s epis=$p(x(27),$c(1),y(1))
 q
 ; List of Episodes
fetch02 f j=1 s y(j)=$g(y(j))+1
 i $l($p(x(24),$c(1),y(1))),$d(^TEPI($p(x(24),$c(1),y(1)))) s epis=$p(x(24),$c(1),y(1))
 q
 ; Episode search
fetch03 i $d(^TEPI(x(20))) d
 .f j=1 s y(j)=$g(y(j))
 .i $l(y(1)) s (epis,y(1))="" q
 .s (y(1),epis)=x(20)
 i '$d(^TEPI(x(20))) d
 .f j=1 s y(j)=$g(y(j))
 ..f  s y(1)=$o(^TIND(13,x(20),y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; DonorID search
fetch04 s x(15)="" f j=1,2 s y(j)=$g(y(j))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TDEBj("DONOR",x(25),y(1))),y(2)="" q:y(1)=""
 .f  s y(2)=$o(^TDEBj("DONOR",x(25),y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; WorkSheet Name/No
fetch05 s x(15)="" f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TWKS(x(21),2,x(22),"EP",y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; MRN search
fetch06 f j=1,2,3 s y(j)=$g(y(j))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TDEBi(x(5),"DATE",y(1)),-1),(y(2),y(3))="" q:y(1)=""
 .f  d:$l(y(2))  q:$l(epis)  s y(2)=$o(^TDEBi(x(5),"DATE",y(1),y(2)),-1),y(3)="" q:y(2)=""
 ..f  s y(3)=$o(^TDEBi(x(5),"DATE",y(1),y(2),y(3))) q:y(3)=""  i '$$val(.x,y(3),user) s epis=y(3) q
 q
 ; future MRN search
fetch07 f j=1,2 s y(j)=$g(y(j))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(6,x(5),y(1))),y(2)="" q:y(1)=""
 .f  s y(2)=$o(^TIND(6,x(5),y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Medicare No
fetch08 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$e($s($l(y(1)):y(1),1:x(12))_"0000000000",1,10)
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(1,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(12)))'=x(12)
 .f  s y(2)=$o(^TIND(1,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; VA No
fetch09 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(13))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(9,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(13)))'=x(13)
 .f  s y(2)=$o(^TIND(9,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Pensioner No
fetch10 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(14))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(10,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(14)))'=x(14)
 .f  s y(2)=$o(^TIND(10,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Date From/To
fetch11 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(8))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TDAY(1,y(1))),y(2)="" q:y(1)=""  q:y(1)>x(9)
 .f  s y(2)=$o(^TDAY(1,y(1),x(18),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Doctor
fetch12 f j=1,2 s y(j)=$g(y(j))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(3,x(6),y(1)),-1),y(2)="" q:y(1)=""
 .f  s y(2)=$o(^TIND(3,x(6),y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Doctor Reference
fetch13 f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TIND(4,x(7),y(1))) i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; Any Name - Normal
fetch14 f j=1,2,3 s y(j)=$g(y(j))
 f  d:$e(y(1))=1  q:$l(epis)  s y(1)=$o(^TIND("Name",y(1))),(y(2),y(3))="" q:y(1)=""
 .s y(2)=$s($l(y(2)):y(2),1:x(1))
 .f  d:$l(y(2))  q:$l(epis)  s y(2)=$o(^TIND("Name",y(1),y(2))),y(3)="" q:y(2)=""  q:$e(y(2),1,$l(x(1)))'=x(1)
 ..f  s y(3)=$o(^TIND("Name",y(1),y(2),y(3))) q:y(3)=""  w !,$$val(.x,y(3),user)," ",y(3) i '$$val(.x,y(3),user) s epis=y(3) q
 q
 ; Any Name - Soundex
fetch15 f j=1,2,3 s y(j)=$g(y(j))
 f  d:$e(y(1))=2  q:$l(epis)  s y(1)=$o(^TIND("Name",y(1))),(y(2),y(3))="" q:y(1)=""
 .s y(2)=$s($l(y(2)):y(2),1:$$valid^LVBCOM07(x(1)))
 .i $l(y(2)) f  s y(3)=$o(^TIND("Name",y(1),y(2),y(3))) q:y(3)=""  i '$$val(.x,y(3),user) s epis=y(3) q
 q
 ; Surname/Given Name - Normal
fetch16 f j=1,2,3,4 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(1))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(0,y(1))),(y(2),y(3),y(4))="" q:y(1)=""  q:$e(y(1),1,$l(x(1)))'=x(1)
 .s y(2)=$s($l(y(2)):y(2),1:x(2))
 .f  d:$l(y(2))  q:$l(epis)  s y(2)=$o(^TIND(0,y(1),y(2))),(y(3),y(4))="" q:y(2)=""  q:$e(y(2),1,$l(x(2)))'=x(2)
 ..s y(3)=$s($l(y(3)):y(3),1:x(3))
 ..f  d:$l(y(3))  q:$l(epis)  s y(3)=$o(^TIND(0,y(1),y(2),y(3))),y(4)="" q:y(3)=""  q:$e(y(3),1,$l(x(3)))'=x(3)
 ...f  s y(4)=$o(^TIND(0,y(1),y(2),y(3),0,y(4))) q:y(4)=""  i '$$val(.x,y(4),user) s epis=y(4) q
 q
 ; Given Name - Normal
fetch17 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(2))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Name",10,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(2)))'=x(2)
 .f  s y(2)=$o(^TIND("Name",10,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Surname - Normal
fetch18 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(1))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Name",11,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(1)))'=x(1)
 .f  s y(2)=$o(^TIND("Name",11,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Extra Name_1 - Normal
fetch19 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(29))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Name",12,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(29)))'=x(29)
 .f  s y(2)=$o(^TIND("Name",12,y(1),y(2))) q:y(2)=""  w !,$$val(.x,y(2),user)," ",y(2) i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Extra Name_2 - Normal
fetch20 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(30))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Name",13,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(30)))'=x(30)
 .f  s y(2)=$o(^TIND("Name",13,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Extra Name_3 - Normal
fetch21 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(31))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Name",14,y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(31)))'=x(31)
 .f  s y(2)=$o(^TIND("Name",13,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Surname/Given Name - Soundex
fetch22 f j=1,2,3 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:$$valid^LVBCOM07(x(1)))
 s y(2)=$s($l(y(2)):y(2),1:$$valid^LVBCOM07(x(2)))
 i $l(y(1)),$l(y(2)) f  s y(3)=$o(^TIND(12,y(1),y(2),y(3))) q:y(3)=""  i '$$val(.x,y(3),user) s epis=y(3) q
 q
 ; Given Name - Soundex
fetch23 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:$$valid^LVBCOM07(x(2)))
 i $l(y(1)) f  s y(2)=$o(^TIND("Name",20,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Surname - Soundex
fetch24 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:$$valid^LVBCOM07(x(1)))
 i $l(y(1)) f  s y(2)=$o(^TIND("Name",21,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Extra Name_1 - Soundex
fetch25 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:$$valid^LVBCOM07(x(29)))
 i $l(y(1)) f  s y(2)=$o(^TIND("Name",22,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Extra Name_2 - Soundex
fetch26 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:$$valid^LVBCOM07(x(30)))
 i $l(y(1)) f  s y(2)=$o(^TIND("Name",23,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Extra Name_3 - Soundex
fetch27 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:$$valid^LVBCOM07(x(31)))
 i $l(y(1)) f  s y(2)=$o(^TIND("Name",24,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; DOB
fetch28 f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TIND(7,x(4),y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; Hospital Location
fetch29 f j=1,2 s y(j)=$g(y(j))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(8,x(11),y(1)),-1),y(2)="" q:y(1)=""
 .f  s y(2)=$o(^TIND(8,x(11),y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Post Code
fetch30 f j=1,2 s y(j)=$g(y(j))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND(11,x(17),y(1))),y(2)="" q:y(1)=""
 .f  s y(2)=$o(^TIND(11,x(17),y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; QC case
fetch32 f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TIND("Case-QC",x(32),y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; Study case
fetch33 f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TIND("Case-Study",x(33),y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; DB Accession number
fetch34 f j=1,2 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(34))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^THIRi("Accession",y(1))),y(2)="" q:y(1)=""  q:$e(y(1),1,$l(x(34)))'=x(34)
 .f  s y(2)=$o(^THIRi("Accession",y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Aerobic bottle
fetch35 f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TIND(14,x(35),y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; Anaerobic bottle
fetch36 f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TIND(15,x(36),y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; National ID
fetch37 f j=1 s y(j)=$g(y(j))
 f  s y(1)=$o(^TIND(18,$p(x(37),"|",1),$p(x(37),"|",2),y(1))) q:y(1)=""  i '$$val(.x,y(1),user) s epis=y(1) q
 q
 ; ToView/ToRead
fetch39(xx) f j=1,2 s y(j)=$g(y(j))
 ;s y(1)=$s($l(y(1)):y(1),$l(x(8)):x(8),1:+$h)
 s y(1)=$s($l(y(1)):y(1),$l(x(8)):x(8),1:0)
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TEPIi(xx,y(1))),y(2)="" q:y(1)=""  q:y(1)>x(9)
 .f  s y(2)=$o(^TEPIi(xx,y(1),y(2))) q:y(2)=""  i '$$val(.x,y(2),user) s epis=y(2) q
 q
 ; Surname/Extra Name 1 - Normal
fetch40 f j=1,2,3 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(1))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Names",12,y(1))),(y(2),y(3))="" q:y(1)=""  q:$e(y(1),1,$l(x(1)))'=x(1)
 .s y(2)=$s($l(y(2)):y(2),1:x(29))
 .f  d:$l(y(2))  q:$l(epis)  s y(2)=$o(^TIND("Names",12,y(1),y(2))),y(3)="" q:y(2)=""  q:$e(y(2),1,$l(x(29)))'=x(29)
 ..f  s y(3)=$o(^TIND("Names",12,y(1),y(2),y(3))) q:y(3)=""  i '$$val(.x,y(3),user) s epis=y(3) q
 q
 ; Surname/Extra Name 2 - Normal
fetch41 f j=1,2,3 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(1))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Names",13,y(1))),(y(2),y(3))="" q:y(1)=""  q:$e(y(1),1,$l(x(1)))'=x(1)
 .s y(2)=$s($l(y(2)):y(2),1:x(30))
 .f  d:$l(y(2))  q:$l(epis)  s y(2)=$o(^TIND("Names",13,y(1),y(2))),y(3)="" q:y(2)=""  q:$e(y(2),1,$l(x(30)))'=x(30)
 ..f  s y(3)=$o(^TIND("Names",13,y(1),y(2),y(3))) q:y(3)=""  i '$$val(.x,y(3),user) s epis=y(3) q
 q
 ; Surname/Extra Name 3 - Normal
fetch42 f j=1,2,3 s y(j)=$g(y(j))
 s y(1)=$s($l(y(1)):y(1),1:x(1))
 f  d:$l(y(1))  q:$l(epis)  s y(1)=$o(^TIND("Names",14,y(1))),(y(2),y(3))="" q:y(1)=""  q:$e(y(1),1,$l(x(1)))'=x(1)
 .s y(2)=$s($l(y(2)):y(2),1:x(31))
 .f  d:$l(y(2))  q:$l(epis)  s y(2)=$o(^TIND("Names",14,y(1),y(2))),y(3)="" q:y(2)=""  q:$e(y(2),1,$l(x(31)))'=x(31)
 ..f  s y(3)=$o(^TIND("Names",14,y(1),y(2),y(3))) q:y(3)=""  i '$$val(.x,y(3),user) s epis=y(3) q
 q
fetch(user,param,sort,p3,p4,p5,p6,p7,p8,p9) s user=$g(user),param=$g(param),sort=$g(sort)
 i $p(param,"\",5)="{New}" s $p(param,"\",5)=""
 k x s j="" f  s j=$o(^TMP($zn,$j,"open",j)) q:j=""  s x(j)=^TMP($zn,$j,"open",j)
 k y s j="" f  s j=$o(^TMP($zn,$j,"fetch",j)) q:j=""  s y(j)=^TMP($zn,$j,"fetch",j)
 s epis="" d
 .
 . ; predefined sorting order
 .i sort="Y" d fetch00 q
 .
 . ; ToView
 .i x(39)="Y" d fetch39("ToView") s found=1 q
 . ; ToRead
 .i x(40)="Y" d fetch39("ToRead") s found=1 q
 .
 . ; List of Hospital_Episodes
 .i $l(x(27)) d fetch01 q
 .
 . ; List of Episodes
 .i $l(x(24)) d fetch02 q
 .
 . ; Episode search
 .i $l(x(20)) d fetch03 q
 .
 . ; DB Accession number search
 .i $l(x(34)) d fetch34 q
 .
 . ; DonorID search
 .i $l(x(25)) d fetch04 q
 .
 . ; WorkSheet Name/No
 .i $l(x(21)),$l(x(22)) d fetch05 q
 .
 . ; MRN search
 .i $l(x(5)),'x(18) d fetch06 q
 .
 . ; future MRN search
 .i $l(x(5)),x(18) d fetch07 q
 .
 . ; Medicare No
 .i $l(x(12)) d fetch08 q
 .
 . ; VA No
 .i $l(x(13)) d fetch09 q
 .
 . ; Pensioner No
 .i $l(x(14)) d fetch10 q
 .
 . ; QC case
 .i $l(x(32)) d fetch32 q
 .
 . ; Study case
 .i $l(x(33)) d fetch33 q
 .
 . ; National IDs
 .i $l($p(x(37),"|",1)),$l($p(x(37),"|",2)) d fetch37 q
 .
 .s (found,x1)="" f  s x1=$o(^TMP($zn,$j,"sort",x1)) q:x1=""  d  q:found
 ..s x2="" f  s x2=$o(^TMP($zn,$j,"sort",x1,x2)) q:x2=""  d  q:found
 ...
 ... ; Date From/To
 ...i x2=1,$l(x(8)),$l(x(9)) d fetch11 s found=1 q
 ...
 ... ; Doctor
 ...i x2=2,$l(x(6)) d fetch12 s found=1 q
 ...
 ... ; Doctor Reference
 ...i x2=3,$l(x(7)) d fetch13 s found=1 q
 ...
 ... ; Any Name - Normal
 ...i x2=4,$l(x(1)),x(16)'="Y",x(28)="Y" d fetch14 s found=1 q
 ...
 ... ; Any Name - Soundex
 ...i x2=4,$l(x(1)),x(16)="Y",x(28)="Y" d fetch15 s found=1 q
 ...
 ... ; Surname/Given Name - Normal
 ...i x2=4,x(16)'="Y",$l(x(1)),$l(x(2)),x(28)'="Y" d fetch16 s found=1 q
 ...
 ... ; Surname/Extra Name 1 - Normal
 ...i x2=4,x(16)'="Y",$l(x(1)),$l(x(29)),x(28)'="Y" d fetch40 s found=1 q
 ...
 ... ; Surname/Extra Name 2 - Normal
 ...i x2=4,x(16)'="Y",$l(x(1)),$l(x(30)),x(28)'="Y" d fetch41 s found=1 q
 ...
 ... ; Surname/Extra Name 3 - Normal
 ...i x2=4,x(16)'="Y",$l(x(1)),$l(x(31)),x(28)'="Y" d fetch42 s found=1 q
 ...
 ... ; Given Name - Normal
 ...i x2=4,$l(x(2)),x(16)'="Y",x(28)'="Y" d fetch17 s found=1 q
 ...
 ... ; Surname - Normal
 ...i x2=4,$l(x(1)),x(16)'="Y",x(28)'="Y" d fetch18 s found=1 q
 ...
 ... ; Extra Name_1 - Normal
 ...i x2=4,$l(x(29)),x(16)'="Y",x(28)'="Y" d fetch19 s found=1 q
 ...
 ... ; Extra Name_2 - Normal
 ...i x2=4,$l(x(30)),x(16)'="Y",x(28)'="Y" d fetch20 s found=1 q
 ...
 ... ; Extra Name_3 - Normal
 ...i x2=4,$l(x(31)),x(16)'="Y",x(28)'="Y" d fetch21 s found=1 q
 ...
 ... ; Surname/Given Name - Soundex
 ...i x2=4,x(16)="Y",$l(x(1)),$l(x(2)),x(28)'="Y" d fetch22 s found=1 q
 ...
 ... ; Given Name - Soundex
 ...i x2=4,x(16)="Y",$l(x(2)),x(28)'="Y" d fetch23 s found=1 q
 ...
 ... ; Surname - Soundex
 ...i x2=4,x(16)="Y",$l(x(1)),x(28)'="Y" d fetch24 s found=1 q
 ...
 ... ; Extra Name_1 - Soundex
 ...i x2=4,x(16)="Y",$l(x(29)),x(28)'="Y" d fetch25 s found=1 q
 ...
 ... ; Extra Name_2 - Soundex
 ...i x2=4,x(16)="Y",$l(x(30)),x(28)'="Y" d fetch26 s found=1 q
 ...
 ... ; Extra Name_3 - Soundex
 ...i x2=4,x(16)="Y",$l(x(31)),x(28)'="Y" d fetch27 s found=1 q
 ...
 ... ; DOB
 ...i x2=5,$l(x(4)) d fetch28 s found=1 q
 ...
 ... ; Hospital Location
 ...i x2=6,$l(x(11)) d fetch29 s found=1 q
 ...
 ... ; Post Code
 ...i x2=7,$l(x(17)) d fetch30 s found=1 q
 s SQLCODE=100
 i $l(epis) d  i SQLCODE>50 g fetch+1
 .s j="" f  s j=$o(y(j)) q:j=""  s ^TMP($zn,$j,"fetch",j)=y(j)
 .i epis'["||",'$$select^LVBEPVIS(epis,,user) s PLIST(68)=$$CT068^at42(epis,user)
 .i epis["||",'$$select^LVBHOSP(epis) d
 ..k plist m plist=PLIST k PLIST
 ..s (PLIST(1),PLIST(2))=epis
 ..s PLIST(3)=plist(5)
 ..s PLIST(4)=plist(4)
 ..s PLIST(5)=plist(8)
 ..s PLIST(6)=plist(7)
 ..s PLIST(7)=plist(6)
 ..s PLIST(8)=plist(24)
 ..s PLIST(9)=plist(38)
 ..s PLIST(12)=plist(26)
 ..s PLIST(13)=plist(27)
 ..s PLIST(15)=plist(17)
 ..s PLIST(22)=plist(15)
 ..s PLIST(32)=plist(9)
 ..s PLIST(33)=plist(10)
 ..s PLIST(34)=plist(11)
 ..s PLIST(35)=plist(31)
 ..s PLIST(36)=plist(12)
 ..s PLIST(43)=plist(13)
 ..s PLIST(44)=plist(32)
 ..s PLIST(47)=plist(21)
 ..s PLIST(51)=plist(34)
 ..s PLIST(60)=plist(33)
 ..s PLIST(65)=plist(3)_","_plist(2)
 ..s PLIST(66)=plist(28)
 ..s PLIST(68)=plist(30)
 ..s PLIST(73)=plist(35)
 ..s PLIST(88)=plist(37)
 ..s PLIST(89)=plist(36)
 ..s PLIST(99)=plist(39)
 ..s PLIST(100)=plist(40)
 ..s PLIST(101)=plist(41)
 ..s str="" f j=1:1:plist s $p(str,$c(28),j)=plist(j)
 ..s PLIST(200)=str
 s PLIST=$o(PLIST(""),-1)
 q SQLCODE
 ; Fetch all data
fetchbuffer(user,param,sort,p3,p4,p5,p6,p7,p8,p9) k ^TMP($zn,$j,1) s user=$g(user),param=$g(param),sort=$g(sort)
 s max=$s($g(PLIST(1)):PLIST(1),1:10)
 f xxx=1:1:max s SQLCODE=$$fetch(user,param,sort) q:SQLCODE  d
 .s cnt=$o(^TMP($zn,$j,1,""),-1)+1 f j=1:1:PLIST s $p(^TMP($zn,$j,1,cnt),$c(28),j)=$g(PLIST(j))
 k PLIST m PLIST=^TMP($zn,$j,1) k ^TMP($zn,$j,1)
 s PLIST=$o(PLIST(""),-1)
 q SQLCODE
fetchall(user,param,sort,p3,p4,p5,p6,p7,p8,p9) N (user,param,sort,PLIST) K ^TMP($zn,$j,1)
 s user=$g(user),param=$g(param),sort=$g(sort)
 i $$open(user,param,sort)
 f cnt=1:1 Q:$$fetch(user,param,sort)  s ^TMP($zn,$j,1,cnt)="" f j=1:1:PLIST s $p(^TMP($zn,$j,1,cnt),$c(28),j)=$g(PLIST(j))
 i $$close()
 k PLIST M PLIST=^TMP($zn,$j,1)
 s PLIST=$o(PLIST(""),-1)
 k ^TMP($zn,$j,1)
 q 0
 ;
close() k ^TMP($zn,$j,"open"),^TMP($zn,$j,"fetch"),^TMP($zn,$j,"buffer"),^TMP($zn,$j,"order"),^TMP($zn,$j,"sort"),^TMP($zn,$j,"SORT")
 q 0
 ; return : 0 - valid
 ;          1 - invalid
val(x,epis,user) n (x,epis,user) s ok=0
 i $l(epis) d
 .i '$d(^TEPI(epis)) s ok=100 q
 .s xx=$g(^TEPI(epis)),xx0=$g(^TEPI(epis,0))
 .i $l(x(3)),$p(xx,"\",3)'=x(3) s ok=3 q
 .i $l(x(4)),$p(xx,"\",4)'=x(4) s ok=4 q
 .i $l(x(5)),$p(xx,"\",18)'=x(5) s ok=5 q
 .i $l(x(6)),$p(xx,"\",13)'=x(6) s ok=6 q
 .i $l(x(7)),$$ALPHAUP^SSUTIL4($p(xx,"\",14))'=x(7) s ok=7 q
 .i $l(x(8)),$p(xx,"\",10)<x(8) s ok=8 q
 .i $l(x(9)),$p(xx,"\",10)>x(9) s ok=9 q
 .i $l(x(10)),'$d(^TEPI(epis,1,x(10))) s ok=10 q
 .i $l(x(11)),$p(xx,"\",20)'=x(11) s ok=11 q
 .i $l(x(12)),$e($$ALPHAUP^SSUTIL4($p(xx,"\",6)),1,$l(x(12)))'=x(12) s ok=12 q
 .i $l(x(13)),$e($$ALPHAUP^SSUTIL4($p(xx,"\",46)),1,$l(x(13)))'=x(13) s ok=13 q
 .i $l(x(14)),$e($$ALPHAUP^SSUTIL4($p(xx,"\",45)),1,$l(x(14)))'=x(14) s ok=14 q
 .i x(15)="Y" s mrn=$p(xx,"\",18) i $l(mrn) s ok=15 i $p($g(^TDEB(mrn)),"\",2)=epis s ok=0
 .i x(15)="Y",ok q
 .i $l(x(17)),$p(xx0,"\",5)'=x(17) s ok=17 q
 .i $l(x(18)),$p(xx,"\",44)>x(18) s ok=18 q
 .i $l(x(19)),$p(xx,"\",36)'=x(19) s ok=19 q
 .i $l(x(21)),$l(x(22)),'$d(^TWKS(x(21),2,x(22),"EP",epis)) s ok=21 q
 .i $l(x(23)),$e($o(^TEPI(epis,1,x(23))))'=x(23) s ok=23 q
 .i $l(x(32)),$p(xx0,"\",27)'=x(32) s ok=32 q
 .i $l(x(33)),$p(xx0,"\",26)'=x(33) s ok=33 q
 .i $l(x(35)),$$UPPER^SSUTIL4($p(xx,"\",52))'=x(35) s ok=35 q
 .i $l(x(36)),$$UPPER^SSUTIL4($p(xx,"\",53))'=x(36) s ok=36 q
 .i $l(x(37)),($p(xx,"\",57)'=$p(x(37),"|",1))!($p(xx,"\",56)'=$p(x(37),"|",2)) s ok=37 q
 .i $l(x(38)),$p(xx,"\",55)'=x(38) s ok=38 q
 .i x(26)="Y",'$l($p(^TEPI(epis,0),"\",18)) s ok=26 q
 .i $l(x(34)) s ok=34 d  i ok q
 ..s x="" f  s x=$o(^THIR(epis,x)) q:x=""  i $e(x,1,$l(x(34)))=x(34) s ok=0 q
 .i x(28)'="Y" d  i ok q
 ..i x(16)'="Y" d
 ...i $l(x(1)),$e($$ALPHAUP^SSUTIL4($p(xx,"\",1)),1,$l(x(1)))'=x(1) s ok=1 q
 ...i $l(x(2)),$e($$ALPHAUP^SSUTIL4($p(xx,"\",2)),1,$l(x(2)))'=x(2) s ok=2 q
 ...i $l(x(29)),$e($$ALPHAUP^SSUTIL4($p(xx0,"\",23)),1,$l(x(29)))'=x(29) s ok=29 q
 ...i $l(x(30)),$e($$ALPHAUP^SSUTIL4($p(xx0,"\",24)),1,$l(x(30)))'=x(30) s ok=30 q
 ...i $l(x(31)),$e($$ALPHAUP^SSUTIL4($p(xx0,"\",25)),1,$l(x(31)))'=x(31) s ok=31 q
 ..i x(16)="Y" d
 ...i $l(x(1)),$$valid^LVBCOM07($p(xx,"\",1))'=$$valid^LVBCOM07(x(1)) s ok=1 q
 ...i $l(x(2)),$$valid^LVBCOM07($p(xx,"\",2))'=$$valid^LVBCOM07(x(2)) s ok=2 q
 ...i $l(x(29)),$$valid^LVBCOM07($p(xx0,"\",23))'=$$valid^LVBCOM07(x(29)) s ok=29 q
 ...i $l(x(30)),$$valid^LVBCOM07($p(xx0,"\",24))'=$$valid^LVBCOM07(x(30)) s ok=30 q
 ...i $l(x(31)),$$valid^LVBCOM07($p(xx0,"\",25))'=$$valid^LVBCOM07(x(31)) s ok=31 q
 .i x(28)="Y" d  i ok q
 ..i x(16)'="Y" d
 ...i $l(x(1)) s ok=1 d
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",1)),1,$l(x(1)))=x(1) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",2)),1,$l(x(1)))=x(1) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",23)),1,$l(x(1)))=x(1) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",24)),1,$l(x(1)))=x(1) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",25)),1,$l(x(1)))=x(1) s ok="" q
 ...i $l(x(2)) s ok=2 d
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",1)),1,$l(x(2)))=x(2) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",2)),1,$l(x(2)))=x(2) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",23)),1,$l(x(2)))=x(2) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",24)),1,$l(x(2)))=x(2) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",25)),1,$l(x(2)))=x(2) s ok="" q
 ...i $l(x(29)) s ok=29 d
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",1)),1,$l(x(29)))=x(29) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",2)),1,$l(x(29)))=x(29) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",23)),1,$l(x(29)))=x(29) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",24)),1,$l(x(29)))=x(29) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",25)),1,$l(x(29)))=x(29) s ok="" q
 ...i $l(x(30)) s ok=30 d
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",1)),1,$l(x(30)))=x(30) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",2)),1,$l(x(30)))=x(30) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",23)),1,$l(x(30)))=x(30) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",24)),1,$l(x(30)))=x(30) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",25)),1,$l(x(30)))=x(30) s ok="" q
 ...i $l(x(31)) s ok=31 d
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",1)),1,$l(x(31)))=x(31) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx,"\",2)),1,$l(x(31)))=x(31) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",23)),1,$l(x(31)))=x(31) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",24)),1,$l(x(31)))=x(31) s ok="" q
 ....i $e($$ALPHAUP^SSUTIL4($p(xx0,"\",25)),1,$l(x(31)))=x(31) s ok="" q
 ..i x(16)="Y" d
 ...i $l(x(1)) s ok=1,sx=$$valid^LVBCOM07(x(1)) d
 ....i $$valid^LVBCOM07($p(xx,"\",1))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx,"\",2))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",23))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",24))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",25))=sx s ok="" q
 ...i $l(x(2)) s ok=2,sx=$$valid^LVBCOM07(x(2)) d
 ....i $$valid^LVBCOM07($p(xx,"\",1))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx,"\",2))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",23))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",24))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",25))=sx s ok="" q
 ...i $l(x(29)) s ok=29,sx=$$valid^LVBCOM07(x(29)) d
 ....i $$valid^LVBCOM07($p(xx,"\",1))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx,"\",2))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",23))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",24))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",25))=sx s ok="" q
 ...i $l(x(30)) s ok=30,sx=$$valid^LVBCOM07(x(30)) d
 ....i $$valid^LVBCOM07($p(xx,"\",1))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx,"\",2))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",23))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",24))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",25))=sx s ok="" q
 ...i $l(x(31)) s ok=31,sx=$$valid^LVBCOM07(x(31)) d
 ....i $$valid^LVBCOM07($p(xx,"\",1))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx,"\",2))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",23))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",24))=sx s ok="" q
 ....i $$valid^LVBCOM07($p(xx0,"\",25))=sx s ok="" q
 .i $$ValUser^LVBEPVIS(user,epis)=60 s ok=60 q
 q ok
]]></Routine>
</Export>
