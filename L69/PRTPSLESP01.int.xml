<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTPSLESP01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTPSLESP01 ; ML 20/10/98 ; Packing slip
 q
 ; Parameters :  1. Packing slip Number
 ;               2. From User Site
 ;               3. To User Site
 ;               4. To Refferal Lab
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 i '$l(PAR2) d  q
 .s p2="" f  s p2=$o(^TPSL(PAR1,p2)) q:p2=""  d
 ..i $l(PAR3),$d(^TPSL(PAR1,p2,"US",PAR3)) d START^PRTREP00(USER,,PAR1_"\"_p2_"\"_PAR3_"\"_PAR4,"PSL")
 ..i $l(PAR4),$d(^TPSL(PAR1,p2,"RL",PAR4)) d START^PRTREP00(USER,,PAR1_"\"_p2_"\"_PAR3_"\"_PAR4,"PSL")
 s line=1,xHEADER(line)="Informe de envío "_PAR1
 s line=2,xHEADER(line)=""
 i $l(PAR2) d
 .s line=line+1,xHEADER(line)="Sede de Origen : "_FROM(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR3) d
 .s line=line+1,xHEADER(line)="Sede de Destino : "_TO(3)
 .s line=line+1,xHEADER(line)=""
 i $l(PAR4) d
 .s line=line+1,xHEADER(line)="Al Laboratorio de Referencia : "_TO(3)
 .s line=line+1,xHEADER(line)=""
 d BODY
 i CNT1 d
 .s line=line+1,xHEADER(line)="Número total de muestras : "_CNT1
 .s line=line+1,xHEADER(line)=""
 i CNT2 d
 .s line=line+1,xHEADER(line)="Número total de Pruebas : "_CNT2
 .s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),PRINT,FILE
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR2_"-"_PAR1 I $$update^LVBPRPR(REPID)
 K ^TMP($zn,$j),^TMP("PRTREP00",$j)
 Q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 k FROM i '$$select^LVBCTUSL(PAR2) m FROM=PLIST
 k TO i $l(PAR3),'$$select^LVBCTUSL(PAR3) m TO=PLIST
 i $l(PAR4),'$$select^LVBCTRL(PAR4) m TO=PLIST
 q
BODY ; create temporary printing file
 s (CNT1,CNT2)=""
 i $l(PAR3) d
 .s epis="" f  s epis=$o(^TPSL(PAR1,PAR2,"US",PAR3,epis)) q:epis=""  d
 ..s rowid=PAR1_"||"_PAR2_"||US||"_PAR3 i '$$select^LVBPSL(rowid) d  i $$update^LVBPSL(rowid)
 ...s PLIST(6)=+$h,PLIST(7)=$p($h,",",2),PLIST(8)=USER
 ...s PLIST(9)=+$h,PLIST(10)=$p($h,",",2),PLIST(11)=USER
 ..s move="" f  s move=$o(^TPSL(PAR1,PAR2,"US",PAR3,epis,move)) q:move=""  d
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s CNT2=CNT2+1
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"SP",x)) q:x=""  s CNT1=CNT1+1
 ...s ^TMP($zn,$j,"REPORT",epis_"||"_move)=""
 i $l(PAR4) d
 .s epis="" f  s epis=$o(^TPSL(PAR1,PAR2,"RL",PAR4,epis)) q:epis=""  d
 ..s rowid=PAR1_"||"_PAR2_"||RL||"_PAR4 i '$$select^LVBPSL(rowid) d  i $$update^LVBPSL(rowid)
 ...s PLIST(6)=+$h,PLIST(7)=$p($h,",",2),PLIST(8)=USER
 ...s PLIST(9)=+$h,PLIST(10)=$p($h,",",2),PLIST(11)=USER
 ..s move="" f  s move=$o(^TPSL(PAR1,PAR2,"RL",PAR4,epis,move)) q:move=""  d
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s CNT2=CNT2+1
 ...s x="" f  s x=$o(^TEPI(epis,9,move,"SP",x)) q:x=""  s CNT1=CNT1+1
 ...s ^TMP($zn,$j,"REPORT",epis_"||"_move)=""
 ...i $l(TO(6)) s x="" f  s x=$o(^TEPI(epis,9,move,"VTS",x)) q:x=""  s ^TMP($zn,$j,"FILE",epis,x)=""
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s oldepis=""
 .s move="" f  s move=$o(^TMP($zn,$j,"REPORT",move)) q:move=""  d
 ..k VISIT s epis=$p(move,"||",1),mv=$p(move,"||",2) i '$$select^LVBEPVIS(epis) d
 ...m VISIT=PLIST
 ...i $d(^TMP($zn,$j,"FILE",epis)) s xxx(4)="MRN : RL_"_$p(VISIT(20),$c(1))
 ...s x="" f cnt=0:1 s x=$o(^TEPI(epis,9,mv,"VTS",x)) q:x=""  d
 ....s xx=$p(x,"||",2) i '$$select^LVBCTTS(xx,"Y") s xxx(1)=$g(xxx(1))_$s($g(xxx(1))'="":", ",1:"")_PLIST(3)
 ....s xxx(4)=$g(xxx(4))_$s($l($g(xxx(4))):" ; ",1:"")_$s('cnt:"Reference No: ",1:" , ")_$p(x,"||",1)_"-"_$p(x,"||",2)_"-"_$p(x,"||",3)
 ....s specinfo=$$GetSpec(xx)
 ....s xxx(2)=$p(specinfo,"^",1)
 ....s xxx(3)=$P(specinfo,"^",2)
 ..i oldepis'=epis d
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ...s oldepis=epis
 ..d PRINT^PRTREP00(.VISIT,.N) k VISIT,xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="PSL"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
GetSpec(ts) 
 n (ts)
 s spdesc=""
 s sp=$O(^TTAB("TS",ts,1,"")) 
 i $l(sp) s spdesc=$P(^TTAB("SPEC",sp),"\",1)
 q:'$l(sp) ""
 s ct=$O(^TTAB("TS",ts,1,sp,""))
 q:'$l(ct) ""
 s comm=""
 i $D(^TTAB("TS",ts,"SC",ct,sp,"REM",1)) s comm=$g(^TTAB("TS",ts,"SC",ct,sp,"REM",1))
 q spdesc_"^"_comm
FILE ; create file
 i $d(^TMP($zn,$j,"FILE")) d
 .s file=TO(6) f j="\","/" i file[j s:$e(file,$l(file))'=j file=file_j
 .s file=file_PAR4_"_"_PAR2_"_"_PAR1_".txt"
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","Start creating file <"_file_">")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .o file:"WNS":2 e  d  q
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..d ADDLINE^PRTREP00(REPID,"PRT","Cannot create file <"_file_">")
 ..d ADDLINE^PRTREP00(REPID,"PRT","!")
 .s epis="" f  s epis=$o(^TMP($zn,$j,"FILE",epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis,"Y") d
 ...s (xx,vts)="" f j=1:1 s vts=$o(^TMP($zn,$j,"FILE",epis,vts)) q:vts=""  s $p(xx,",",j)=vts
 ...s str="" f j=1:1:PLIST s $p(str,$c(28),j)=$p($g(PLIST(j)),$c(1))
 ...s $p(str,$c(28),200)=xx
 ...u file w str,!
 .c file
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","Finish creating file <"_file_">")
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 q
]]></Routine>
</Export>
