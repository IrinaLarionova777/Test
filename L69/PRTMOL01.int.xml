<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTMOL01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTMOL01 ; ML 21/12/98 ; MICRO ORGANISMS PER LOCATION
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Organism
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Micro Organisms by Location"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Organism : "_$s($l(PAR3):BUG(3)_" ("_PAR3_")",1:"All")
 s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 i $l(PAR3),'$$select^LVBCTBUG(PAR3,"Y") k BUG m BUG=PLIST
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 f j="TS","TC" s ^TMP($zn,$j,j)=""
 s tc="" f  s tc=$o(^TTAB("TC",tc)) q:tc=""  d
 .s format=$p(^TTAB("TC",tc),"\",3) i format="V" d
 ..s ^TMP($zn,$j,"TC")=^TMP($zn,$j,"TC")_$s($l(^TMP($zn,$j,"TC")):",",1:"")_tc
 ..s ts="" f  s ts=$o(^TTAB("TC",tc,1,ts)) q:ts=""  s ^TMP($zn,$j,"TS",ts)=""
 s ts="" f  s ts=$o(^TMP($zn,$j,"TS",ts)) q:ts=""  s ^TMP($zn,$j,"TS")=^TMP($zn,$j,"TS")_$s($l(^TMP($zn,$j,"TS")):",",1:"")_ts
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..s loc=$$seldata^LVBEPVIS(epis,22)
 ..i '$l(loc) s loc=" "
 ..s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 ...i (","_^TMP($zn,$j,"TS")_",")'[(","_ts_",") q
 ...s tscnt="" f  s tscnt=$o(^TEPI(epis,1,ts,tscnt)) q:tscnt=""  d
 ...s vtsRID=epis_"||"_ts_"||"_tscnt i $$status^LVBVIS1(vtsRID)'="A" q
 ...s tc="" f  s tc=$o(^TEPI(epis,1,ts,tscnt,"DATA",tc)) q:tc=""  s bug=^(tc) i $l(bug) d
 ....i (","_^TMP($zn,$j,"TC")_",")'[(","_tc_",") q
 ....i $l(PAR3),bug'=PAR3 q
 ....s ^TMP($zn,$j,"REPORT",bug,loc)=$g(^TMP($zn,$j,"REPORT",bug,loc))+1
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s bug="" f  s bug=$o(^TMP($zn,$j,"REPORT",bug)) q:bug=""  d
 ..i '$$select^LVBCTBUG(bug,"Y") s xxx(1)=PLIST(3)_" ("_bug_")"
 ..s loc="" f  s loc=$o(^TMP($zn,$j,"REPORT",bug,loc)) q:loc=""  d
 ...s xxx(2)=loc,xxx(4)=^(loc) i '$$select^LVBCTHOS(loc,"Y") s xxx(3)=PLIST(3)
 ...d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="MOL"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
