<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDR516" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDR516 ;IG 27/05/98 ;Doctors Reports printing (Vertical cumulative) - IDS
 ;
 ; supported calls :
 ;
 ; $$print^PRTDR516(vtsRID,type,DH,EpisodeList,group,cOrder)
 ;
 ; type          : P - print
 ;                 V - view from Enquiry
 ;                   - view from Entry
print(vtsRID,type,DH,EpisodeList,group,cOrder) n (vtsRID,type,DH,EpisodeList,group,cOrder,tsLM,tsWD,SPLIT,GroupName,BMP,COMMENTS,PAR1) 
 s vtsRID=$g(vtsRID),type=$g(type),DH=$g(DH),EpisodeList=$g(EpisodeList),group=$g(group),cOrder=$g(cOrder),dh1=$g(dh1),dh2=$g(dh2)
 s epis=$p(vtsRID,"||",1),test=$p(vtsRID,"||",2),max=6,ColWidth=8,ColLeft=25,FullYear="N",tsLM=$g(tsLM),tsWD=$g(tsWD)
 K ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-COMMENTS")
 i '$$select^LVBEPVIS(epis,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k VISIT m VISIT=PLIST
 s range1="(",range2=")",(flaglow,flaghigh,Alternate)=""
 i '$$select^LVBCTTS(test,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .k TEST m TEST=PLIST s tsLM=TEST(41),dep=PLIST(26)
 i 'tsLM,'$$select^LVBCTDEP(dep,"Y"),PLIST(13) s tsLM=PLIST(13)
 i '$$select^MVBCFLAB(1) d
 .i $l(PLIST(18)) s flaglow=PLIST(18)
 .i $l(PLIST(19)) s flaghigh=PLIST(19)
 .i $l(PLIST(20)) s range1=PLIST(20)
 .i $l(PLIST(21)) s range2=PLIST(21)
 .i $l(PLIST(14)) s max=PLIST(14)
 .i 'tsLM,PLIST(43) s tsLM=PLIST(43)
 .i 'tsWD,PLIST(42) s tsWD=PLIST(42)
 .i PLIST(37)="Y" s ColWidth=11,FullYear="Y"
 .i $l(PLIST(69)) s Alternate=PLIST(69)
 i '$$getit^LVBCTTSF(,vtsRID,"Y") d
 .k FORMAT f j=1:1:PLIST s ti=$p(PLIST(j),$c(2),10) i $l(ti) s FORMAT(ti)=PLIST(j)
 ; create list of episodes and results
 s date=+VISIT(12),time=+VISIT(13)\60
 s (row,cnt)="" f  s cnt=$o(^TEPI(epis,1,test,cnt)) q:cnt=""  d
 .s row=row+1,^TMP($zn,$j,"CUM",row)=epis_$c(1)_date_$c(1)_time
 .s rowid=epis_"||"_test_"||"_cnt d TestItems(row,rowid)
 s ur=VISIT(20),date=+VISIT(12),time=VISIT(13)\60
 i $l(ur),$l(date),$l(time) d
 .f  d:$l(date)  s date=$o(^TDEBi(ur,"DATE",date),-1) q:date=""  s time=""
 ..f  d:$l(time)  s time=$o(^TDEBi(ur,"DATE",date,time),-1) q:time=""
 ...s ep="" f  s ep=$o(^TDEBi(ur,"DATE",date,time,ep)) q:ep=""  i ep'=epis d
 ....s cnt="" f  s cnt=$o(^TEPI(ep,1,test,cnt)) q:cnt=""  d
 .....s row=row+1,^TMP($zn,$j,"CUM",row)=ep_$c(1)_date_$c(1)_(time*60)
 .....s rowid=ep_"||"_test_"||"_cnt d TestItems(row,rowid)
 i '$d(^TMP($zn,$j,"CUM")) d  q 100
 .k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-COMMENTS")
 ; rearange TestItems
 m x=^TMP($zn,$j,"CUM-TC-ORDER") k ^TMP($zn,$j,"CUM-TC-ORDER")
 s x="" f j=1:1 s x=$o(x(x)) q:x=""  s ^TMP($zn,$j,"CUM-TC-ORDER",j)=x(x)
 d:$g(PAR1)="F" ADD^PRTDR00(,"~BOLD")
 ; print test set header
 i $l(TEST(17)) d
 .d ADD^PRTDR00(,"~BOLD","Y"),LN^PRTDR00("Y"),ADD^PRTDR00(tsLM,$s(TEST(17)="*":TEST(3),1:TEST(17)),"Y")
 .d:$g(PAR1)'="F" ADD^PRTDR00(,"~NORMAL","Y") d LN^PRTDR00("Y")
 ; print cumulative header
 d ADD^PRTDR00(,"~BOLD","Y")
 d LN^PRTDR00("Y")
 s maxcol=$o(^TMP($zn,$j,"CUM-TC-ORDER",""),-1)
 d ADD^PRTDR00(tsLM,$e("Test Item"_$j("",tsWD*10),1,ColLeft-3)_"  ","Y")
 f col=1:1:maxcol s ti=$g(^TMP($zn,$j,"CUM-TC-ORDER",col)) i $l(ti) d
 .s name=$p(FORMAT(ti),$c(2),25) i '$l(name) s name=$tr($p(FORMAT(ti),$c(2),3),":")
 .s xx=$e(name_$j("",tsWD*10),1,ColWidth)
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 d LN^PRTDR00("Y")
 d ADD^PRTDR00(tsLM,$e("Units"_$j("",tsWD*10),1,ColLeft-3)_"  ","Y")
 f col=1:1:maxcol s ti=$g(^TMP($zn,$j,"CUM-TC-ORDER",col)) i $l(ti) d
 .s xx=$p(FORMAT(ti),$c(2),17)
 .d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),xx,"Y")
 d LN^PRTDR00("Y")
 d:$g(PAR1)'="F" ADD^PRTDR00(,"~NORMAL","Y")
 ; print results
 s row="" f  s row=$o(^TMP($zn,$j,"CUM",row)) q:row=""  d
 .s epis=$p(^(row),$c(1),1),date=$p(^(row),$c(1),2),time=$p(^(row),$c(1),3)
 .s xx=$e(epis_$j("",tsWD*10),1,10),xx=$e(xx_$$extdate2^SSUTIL4(date)_$j("",tsWD*10),1,22)
 .d ADD^PRTDR00(tsLM,xx)
 .f col=1:1:maxcol s ti=$g(^TMP($zn,$j,"CUM-TC-ORDER",col)) i $l(ti),$d(^TMP($zn,$j,"CUM",row,ti)) d
 ..s result=$p($g(^TMP($zn,$j,"CUM",row,ti)),$c(1),1),flag=$p($g(^TMP($zn,$j,"CUM",row,ti)),$c(1),2)
 ..i result="" d
 ...i $p(FORMAT(ti),$c(2),8)="M" s result="TF"
 ...e  s result=""
 .. ; print extra info for numeric fields
 ..i "MN"[$e($p(FORMAT(ti),$c(2),18)) d
 ...i flag="L" d
 ....i $l(flaglow) s result=result_" "_flaglow
 ....d ADD^PRTDR00(,"~BOLD")
 ...i flag="H" d
 ....i $l(flaghigh) s result=result_" "_flaghigh
 ....d ADD^PRTDR00(,"~BOLD")
 ..d ADD^PRTDR00(tsLM+ColLeft+(col-1*ColWidth),result)
 ..i "MN"[$e($p(FORMAT(ti),$c(2),18)) d
 ...i (flag="L")!(flag="H") d:$g(PAR1)'="F" ADD^PRTDR00(,"~NORMAL")
 .d LN^PRTDR00()
 ; create SPLIT
 k SPLIT s SPLIT=1,SPLIT("SPLIT",0)="0-"_(tsLM+ColLeft)
 s step=tsWD-tsLM-ColLeft\ColWidth
 f col=1:step:maxcol  d  s SPLIT=SPLIT+1
 .s $p(SPLIT("SPLIT",SPLIT),"-",1)=$p(SPLIT("SPLIT",SPLIT-1),"-",2)+1
 .s $p(SPLIT("SPLIT",SPLIT),"-",2)=$p(SPLIT("SPLIT",SPLIT),"-",1)+(step*ColWidth)-1
 s SPLIT("H1")=1 i $l(TEST(17)) s SPLIT("H1")=8
 i $d(^TMP($zn,$j,"CUM-COMMENTS")) d
 .s row="" f  s row=$o(^TMP($zn,$j,"CUM-COMMENTS",row),-1) q:row=""  d
 ..d ADD^PRTDR00(tsLM,^TMP($zn,$j,"CUM-COMMENTS",row,1)),LN^PRTDR00()
 ..f j=2:1 q:'$d(^TMP($zn,$j,"CUM-COMMENTS",row,j))  d ADD^PRTDR00(tsLM,^TMP($zn,$j,"CUM-COMMENTS",row,j)),LN^PRTDR00()
 .f j=1,2 d LN^PRTDR00()
 k ^TMP($zn,$j,"CUM"),^TMP($zn,$j,"CUM-TC-ORDER"),^TMP($zn,$j,"CUM-COMMENTS")
 q 0
 ; collect Test Items information
TestItems(row,rowid) n (row,rowid,type,FORMAT,vtsRID,Alternate,tsLM,tsWD) s row=$g(row),rowid=$g(rowid)
 ; hard code hormone profile test to always print all test items
 i $p(vtsRID,"||",2)="N215" d
 .s ti="" f  s ti=$o(FORMAT(ti)) q:ti=""  i ti'="N0337",ti'="N0339" d
 ..s col=$p(FORMAT(ti),$c(2),27) i '$l(col) s col=$p(FORMAT(ti),$c(2),2)
 ..s ^TMP($zn,$j,"CUM-TC-ORDER",col)=ti
 s status=$$status^LVBVIS1(rowid)
 i '$$results^LVBVISTS(rowid) f j=1:1:PLIST d
 .s (tc,ti)=$p(PLIST(j),$c(1),2),(result,flag)=""
 .i (status="A")!(vtsRID=rowid) s result=$p(PLIST(j),$c(1),3),flag=$p(PLIST(j),$c(1),5)
 .i '$d(FORMAT(ti)) q
 . ; not included in cumulative
 .i $p(FORMAT(ti),$c(2),20)'="Y" q
 .i $l(ti),$l(result) d
 .. ; Alternate result entered
 ..i type="P",$l(Alternate),result=Alternate q
 .. ; non printable/viewable test item
 ..i type="P",$p(FORMAT(ti),$c(2),15)'="Y" q
 ..i type="V",$p(FORMAT(ti),$c(2),16)'="Y" q
 ..i $e($p(FORMAT(ti),$c(2),18))="N" d IN
 ..i $e($p(FORMAT(ti),$c(2),18))="D" d ID^PRTDR00
 ..i $e($p(FORMAT(ti),$c(2),18))="T" d IT^PRTDR00
 ..i $e($p(FORMAT(ti),$c(2),18))="Y" d IY^PRTDR00
 ..i $e($p(FORMAT(ti),$c(2),18))="S" s cnt=ti,resultx=result d IS^PRTDR00 d
 ...i $o(result(""),-1)=1,$l(result(1))<ColWidth s x=result(1) k result s result=x q
 ...m rescom(ti,resultx)=result s result=resultx
 ..s col=$p(FORMAT(ti),$c(2),27) i '$l(col) s col=$p(FORMAT(ti),$c(2),2)
 ..s ^TMP($zn,$j,"CUM-TC-ORDER",col)=ti
 ..s ^TMP($zn,$j,"CUM",row,ti)=result_$c(1)_flag
 ; don't print comments if not most recent episode
 i $p(rowid,"||",1)'=$p(vtsRID,"||",1) k rescom q
 ; compile test item standard comments
 i $d(rescom) d
 .s ^TMP($zn,$j,"CUM-COMMENTS",row,1)="Std Comm : "
 .s ti="",b=1 f  s ti=$o(rescom(ti)) q:ti=""  d
 ..s res="" f  s res=$o(rescom(ti,res)) q:res=""  d
 ...s ^TMP($zn,$j,"CUM-COMMENTS",row,1)=^TMP($zn,$j,"CUM-COMMENTS",row,1)_res_$s($o(rescom(ti,res))'="":", ",1:"")
 ...s x="" f  s x=$o(rescom(ti,res,x)) q:x=""  s b=b+1,^TMP($zn,$j,"CUM-COMMENTS",row,b)=rescom(ti,res,x)
 ; compile test set comments
 k PLIST i '$$COMMENTS^LVBCOM01(rowid,,type,"N",,"N","Y",.ExcludeList) i PLIST d
 .f j=1:1:PLIST s xx=$o(^TMP($zn,$j,"CUM-COMMENTS",row,""),-1)+1,^TMP($zn,$j,"CUM-COMMENTS",row,xx)=PLIST(j)
 q
IN ; Numeric
 i $l(result),$d(^TTAB("RC",result)) q
 s Flag="" f  q:'$l(result)  q:"<>+-"'[$e(result)  s Flag=Flag_$e(result),result=$e(result,2,$l(result))
 s result=Flag_$j(result,0,$e($p(FORMAT(ti),$c(2),18),2,9))
 q
]]></Routine>
</Export>
