<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV26" type="INT" languagemode="0"><![CDATA[
PRTINV26 ; BJ 05/05/2008 ; PRIVATE INVOICES - Adelaide Pathology Partners  ; Compiled May 8, 2008 13:41:28 for Cache for Windows
 Q
 ; Parameters :  1.Payment Code
 ;               2.Batch Number
 ;               3.Episode Number
 ;               4.Sequence Number
 ;               5.  - Invoice (Original)
 ;                 Y - Invoice (Duplicate)
 ;                 1 - Reminder 1
 ;                 2 - Reminder 2
 ;                 3 - Reminder 3
 ;                 R - Receipt
 ;                 H - Invoice history
 ;               6.Receipt Number
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($zn,$j)
 D INIT,BODY,PRINT
CREND I " H Y  "[(" "_PAR5_" "),'$$select^LVBPRPR(REPID) S PLIST(12)=PAR1_"-"_PAR2 I $$update^LVBPRPR(REPID)
 I " 1 2 3 "[(" "_PAR5_" "),'$$select^LVBPRPR(REPID) S PLIST(12)="REM-"_PAR5 I $$update^LVBPRPR(REPID)
 I " R "[(" "_PAR5_" "),'$$select^LVBPRPR(REPID) S PLIST(7)="REC",PLIST(12)=PAR6 I $$update^LVBPRPR(REPID)
 K ^TMP($zn,$j),^TMP("PRTREP00",$J)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 s BPAY="" i '$$select^MVBCFLAB(1),PLIST(32)?2N s BPAY=PLIST(32)
 s gapBI=$$seldata^LVBCTPC(PAR1,83)
 Q
 ; create temporary printing file
BODY ; print single patient
 i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
 .s xx1=$p(PLIST(9),$c(1)),xx2=$e(PLIST(57),$l(xx1)+1,$l(PLIST(57)))
 . ; find sequence
 .s xx3="" i $l(xx1),$l(xx2) s xx3=$p($g(^TMCB(xx1,xx2,PAR3)),"\",2)
 . ; collect all episodes for this sequence
 .i $l(xx3) d
 .. ; get provider name and provider number
 ..k INVOICE i '$$select^LVBINVB(xx1_"||"_xx2) m INVOICE=PLIST
 ..k COMP i '$$select^LVBCTCO(INVOICE(6)) m COMP=PLIST 
 ..s ^TMP($zn,$j,"REPORT")=$p(INVOICE(10),$c(1),2)_" - "_INVOICE(5)_$c(1)_$p(COMP(10),$c(1))
 ..s epis="" f  s epis=$o(^TMCB(xx1,xx2,epis)) q:epis=""  i xx3=$p(^TMCB(xx1,xx2,epis),"\",2) d
 ...i $$CT007^at345(epis,xx1_"||"_xx2)="Y" q
 ...i '$$select^LVBEPVIS(epis,"Y") d  q
 ....s sort=$s(PLIST(12):PLIST(12),1:" ")
 ....s ^TMP($zn,$j,"REPORT",xx3,"EPISODE",sort,epis)=PLIST(23)
 ....s ^TMP($zn,$j,"REPORT",xx3)=xx1_"-"_xx2_"-"_xx3
 ; print batch
 i $l(PAR1),$l(PAR2) d
 .k INVOICE i '$$select^LVBINVB(PAR1_"||"_PAR2) m INVOICE=PLIST
 .k COMP i '$$select^LVBCTCO(INVOICE(6)) m COMP=PLIST
 .s ^TMP($zn,$j,"REPORT")=$p(INVOICE(10),$c(1),2)_" - "_INVOICE(5)_$c(1)_$p(COMP(10),$c(1))
 .s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
 ..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
 ..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
 ..i $l(PAR4),PAR4'=seq q
 ..i '$$select^LVBEPVIS(epis,"Y") d  q
 ...s sort=$s(PLIST(12):PLIST(12),1:" ")
 ...s ^TMP($zn,$j,"REPORT",seq,"EPISODE",sort,epis)=PLIST(23)
 ...s ^TMP($zn,$j,"REPORT",seq)=PAR1_"-"_PAR2_"-"_seq
 ; check credits
 i " 1 2 3 R H "[(" "_PAR5_" ") d
 .s seq="" f  s seq=$o(^TMP($zn,$j,"REPORT",seq)) q:seq=""  d
 ..s (credit,sort,cnt)="" f  s sort=$O(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort)) q:sort=""  d
 ...s epis="" f  s epis=$O(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort,epis)) q:epis=""  d
 ....s cnt=cnt+1
 ....s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 ....s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 .....s rowid=$p(deb,$c(1))_"||"_$p(date,$c(1))_"||"_ep
 .....i $$open^LVBDEBVT(rowid)
 .....f  q:$$fetch^LVBDEBVT(rowid)  d
 ......f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ......k TRAN m TRAN=PLIST
 ......i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) i PLIST(14) d
 .......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate(PLIST(4))
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="JOURNAL "_$p(PLIST(3),$c(1),2)_"-"_PLIST(6)
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(14)
 .......s credit=credit-PLIST(14)
 ......i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) i PLIST(16) d
 .......s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1
 .......s x1=$p(PLIST(1),"||",1),x2=$p(PLIST(1),"||",2),receipt=$p(^TBAT(x1,x2),"\",3)
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate(PLIST(12))
 .......i $l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Receipt "_receipt
 .......i '$l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Credit "_x1_"-"_x2
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=PLIST(16)
 .......s credit=credit-PLIST(16)
 .......s xLength=$l(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2))+1
 .......i $$open^LVBCRBLA(TRAN(2))
 .......f jj=0:1 q:$$fetch^LVBCRBLA(TRAN(2))  d
 ........f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ........s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1) i jj s x=x+1
 ........f j=1:1:3 s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j))
 ........s xx=$e($s(PLIST(3)="CQ":"Cheque",PLIST(3)="CC":"Cr.Card",1:"Cash")_$s($l(PLIST(8)):" "_PLIST(8),1:""),1,30)
 ........s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)=$e(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)_$j("",xLength),1,xLength)_"("_xx_")"
 .......i $$close^LVBCRBLA()
 ......i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) i PLIST(9) d
 .......s x1=$p(PLIST(1),"||",1),x2=$p(PLIST(1),"||",2),receipt=$p(^TBAT(x1,x2),"\",3)
 .......s pay=1 i $d(^TMP($zn,$j,"REPORT",seq,"CREDIT-IN",x1_" "_x2)) s x=^(x1_" "_x2),pay=0
 .......e  s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1)+1,^TMP($zn,$j,"REPORT",seq,"CREDIT-IN",x1_" "_x2)=x
 .......s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,3))+PLIST(9)
 .......s credit=credit-PLIST(9) i pay d
 ........s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,1)=$$extdate(PLIST(5))
 ........i $l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Receipt "_receipt
 ........i '$l(receipt) s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)="Credit "_x1_"-"_x2
 ........s xLength=$l(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2))+1
 ........i $$open^LVBCRBLA(TRAN(2))
 ........f jj=0:1 q:$$fetch^LVBCRBLA(TRAN(2))  d
 .........f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .........s x=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",""),-1) i jj s x=x+1
 .........f j=1:1:3 s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,j))
 .........s xx=$e($s(PLIST(3)="CQ":"Cheque",PLIST(3)="CC":"Cr.Card",PLIST(3)="EDI":"BPay",1:"Cash")_$s($l(PLIST(8)):" "_PLIST(8),1:""),1,30)
 .........s ^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)=$e(^TMP($zn,$j,"REPORT",seq,"CREDIT",x,2)_$j("",xLength),1,xLength)_"("_xx_")"
 ........i $$close^LVBCRBLA()
 .....i $$close^LVBDEBVT()
 ..i credit s ^TMP($zn,$j,"REPORT",seq,"CREDIT")=credit
 ; set events
 s seq="" f  s seq=$o(^TMP($zn,$j,"REPORT",seq)) q:seq=""  s invoice=^(seq) d
 .s sort="" f  s sort=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort)) q:sort=""  d
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort,epis)) q:epis=""  d
 ...i '$l(PAR5) d events^LVBCOM01(epis,"ACC","Original invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ...i PAR5="R" d events^LVBCOM01(epis,"ACC","Receipt "_PAR6_$s($l(USER):" printed by "_USER,1:"")) q
 ...i PAR5="H" d events^LVBCOM01(epis,"ACC","Invoice history "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ...i 'PAR5 d events^LVBCOM01(epis,"ACC","Duplicate invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 ...d events^LVBCOM01(epis,"ACC","Reminder "_PAR5_$s($l(USER):" printed by "_USER,1:""))
 Q
PRINT s max=25
 s seq="" f  s seq=$O(^TMP($zn,$j,"REPORT",seq)) q:seq=""  d
 .s pc=$p(^TMP($zn,$j,"REPORT",seq),"-",1)
 .s (pcMessage1,pcMessage2,pcMessage3,pcItem,discount)=""
 .i '$$select^LVBCTPC(pc,"Y") s pcItem=PLIST(25),pcMessage1=PLIST(10),pcMessage2=PLIST(17),pcMessage3=PLIST(37)
 . ;
 . ; get billing site
 . ;
 .s bs=$$BS($p(^TMP($zn,$j,"REPORT",seq),"-",1),$p(^TMP($zn,$j,"REPORT",seq),"-",2))
 . ;
 . ; calculate amount
 . ;
 .k amount s (pcRef,amount,amountOwed)="" s INVOICE=^TMP($zn,$j,"REPORT",seq)
 .s sort="" f  s sort=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort)) q:sort=""  d
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort,epis)) q:epis=""  d
 ...i '$l(pcRef) s pcRef=^(epis)
 ...s item="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 ....s amount=amount+^(item),amount(epis)=$g(amount(epis))+^(item)
 .s credit=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT"))
 . ;
 . ; print invoice
 . ;
 .s (start,sort,un)="" f  s sort=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort)) q:sort=""  d
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",seq,"EPISODE",sort,epis)) q:epis=""  d
 ...s rowid=$p(^TMP($zn,$j,"REPORT",seq),"-",1)_"||"_$p(^TMP($zn,$j,"REPORT",seq),"-",2)_"||"_epis
 ...k INVBP i '$$select^LVBINVBP(rowid) m INVBP=PLIST
 ...i INVBP(13)="" d un(rowid,.un) i '$$select^LVBINVBP(rowid) m INVBP=PLIST
 ...s amountOwed=amountOwed+amount(epis)
 ...i '$$select^LVBEPVIS(epis) k VISIT m VISIT=PLIST
 ...s inpat="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(4)="Y" s inpat=1
 ...i '$$select^LVBCTDR($p(VISIT(15),$c(1)),"Y") k DOC m DOC=PLIST
 ...i 'start s start=1 d ADDRESS,QDR,S1 s prevLAB="",prevACC=""
 ...s icDescr="" i '$$select^LVBCTIC(VISIT(18),"Y") s icDescr=PLIST(5)
 ...i icDescr="" s icDescr="Processing Fee"
 ...s item="" f  s item=$o(^TEPI(epis,7,item)) q:item=""  s xx=^(item) i xx d
 ....i item=gapBI s discount=discount-xx q
 ....k x s x(1)=$$extdate(VISIT(12)),x(2)=item,x(3)=$p(xx,"\",2),x(4)=$p(xx,"\",1),x(5)=$p(xx,"\",4),x(6)=""
 ....i item=VISIT(54),'$l(x(3)) s x(3)=icDescr
 ....i $o(^TEPI(epis,7,item))="" s x(6)=amount(epis)
 ....i $o(^TEPI(epis,7,item))=gapBI,$o(^TEPI(epis,7,gapBI))="" s x(6)=amount(epis)
 ....d S3 i $p(xx,"\",3)="Y" k x s (x(1),x(2),x(4),x(5),x(6))="",x(3)="Rule 3 exemption" d S3
 ... ; print SCP number
 ...i $l(VISIT(18)),$l(VISIT(27)) d
 ....i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1))) d
 .....d ADD^SSPRINT(REPID,"PRT","?5  Specimen Collection Point Number : "_$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))),LN s line=line+1
 .i " 1 2 3 R H "[(" "_PAR5_" ") d S6
 .d S5(1)
 q
xx(number) s number=BPAY_$e(number+10000000000,2,11)
 s ww3="",ww1=1 f j=1:1:$l(number) s ww2=$e(number,j)*ww1,ww1=3-ww1 s:$l(ww2)=2 ww2=$e(ww2,1)+$e(ww2,2) s ww3=ww3+ww2
 s cd=ww3#10 i cd s cd=10-cd
 q cd
un(rowid,un) s rowid=$g(rowid),un=$g(un)
 i '$l(un) d  s un=number_cd
 .s number=$o(^TMCBi("UN",BPAY_"99999999999"),-1),number=$s($e(number,1,2)=BPAY:$e(number,3,12),1:"")+1,number=BPAY_$e(number+10000000000,2,11)
 .s ww3="",ww1=1 f j=1:1:$l(number) s ww2=$e(number,j)*ww1,ww1=3-ww1 s:$l(ww2)=2 ww2=$e(ww2,1)+$e(ww2,2) s ww3=ww3+ww2
 .s cd=ww3#10 i cd s cd=10-cd
 i '$$select^LVBINVBP(rowid) s PLIST(13)=un i $$update^LVBINVBP(rowid)
 q
BS(P1,P2) N (P1,P2) S P1=$G(P1),P2=$G(P2),RESULT=""
 I $L(P1),$L(P2) S RESULT=$P($G(^TMCB(P1,P2)),"\",6)
 q RESULT
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 i '$l(ADDRESS(1)) d
 .i $l($p(VISIT(7),$c(1),2)) s ADDRESS(1)=$p(VISIT(7),$c(1),2)_" "_$e(VISIT(4))_" "_VISIT(3)
 .e  s ADDRESS(1)=VISIT(4)_" "_VISIT(3)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 d
 .i $l(ADDRESS(4)),ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6),ADDRESS(6)="" q
 .i $l(ADDRESS(3)),ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(5)_" "_ADDRESS(6),ADDRESS(6)="" q
 .i $l(ADDRESS(2)),ADDRESS(2)'[ADDRESS(6) s ADDRESS(2)=ADDRESS(2)_" "_ADDRESS(5)_" "_ADDRESS(6),ADDRESS(6)="" q
 q
 ; Do QDR
QDR n x s QDR="APP"
 s x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))   
 q
 ; print invoice header
S1 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4099T")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a600h200V"_COMP(3))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a2500h200V"_"Telephone: "_COMP(12))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a2500h300V"_"Facsimile: 08 8238 9899")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a2500h400V"_"Email : accounts@adelaidepath.com.au")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a2500h500V"_"Website : www.adelaidepath.com.au")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a4200h200V"_COMP(6))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a4200h300V"_COMP(8)_" "_COMP(17)_" "_COMP(15))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a4200h400VABN "_COMP(10))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h800VBilled to")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p11v0s0b4099T")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a1200h900V"_$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3)))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a1200h1000V"_ADDRESS(1))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a1200h1100V"_ADDRESS(2))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a1200h1200V"_ADDRESS(3))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a1200h1300V"_ADDRESS(4))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p8v0s0b4099T")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a4200h800VPrinted:       "_$$extdate($p($h,",")))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4099T")
 d ADD^SSPRINT(REPID,"PRT","~BOLD")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h1500VPatient: ")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a1200h1500V"_$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3)))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h1800VService Provider : "_$p($p(^TMP($zn,$j,"REPORT"),$c(1),1)," - ",1))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h1900VProvider Number : "_$p($p(^TMP($zn,$j,"REPORT"),$c(1),1)," - ",2))
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a3300h1500VReferred By: "_$$doc(DOC))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a3300h1600V"_DOC(9)_" "_DOC(10)_" "_DOC(11)_" "_DOC(13)_" "_DOC(12))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a3300h1700VProvider No: "_DOC(18))
 d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a3300h1800VReferral Date: "_$$extdate(VISIT(14)))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4099T")
 d ADD^SSPRINT(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a420h6550VBiller Code: 696740     Ref: "_epis)
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4099T")
 d ADD^SSPRINT(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h7200VAccounts Receivable")
 d ADD^SSPRINT(REPID,"PRT","~NORMAL")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h7300V46 Sir Donald Bradman Drive")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h7400VMILE END  SA  5031")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h7500VABN:32 123 251 043")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4100h7600V08 8238 9866")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p14h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h2400V") d LN
 s line=0
 q
 ; print episode header
S2 i line+3>max d S5(0),S1
 d LN s line=line+1
 s provider=DOC(18) i VISIT(12)<DOC(37) s provider=DOC(36)
 d ADD^SSPRINT(REPID,"PRT","?5  Req.No. "_epis_" "_$$extdate2^SSUTIL4(VISIT(14))_" - "_$$doc($p(VISIT(15),$c(1),1))_" ("_provider_")")
 d LN s line=line+1
 q
 ; print doctors name(s)
doc(doc) s doc=$g(doc) n (doc) s result=""
 i '$$select^LVBCTDR(doc,"Y") s result=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3))
 q result
 ; print single item
S3 i line+3>max d S5(0),S1
 ; split description
 s x(3,1)=x(3) i $l(x(3))>40 s x=0 d
 .f j=1:1:$l(x(3)," ") i $l($p(x(3)," ",1,j))>40 s x=x+1,x(3,x)=$p(x(3)," ",1,j-1),(x(3),x(3,x+1))=$p(x(3)," ",j,999) q:$l(x(3))'>40
 i epis'=prevLAB d  s prevLAB=epis
 .d ADD^SSPRINT(REPID,"PRT","?2  "_x(1))
 .d ADD^SSPRINT(REPID,"PRT","?14 "_epis)
 i $p(VISIT(20),$c(1),1)'=prevACC d ADD^SSPRINT(REPID,"PRT","?24 "_$p(VISIT(20),$c(1),1)) s prevACC=$p(VISIT(20),$c(1),1)
 i $l(x(2)) d ADD^SSPRINT(REPID,"PRT","?32 "_$s('$l(pcItem):x(2),1:$j("",$l(x(2))))_$s(inpat:"*",1:" ")_$s(x(5)="Y":"#",1:" "))
 d ADD^SSPRINT(REPID,"PRT","?41 "_"LCC")
 d ADD^SSPRINT(REPID,"PRT","?54 "_x(3,1))
 i $l(x(4)) d ADD^SSPRINT(REPID,"PRT","?84 "_$j(x(4),7,2))
 i $l(x(6)) d ADD^SSPRINT(REPID,"PRT","?100"_$j(x(6),7,2))
 d LN s line=line+1
 f j=2:1 q:'$d(x(3,j))  d ADD^SSPRINT(REPID,"PRT","?54 "_x(3,j)),LN s line=line+1
 q
S6 ; print credits
 d LN s line=line+1
 i '($d(^TMP($zn,$j,"REPORT",seq,"CREDIT"))\10) d ADD^SSPRINT(REPID,"PRT","?5  No payments have been received for this account")
 i $d(^TMP($zn,$j,"REPORT",seq,"CREDIT"))\10 d
 .s xx="" f  s xx=$o(^TMP($zn,$j,"REPORT",seq,"CREDIT",xx)) q:xx=""  d
 ..i line+3>max d S5(0),S1
 ..f j=1:1:3 s @("x"_j)=$g(^TMP($zn,$j,"REPORT",seq,"CREDIT",xx,j))
 ..d ADD^SSPRINT(REPID,"PRT","?5  "_x1)
 ..d ADD^SSPRINT(REPID,"PRT","?18 "_x2)
 ..i x3 d
 ...s amountOwed=amountOwed+x3
 ...d ADD^SSPRINT(REPID,"PRT","?80 "_$j(x3,12,2))
 ...d ADD^SSPRINT(REPID,"PRT","?100"_$j(amountOwed,7,2))
 ..d LN s line=line+1
 q
S5(last) s last=$g(last)
 ; count number of lines
 s lines=1
 i (amount-credit)>0,(PAR5="")!(PAR5="Y") s lines=lines+1
 i (amount-credit)>0,PAR5=1 s lines=lines+3
 i (amount-credit)>0,PAR5=2 s lines=lines+3
 i $l(pcMessage1) s lines=lines+1
 i $l(pcMessage2) s lines=lines+1
 i $l(pcMessage3) s lines=lines+1
 i $l(pcRef) s lines=lines+1
 i last d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4099T")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD")
 .d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h5000VIf a discount applies, please deduct at time of payment. No refunds given.")
 .d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h5100VVeterans Affairs patients: please telephone us on the above number with your DVA details.")
 .d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h5200VThe initiation fee is an episode induced government charge, which is paid to pathology practicec that comply with all registration")
 .d ADD^SSPRINT(REPID,"PRT",$c(27)_"&a250h5300Vregulations. The fee is designed to cover the cost of collection and the consumables involved")
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 q
LN d ADD^SSPRINT(REPID,"PRT","!")
 q
extdate(date) n (date) s result=$$extdate2^SSUTIL4(date)
 s result=$p(result,"/",1)_"/"_$p("JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC"," ",+$p(result,"/",2))_"/"_$e($p(result,"/",3),3,4)
 q result
]]></Routine>
</Export>
