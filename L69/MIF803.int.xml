<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF803" type="INT" languagemode="0" generated="1"><![CDATA[
MIF803(mi) ; ML 11/8/99 ; HITACHI 747 (Bi-Directional)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),cr=$c(13)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000(stx,cr) q:'$l(record)
 d trace^MIF000(mi,$tr(record,etx," "),"H<--M")
 i $e(record)=">" d SEND(">") q
 i $e(record)="?" d SEND(">") q
 i $e(record)=1 f  s xrecord=$$read^MIF000(stx,cr) q:'$l(xrecord)  d trace^MIF000(mi,$tr(xrecord,etx," "),"H<--M"),SEND(">") s record=record_$e(xrecord,29,999) q:$e(record)=":"
 i $e(record)=";" d BUILD q
 d SEND(">")
 i $e(record,2)]"M" q
 s (sample,epis,surname,result,date,time,QC)=""
 s epis=$tr($e(record,14,26)," ")
 s epis=$e(epis,$l(epis)-6,$l(epis))
 i "KLIJ"[$e(record,2) s QC=1
 s resx=$e(record,29,$l(record)-3)
 f i=1:1:$l(resx,"@") d
 .s res=$p(resx,"@",i)
 .s code=$e(res,1,2),res=$tr($e(res,3,$l(res))," ")
 .s result=result_code_$c(par10)_res_$c(par11)
 i epis="" s epis="*"
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
BUILD ; compile string to send to instrument
 s epis=$tr($e(record,14,26)," ")
 s epis=$e(epis,$l(epis)-6,$l(epis))
 i $d(^TEPI(epis))#10=0 d TPRE q
 ; create order for pending episode
 s record=$e(record,1,26)
 k tst s tst="",tcch=""
 d scanone^MIF000(mi,epis)
 k tst s (tst,tcch)="" f  s tcch=$o(^TMP("MIF-SINGLE",$j,mi,epis,tcch)) q:tcch=""  s tst(tcch)=""
 f i=1:1:49 s tst=tst_$d(tst(i))
 d SEND(record_"49"_tst_"0")
 k tst
 q
TPRE ; create order for pre-entered episode
 i '$d(^TPRE(epis)) d SEND(">") q
 s (z,epa)=^TPRE(epis),drcode=$p(z,"\",1) k z
 k epa
 s c1="",$p(c1," ",31)=""
 s c2=$e(c1,1,25)
 s c3=""
 i drcode'="" s c3=$p($g(^TTAB("DR",drcode)),"\")
 s c3=$e(c3_c1,1,20)
 s c4=$e(c1,1,25)
 d scanone^MIF000(mi,epis)
 k tst s (tst,tcch)=""
 f  s tcch=$o(^TMP("MIF-SINGLE",$j,mi,epis,tcch)) q:tcch=""  s tst(tcch)=""
 f i=1:1:48 s tst=tst_$d(tst(i))
 d SEND(record_"48"_tst_"11110"_c1_c2_c3_c4)
 k tst
 q
CHSUM(x) ; calculate check sum
 s z="" f j=1:1:$l(x) s z=z+$a(x,j)
 s z=z#256,z=$$HH(z\16)_$$HH(z#16)
 q z
HH(y) q $s(y:$e("123456789ABCDEF",y),1:0)
SEND(str) ; send string to instrument
 s chsum=$$CHSUM(str)
 w stx,str,etx,chsum,cr,*-3
 d trace^MIF000(mi,str_" "_chsum,"H-->M")
 q
]]></Routine>
</Export>
