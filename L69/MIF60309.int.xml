<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF60309" type="INT" languagemode="0" generated="1"><![CDATA[
MIF60309(mi) ; ML 1/3/2001 ; COULTER Handshaking - Including Scatterplot/Histogram : Quinn Pathology
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),crlf=$c(13,10),dc1=$c(17),nak=$c(21),syn=$c(22)
 s hex="0123456789ABCDEF"
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *x:20 q:$c(x)'=syn
 d trace^MIF000(mi,"SYN - Synchronising","H<--M")
 w syn,*-3 d trace^MIF000(mi,"SYN - Synchronising","H-->M")
 s ok="" f  r BlockCnt#2:10 d  q:ok=1  q:ok=2
 .i BlockCnt=syn d trace^MIF000(mi,"SYN - Synchronising","H<--M")
 .i BlockCnt=syn w syn,*-3 d trace^MIF000(mi,"SYN - Synchronising","H-->M") q
 .i 'BlockCnt w nak,*-3 d trace^MIF000(mi,"NAK - Block count not received","H-->M") s ok=2 q
 .s ok=1
 i ok=2 q
 s BlockCnt=$$HEX(BlockCnt) d trace^MIF000(mi,"Block Count - "_BlockCnt,"H<--M")
 i 'BlockCnt w nak,*-3 d trace^MIF000(mi,"NAK - No/Bad block count","H-->M") q
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 k ^TMP($zn,$j) s er=0
Read f i=1:1:BlockCnt d  i er q
 .s rec=$$read^MIF000(stx,etx) i '$l(rec) e  s er=er+1 q
 .s record=$tr($e(rec,3,$l(rec)-4),crlf,"\"),record=$tr(record,$c(0)," "),record=$tr(record,dc1,""),^TMP($zn,$j,i)=record,er=0
 .d trace^MIF000(mi,record,"H<--M")
 .w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 i er=1 w syn,*-3 d trace^MIF000(mi,"SYN - Resend data","H-->M") k ^TMP($zn,$j) g Read
 i er q
 r *x:2 i $c(x)'=syn w nak,*-3 d trace^MIF000(mi,"NAK - Did not receive synch after data","H-->M") q
 d trace^MIF000(mi,"SYN - Packet transfer completed","H<--M")
 s record="" f i=1:1:BlockCnt s record=record_^TMP($zn,$j,i)
 ; find number of fields to extract patient details
 s num=$$HEX($p(record,"\",8)),recx=$p(record,"\",9,8+num),record=$p(record,"\",9+num,999)
 s (sample,epis,result,res,surname,date,time,QC)=""
 s epis=+$p($p(recx,"ID1 ",2),"\"),epis=$tr(epis," ") i epis=""!(epis["----") s epis=$p($p(recx,"CASS/POS",2),"\"),epis=$tr(epis," ")
 ; check auto-validation markers
 s (AutoVal,marker)="",AutoVal=record,AutoVal=$p($p($p(AutoVal,"BA%",2),"\",2,99),"\--")
 f i=1:1:$l(AutoVal,"\") s marker=$p(AutoVal,"\",i) i marker'="",marker'="00" s marker="fail" q
 ; extract results
 s prevchan="" f  d  q:record=""  q:prevchan="BA%"  q:prevchan="RET#"
 .s num=$$HEX($p(record,"\")),recx=$p(record,"\",2,1+num),record=$p(record,"\",2+num,999)
 .f i=1:1:$l(recx,"\") s res=$tr($p(recx,"\",i)," ") i $l(res) d  q:prevchan="BA%"  q:prevchan="RET#"
 .. ; extract channel
 ..s x1="" f j=1:1 q:$e(res,j)?1N  q:$e(res,j)=":"  s x1=x1_$e(res,j) q:$l(res)=j
 ..i x1=""!(x1?1.C.E) q
 ..q:$e(x1,1,2)=".."  s next=j,prevchan=x1
 .. ; ignore machine voted out results
 ..i res["::" q
 .. ; extract result
 ..s (x2,flag)="" f j=next:1:$l(res) q:$e(res,j)=""  s x2=x2_$e(res,j) d  q:flag
 ...i $e(res,j)'?1N,($e(res,j)'?1P)!($e(res,j)="*")!($e(res,j)="+") s flag=1 q
 ..q:$e(x2,1,2)=".."  i flag s x2=$e(x2,1,$l(x2)-1)
 .. ; extract flag
 ..s x3=$e(res,j,999)
 ..s result=result_x1_$c(par10)_x2_$c(par10)_x3_$c(par11)
 i marker'="fail" s result=result_"COM"_$c(par10)_"Y"_$c(par10)_$c(par11)
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 ; extract scatterplot/histogram data
 s result="" i record["VAL1"!(record["RBCH")!(record["PLTH") d
 .f chan="VAL1","VAL2","VAL3","VAL4","VAL5","RBCH","PLTH","PLTF","DIF1","DIF2" d
 ..s (x1,x2,x3)="",res=$tr($p($p(record,chan,2),"\")," ")
 ..s x1=$s(chan="DIF1":"DIFF",chan="DIF2":"DIFF",1:chan) i '$d(ScatRes(x1)) s ScatRes(x1)=$s(x1="DIFF":1,1:"0")
 ..i $e(x1,1,3)="VAL" s ScatRes(x1)=res q
 ..i x1="DIFF" d
 ...s Block128=$l(res)/64 f j=1:1:Block128 d
 ....s x2=$e(res,1,64),res=$e(res,65,99999)
 ....s x=ScatRes(x1),ScatRes(x1)=x+1,ScatRes(x1,x)=x1_$c(par10)_x_$c(par10)_x2_$c(par11)
 .. ; convert hex to dec for RBCH, PLTH, PLTF histograms
 ..i x1="RBCH"!(x1="PLTH")!(x1="PLTF") d
 ...f  q:'$l(res)  s hex=$e(res,1,2),res=$e(res,3,99999),hex=$$HEX(hex),x2=x2_$s($l(x2):",",1:"")_hex
 ...s x=ScatRes(x1),ScatRes(x1)=x+1,ScatRes(x1,x)=x1_$c(par10)_x_$c(par10)_x2_$c(par11)
 . ; file scatterplot/histogram data
 .i $d(ScatRes) d
 ..s result="DIFF"_$c(par10)_"0"_$c(par10)_ScatRes("VAL1")_$c(20)_ScatRes("VAL2")_$c(20)_ScatRes("VAL3")_$c(20)_ScatRes("VAL4")_$c(20)_ScatRes("VAL5")_$c(par11)
 ..f i="DIFF","RBCH","PLTH","PLTF" d
 ...s x="" f  s x=$o(ScatRes(i,x)) q:x=""  s result=result_ScatRes(i,x)
 .i $l(result) d filesp^MIF000(mi,sample,epis,result)
 k ^TMP($zn,$j)
 q
 ; convert to hexidecimal
HEX(X) q $f(hex,$e(X))-2*16+$f(hex,$e(X,2))-2
]]></Routine>
</Export>
