<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTERR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTERR01 ; ML 06/06/00 ; Episode Result Report
 q
 ; Parameters :  1. Date From
 ;               2. Date To
 ;               3. Select criteria : DR - Doctor
 ;                                    DG - Doctor group
 ;                                    HW - Hospital/Ward
 ;                                    PC - Pay code
 ;                                    CC - Collection centre
 ;                                    CP - Collection personel
 ;                                    CO - Company
 ;                                    UL - User location
 ;                                    MC - Marketing code
 ;               4. Value
 ;               5. Select criteria : DR - Doctor
 ;                                    DG - Doctor group
 ;                                    HW - Hospital/Ward
 ;                                    PC - Pay code
 ;                                    CC - Collection centre
 ;                                    CP - Collection personel
 ;                                    CO - Company
 ;                                    UL - User location
 ;                                    MC - Marketing code
 ;               6. Value
 ;               7. Test Sets
 ;               8. Sort key_1 :      DR - Doctor
 ;                                    DG - Doctor group
 ;                                    HW - Hospital/Ward
 ;                                    PC - Pay code
 ;                                    DT - Collection date
 ;                                    CC - Collection centre
 ;                                    CP - Collection personel
 ;                                    CO - Company
 ;                                    UL - User location
 ;                                    MC - Marketing code
 ;               9. Sort key_2
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Episode Result Report"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="From "_$$extdate2^SSUTIL4(PAR1)_" to "_$$extdate2^SSUTIL4(PAR2)
 s line=4,xHEADER(line)=""
 i '$l(PAR3) q
 s line=5,xHEADER(line)="Selection criteria : "_$$name(PAR3)_" - "_PAR4
 s line=6,xHEADER(line)="" i $l(PAR5) d
 .s xHEADER(line)="                   : "_$$name(PAR5)_" - "_PAR6
 .s line=7,xHEADER(line)=""
 s line=line+1,xHEADER(line)="Test Sets : "_PAR7
 s line=line+1,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,,,,,PAR8_"^"_PAR9),BODY,PRINT,FILE
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 s extra=0 k xxx f j=1:1:4 s xxx=$p(PAR10,",",j),par=$s(j=1:PAR8,j=2:PAR9,j=3:PAR3,1:PAR5) d  s $p(PAR10,",",j)=xxx
 .s $p(xxx,$c(2),3)=$$name(par)
 .i par="DR" s $p(xxx,$c(2),4)=25
 .i j=2,'$l(PAR9) s $p(xxx,$c(2),4)=0,extra=extra+15
 .i j=3 d
 ..i PAR3=PAR8!(PAR3=PAR9) s $p(xxx,$c(2),4)=0,extra=extra+15
 .i j=4 d
 ..i '$l(PAR5) s $p(xxx,$c(2),4)=0,extra=extra+15 q
 ..i PAR5=PAR8!(PAR5=PAR9) s $p(xxx,$c(2),4)=0,extra=extra+15
 s xxx="xxx",$p(xxx,$c(2),2)=6,$p(xxx,$c(2),3)="Result",$p(xxx,$c(2),4)=63+extra,$p(PAR10,",",9)=xxx
 s PAR10=$p(PAR10,",",1,10)
 k xxx,xSORT d H^PRTREP00(PAR10,$zn)
 f j="DR","HW","PC","DT","CC","CP","CO","UL","MC" s xSORT(j)=$$name(j)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
name(par) n (par) s name=""
 i par="DR" s name="Doctor"
 i par="DG" s name="Doctor group"
 i par="HW" s name="Hospital/Ward"
 i par="PC" s name="Pay code"
 i par="DT" s name="Collection Date"
 i par="CC" s name="Collection centre"
 i par="CP" s name="Collection personel"
 i par="CO" s name="Company"
 i par="UL" s name="User location"
 i par="MC" s name="Marketing code"
 q name
BODY ; create temporary printing file
 s date=PAR1-1 f  s date=$o(^TDAY(1,date)) q:date=""  q:date>PAR2  d
 .s epis="" f  s epis=$o(^TDAY(1,date,0,epis)) q:epis=""  d
 ..i '$$select^LVBEPVIS(epis,"Y") d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k VISIT m VISIT=PLIST
 ... ; check selection type - 1
 ...s ok=0,(Search1,Search2)="" d  i 'ok q
 ....i PAR3="DR",(","_PAR4_",")[(","_VISIT(15)_",") s Search1="DR^"_VISIT(15),ok=1 q
 ....i PAR3="DG",'$$select^LVBCTDGD(PAR4_"||"_VISIT(15)) s Search1="DG^"_PAR4_"||"_VISIT(15),ok=1 q
 ....i PAR3="HW",(","_PAR4_",")[(","_VISIT(22)_",") s Search1="HW^"_VISIT(22),ok=1 q
 ....i PAR3="PC",(","_PAR4_",")[(","_VISIT(9)_",") s Search1="PC^"_VISIT(9),ok=1 q
 ....i PAR3="CC",(","_PAR4_",")[(","_VISIT(27)_",") s Search1="CC^"_VISIT(27),ok=1 q
 ....i PAR3="CP",(","_PAR4_",")[(","_VISIT(28)_",") s Search1="CP^"_VISIT(28),ok=1 q
 ....i PAR3="CO",(","_PAR4_",")[(","_$$epcomp^LVBVIS1(epis)_",") s Search1="CO^"_$$epcomp^LVBVIS1(epis),ok=1 q
 ....i PAR3="UL",(","_PAR4_",")[(","_VISIT(66)_",") s Search1="UL^"_VISIT(66),ok=1 q
 ....i PAR3="MC" d  i (","_PAR4_",")[(","_$p(result,$c(1))_",") s Search1="MC^"_$p(result,$c(1)),ok=1 q
 .....s result="" s result=$$seldata^LVBCTDR(VISIT(15),28)
 ... ; check selection type - 2
 ...i PAR5'="" s ok=0 d  i 'ok q
 ....i PAR5="DR",(","_PAR6_",")[(","_VISIT(15)_",") s Search2="DR^"_VISIT(15),ok=1 q
 ....i PAR5="DG",'$$select^LVBCTDGD(PAR6_"||"_VISIT(15)) s Search2="DG^"_PAR6_"||"_VISIT(15),ok=1 q
 ....i PAR5="HW",(","_PAR6_",")[(","_VISIT(22)_",") s Search2="HW^"_VISIT(22),ok=1 q
 ....i PAR5="PC",(","_PAR6_",")[(","_VISIT(9)_",") s Search2="PC^"_VISIT(9),ok=1 q
 ....i PAR5="CC",(","_PAR6_",")[(","_VISIT(27)_",") s Search2="CC^"_VISIT(27),ok=1 q
 ....i PAR5="CP",(","_PAR6_",")[(","_VISIT(28)_",") s Search2="CP^"_VISIT(28),ok=1 q
 ....i PAR5="CO",(","_PAR6_",")[(","_$$epcomp^LVBVIS1(epis)_",") s Search2="CO^"_$$epcomp^LVBVIS1(epis),ok=1 q
 ....i PAR5="UL",(","_PAR6_",")[(","_VISIT(66)_",") s Search2="UL^"_VISIT(66),ok=1 q
 ....i PAR5="MC" d  i (","_PAR6_",")[(","_$p(result,$c(1))_",") s Search2="MC^"_$p(result,$c(1)),ok=1 q
 .....s result="" s result=$$seldata^LVBCTDR(VISIT(15),28)
 ... ; expand search criteria findings
 ...s Search="" f i=Search1,Search2 s par=$p(i,"^"),value=$p(i,"^",2) s Search=Search_$$SORT(par,value)_"\"
 ... ; check test sets
 ...s ok=0 d  i 'ok q
 ....s ts="" f  s ts=$o(^TEPI(epis,1,ts)) q:ts=""  d
 .....i (","_PAR7_",")[(","_ts_",") s ok=1,^TMP($zn,$j,"EPIS-TS",epis,ts)=""
 ... ; set sorting key - 1
 ...k Sort1 s Sort1=" ",par=PAR8 d
 ....i par="MC" d  q
 .....s x=$$seldata^LVBCTDR(VISIT(15),28)
 .....s x=$p(x,$c(1)) i $l(x) s Sort1=x
 ....i par="DG" d  q
 .....s x="" f  s x=$o(^TTAB("DG",x)) q:x=""  i $d(^TTAB("DG",x,$p(VISIT(15),$c(1)))) s SORT1(x)=""
 ....s x=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
 ...i $l(x) s Sort1=x
 ... ; set sorting key - 2
 ...k Sort2 s Sort2=" ",par=PAR9 d
 ....i par="MC" d  q
 .....s x=$$seldata^LVBCTDR(VISIT(15),28)
 .....s x=$p(x,$c(1)) i $l(x) s Sort2=x
 ....i par="DG" d  q
 .....s x="" f  s x=$o(^TTAB("DG",x)) q:x=""  i $d(^TTAB("DG",x,$p(VISIT(15),$c(1)))) s SORT1(x)=""
 ....s x=$s(par="DR":VISIT(15),par="HW":VISIT(22),par="PC":VISIT(9),par="DT":VISIT(12),par="CC":VISIT(27),par="CP":VISIT(28),par="CO":$$epcomp^LVBVIS1(epis),par="UL":VISIT(66),1:"")
 ...i $l(x) s Sort2=x
 ...s ^TMP($zn,$j,"SORT",Sort1,Sort2,epis)=Search
 ; build test set details
 f i=1:1:$l(PAR7,",") q:$p(PAR7,",",i)=""  q:$$select^LVBCTTS($p(PAR7,",",i),"Y")  d
 .s ^TMP($zn,$j,"TS",$p(PAR7,",",i))=$s(PLIST(4)'="":PLIST(4),PLIST(5)'="":PLIST(5),1:PLIST(2))
 q
SORT(par,value) n (par,value) s result="" i value'=" " d
 .i par="DR",'$$select^LVBCTDR(value,"Y") s result=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 .i par="DG",'$$select^LVBCTDG(value,"Y") s result=PLIST(3)
 .i par="HW",'$$select^LVBCTHOS(value,"Y") s result=PLIST(3)
 .i par="PC",'$$select^LVBCTPC(value,"Y") s result=PLIST(3)
 .i par="DT" s result=$$extdate2^SSUTIL4(value)
 .i par="CC",'$$select^LVBCTCC(value,"Y") s result=PLIST(3)
 .i par="CP",'$$select^LVBCTPER(value,"Y") s result=PLIST(3)
 .i par="CO",'$$select^LVBCTCO(value,"Y") s result=PLIST(3)
 .i par="UL",'$$select^LVBCTUSL(value,"Y") s result=PLIST(3)
 .i par="MC",'$$select^LVBCTMC(value,"Y") s result=PLIST(3)
 q result
PRINT ; print report
 s sort1="" f  s sort1=$o(^TMP($zn,$j,"SORT",sort1)) q:sort1=""  d
 .s xxx(1)=sort1 s:sort1'=" " xxx(1)=$$SORT(PAR8,sort1)
 .s sort2="" f  s sort2=$o(^TMP($zn,$j,"SORT",sort1,sort2)) q:sort2=""  d
 ..s xxx(2)=$s(sort2'=" ":$$SORT(PAR9,sort2),1:"")
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"SORT",sort1,sort2,epis)) q:epis=""  d
 ...s xxx(3)=$p(^(epis),"\",1),xxx(4)=$p(^(epis),"\",2)
 ...q:$$select^LVBEPVIS(epis)  m VISIT=PLIST k PLIST
 ...s ts="" f  s ts=$o(^TMP($zn,$j,"EPIS-TS",epis,ts)) q:ts=""  d
 ....s nn="" f  s nn=$o(^TEPI(epis,1,ts,nn)) q:nn=""  d
 .....s xxx(5)=$g(^TMP($zn,$j,"TS",ts))
 ..... ; check test status
 .....s status="",status=$p(^TEPI(epis,1,ts,nn),"\",31)
 .....k res i status="A" d
 ...... ; extract results
 ......s vtsRID=epis_"||"_ts_"||"_nn q:$$results^LVBVISTS(vtsRID,,,,"Y")  m visres=PLIST k PLIST
 ......s (line,linex,cnt)="" f i=1:1:visres s resstr=$p(visres(i),$c(2)),tc=$p(resstr,$c(1),2),res=$p(resstr,$c(1),3) d
 .......q:$$select^LVBCTTC(tc,"Y")  s tcnm=$p(PLIST(3),$c(1)),type=$p(PLIST(5),$c(1))
 .......i type="D",res'="" s res=$$extdate2^SSUTIL4(res)
 .......s linex=line_" "_tcnm_" "_res
 .......i $l(linex)>N(9,4) s cnt=cnt+1,res(cnt)=line,line=""
 .......s line=line_" "_tcnm_" "_res
 ......i line'="" s res(cnt+1)=line
 .....e  s res(1)="Results not authorised"
 .....f i=1:1 q:'$d(res(i))  d
 ......s xxx(6)=res(i)
 ......i $d(VISIT) m PLIST=VISIT
 ......d PRINT^PRTREP00(.PLIST,.N) k xxx,PLIST,VISIT
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="ERS"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
 ; send report to file
FILE i $d(^TMP($zn,$j,"SORT")) d
 .s dir="" i '$$select^MVBCFLAB(1) s dir=PLIST(32)
 .f j="/","\" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
 .s file=dir_"ERR_"_REPID_".csv" o file:"WNS" u file
 . ; write report parameters
 .w "Episode Result Report",!,!
 .w "From "_$$extdate2^SSUTIL4(PAR1)_" to "_$$extdate2^SSUTIL4(PAR2),!,!
 .i '$l(PAR3) q
 .w "Selection criteria : "_$$name(PAR3)_" - "_PAR4,!,!
 .i $l(PAR5) w "                   : "_$$name(PAR5)_" - "_PAR6,!,!
 .w "Test Sets : "_PAR7,!,!
 . ; write report header
 .f i=1:1 q:'$d(N(i,4))  d
 ..w $s(N(i,4)'="0":$p(N(i,3),","),1:""),$s($d(N(i+1,4))'="0":",",1:"")
 .w !
 . ; write body
 .s sort1="" f  s sort1=$o(^TMP($zn,$j,"SORT",sort1)) q:sort1=""  d
 ..s xxx(1)=sort1 s:sort1'=" " xxx(1)=$$SORT(PAR8,sort1) s xxx(1)=$tr(xxx(1),",","")
 ..s sort2="" f  s sort2=$o(^TMP($zn,$j,"SORT",sort1,sort2)) q:sort2=""  d
 ...s xxx(2)=$s(sort2'=" ":$$SORT(PAR9,sort2),1:"") s xxx(2)=$tr(xxx(2),",","")
 ...s epis="" f  s epis=$o(^TMP($zn,$j,"SORT",sort1,sort2,epis)) q:epis=""  d
 ....s xxx(3)=$p(^(epis),"\",1),xxx(4)=$p(^(epis),"\",2)
 ....s xxx(3)=$tr(xxx(3),",",""),xxx(4)=$tr(xxx(4),",","")
 ....q:$$select^LVBEPVIS(epis)  m VISIT=PLIST k PLIST
 ....s ts="" f  s ts=$o(^TMP($zn,$j,"EPIS-TS",epis,ts)) q:ts=""  d
 .....s nn="" f  s nn=$o(^TEPI(epis,1,ts,nn)) q:nn=""  d
 ......s xxx(5)=$g(^TMP($zn,$j,"TS",ts)),xxx(5)=$tr(xxx(5),",","")
 ...... ; check test status
 ......s status="",status=$p(^TEPI(epis,1,ts,nn),"\",31)
 ......k res i status="A" d
 ....... ; extract results
 .......s vtsRID=epis_"||"_ts_"||"_nn q:$$results^LVBVISTS(vtsRID,,,,"Y")  m visres=PLIST k PLIST
 .......s (line,linex,cnt)="" f i=1:1:visres s resstr=$p(visres(i),$c(2)),tc=$p(resstr,$c(1),2),res=$p(resstr,$c(1),3) d
 ........q:$$select^LVBCTTC(tc,"Y")  s tcnm=$p(PLIST(3),$c(1)),type=$p(PLIST(5),$c(1))
 ........i type="D",res'="" s res=$$extdate2^SSUTIL4(res)
 ........s linex=line_" "_tcnm_" "_res
 ........i $l(linex)>N(9,4) s cnt=cnt+1,res(cnt)=line,line=""
 ........s line=line_" "_tcnm_" "_res
 .......i line'="" s res(cnt+1)=line
 ......e  s res(1)="Results not authorised"
 ......f i=1:1 q:'$d(res(i))  d
 .......s xxx(6)=$tr(res(i),",","")
 .......w xxx(1),",",$s(N(2,4)'="0":xxx(2),1:""),",",$s(N(3,4)'="0":xxx(3),1:""),",",$s(N(4,4)'="0":xxx(4),1:""),",",$g(VISIT(2)),",",$p($g(VISIT(5)),$c(1),2),",",$p($g(VISIT(7)),$c(1),2)_" "_$g(VISIT(3))_" "_$g(VISIT(4)),",",xxx(5),",",xxx(6),!
 .......k xxx,VISIT f i=1:1:10 s xxx(i)=""
 .c file
 q
]]></Routine>
</Export>
