<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF209" type="INT" languagemode="0" generated="1"><![CDATA[
MIF209(mi) ; ML 29/2/00 ; DADE Stratus II
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s ack=$c(6),lf=$c(10)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000("",lf) q:'$l(record)
 d trace^MIF000(mi,record,"H<--M")
 s error="" d  q:error
 . ; check length of cup #1 initially
 .i $p(record,",",1)="CK-MB" d  q:error
 ..i $l(record)'=122 s error=1
 .i $p(record,",",1)="TROPONIN I" d  q:error
 ..i $l(record)'=123 s error=1
 . ; check if cup #1 initially
 .s cup=+$p(record,",",8) i cup'=1 s error=1 q
 ; first cup found
 s rec(cup)=record
 ; read in remaining 30 lines
 s (nextcup,error)=""
 f  s record=$$read^MIF000("",lf) q:'$l(record)  d  q:error
 .s error="",nextcup=cup+1
 .d trace^MIF000(mi,record,"H<--M")
 . ; check length of other cups
 .i $p(record,",",1)="CK-MB" d  q:error
 ..i $l(record)'=112 s error=1
 .i $p(record,",",1)="TROPONIN I" d  q:error
 ..i $l(record)'=117 s error=1
 . ; check status of nextcup
 .s cup=+$p(record,",",8) i nextcup'=cup s error=1 q
 .s rec(cup)=record
 ; finish processing if error exists
 q:error  s error=""
 s x="" f  s x=$o(rec(x)) q:x=""  d
 .s result=rec(x) q:result=""
 .s error=$tr($p(result,",",14)," "),cup=+$p(result,",",8)
 .s error=$tr(error,"""")
 .i (error="")!(error="B2")!(error="B3")!(error="B5") d
 .. ; file results
 ..s code=$p(result,",",1),code=$tr(code,"""")
 ..s epis=$tr($p(result,",",13)," ")
 ..q:+epis=0  s epis=+$e(epis,2,$l(epis))
 ..s flag=$p(result,",",5)
 ..s res=$tr($p(result,",",10)," ")
 ..s res=code_$c(par10)_res_$c(par10)_flag_$c(par11)
 ..d file^MIF000(mi,epis,epis,"",res)
 q
]]></Routine>
</Export>
