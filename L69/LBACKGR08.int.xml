<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LBACKGR08" type="INT" languagemode="0" generated="1"><![CDATA[
LBACKGR08 ; BackGround Job - 08 - background report schedule
 q
 ;
DOIT(zn,job,DATE,TIME) s zn=$g(zn),job=$g(job),DATE=$g(DATE),TIME=$g(TIME)
 l +^TMP(zn,job,"DOIT08"):0 e  q
 s zt=$zt,$zt="ERROR^"_$zn
 s xx1="" f  s xx1=$o(^TTAB("REPORT-GENERIC",xx1)) q:xx1=""  d
 .s xx2="" f  s xx2=$o(^TTAB("REPORT-GENERIC",xx1,"SCHEDULE",xx2)) q:xx2=""  d
 ..f j=1:1:$l(^(xx2),"\") s @("x"_j)=$p(^(xx2),"\",j)
 ..i $l(x9),x9>TIME q
 ..i $l(x10),x10<TIME q
 ..s RUN=0
 ..i x3="H" s xTIME=TIME\30\x2,RUN=1
 ..i " D W M Y "[(" "_x3_" "),$l(x2)!$l(x9)!$l(x10) d
 ...i $l(x2),x2>TIME q
 ...i x3="D" s xTIME=+$h,RUN=1
 ...i x3="W" d
 ....i $$daynum^SSUTIL4(+$h)'=($f("MON TUE WED THU FRI SAT SUN",x4,0)\4) q
 ....s xTIME=x4,RUN=1
 ...i x3="M" d
 ....i $$extdate2^SSUTIL4(+$h)-x4 q
 ....s xTIME=x4,RUN=1
 ...i x3="Y" d
 ....i $$julian^SSUTIL4(+$h)-x4 q
 ....s xTIME=x4,RUN=1
 ..i RUN d
 ...i $d(^TMP(zn,job,"DOIT08",DATE,xx1_"||"_xx2,xTIME)) q
 ...j DOIT8a(zn,job,DATE,xTIME,x1,xx1,xx2)
 l -^TMP(zn,job,"DOIT08")
 s $zt=zt
 q
ERROR d BACK^%ETN
 s $zt=$g(zt)
 q
 ; job scheduled print jobs to background
DOIT8a(zn,job,DATE,xTIME,PAR,xx1,xx2) n (zn,job,DATE,xTIME,PAR,xx1,xx2)
 s zn=$g(zn),job=$g(job),DATE=$g(DATE),xTIME=$g(xTIME),PAR=$g(PAR),xx1=$g(xx1),xx2=$g(xx2)
 s PAR=$tr(PAR,$c(2),"\")
 ; check and convert date variables
 f i=1:1:$l(PAR,"\") i $p(PAR,"\",i)["{" d
 .s date="",DateType=$tr($p(PAR,"\",i),"{}")
 .s DateType=$$UPPER^SSUTIL4(DateType)
 .i $e(DateType,1,2)="YS" d
 ..s x=$$julian^SSUTIL4(+$h),date=$h-(x-1)
 ..i $e(DateType,3,4)="--" s x=$p(DateType,"--",2),year=$p($$extdate2^SSUTIL4($h),"/",3)-x,date=$$intdate^SSUTIL4("1/1/"_year) q
 ..i $e(DateType,3,4)="++" s x=$p(DateType,"++",2),year=$p($$extdate2^SSUTIL4($h),"/",3)+x,date=$$intdate^SSUTIL4("1/1/"_year) q
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1,2)="YE" d
 ..s x=$$extdate2^SSUTIL4(+$h),date=$$intdate^SSUTIL4("12/31/"_$p(x,"/",3))
 ..i $e(DateType,3,4)="--" s x=$p(DateType,"--",2),year=$p($$extdate2^SSUTIL4($h),"/",3)-x,date=$$intdate^SSUTIL4("12/31/"_year) q
 ..i $e(DateType,3,4)="++" s x=$p(DateType,"++",2),year=$p($$extdate2^SSUTIL4($h),"/",3)+x,date=$$intdate^SSUTIL4("12/31/"_year) q
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1,2)="FS" d
 ..s x=$$extdate2^SSUTIL4(+$h),month=$p(x,"/",2),date=$$intdate^SSUTIL4("7/1/"_$s(month>6:$p(x,"/",3),1:$p(x,"/",3)-1))
 ..i $e(DateType,3,4)="--" s x=$p(DateType,"--",2),month=$p($$extdate2^SSUTIL4($h),"/",2),year=$p($$extdate2^SSUTIL4($h),"/",3)-$s(month>6:x,1:x+1),date=$$intdate^SSUTIL4("7/1/"_year) q
 ..i $e(DateType,3,4)="++" s x=$p(DateType,"++",2),month=$p($$extdate2^SSUTIL4($h),"/",2),year=$p($$extdate2^SSUTIL4($h),"/",3)+$s(month>6:x+1,1:x),date=$$intdate^SSUTIL4("7/1/"_year) q
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1,2)="FE" d
 ..s x=$$extdate2^SSUTIL4(+$h),month=$p(x,"/",2),date=$$intdate^SSUTIL4("6/30/"_$s(month>6:$p(x,"/",3)+1,1:$p(x,"/",3)))
 ..i $e(DateType,3,4)="--" s x=$p(DateType,"--",2),month=$p($$extdate2^SSUTIL4($h),"/",2),year=$p($$extdate2^SSUTIL4($h),"/",3)-$s(month>6:x+1,1:x),date=$$intdate^SSUTIL4("6/30/"_year) q
 ..i $e(DateType,3,4)="++" s x=$p(DateType,"++",2),month=$p($$extdate2^SSUTIL4($h),"/",2),year=$p($$extdate2^SSUTIL4($h),"/",3)+$s(month>6:x,1:x+1),date=$$intdate^SSUTIL4("6/30/"_year) q
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1,2)="MS" d
 ..s x=$$extdate2^SSUTIL4(+$h),x=$p(x,"/",1),date=$h-(x-1)
 ..i $e(DateType,3,4)="--" d  q
 ...s x=$p(DateType,"--",2),month=$p($$extdate2^SSUTIL4($h),"/",2)-x,year=$p($$extdate2^SSUTIL4($h),"/",3)
 ...f  q:month>0  s month=month+12,year=year-1
 ...s date=$$intdate^SSUTIL4(month_"/01/"_year)
 ..i $e(DateType,3,4)="++" d  q
 ...s x=$p(DateType,"++",2),month=$p($$extdate2^SSUTIL4($h),"/",2)+x,year=$p($$extdate2^SSUTIL4($h),"/",3)
 ...f  q:month<13  s month=month-12,year=year+1
 ...s date=$$intdate^SSUTIL4(month_"/01/"_year)
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1,2)="ME" d
 ..s x=$$extdate2^SSUTIL4(+$h),month=$p(x,"/",2)+1,year=$p(x,"/",3)
 ..f  q:month<13  s month=month-12,year=year+1
 ..s date=$$intdate^SSUTIL4(month_"/01/"_year)-1
 ..i $e(DateType,3,4)="--" d  q
 ...s x=$p(DateType,"--",2),month=$p($$extdate2^SSUTIL4($h),"/",2)-x+1,year=$p($$extdate2^SSUTIL4($h),"/",3)
 ...f  q:month>0  s month=month+12,year=year-1
 ...s date=$$intdate^SSUTIL4(month_"/01/"_year)-1
 ..i $e(DateType,3,4)="++" d  q
 ...s x=$p(DateType,"++",2),month=$p($$extdate2^SSUTIL4($h),"/",2)+x+1,year=$p($$extdate2^SSUTIL4($h),"/",3)
 ...f  q:month<13  s month=month-12,year=year+1
 ...s date=$$intdate^SSUTIL4(month_"/01/"_year)-1
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1,2)="WS" d
 ..s x=$$daynum^SSUTIL4(+$h),date=$h-(x-1)
 ..i $e(DateType,3,4)="--" s x=$p(DateType,"--",2),date=date-(7*x) q
 ..i $e(DateType,3,4)="++" s x=$p(DateType,"++",2),date=date+(7*x) q
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1,2)="WE" d
 ..s x=$$daynum^SSUTIL4(+$h),date=$h+(7-x)
 ..i $e(DateType,3,4)="--" s x=$p(DateType,"--",2),date=date-(7*x) q
 ..i $e(DateType,3,4)="++" s x=$p(DateType,"++",2),date=date+(7*x) q
 ..i $e(DateType,3)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,3)="-" s x=$p(DateType,"-",2),date=date-x q
 .i $e(DateType,1)="T" d
 ..s date=$h
 ..i $e(DateType,2)="+" s x=$p(DateType,"+",2),date=date+x q
 ..i $e(DateType,2)="-" s x=$p(DateType,"-",2),date=date-x q
 .s $p(PAR,"\",i)=date
 s start=$h,$p(^TMP(zn,job,"DOIT08",DATE,xx1_"||"_xx2,xTIME),"^",1)="START : "_start
 i '$$select^LVBCTCLA(xx1_"||"_xx2) s PLIST(7)=$p(start,","),PLIST(8)=$p(start,",",2) i $$update^LVBCTCLA(xx1_"||"_xx2)
 i $$START^PRTREP00("","",PAR,xx1)
 s finish=$h,$p(^TMP(zn,job,"DOIT08",DATE,xx1_"||"_xx2,xTIME),"^",2)="FINISH : "_finish
 i '$$select^LVBCTCLA(xx1_"||"_xx2) s PLIST(9)=$p(finish,","),PLIST(10)=$p(finish,",",2) i $$update^LVBCTCLA(xx1_"||"_xx2)
 q
]]></Routine>
</Export>
