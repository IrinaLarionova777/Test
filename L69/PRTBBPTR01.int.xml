<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBBPTR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBBPTR01 ; IG 22/09/99 ; Patient/Pack Transaction Report
 q
 ; Parameters :  1. MRN
 ;               2. Unit RowID
 ;               3. Active only
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 i $l(PAR1) d
 .s line=1,xHEADER(line)="Patient Transaction Report"_$s(PAR3="Y":" (Active only)",1:"")
 .s line=2,xHEADER(line)=""
 .s line=3,xHEADER(line)="for "_PAR1_" "_NAME
 .s line=4,xHEADER(line)=""
 i $l(PAR2) d
 .s line=1,xHEADER(line)="Blood Unit Transaction Report"_$s(PAR3="Y":" (Active only)",1:"")
 .s line=2,xHEADER(line)=""
 .s line=3,xHEADER(line)="for "_$p(PAR2,"||",1)
 .s line=4,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 s NAME=""
 i $l(PAR1),'$$select^LVBDEB(PAR1,"Y"),'$$select^LVBEPVIS(PLIST(4),"Y") s NAME=$$NameFormat^LVBCOM03("PT",$p(PLIST(7),$c(1),2),PLIST(4),PLIST(3),PLIST(99),PLIST(100),PLIST(101))
 q
BODY ; create temporary printing file
 i $l(PAR1) d
 .i $$open^LVBBBPT(,PAR1)
 .f cnt=1:1 q:$$fetch^LVBBBPT(,PAR1)  d
 ..i PAR3="Y",PLIST(13)="Y" q
 ..m ^TMP($zn,$j,"REPORT",cnt)=PLIST
 .i $$close^LVBBBPT()
 i $l(PAR2) d
 .i $$open^LVBBBPT(PAR2)
 .f cnt=1:1 q:$$fetch^LVBBBPT(PAR2)  d
 ..i PAR3="Y",PLIST(13)="Y" q
 ..m ^TMP($zn,$j,"REPORT",cnt)=PLIST
 .i $$close^LVBBBPT()
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s cnt="" f  s cnt=$o(^TMP($zn,$j,"REPORT",cnt)) q:cnt=""  d
 ..k BBPT m BBPT=^TMP($zn,$j,"REPORT",cnt)
 ..k BBP i '$$select^LVBBBP($p(BBPT(1),"||",1,2)) m BBP=PLIST
 ..s xxx(1)=$$extdate2^SSUTIL4(BBPT(4))
 ..s xxx(2)=$$exttime^SSUTIL4(BBPT(5))
 ..s xxx(3)=BBPT(6)
 ..s xxx(4)=BBP(2)
 ..s xxx(5)=BBP(8)
 ..s xxx(6)=BBP(9)
 ..s xxx(7)=BBPT(3)
 ..s xxx(8)=BBPT(10)
 ..s xxx(9)=$s(BBPT(13)="Y":"Yes",1:"")
 ..d PRINT^PRTREP00(,.N) k xxx
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="BBPTR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
