<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF150" type="INT" languagemode="0" generated="1"><![CDATA[
MIF150(mi) ; ML 18/11/2000 ; Optical Reader (Mark & Barcode)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),enq=$c(5),eot=$c(4),lf=$c(10),cr=$c(13),nak=$c(21)
 s (result,epis)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main d Next
 s record=$$read^MIF000("",lf) q:'$l(record)
 d trace^MIF000(mi,record,"H<--M")
 i $e(record,1,1)="M" q
 s Res=$$PROCESS(record)
 s ok=$$Crear(Res)
 d ACK:'ok,NACK:ok
 q
Next ;
 w "L",*-3
 d trace^MIF000(mi,"ENQ","H-->M")
 q
ACK 
 w "G",*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
NACK 
 w "S",*-3
 d trace^MIF000(mi,"NACK","H-->M")
 q
PROCESS(STR) 
 N (STR)
 ; INICIALIZAR LINEAS
 F I=1:1:34 S LIN(I)=""
 ; OBTENER LINEAS
 F  S ST=$E(STR,1,12),$E(STR,1,12)="" Q:$e(ST,3,12)="FFFFFFFFFF"  q:$l(ST)<12  D
 . S LIN(+$E(ST,1,2))=$ZH($E(ST,3,12))
 ; Obtener Doctor
 s Doc="     "
 f p=1:1:5 d  q:Doc=""
 . f i=0:1:9 i $$BIT(LIN(i+1),39-(p*2)) s:$e(Doc,p,p)=" " $e(Doc,p,p)=i i i'=$e(Doc,p,p) s Doc="" q
 i Doc="     " s Doc=0
 f p=1:1:5 i $e(Doc,1)=" " s Doc=$e(Doc,2,$l(Doc))
 i Doc'?1.N s Doc=0
 i $$CHKDOC(Doc) s Doc=0
 ; Obtener Centro
 s Cen=" "
 f i=1:2:7 i $$BIT(LIN(i),25) s:Cen'=" " Cen="" q:Cen=""  s Cen=$p("G--M--I--T","-",i)
 s:Cen=" " Cen=""
 ; Obtener Ala
 s Ala=" "
 f i=3:2:9 i $$BIT(LIN(i),23) s:Ala'=" " Ala="" q:Ala=""  s Ala=$p("Centro--Pre--Diagonal--Reanim","-",i-2)
 s:Ala=" " Ala=""
 ; Obtener Planta
 s Pla=" "
 f i=1:1:16 i $$BIT(LIN(i),20) s:Pla'=" " Pla="" q:Pla=""  s Pla=i-2 s:Pla<1 Pla=$s(i=1:"S",1:"SS")
 s:Pla=" " Pla=""
 ; Obtener Habitacion
 s Hab="  "
 f p=1,2 d  q:Hab=""
 . f i=0:1:9 i $$BIT(LIN(i+1),20-(p*2)) s:$e(Hab,p,p)=" " $e(Hab,p,p)=i i i'=$e(Hab,p,p) s Hab="" q
 s:$F(Hab," ") Hab=""
 ; Obtener Cama
 s Cam=" "
 f i=1:1:10 i $$BIT(LIN(i),14) s:Cam'=" " Cam="" q:Cam=""  s Cam=i
 s:Cam=" " Cam=""
 ; Obtener Test Codificados
 s Test=""
 f i=1:1:35 i $$BIT(LIN(((i-1)#7)+1),(((i-1)\7)*-2)+10) s Test=Test_$S($D(^ORTEST(i)):^ORTEST(i)_",",1:"")
 ; Obtener tratamiento
 s (Oxig,Anti)=""
 i $$BIT(LIN(17),25) s Anti="SINTROM"
 i $$BIT(LIN(17),20) s Anti=$s(Anti="":"",1:Anti_",")_"HEPARINA"
 i $$BIT(LIN(17),9) s Oxig="OXIGENOTERAPIA"
 ; Obtener Resto Test
 i $$BIT(LIN(18),38) s Test=Test_"U005,"
 i $$BIT(LIN(23),38) s Test=Test_"U280,"
 i $$BIT(LIN(24),38) s Test=Test_"U285,"
 i $$BIT(LIN(27),38) s Test=Test_"U015,"
 i $$BIT(LIN(28),38) s Test=Test_"U030,"
 i $$BIT(LIN(29),38) s Test=Test_"U015,"
 i $$BIT(LIN(33),38) s Test=Test_"U010,"
 i $$BIT(LIN(34),38) s Test=Test_"U050,"
 i $$BIT(LIN(18),25) s Test=Test_"U150,"
 i $$BIT(LIN(19),25) s Test=Test_"U070,"
 i $$BIT(LIN(20),25) s Test=Test_"U105,"
 i $$BIT(LIN(21),25) s Test=Test_"U065,"
 i $$BIT(LIN(22),25) s Test=Test_"U055,"
 i $$BIT(LIN(23),25) s Test=Test_"U100,"
 i $$BIT(LIN(24),25) s Test=Test_"U090,"
 i $$BIT(LIN(25),25) s Test=Test_"U095,"
 i $$BIT(LIN(26),25) s Test=Test_"U060,"
 i $$BIT(LIN(30),25) s Test=Test_"U215,"
 i $$BIT(LIN(31),25) s Test=Test_"U220,"
 i $$BIT(LIN(32),25) s Test=Test_"U210,"
 i $$BIT(LIN(18),12) s Test=Test_"U009,"
 i $$BIT(LIN(19),12) s Test=Test_"U152,"
 i $$BIT(LIN(25),12) s Test=Test_"U305,"
 i $$BIT(LIN(26),12) s Test=Test_"U247,"
 i $l(Test) s Test=$e(Test,1,$l(Test)-1)
 ; Obtener numero Peticion
 s Pet=$p($p(STR,"'",2),"'",1)
 k tc
 f i=1:1:$l(Test,",") s tc=$p(Test,",",i) i '$d(tc(0,tc)) s tc(0,tc)="",tc(1,i)=tc
 s Test="",tc="" f  s tc=$o(tc(1,tc)) q:tc=""  s Test=Test_$s($l(Test):",",1:"")_tc(1,tc)
 q "|"_Pet_"|"_Doc_"|"_Cen_"|"_Ala_"|"_Pla_"|"_Hab_"|"_Cam_"|"_Test_"|"_Anti_"|"_Oxig_"|"
BIT(VAL,BIT) 
 N POT
 S POT=2**(BIT-1)
 Q VAL\POT#2
Crear(PLISTA)  ;Rutina de carga de los Lectores de marcas
 ;n (PLISTA)
 s ^TMP("USER",$j)="al"
 s npet=$p(PLISTA,"|",2)
 q:$l(npet)=0 4
 s Aux=$p(PLISTA,"|",9)
 q:Aux="" 3
 q:$d(^TEPI(npet)) 0
 q:$$vtsLock^SSLOCK("PEI",npet,"","al","OPTICALREADER")'=-1 2
 f i=1:1:98 s PLIST(i)=""
 s PLIST(2)=npet
 s PLIST(10)=$p($H,",",1)
 s PLIST(11)=$p($H,",",2)
 s PLIST(61)=PLIST(10)
 s PLIST(62)=PLIST(11)
 s PLIST(15)=$p(PLISTA,"|",3)
 s PLIST(29)="PR"
 s PLIST(65)=","
 s PLIST(66)="HULP"
 s PLIST(86)=1
 s ok=$$insert^LVBEPVIS() i +ok d  q 5
 .s ok=$$vtsLockClr^SSLOCK("PEI",npet,"")
 f o=1:1:$l(Aux,",") d  
 .f i=1:1:57 s PLIST(i)=""
 .s PLIST(1)=npet
 .s PLIST(2)=npet_"||"_$p(Aux,",",o)
 .s PLIST(3)=$p(Aux,",",o)
 .s PLIST(35)="~~~"
 .s PLIST(52)=o
 .s ok=$$insert^LVBVISTS(npet,"Patient PreEntry : Patient Pre-Entry","")
 s ok=$$vtsLockClr^SSLOCK("PEI",npet,"")
 q 0
CHKDOC(doc) 
 n (doc)
 q $$select^LVBCTDR(doc)
]]></Routine>
</Export>
