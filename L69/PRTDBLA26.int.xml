<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDBLA26" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDBLA26 ; ML 04/05/2004 ; Day Book Labels - APP
 q
 ; Parameters :  1.Laboratory
 ;               2.Site
 ;               3.Date from
 ;               4.Date to
 ;               5.List episodes/accesion numbers
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=$g(TYPE),PLIST(9)="M" i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$J)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 q
 ; check if visit/accession should print labels
CHECK(XX) n (XX,PAR3,PAR4) s XX=$g(XX)
 ; check all blocks
 i $$open^LVBHIVAB(XX)
 f  q:$$fetch^LVBHIVAB(XX)  d
 .f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 .k BLOCK m BLOCK=PLIST
 . ; check all records
 .i $$open^LVBHIVAO(BLOCK(1))
 .f  q:$$fetch^LVBHIVAO(BLOCK(1))  d
 ..f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ..i $l(PAR3),PLIST(8)<PAR3 q
 ..i $l(PAR4),PLIST(8)>PAR4 q
 ..i PLIST(7)'="Y" s ^TMP($zn,$j,"LIST",PLIST(1))=""
 .i $$close^LVBHIVAO()
 i $$close^LVBHIVAB()
 q
 ; create list of labels to print
BODY k ^TMP($zn,$j)
 ; create list of labels for a laboratory
 i '$l(PAR5) d
 .s x1="" f  s x1=$o(^THIRi("PRINT-LABEL",x1)) q:x1=""  d
 ..i $$select^LVBEPVIS(x1,"Y") q
 ..f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ..k VISIT m VISIT=PLIST
 ..s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 ... ; check laboratory
 ...i $l(PAR1),$e(x2,1)'=PAR1 q
 ... ; check site
 ...i $l(PAR2),$e(x2,2)'=PAR2 q
 ...d CHECK(x1_"||"_x2)
 ; create list of labels for an episodes
 i $l(PAR5) d
 .f j=1:1:$l(PAR5,"|") s x1=$p($p(PAR5,"|",j),"~",1),x2=$p($p(PAR5,"|",j),"~",2) i $l(x1),$l(x2) d
 ..i x2'="*",$d(^THIRi("PRINT-LABEL",x1,x2)) d CHECK(x1_"||"_x2)
 ..i x2="*" s x2="" f  s x2=$o(^THIRi("PRINT-LABEL",x1,x2)) q:x2=""  d
 ... ; check laboratory
 ...i $l(PAR1),$e(x2,1)'=PAR1 q
 ... ; check site
 ...i $l(PAR2),$e(x2,2)'=PAR2 q
 ...d CHECK(x1_"||"_x2)
 ; Sort
 s xROWID="" f  s xROWID=$o(^TMP($zn,$j,"LIST",xROWID)) q:xROWID=""  d
 .s SORT=$p(xROWID,"||",2)
 .s ^TMP($zn,$j,"REPORT",SORT,xROWID)=""
 q
 ; print all labels
PRINT i $d(^TMP($zn,$j,"REPORT")) s Position=0 d
 .s SORT="" f  s SORT=$o(^TMP($zn,$j,"REPORT",SORT)) q:SORT=""  d
 ..s xROWID="" f  s xROWID=$o(^TMP($zn,$j,"REPORT",SORT,xROWID)) q:xROWID=""  d
 ...s TYPE=$e($p(xROWID,"||",2))_"-"_PAR2 d
 .... ; order information
 ....i $$select^LVBHIVAO(xROWID) q
 ....f j=1:1:4,6:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k ORDER m ORDER=PLIST
 .... ; update record that it had been printed already
 ....f j=1:1:9 i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....s PLIST(7)="Y" i $$update^LVBHIVAO(xROWID)
 .... ; visit information
 ....i $$select^LVBEPVIS($p(xROWID,"||"),"Y") q
 ....f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k VISIT m VISIT=PLIST
 .... ; accession number information
 ....i $$select^LVBHIVAC($p(xROWID,"||",1,2)) q
 ....f j=1:1:PLIST i $d(PLIST(j)) s PLIST(j)=$p(PLIST(j),$c(1))
 ....k ACCESS m ACCESS=PLIST
 ....
 .... ; histo slides
 ....
 ....i $p(TYPE,"-")="H" d
 ..... ; line - 1
 .....d ADD^SSPRINT(REPID,"PRT","$#1"_VISIT(2)_"#N")
 ..... ; line - 2
 .....s x=$e(ACCESS(2),1,$l(ACCESS(2))-1),x=$e(x)_"-"_$e(x,2,3)_" "_$e(x,4,$l(x))
 .....d ADD^SSPRINT(REPID,"PRT","#2"_x_"#N")
 ..... ; line - 3
 .....d ADD^SSPRINT(REPID,"PRT","#2"_VISIT(3)_"#N")
 ..... ; line - 4
 .....s x=($a($e(ACCESS(2),$l(ACCESS(2))))-64)_$p(ORDER(1),"||",3)_" "_ORDER(10)
 .....d ADD^SSPRINT(REPID,"PRT","#2"_x)
 ..... ; eject cassete
 .....d ADD^SSPRINT(REPID,"PRT",$c(13))
 ..... ; wait
 .....d ADD^SSPRINT(REPID,"PRT","~WAIT=15")
 ....
 .... ; cyto labels
 ....
 ....i $p(TYPE,"-")="C" d
 .....s LF=$c(10),LABnum=1,tests=""
 .....s vts="" s vts=$o(^THIR($p(xROWID,"||",1),$p(xROWID,"||",2),"TEST",vts)) q:vts=""  d
 ......s syn=$$seldata^LVBCTTS($p(vts,"||",2),4) i $l(syn) s tests=tests_$s($l(tests):" ",1:"")_syn
 .....d ADD^SSPRINT(REPID,"PRT",LF)
 .....d ADD^SSPRINT(REPID,"PRT","FK""*"""_LF)
 .....d ADD^SSPRINT(REPID,"PRT","FK""LABEL"_LABnum_""""_LF)
 .....d ADD^SSPRINT(REPID,"PRT","FS""LABEL"_LABnum_""""_LF)
 .....d ADD^SSPRINT(REPID,"PRT","Q152,48"_LF)
 .....d ADD^SSPRINT(REPID,"PRT","R8,0"_LF)
 .....f jCol=0:1:2 d
 ......s left=130+(jCol*200)
 ......d ADD^SSPRINT(REPID,"PRT","A"_left_",20,0,3,1,1,N,""APP-"_VISIT(2)_""""_LF)
 .....f jCol=0:1:2 d
 ......s left=130+(jCol*200)
 ......s x=ACCESS(2),x=$e(x,1,3)_" "_$e(x,4,$l(x)-1)
 ......d ADD^SSPRINT(REPID,"PRT","A"_left_",50,0,4,1,1,N,"""_x_""""_LF)
 .....f jCol=0:1:2 d
 ......s left=130+(jCol*200)
 ......d ADD^SSPRINT(REPID,"PRT","A"_left_",90,0,2,1,1,N,"""_VISIT(3)_" "_$e(VISIT(4))_"."""_LF)
 .....f jCol=0:1:2 d
 ......s left=130+(jCol*200)
 ......d ADD^SSPRINT(REPID,"PRT","A"_left_",130,0,2,1,1,N,"""_tests_""""_LF)
 .....d ADD^SSPRINT(REPID,"PRT","FE"_LF)
 .....d ADD^SSPRINT(REPID,"PRT","FR""LABEL"_LABnum_""""_LF)
 .....d ADD^SSPRINT(REPID,"PRT","P1"_LF)
 q
]]></Routine>
</Export>
