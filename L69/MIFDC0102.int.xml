<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIFDC0102" type="INT" languagemode="0" generated="1"><![CDATA[
MIFDC0102(mi) ; ML 21/3/01 ; Daybook Cassette Writer - RA LAMB Cassette MicroWriter - SJOG(new)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s white=1,orange=3
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main h 30
 s epis="" f  s epis=$o(^THIRi("BLOCK-PRINT",epis)) q:epis=""  d
 .q:$$select^LVBEPVIS(epis)  k VISIT m VISIT=PLIST k PLIST
 .l +^THIRi("BLOCK-PRINT",epis):5 e  q
 .s accession="" f  s accession=$o(^THIRi("BLOCK-PRINT",epis,accession)) q:accession=""  d
 ..q:$$select^LVBHIVAC(epis_"||"_accession)  k ACC m ACC=PLIST k PLIST
 ..s AccNum=$p(ACC(9),$c(1))_$p(ACC(8),$c(1))_$p(ACC(14),$c(1))_$e($p(ACC(10),$c(1)),1,$l($p(ACC(10),$c(1)))-1)_" "_($a($e($p(ACC(10),$c(1)),$l($p(ACC(10),$c(1)))))-64)
 ..s block="" f  s block=$o(^THIRi("BLOCK-PRINT",epis,accession,block)) q:block=""  d
 ...q:$$select^LVBHIVAB(epis_"||"_accession_"||"_block)  k BLOCK m BLOCK=PLIST k PLIST
 ...s hopper=$$Hopper(BLOCK(3)) i hopper d
 .... ; change to multicolour
 ....w "$#KC",$c(13),*-3
 .... ; font size 2
 ....w "$#2",*-3
 .... ; switch to correct hopper
 ....w "#H"_hopper,*-3
 ....w AccNum_"."_block,*-3
 .... ; new line
 ....w "#N",*-3
 .... ; font selection
 ....w "#2",*-3
 ....w $e(VISIT(3),1,5)_$s($l(VISIT(3))>5:"*",1:"")_" "_$p(ACC(3),$c(1)),*-3
 .... ; eject cassette
 ....w $c(13),*-3 h 5
 ...m PLIST=BLOCK s PLIST(5)="Y" i $$update^LVBHIVAB(epis_"||"_accession_"||"_block)
 .l -^THIRi("BLOCK-PRINT",epis)
 q
 ; hopper colours - 1 : white   - 
 ;                  2 : white   - 
 ;                  3 : orange  - 
 ;                  4 : orange  - 
 ;                  5 : blue    - 
 ;                  6 : green   - 
 ;                  6 : pink    - 
 ;                  6 : yellow  - 
Hopper(block) s block=$g(block),hopper=""
 i '$$select^LVBCTDBA($p(ACC(8),$c(1))_"||"_block) d
 .s x=$$UPPER^SSUTIL4(PLIST(3))
 .i x["WHITE" s hopper=white,white=3-hopper
 .i x["ORANGE" s hopper=orange,orange=7-hopper
 .i (x["BLUE")!(x["PINK") s hopper=5
 .i (x["GREEN")!(x["YELLOW") s hopper=6
 q hopper
]]></Routine>
</Export>
