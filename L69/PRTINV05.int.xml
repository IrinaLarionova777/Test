<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV05" type="INT" languagemode="0" generated="1"><![CDATA[
PRTINV05 ; ML 03/03/00 ; PRIVATE INVOICES - WESTPATH SERVICES
 q
 ; Parameters :  1.Payment Code
 ;               2.Batch Number
 ;               3.Episode Number
 ;               4.Sequence Number
 ;               5.Reminder
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i 'PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)=PAR1_"-"_PAR2 i $$update^LVBPRPR(REPID)
 i PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)="REM-"_PAR5 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
BODY ; create temporary printing file
 ; print single patient
 i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
 .m VISIT=PLIST k PLIST s epis=PAR3
 .s pc=$p(VISIT(57),"||",1),batch=$p(VISIT(57),"||",2)
 .s seq=$p($g(^TMCB(pc,batch,epis)),"\",2) i $l(seq) d
 ..s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq
 ..d ITEM
 ; print batch
 i $l(PAR1),$l(PAR2) d
 .s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
 ..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
 ..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
 ..i $l(PAR4),PAR4'=seq q
 ..i '$$select^LVBEPVIS(epis,"Y") d  q
 ...m VISIT=PLIST k PLIST
 ...s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq
 ...d ITEM
 ; check credits
 s (credit,epis)="" f cnt=1:1 s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
 .s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 .s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ..s rowid=deb_"||"_date_"||"_ep
 ..i $$open^LVBDEBVT(rowid)
 ..f  q:$$fetch^LVBDEBVT(rowid)  d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k TRAN m TRAN=PLIST
 ...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) d
 ....s credit=credit-PLIST(14)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) d
 ....s credit=credit-PLIST(16)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) d
 ....s credit=credit-PLIST(9)
 ..i $$close^LVBDEBVT()
 .i credit s ^TMP($zn,$j,"EPIS",epis,"CREDIT")=credit,credit=""
 ; set events
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
 .s invoice=$p($p(^(epis),"\",2),"||")_"-"_$p($p(^(epis),"\",2),"||",2)_"-"_$p($p(^(epis),"\",2),"||",3)
 .i '$l(PAR5) d events^LVBCOM01(epis,"ACC","Original invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 .i 'PAR5 d events^LVBCOM01(epis,"ACC","Duplicate invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 .d events^LVBCOM01(epis,"ACC","Reminder "_PAR5_$s($l(USER):" printed by "_USER,1:""))
 q
PRINT ; print report
 d QDR,PATH
 s max=20
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
 .k PAYC i '$$select^LVBCTPC(VISIT(9)) m PAYC=PLIST k PLIST
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3))
 .f  q:$e(name)'=" "  s name=$e(name,2,99)
 .s (line,nextpg,item)=""
 .d ADDRESS,PHEAD
 .f  s item=$o(^TMP($zn,$j,"EPIS",epis,"ITEM",item)) q:item=""  d
 ..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2)
 .. ; check item desc length
 ..s x="" s xdesc=desc k desc s desc=xdesc
 ..i $l(desc)<23 s desc(1)=desc
 ..i $l(desc)>22 d
 ...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>22 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>22
 ..d ADD^PRTREP00(REPID,"PRT","?10 "_desc(1))
 ..d ADD^PRTREP00(REPID,"PRT","?42 "_item)
 ..d ADD^PRTREP00(REPID,"PRT","?74 "_$j(amt,7,2)),LN s line=line+1
 ..f i=2:1 q:'$d(desc(i))  d ADD^PRTREP00(REPID,"PRT","?10 "_desc(i)),LN s line=line+1
 ..i line=18,$o(^TMP($zn,$j,"EPIS",epis,"ITEM",item))'="" d
 ...d LN,ADD^PRTREP00(REPID,"PRT","?61 Continued over..")
 ...s nextpg=1 d PHEAD s (line,nextpg)=""
 .s lcc="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1)),"Y") s lcc=$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))
 .i $l(lcc) d
 ..d LN,LN s line=line+2
 ..d ADD^PRTREP00(REPID,"PRT","?10 SCP No: "_lcc)
 .f i=1:1:18-line-$s(PAR5:3,1:0) d LN
 .i PAR5=1 d
 ..d ADD^PRTREP00(REPID,"PRT","?10 Our records show that this account is outstanding."),LN
 ..d ADD^PRTREP00(REPID,"PRT","?10 If payment has been forwarded please disregard this reminder, otherwise prompt"),LN
 ..d ADD^PRTREP00(REPID,"PRT","?10 payment would be appreciated."),LN
 .i PAR5=2 d
 ..d ADD^PRTREP00(REPID,"PRT","?10 This account is now overdue. Payment is required immediately, as further"),LN
 ..d ADD^PRTREP00(REPID,"PRT","?10 action may commence. Please call us if you have any queries."),LN
 ..d LN
 .i PAR5=3 d LN,LN,LN
 . ; print paycode invoice message
 .s tot=$g(^TMP($zn,$j,"EPIS",epis,"TOT")),credit=$g(^TMP($zn,$j,"EPIS",epis,"CREDIT"))
 .i credit d ADD^PRTREP00(REPID,"PRT","?67 Credit:"_$j(credit,7,2)_" CR")
 .d LN
 .i $l(PAYC(10)) d
 ..d ADD^PRTREP00(REPID,"PRT","?10 ")
 ..d ADD^PRTREP00(REPID,"PRT","~BOLD"),ADD^PRTREP00(REPID,"PRT","~ITALIC")
 ..d ADD^PRTREP00(REPID,"PRT",PAYC(10))
 ..d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 .d LN
 .d ADD^PRTREP00(REPID,"PRT","?74 "_$j(tot-credit,7,2)),LN
 .f i=1:1:3 d LN
 .s drtab=81-$l(drprov)_"   ",pathtab=81-$l(pathprv)_"   ",drtab=$e(drtab,1,3),pathtab=$e(pathtab,1,3)
 .d ADD^PRTREP00(REPID,"PRT","?26 "_drdet),ADD^PRTREP00(REPID,"PRT","?"_drtab_drprov),LN
 .d ADD^PRTREP00(REPID,"PRT","?26 "_pathdr),ADD^PRTREP00(REPID,"PRT","?"_pathtab_pathprv)
 .f i=1:1:11 d LN
 .d ADD^PRTREP00(REPID,"PRT","?20 "_$j(tot-credit,0,2)) f i=1:1:2 d LN
 .d ADD^PRTREP00(REPID,"PRT","?20 "_name) f i=1:1:2 d LN
 .d ADD^PRTREP00(REPID,"PRT","?20 "_epis)
 .d ADD^PRTREP00(REPID,"PRT","?49 "_$$extdate2^SSUTIL4(VISIT(12)))
 .d ADD^PRTREP00(REPID,"PRT","?76 "_$$extdate2^SSUTIL4(VISIT(56)))
 q
PHEAD ; print patient details
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h50V")
 f i=1:1:10 d LN
 d ADD^PRTREP00(REPID,"PRT","?12 "_name),LN
 f i=1:1:4 i $l(ADDRESS(i)) d ADD^PRTREP00(REPID,"PRT","?12 "_ADDRESS(i)) i i'=4 d LN
 f i=1:1:3 i '$l(ADDRESS(i)) d LN
 d ADD^PRTREP00(REPID,"PRT","?73 "_$$extdate2^SSUTIL4(VISIT(56)))
 f i=1:1:3 d LN
 d ADD^PRTREP00(REPID,"PRT","?23 "_$$extdate2^SSUTIL4(VISIT(14)))
 d ADD^PRTREP00(REPID,"PRT","?50 "_$$extdate2^SSUTIL4(VISIT(12)))
 d ADD^PRTREP00(REPID,"PRT","?73 "_epis),LN
 d DOC
 f i=1:1:3 d LN
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 .s ^TMP($zn,$j,"EPIS",epis,"ITEM",item)=det
 s ^TMP($zn,$j,"EPIS",epis,"TOT")=amount
 q
DOC ; extract referring doctor details
 s (drdet,drprov)=""
 q:$$select^LVBCTDR(VISIT(15))  m DOC=PLIST k PLIST
 s drdet=$$NameFormat^LVBCOM03("DR",DOC(26),DOC(4),DOC(3)),drprov="Prov No: "_DOC(18)
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
PATH ; extract pathology doctor details
 s (pathdr,pathprv)=""
 i '$$select^LVBINVB(PAR1_"||"_PAR2) s pathdr=$p(PLIST(10),$c(1),2),pathprv="Prov No: "_PLIST(5)
 q
LN ; add line to report
 d ADD^PRTREP00(REPID,"PRT","!")
 q
QDR ; set header
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"E")
 s x="" f  s x=$o(^SS("QDR","WPS-INV",x)) q:x=""  d ADD^PRTREP00(REPID,"HDR",^(x))
 q
]]></Routine>
</Export>
