<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBHL7WEBOUT01" type="INT" languagemode="0" generated="1"><![CDATA[
LVBHL7WEBOUT01 ; ML 15/03/02 ; HL7 RESULT COMPILER - Labtrak to Medtrak Web
 q
 ; loop thru and find unsent episodes
create 
 n
 i '$$select^MVBCFLAB(1) m SYSDET=PLIST k PLIST
 s:'$g(SYSDET(18)) SYSDET(18)="*"
 s:'$g(SYSDET(19)) SYSDET(19)="*"
 ; environment - T = Test
 ;               P = Production/Live
 s environ=$p($g(SYSDET(45)),$c(1)) i environ="L" s environ="P"
 i " P T "[(" "_environ_" ") s Pcount=0 d
 . ; check episode detail changes
 .s reptype="" f  s reptype=$o(^TEDI("OUT","HL7",reptype)) q:reptype=""  d  q:Pcount=50
 ..s client="" f  s client=$o(^TEDI("OUT","HL7",reptype,client)) q:client=""  d  q:Pcount=50
 ...s hospdoc="" f  s hospdoc=$o(^TEDI("OUT","HL7",reptype,client,hospdoc)) q:hospdoc=""  d  q:Pcount=50
 .... ; ignore uncoded doctors
 ....i hospdoc["NC-" q
 ....s epis="" f  s epis=$o(^TEDI("OUT","HL7",reptype,client,hospdoc,epis)) q:epis=""  s Pcount=Pcount+1 d  q:Pcount=50
 ..... ; check if new patient
 .....i '$d(^TEDI("OUT","HL7",reptype,client,hospdoc,epis,42)) q
 .....d DETpat
 .....i $$NewPat() d
 ......d BUILD
 .....k ^TEDI("OUT","HL7",reptype,client,hospdoc,epis,42)
 q
 ; is this a new patient
NewPat() s (cnt,date)="" f  s date=$o(^TDEBi(mrn,"DATE",date)) q:date=""  s cnt=cnt+1
 i cnt>1 q 0
 s (cnt,date)="" f  s date=$o(^TDEBi(mrn,"DATE",date)) q:date=""  d
 .s episx="" f  s episx=$o(^TDEB(mrn,"DATE",date,0,episx)) q:episx=""  s cnt=cnt+1
 q $s(cnt>1:0,1:1)
 ; extract details - patient
DETpat s (mrn,carepis,caremrn,name,dob,sex,add,homeph)=""
 s (drcode,drcumul,drprov,drname)=""
 i '$$select^LVBEPVIS(epis,"Y") m VISIT=PLIST k PLIST d
 .s title=$p(VISIT(7),$c(1)),colldate=$p(VISIT(12),$c(1)),colltime=$p(VISIT(13),$c(1)),loc=$p(VISIT(66),$c(1))
 .s mrn=$p(VISIT(20),$c(1)),carepis=$p(VISIT(65),",",1),caremrn=$p(VISIT(65),",",2)
 .i caremrn="",$d(^THOS("H","PATIENT",mrn)) s caremrn=mrn
 .s ccisorder=$p(VISIT(67),$c(1))
 .s name=$p(VISIT(3),$c(1))_"^"_$p(VISIT(4),$c(1))_"^^^"_title
 .i VISIT(6)'=-1,(VISIT(6)'="") s dob=$tr($zd($p(VISIT(6),$c(1)),3),"-","")
 .s sex=$p(VISIT(5),$c(1))
 .s add=$p(VISIT(32),$c(1))_"^"_$p(VISIT(33),$c(1))_"^"_$p(VISIT(34),$c(1))_"^^"_$p(VISIT(36),$c(1))
 .s homeph=$p(VISIT(43),$c(1))
 . ; extract details - doctor
 .s drcode=$p(VISIT(15),$c(1))
 .i '$$select^LVBCTDR(drcode,"Y") m DOC=PLIST k PLIST d
 ..s drname=$p(DOC(26),$c(1))_" "_$p(DOC(3),$c(1))_" "_$p(DOC(4),$c(1))
 ..s drprov=$p(DOC(18),$c(1)),drcumul=$p(DOC(34),$c(1))
 q
BUILD ; build packet strings
 l +^TMPclean k ^TMP($zn,$j) k PLIST,visres s uniseq=$$new()
 s currdate=$tr($zd(+$h,3),"-",""),currtime=$tr($$exttime^SSUTIL4($p($h,",",2)),":","")
 s ^TMP($zn,$j,"MSH")="MSH|^~\&|LABTRAK||MEDTRAK||"_currdate_currtime_"||ADT^A28|"_uniseq_"|"_environ_"|2.3|||AL|NE"
 s ^TMP($zn,$j,"PID")="PID||"_mrn_"|||"_name_"||"_dob_"|"_sex_"|||"_add_"||"_homeph_"|"
 i ccisorder="" s ^TMP($zn,$j,"PV1")="PV1||*|",$p(^TMP($zn,$j,"PV1"),"|",20)=carepis
 s colltimex=$tr($$exttime^SSUTIL4(colltime),":","") s:colltimex="-1" colltimex="0000"
 s colldatex=$tr($zd(colldate,3),"-","")
 ; get packets in order of transmission
BUILDend s system="MED"
 l +^TEDI("OUT-DATA","HL7",system,epis,mrn,uniseq)
 k str f type="MSH","PID","PV1" s str=$g(^TMP($zn,$j,type)) d SET(str,system)
 f xx="A","I" d
 .s j="" f  s j=$o(^TMP($zn,$j,"OBR",xx,j)) q:j=""  s str=^TMP($zn,$j,"OBR",xx,j) d SET(str,system) d
 ..s k="" f  s k=$o(^TMP($zn,$j,"OBX",xx,j,k)) q:k=""  s str=^TMP($zn,$j,"OBX",xx,j,k) d SET(str,system)
 l -^TEDI("OUT-DATA","HL7",system,epis,mrn,uniseq)
 l -^TMPclean k ^TMP($zn,$j)
 q
 ; build transmission global
SET(line,system) s line=$g(line)
 s x=$o(^TEDI("OUT-DATA","HL7",system,epis,mrn,uniseq,""),-1)+1
 s ^TEDI("OUT-DATA","HL7",system,epis,mrn,uniseq,x)=line
 q
 ; create new sequence number for MSH record
new() s num=$$next("^CF(""HOSP-SEQ"")")
 q num
next(glo) n a
 l +@glo
 s a=$g(@glo)+1,@glo=a
 l -@glo
 q a
]]></Routine>
</Export>
