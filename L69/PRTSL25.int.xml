<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTSL25" type="INT" languagemode="0"><![CDATA[
PRTSL25 ; ML 13/08/2004 ; Summons list (with file creation) - South Coast Independent Pathology
 q
 ; Parameters : 1.Date
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Summons List"
 s line=2,xHEADER(line)=""
 d BODY,PRINT,PRINTSL,FILE
CREND k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 k xSORT d H(PAR10,$zn)
 ; set 'page max lines'
 s MaxLine="" i '$$select^LVBPRPR(REPID) d
 .s PageType=$p(PLIST(8),$c(1)) k PLIST i $$select^LVBCTPSF(PageType)
 .s MaxLine=$p($g(PLIST(4)),$c(1))
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
 ; create temporary printing file
BODY s epis="" f  s epis=$o(^TDAY(3,"S-"_PAR1,epis)) q:epis=""  d
 . ; sort in invoice order
 .s x=$$invoice^LVBEPVIS(epis)
 .f j=1:1:3 s @("x"_j)=$p(x,"||",j) i '$l(@("x"_j)) s @("x"_j)=" "
 .s ^TMP($zn,$j,"REPORT",x1,x2,x3,epis)=""
 .d events^LVBCOM01(epis,"ACC","Summons list"_$s($l(USER):" printed by "_USER,1:""))
 q
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s page=1 d HEADER(.xHEADER,PAR1)
 .s x1="" f  s x1=$o(^TMP($zn,$j,"REPORT",x1)) q:x1=""  d
 ..s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",x1,x2)) q:x2=""  d
 ...s x3="" f  s x3=$o(^TMP($zn,$j,"REPORT",x1,x2,x3)) q:x3=""  d
 ....k XXX s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",x1,x2,x3,epis)) q:epis=""  d
 .....k PLIST i $$select^LVBEPVIS(epis) q
 .....f j=12,20 s PLIST(j)=$p(PLIST(j),$c(1))
 .....i $l(PLIST(57)) d
 ......s XXX(1,3)=$p(PLIST(57),"||",1)_"-"_$p(PLIST(57),"||",2)
 ......s seq=$p(^TMCB($p(PLIST(57),"||",1),$p(PLIST(57),"||",2),epis),"\",2) i $l(seq) s XXX(1,3)=XXX(1,3)_"-"_seq
 ......s XXX(1,5)=$g(XXX(1,5))+$p(^TMCB($p(PLIST(57),"||",1),$p(PLIST(57),"||",2),epis),"\",1)
 .....s XXX(1,4)=$g(XXX(1,4))+$p(^TDEB(PLIST(20),PLIST(12),0,epis),"\",1)
 .....s XXX(1,1)=PLIST(32),XXX(1,2)=$s($l(PLIST(37)):PLIST(37),1:PLIST(32))
 .....s XXX(2,1)=PLIST(33),XXX(2,2)=$s($l(PLIST(37)):PLIST(38),1:PLIST(33))
 .....s XXX(3,1)=PLIST(34)_","_PLIST(35)_" "_PLIST(36),XXX(3,2)=$s($l(PLIST(37)):PLIST(39)_","_PLIST(40)_" "_PLIST(41),1:PLIST(34)_","_PLIST(35)_" "_PLIST(36))
 ....i $l($g(xxx(3)),"-")=2 d
 .....s PLIST(41)="",PLIST(42)=$p($g(^TTAB("BI",$p(xxx(3),"-"))),"\")
 .....f j=1:1:3 s XXX(j,2)=$p($g(^TTAB("BI",$p(xxx(3),"-"))),"\",8+j)
 ....f j=1,2,3 k xxx m xxx=XXX(j) d PRINT^PRTREP00(.PLIST,.N) k xxx,PLIST
 ....d ADDLINE^PRTREP00(REPID,"PRT","!")
 ....d ADDLINE^PRTREP00(REPID,"PRT",xLINE(4))
 .... ; check if new page required
 ....f i=1:1:2 d LINECOUNT
 ....i linecnt>(MaxLine-1) d
 .....d ADDLINE^PRTREP00(REPID,"PRT","#") s page=page+1 d HEADER(.xHEADER,PAR1)
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="SL"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" d PRINTEND^PRTREP00(xLINE(1))
 q
 ; print summons letter
PRINTSL s x1="" f  s x1=$o(^TMP($zn,$j,"REPORT",x1)) q:x1=""  d
 . ; calculate amount
 .s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",x1,x2)) q:x2=""  d
 ..s x3="" f  s x3=$o(^TMP($zn,$j,"REPORT",x1,x2,x3)) q:x3=""  d
 ...s amount=""
 ...s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",x1,x2,x3,epis)) q:epis=""  d
 ....k PLIST i $$select^LVBEPVIS(epis) q
 ....k VISIT m VISIT=PLIST k PLIST f j=12,20 s VISIT(j)=$p(VISIT(j),$c(1))
 ....s Invoice="" i $l(VISIT(57)) d
 .....s Invoice=$p(VISIT(57),"||",1)_"-"_$p(VISIT(57),"||",2)
 .....s seq=$p(^TMCB($p(VISIT(57),"||",1),$p(VISIT(57),"||",2),epis),"\",2) i $l(seq) s Invoice=Invoice_"-"_seq
 ....s amount=$g(amount)+$p(^TDEB(VISIT(20),VISIT(12),0,epis),"\",1)
 ...d ADD^SSPRINT(REPID,"PRT","#")
 ...d ADD^SSPRINT(REPID,"PRT","~ORIENTATION=0")
 ...d ADD^SSPRINT(REPID,"PRT",$c(27)_"(s1p11v0s0b4148T")
 ...f j=1:1:9 d LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 "_$$extdate2^SSUTIL4(+$h))
 ...f j=1:1:2 d LN
 ...d ADDRESS
 ...i $l(ADDRESS(1)) d ADD^SSPRINT(REPID,"PRT","?17 "_ADDRESS(1)),LN
 ...i $l(ADDRESS(2)) d ADD^SSPRINT(REPID,"PRT","?17 "_ADDRESS(2)),LN
 ...i $l(ADDRESS(3)) d ADD^SSPRINT(REPID,"PRT","?17 "_ADDRESS(3)),LN
 ...i $l(ADDRESS(4)) d ADD^SSPRINT(REPID,"PRT","?17 "_ADDRESS(4)) 
 ...d ADD^SSPRINT(REPID,"PRT",$c(27)_"(s1p11v0s0b4148T")
 ...f j=1:1:6 d LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 Dear Sir/Madam"),LN
 ...d ADD^SSPRINT(REPID,"PRT",$c(27)_"(s1p14v0s3b4148T") f j=1:1:3 d LN
 ...d ADD^SSPRINT(REPID,"PRT","?50 FINAL NOTICE") f i=1:1:2 d LN
 ...d ADD^SSPRINT(REPID,"PRT","?8  RE: SOUTH COAST PATHOLOGY - INVOICE NUMBER "_Invoice),LN
 ...d ADD^SSPRINT(REPID,"PRT","?8  TOTAL OUTSTANDING $"_$j(amount,0,2)),LN
 ...d ADD^SSPRINT(REPID,"PRT",$c(27)_"(s1p11v0s0b4148T")
 ...f j=1:1:2 d LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 We have been appointed by South Coast Independent Pathology to act as their agents to receive"),LN 
 ...d ADD^SSPRINT(REPID,"PRT","?10 and collect outstanding accounts beyond the due date."),LN
 ...d LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 We are instructed that you have ignored previous requests for payment of the above"),LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 pathology account."),LN
 ...d LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 Should you not settle this matter or make arrangements to do so within 14 days, action"),LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 to recover this outstanding amount will continue."),LN
 ...d LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 Should you have any queries in relation to this matter you must contact South Coast"),LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 Independent Pathology on (02) 4226 5277 without delay."),LN
 ...f i=1:1:2 d LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 Yours faithfully,"),LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 ILLAWARRA MERCANTILE PTY. LTD."),LN
 ...d ADD^SSPRINT(REPID,"PRT","?10 WOLLONGONG")
 q
FILE i $d(^TMP($zn,$j,"REPORT")) d
 .s dir="" i '$$select^LVBCTPCT("HIC") s dir=PLIST(7)
 .s dir=$tr(dir,$c(16,17,18),"^\|")
 .f j="/","\" i dir[j,$e(dir,$l(dir))'=j s dir=dir_j
 .s file=dir_"SL_"_PAR1_".txt" o file:"WNS" u file
 .s x1="" f  s x1=$o(^TMP($zn,$j,"REPORT",x1)) q:x1=""  d
 ..s x2="" f  s x2=$o(^TMP($zn,$j,"REPORT",x1,x2)) q:x2=""  d
 ...s x3="" f  s x3=$o(^TMP($zn,$j,"REPORT",x1,x2,x3)) q:x3=""  d
 ....s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",x1,x2,x3,epis)) q:epis=""  d
 .....k PLIST i $$select^LVBEPVIS(epis) q
 .....f j=12,20 s PLIST(j)=$p(PLIST(j),$c(1))
 .....s inv=$p(PLIST(57),"||",1)_"-"_$p(PLIST(57),"||",2)
 .....i $l(PLIST(57)) s seq=$p(^TMCB($p(PLIST(57),"||",1),$p(PLIST(57),"||",2),epis),"\",2) i $l(seq) s inv=inv_"-"_seq
 .....w inv,","
 .....i $l(PLIST(42)) w $p(PLIST(42),$c(1)),",,,"
 .....i '$l(PLIST(42)) w $p(PLIST(3),$c(1)),",",$p(PLIST(4),$c(1)),",",$p(PLIST(7),$c(1),2),","
 .....i $l(PLIST(37)) w $p(PLIST(37),$c(1)),",",$p(PLIST(38),$c(1)),",",$p(PLIST(39),$c(1)),",",$p(PLIST(41),$c(1)),","
 .....i '$l(PLIST(37)) w $p(PLIST(32),$c(1)),",",$p(PLIST(33),$c(1)),",",$p(PLIST(34),$c(1)),",",$p(PLIST(36),$c(1)),","
 .....w $p(PLIST(3),$c(1)),",",$p(PLIST(4),$c(1)),",",$p(PLIST(7),$c(1),2),",",$p($s($l(PLIST(43)):PLIST(43),1:PLIST(44)),$c(1)),","
 .....w $$extdate2^SSUTIL4($p(PLIST(6),$c(1))),",",$p(^TDEB(PLIST(20),PLIST(12),0,epis),"\",2),",",$p(^TDEB(PLIST(20),PLIST(12),0,epis),"\",1),","
 .....w $$extdate2^SSUTIL4($p(PLIST(56),$c(1))),",",$p(PLIST(74),$c(1)),!
 .c file
 q
 ; if billing address different to actual address use billing address
ADDRESS k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 i '$l(ADDRESS(1)) s ADDRESS(1)=$p(VISIT(7),$c(1),2)_" "_$e(VISIT(4))_" "_VISIT(3)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 d
 .i $l(ADDRESS(4)),ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6),ADDRESS(6)="" q
 .i $l(ADDRESS(3)),ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(5)_" "_ADDRESS(6),ADDRESS(6)="" q
 .i $l(ADDRESS(2)),ADDRESS(2)'[ADDRESS(6) s ADDRESS(2)=ADDRESS(2)_" "_ADDRESS(5)_" "_ADDRESS(6),ADDRESS(6)="" q
 q
 ; param : 1-Do not print Page No
HEADER(xHEADER,d1,d2,t1,t2,SORT,param) ; SetUp Header parameters
 s d1=$g(d1),d2=$g(d2),t1=$g(t1),t2=$g(t2),SORT=$g(SORT),param=$g(param),linecnt=""
 s xDATE=$$extdate2^SSUTIL4(+$h)
 i $l(SORT) s x=SORT,SORT="" d
 .f J=1:1:$l(x,"^") s x1=$p(x,"^",J) i $l(x1),$d(xSORT(x1)) d
 ..i $l(SORT) s SORT=SORT_" and "_xSORT(x1)
 ..e  s SORT="Sorted by "_xSORT(x1)
 s xTIME=$$exttime^SSUTIL4($p($h,",",2))
 ; SetUp Header
 i $p(param,"\",1)="" d
 .d ADDLINE^PRTREP00(REPID,"PRT","?"_$e((MAX-20)_SPACE,1,3)_"Page : "_page)
 .d ADDLINE^PRTREP00(REPID,"PRT","!"),LINECOUNT
 i $d(xHEADER) d
 .d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 .f J=1:1 q:'$d(xHEADER(J))  d
 ..i $e(xHEADER(J))="~" d ADDLINE^PRTREP00(REPID,"PRT",xHEADER(J))
 ..i $e(xHEADER(J))'="~" d ADDLINE^PRTREP00(REPID,"PRT","?"_$e(MAX-$l(xHEADER(J))\2_SPACE,1,3)_xHEADER(J))
 ..d ADDLINE^PRTREP00(REPID,"PRT","!"),LINECOUNT
 .d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 i $l(SORT) d
 .d ADDLINE^PRTREP00(REPID,"PRT","?"_$e(MAX-$L(SORT)\2_SPACE,1,3)_SORT)
 .d ADDLINE^PRTREP00(REPID,"PRT","!"),LINECOUNT
 i $l(d1),$l(d2) d
 .s X="From "_$$extdate2^SSUTIL4(d1)_" to "_$$extdate2^SSUTIL4(d2)
 .i $l(t1),$l(t2) s X=X_"  from "_$$exttime^SSUTIL4(t1)_" to "_$$exttime^SSUTIL4(t2)
 .i $l(t1),'$l(t2) s X=X_"  until "_$$exttime^SSUTIL4(t1)
 .d ADDLINE^PRTREP00(REPID,"PRT","?"_$E(MAX-$L(X)\2_SPACE,1,3)_X)
 .d ADDLINE^PRTREP00(REPID,"PRT","!"),LINECOUNT
 i $l(d1),'$l(d2) d
 .s X="For "_$$extdate2^SSUTIL4(d1)
 .i $l(t1),$l(t2) s X=X_"  from "_$$exttime^SSUTIL4(t1)_" to "_$$exttime^SSUTIL4(t2)
 .i $l(t1),'$l(t2) s X=X_"  until "_$$exttime^SSUTIL4(t1)
 .d ADDLINE^PRTREP00(REPID,"PRT","?"_$E(MAX-$L(X)\2_SPACE,1,3)_X)
 .d ADDLINE^PRTREP00(REPID,"PRT","!"),LINECOUNT
 d ADDLINE^PRTREP00(REPID,"PRT"," Date : "_xDATE_"   Time : "_xTIME)
 d
 .n (USER,nUSER)
 .s nUSER="" i '$$selrow^MVBSSUSR(USER,"Y") s nUSER=PLIST(3)
 d ADDLINE^PRTREP00(REPID,"PRT","?"_$e((MAX-30)_SPACE,1,3)_$e("User : "_nUSER,1,30))
 i $d(xLINE) f J=1:1 q:'$d(xLINE(J))  d ADDLINE^PRTREP00(REPID,"PRT","!"),ADDLINE^PRTREP00(REPID,"PRT",xLINE(J)),LINECOUNT
 q
 ; common header
H(FIELDS,ROUTINE) n (FIELDS,ROUTINE,xLINE,N) k xLINE,N s ROUTINE=$g(ROUTINE)
 s xLINE(1)=" "
 s xLINE(2)="|"
 s xLINE(3)="|"
 s xLINE(4)="|"
 f J=1:1:$l(FIELDS,",") d
 .s OK=0,DATA=$p(FIELDS,",",J),ROWID=$p(DATA,$C(2),1)
 .i 'OK,$p(DATA,$c(2),1)="xxx" d
 ..i 'OK,'$$select^MVBSSRN(ROUTINE_"||"_$p(DATA,$c(2),2),"Y") s OK=1,PLIST(7)="" m xFIELD(J)=PLIST
 ..i 'OK s OK=1 f JJ=1:1:6 s xFIELD(J,JJ)=$p(DATA,$c(2),JJ)
 .i 'OK,'$$select^MVBSSRH(ROWID,"Y") s OK=1 m xFIELD(J)=PLIST
 .i OK f JJ=1:1:6 i '$l($g(xFIELD(J,JJ)))!$l($p(DATA,$c(2),JJ)) s xFIELD(J,JJ)=$p(DATA,$c(2),JJ)
 s JJ=0 f J=1:1:$l(FIELDS,",") i $d(xFIELD(J)) d
 .s JJ=JJ+1 m N(JJ)=xFIELD(J)
 .s NAME=N(JJ,3),LEN=N(JJ,4)
 .s x="",$p(x,"-",LEN+1)=""
 .s xLINE(1)=xLINE(1)_x_"-"
 .s xLINE(2)=xLINE(2)_$$PART^PRTREP00(.NAME,LEN)_"|"
 .s xLINE(3)=xLINE(3)_$$PART^PRTREP00(.NAME,LEN)_"|"
 .s xLINE(4)=xLINE(4)_x_"-"
 s xLINE(1)=$e(xLINE(1),1,$l(xLINE(1))-1)_" "
 s xLINE(4)=$e(xLINE(4),1,$l(xLINE(4))-1)_"|"
 s X=xLINE(2)
 s N(1,"Z")=1 f J=2:1 q:'$f(X,"|",N(J-1,"Z")+1)  s N(J,"Z")=$f(X,"|",N(J-1,"Z")+1)-1
 f J=1:1 q:'$d(N(J+1,"Z"))  s N(J,"L")=N(J+1,"Z")-N(J,"Z")-1
 q
 ; add line feed
LN d ADD^SSPRINT(REPID,"PRT","!")
 q
 ; count number of lines
LINECOUNT s linecnt=$g(linecnt)+1
 q
]]></Routine>
</Export>
