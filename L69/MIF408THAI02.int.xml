<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF408THAI02" type="INT" languagemode="0" generated="1"><![CDATA[
MIF408THAI02(mi) ; ML 24/9/98 ; ASTM protocol - ORGANON TEKNIKA BacT/Alert : Bangkok General Hospital (Bi-Directional)
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),eot=$c(4),enq=$c(5),ack=$c(6),lf=$c(10),cr=$c(13),nak=$c(21)
 s (sample,epis,surname,result,date,time,QC,TimeCheck)=""
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main r *R:10 e  d  q
 .d:" L "[(" "_par9_" ") loadlist^MIF000(mi)
 . ; check loaded bottles status - check every hour
 .i +$h*(3600*24)+$p($h,",",2)>TimeCheck s TimeCheck=+$h*(3600*24)+$p($h,",",2)+3600 d BottleCheck
 i $c(R)=enq d
 .d trace^MIF000(mi,"ENQ","H<--M")
 .d ACK
 .f  r *R:10 q:$c(R)=eot  d
 ..i $c(R)'=stx q
 ..s record=$$read^MIF000("",lf) q:'$l(record)
 ..s record=$e(record,1,$l(record)-1)
 ..d trace^MIF000(mi,$tr(record,$c(13,3)," "),"H<--M")
 .. ; check record validity
 ..s chsum=$p(record,etx,2),chrecord=$p(record,etx,1)_etx
 ..i $$CHSUM(chrecord)'=chsum d  q
 ...w nak,*-3 d trace^MIF000(mi,"NAK - Incorrect Checksum","H-->M")
 ..d ACK
 .. ; query record
 ..i $e(record,2)="Q" d  q
 ...s epis=$p($p(record,"|",3),"^",2)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)="" q
 ...s suffix=$e(epis,$l(epis)),epis=$e(epis,1,$l(epis)-1)
 ...i $l(epis),$d(^TEPI(epis,1)) s ^TMP($zn,$j,"ENQ",epis)=suffix q
 .. ; header record
 ..q:$e(record,2)="H"
 .. ; patient record
 ..i $e(record,2)="P" d  q
 ...d Last s (sample,epis,surname,result,date,time,QC)=""
 ...s surname=$tr($tr($p(record,"|",6)," "),"^"," ")
 .. ; order record
 ..i $e(record,2)="O" d  q
 ...s epis=$tr($p($p(record,"|",3),"^",1)," "),QC=$tr($p(record,"|",12)," ") s:'$l(epis) epis=$tr($p($p(record,"|",4),"^",1)," ")
 ...s cup=$p($p(record,"|",4),"^",3)
 ...i QC="Q" s QC=cup_"\"_epis
 .. ; result record
 ..i $e(record,2)="R" d  q
 ... ; result of testing so far + = pos, - = neg, * = neg, still under testing
 ...q:" + - * "'[(" "_$p(record,"|",4)_" ")  s res=$p(record,"|",4)
 ... ; extract status of bottle - I = negative to date, still under test.  P = prelim status.  F = final status
 ...s stat=$p(record,"|",9) q:stat=""
 ... ; extract bottle type and bottle number
 ...s bot=$p($p(record,"|",3),"^",5),botnum=$p($p(record,"|",3),"^",6) q:bot=""  q:botnum=""  q:$p($g(^TMIF(mi,33,"Res",epis,bot,botnum)),"\",1)="F"
 ... ; allow only bottles with previous negative history to continue
 ...q:$p($g(^TMIF(mi,33,"Res",epis,bot,botnum)),"\",2)="+"
 ...s code=$s((bot="A")!(bot="L")!(bot="D")!(bot="FA"):"AE",(bot="N")!(bot="S")!(bot="H")!(bot="FN"):"AN",(bot="PR")!(bot="PE")!(bot="PA"):"PA",1:"") q:code=""
 ...s (DateLoad,Datelo,DateRes,Daters,DateDiff)=""
 ... ; date bottle loaded
 ...s DateLoad=$p(record,"|",12)
 ...i DateLoad="" s Datelo=$p($g(^TMIF(mi,33,"Epis",epis,bot,botnum)),"\",2) q:Datelo=""
 ...e  s Datelo=$$intdate^SSUTIL4($e(DateLoad,1,8))*24*3600+$$inttime^SSUTIL4($e(DateLoad,9,10)_":"_$e(DateLoad,11,12))+$e(DateLoad,13,14)
 ... ; use machine date and time loaded if available
 ...i DateLoad'="" s $p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",2)=Datelo
 ... ; date result determined
 ...s DateRes=$p(record,"|",13) i DateRes="" s Daters=+$h*24*3600+$p($h,",",2)
 ...e  s Daters=$$intdate^SSUTIL4($e(DateRes,1,8))*24*3600+$$inttime^SSUTIL4($e(DateRes,9,10)_":"_$e(DateRes,11,12))+$e(DateRes,13,14)
 ... ; calculate date difference between date loaded and date of result
 ...s DateDiff=Daters-Datelo,DateDiff=DateDiff\24\3600
 ...i res="-"!(res="*"),DateDiff<3 q
 ... ; ignore already reported 3 day negatives - update only
 ...i DateDiff>3,$p($g(^TMIF(mi,33,"Res",epis,bot,botnum)),"\",3)=3 s ^TMIF(mi,33,"Res",epis,bot,botnum)=stat_"\"_res_"\"_DateDiff q
 ...i DateDiff>3,(res="-"!(res="*")) s DateDiff=3
 ...i res="+",DateDiff>5 s DateDiff=5
 ...s ^TMIF(mi,33,"Res",epis,bot,botnum)=stat_"\"_res_"\"_DateDiff
 ...s result=result_code_$c(par10)_res_"/"_DateDiff_$c(par11)
 .. ; last record
 ..i $e(record,2)="L" d Last q
 .d trace^MIF000(mi,$s($c(R)=eot:"EOT",1:R),"H<--M")
 .d ORDER
 q
Last ; file result if exist
 i epis?1.99AN,$l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 s (sample,epis,surname,result,date,time,QC)=""
 q
ORDER ; create list of orders if exists
 s epis="" f  s epis=$o(^TMP($zn,$j,"ENQ",epis)) q:epis=""  d
 .d scanone^MIF000(mi,epis)
 .d PATDET(epis)
 .d SET(epis)
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 .d Send
 .k ^TMP("MIF-SINGLE",$j,mi,epis),^TMP($zn,$j,"ENQ",epis)
 q
SAVE(mi) ; create loadlist records
 s epis=^TMP("MIF000",$j,"LL","EPISODE"),epis=$p(epis,$c(1),1)
 s sample=^TMP("MIF000",$j,"LL","SAMPLE")
 d PATDET(epis)
 d SET(epis)
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="L|1|F"
 q 0
PATDET(epis) ; set patient details record
 i '$$select^LVBEPVIS(epis,"Y") d  q
 .f j=1:1:PLIST i $d(PLIST) s PLIST(j)=$p(PLIST(j),$c(1))
 .s dob="" i PLIST(6)'="" s dob=$$extdate2^SSUTIL4(PLIST(6)),dob=$p(dob,"/",3)_$p(dob,"/",2)_$p(dob,"/",1)
 .s sex=$s($g(PLIST(5))="M":"M",$g(PLIST(5))="F":"F",1:"U")
 .s mrn=$s(PLIST(65)'=",":$p(PLIST(65),",",2),1:PLIST(20))
 .s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1|"_mrn_"|||"_$g(PLIST(3))_"^"_$g(PLIST(4))_"||"_dob_"|"_sex_"|||||"
 s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="P|1||||^||||||||"
 q
SET(epis) ; set record to be sent to instrument
 s tc="",tcx=""
 ; check for inpatient test set or outpatient test set
 s ts="" f ts="E090","E091" q:'$d(^TEPI(epis,1,ts))  d
 . ; extract bottle numbers and set loaded date and time
 .s num="" f  s num=$o(^TEPI(epis,1,ts,num)) q:num=""  d
 .. ; ignore pathologist attention and authorised results
 ..s status=$p(^(num),"\",31) i status="P"!(status="A") q
 ..f tc="E1152","E1157","E1162" s botnum=$g(^TEPI(epis,1,ts,num,"DATA",tc)) d
 ...q:botnum=""  s bot="" f i=1:1 q:$e(botnum,i)'?1A  s bot=bot_$e(botnum,i)
 ...s:bot="PE" bot="PA"
 ...s tcx=tcx_$s($l(tcx):"\",1:"")_"^^^BC^^"_botnum_"^"
 ... ; set date bottle 'effectively' loaded onto instrument using $h and seconds since 0,0 $h
 ...s $p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",1)=$h,$p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",2)=+$h*(3600*24)+$p($h,",",2)
 ...s ^TMIF(mi,33,"Date",+$h,epis,bot,botnum)="",^TMIF(mi,33,"Res",epis,bot,botnum)=""
 i $l(tcx) s line=$o(^TMIF(mi,10,""),-1)+1,^TMIF(mi,10,line)="O|1|"_epis_"|"_epis_"|"_tcx_"|||||||N||||||||||||||Q"
 q
BottleCheck ; check result status of bottles
 s epis="" f  s epis=$o(^TMIF(mi,33,"Epis",epis)) q:epis=""  d
 .s bot="" f  s bot=$o(^TMIF(mi,33,"Epis",epis,bot)) q:bot=""  d
 ..s botnum="" f  s botnum=$o(^TMIF(mi,33,"Epis",epis,bot,botnum)) q:botnum=""  d
 ...s DateLoaded=$p(^TMIF(mi,33,"Epis",epis,bot,botnum),"\",2),DateNow=+$h*3600*24+$p($h,",",2)
 ...i DateNow-DateLoaded>259200 d
 .... ; ignore ones which have been loaded manually
 ....i '$d(^TMIF(mi,33,"Res",epis,bot,botnum)) q
 .... ; ignore previous +ve or final results
 ....i $p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="+"!($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",1)="F") q
 .... ; calculate days since loaded - ignore -ve results < 3 days old
 ....s DateDiff=DateNow-DateLoaded,DateDiff=DateDiff\24\3600 q:DateDiff<3
 .... ; ignore already reported 3 day negatives
 ....i $p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",3)=2,($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="*"!($p(^TMIF(mi,33,"Res",epis,bot,botnum),"\",2)="-")) q
 ....s code=$s((bot="A")!(bot="L")!(bot="D")!(bot="FA"):"AE",(bot="N")!(bot="S")!(bot="H")!(bot="FN"):"AN",(bot="PR")!(bot="PE")!(bot="PA"):"PA",1:"") q:code=""
 ....s result=result_code_$c(par10)_"*/"_DateDiff_$c(par11),episx=epis
 ....i episx?1.99AN,$l(result) d file^MIF000(mi,sample,episx,surname,result,date,time,QC) s (sample,episx,surname,result,date,time,QC)=""
 ....s ^TMIF(mi,33,"Res",epis,bot,botnum)="I\*\3"
 ; delete bottles older than 15 days
 s date="" f  s date=$o(^TMIF(mi,33,"Date",date)) q:date=""  q:date>+$h-15  d
 .s epis="" f  s epis=$o(^TMIF(mi,33,"Date",date,epis)) q:epis=""  d
 ..s bot="" f  s bot=$o(^TMIF(mi,33,"Date",date,epis,bot)) q:bot=""  d
 ...s botnum="" f  s botnum=$o(^TMIF(mi,33,"Date",epis,date,bot,botnum)) q:botnum=""  d
 ....k ^TMIF(mi,33,"Epis",epis,bot,botnum),^TMIF(mi,33,"Res",epis,bot,botnum),^TMIF(mi,33,"Date",date,epis,bot,botnum)
 q
Send ; send list of orders if exists
 w enq,*-3 d trace^MIF000(mi,"ENQ","H-->M")
 f j=1:1:10 r *R:1 i $c(R)=ack!($c(R)=enq) q
 d trace^MIF000(mi,$s($c(R)=ack:"ACK",$c(R)=enq:"ENQ",$c(R)=nak:"NAK",1:R),"H<--M")
 i $c(R)=enq q
 i $c(R)'=ack w eot,*-3 d trace^MIF000(mi,"EOT","H-->M") q
 s date=$$extdate2^SSUTIL4(+$h),date=$p(date,"/",3)_$p(date,"/",2)_$p(date,"/",1)_$tr($$exttime^SSUTIL4($p($h,",",2)),":","")_"00"
 q:$$SEND("1H|\^&|||LABTRAK|||||BACT/ALERT||P|1"_date)
 s x="" f FRAME=2:1 s x=$O(^TMIF(mi,10,x)) q:x=""  q:$$SEND(FRAME#8_^TMIF(mi,10,x))
 i x="" k ^TMIF(mi,10)
 w eot,*-3 d trace^MIF000(mi,"EOT","H-->M")
 q
SEND(str) ; send string to instrument
 s str=str_cr_etx,chsum=$$CHSUM(str)
 w stx,str,chsum,cr,lf,*-3 d trace^MIF000(mi,str_chsum,"H-->M")
 f j=1:1:6 r *R:1 i ($c(R)=ack)!($c(R)=eot) q
 i $c(R)=ack d trace^MIF000(mi,"ACK","H<--M") q 0
 i $c(R)=eot d trace^MIF000(mi,"EOT","H<--M") q 1
 d trace^MIF000(mi,R,"H<--M")
 q 2
CHSUM(x) ; calculate check sum
 n (x) s z=0 f y=1:1:$l(x) s z=z+$a(x,y)
 s z=$e("0123456789ABCDEF",z#256\16+1)_$e("0123456789ABCDEF",z#16+1)
 q z
ACK ; send 'ack' to instrument
 w ack,*-3
 d trace^MIF000(mi,"ACK","H-->M")
 q
NEXTTRAY(tray) 
 q tray
]]></Routine>
</Export>
