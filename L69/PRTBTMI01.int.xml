<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTBTMI01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTBTMI01 ; ML 23/10/2000 ; BLOOD TRANSFUSION MONTHLY INVENTORY STATISTICS
 q
 ; Parameters :  1. Site
 ;               2. Date From
 ;               3. Date To
 ;               4. Location
 ;               5. Product
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Blood Transfusion Monthly Inventory"
 s line=2,xHEADER(line)=""
 s line=3,xHEADER(line)="Site : "_$s(PAR1="":"All Sites",1:$p(^TTAB("USLOC",PAR1),"^"))
 s line=4,xHEADER(line)=""
 s line=5,xHEADER(line)="Location : "_$s(PAR4="":"All Locations",1:$p(^TTAB("BB-LOC",PAR4),"\"))
 s line=6,xHEADER(line)=""
 s line=7,xHEADER(line)="Product : "_$s(PAR5="":"All Products",1:$p(^TTAB("BB-BP",PAR5),"\"))
 s line=8,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER,PAR2,PAR3),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 i $$fetchall^LVBBBTR()
 f i=1:1:PLIST d
 .s trcode=$p(PLIST(i),$c(28),2),trdesc=$p(PLIST(i),$c(28),3)
 .s ^TMP($zn,$j,"TR-Code",trcode)=trdesc
 s date=PAR2-1 f  s date=$o(^TBBPi("TR-Date",date)) q:date=""  q:date>PAR3  d
 .s trans="" f  s trans=$o(^TBBPi("TR-Date",date,trans)) q:trans=""  d
 ..s PackID="" f  s PackID=$o(^TBBPi("TR-Date",date,trans,PackID)) q:PackID=""  d
 ...s SecondID="" f  s SecondID=$o(^TBBPi("TR-Date",date,trans,PackID,SecondID)) q:SecondID=""  d
 ....s TransCnt="" f  s TransCnt=$o(^TBBPi("TR-Date",date,trans,PackID,SecondID,TransCnt)) q:TransCnt=""  d
 .....q:$$select^LVBBBPT(PackID_"||"_SecondID_"||"_TransCnt)  i PLIST(13)="Y" q
 .....q:$$select^LVBBBP(PackID_"||"_SecondID)  m PackDet=PLIST k PLIST
 .....s location=$p(PackDet(11),$c(1),2),site=$p(PackDet(11),$c(1),3),product=$p(PackDet(8),$c(1)),BloodGrp=$p(PackDet(9),$c(1)),DateExp=$p(PackDet(13),$c(1))
 .....s:site="" site=" "  s:location="" location=" "
 .....s:product="" product=" "  s:BloodGrp="" BloodGrp=" "
 .....i PAR1'="",site'=PAR1 q
 .....i PAR4'="",location'=PAR4 q
 .....i PAR5'="",product'=PAR5 q
 .....s ^TMP($zn,$j,"Site",site,location,product,BloodGrp,trans)=$g(^TMP($zn,$j,"Site",site,location,product,BloodGrp,trans))+1
 q
PRINT ; print report
 s site="" f  s site=$o(^TMP($zn,$j,"Site",site)) q:site=""  d
 .s xxx(1)=site,location="" f  s location=$o(^TMP($zn,$j,"Site",site,location)) q:location=""  d
 ..s xxx(2)=location,product="" f  s product=$o(^TMP($zn,$j,"Site",site,location,product)) q:product=""  d
 ...s xxx(3)=product,BloodGrp="" f  s BloodGrp=$o(^TMP($zn,$j,"Site",site,location,product,BloodGrp)) q:BloodGrp=""  d
 ....s xxx(4)=BloodGrp,trans="" f  s trans=$o(^TMP($zn,$j,"Site",site,location,product,BloodGrp,trans)) q:trans=""  d
 .....s cnt=^(trans),xxx(5)=^TMP($zn,$j,"TR-Code",trans)_" : "_cnt
 .....d PRINT^PRTREP00(,.N)
 .....k xxx f i=1:1:10 s xxx(i)=""
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="BTMI"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
