<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDLRESP01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDLRESP01 ;IG 27/05/98 ;Daily list
 q
 ; Parameters :  1.Date
 ;               2.Site
 ;               3.Sort by -   1.Collection Centre
 ;                             2.Priority
 ;                             3.Visit Action
 ;                             4.Lab.Numbers
 ;                             5.Doctors
 ;                             6.Surname
 ;                             7.Hospital
 ;                             8.Site
 ;               4.Patient Status
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT s line=1,xHEADER(line)="Listado diario de Episodios"
 i $l(SITE1) s line=line+1,xHEADER(line)="Sede : "_SITE1_" - "_SITE2
 i PAR4 s line=line+1,xHEADER(line)="Estado de la Petición : "_$s(PAR4=1:"Pre-entrado",PAR4=2:"Recogido",PAR4=3:"Para ser recogido",1:"")
 d HEADER^PRTREP00(.xHEADER,PAR1),BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) s PLIST(12)=PAR2 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f J=1:1:$l(PAR,$c(1)) s @("PAR"_J)=$p(PAR,$c(1),J)
 k xSORT D H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 ; additional parameters
 s (SITE1,SITE2,SORT)=""
 ; find site
 s PAR4=+PAR4
 i $l(PAR2),'$$select^LVBCTUSL(PAR2,"Y") s SITE1=$p(PLIST(2),"\"),SITE2=$p(PLIST(3),"\")
 i $l(PAR3) d
 .i PAR3=1 s SORT="Centro de Recogida"
 .i PAR3=2 s SORT="Prioridad"
 .i PAR3=3 s SORT="Acción de entrada"
 .i PAR3=4 s SORT="Episodio"
 .i PAR3=5 s SORT="Doctor"
 .i PAR3=6 s SORT="Apellidos"
 .i PAR3=7 s SORT="Hospital"
 .i PAR3=8 s SORT="Sede"
 q
 ; create temporary printing file
BODY ; test set
 s epis="" f  s epis=$o(^TDAY(1,PAR1,PAR4,epis)) q:epis=""  d
 .; print only entered patients
 .i $$select^LVBEPVIS(epis) q
 .; check test location
 .i $l(PAR2),PAR2'=$p(PLIST(66),$c(1)) q
 .s (SORT,SORTNAME)="" i $l(PAR3) d
 ..i PAR3=1 s SORT=$p(PLIST(27),$c(1)),SORTNAME=$p(PLIST(27),$c(1),2)
 ..i PAR3=2 s SORT=$p(PLIST(17),$c(1)),SORTNAME=$p(PLIST(17),$c(1),2)
 ..i PAR3=3 s SORT=$p(PLIST(25),$c(1)),SORTNAME=$p(PLIST(25),$c(1),2)
 ..i PAR3=4 s (SORT,SORTNAME)=PLIST(2)
 ..i PAR3=5 s SORT=$p(PLIST(15),$c(1)),SORTNAME=$p(PLIST(15),$c(1),2)
 ..i PAR3=6 s (SORT,SORTNAME)=$$ALPHAUP^SSUTIL4(PLIST(3))
 ..i PAR3=7 s SORT=$p(PLIST(22),$c(1)),SORTNAME=$p(PLIST(22),$c(1),2)
 ..i PAR3=8 s SORT=$p(PLIST(66),$c(1)),SORTNAME=$p(PLIST(66),$c(1),2)
 .i '$l(SORT) s SORT=" "
 .s ^TMP($zn,$j,"REPORT",SORT,epis)=""
 .s ^TMP($zn,$j,"REPORT",SORT)=SORTNAME
 q
PRINT i $d(^TMP($zn,$j,"REPORT")) d
 .s SORT="" f  s SORT=$o(^TMP($zn,$j,"REPORT",SORT)) q:SORT=""  d
 ..s SORTNAME=^(SORT)
 ..i $l(PAR3)," 1 2 3 5 7 8 "[(" "_PAR3_" ") d
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT","~BOLD")
 ...d ADDLINE^PRTREP00(REPID,"PRT","~ITALIC")
 ...d ADDLINE^PRTREP00(REPID,"PRT","?5  "_SORT)
 ...d ADDLINE^PRTREP00(REPID,"PRT","?15 "_SORTNAME)
 ...d ADDLINE^PRTREP00(REPID,"PRT","~NORMAL")
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ..s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",SORT,epis)) q:epis=""  d
 ...i '$$select^LVBEPVIS(epis) d
 ....s epistot=$g(epistot)+1
 ....i $l(PLIST(68)) s yTEST=PLIST(68),xTEST="" d  s PLIST(68)=xTEST
 .....n (yTEST,xTEST) f J=1:1:$l(yTEST," ") s TEST=$p($p(yTEST," ",J),"x",1) d
 ......i '$$select^LVBCTTS(TEST,"Y") s:$l(xTEST) xTEST=xTEST_" " s xTEST=xTEST_PLIST(4)
 ....s XX=PLIST(68) f IU=0:1 d  q:'$l(XX)
 .....i $l(XX)'>30 s PLIST(68)=XX,XX=""
 .....e  f IO=1:1:$l(XX," ") i $l($P(XX," ",1,IO))>30 s PLIST(68)=$p(XX," ",1,IO-1),XX=$p(XX," ",IO,$l(XX," ")) q
 .....d PRINT^PRTREP00(.PLIST,.N) k PLIST
 ..i $l(PAR3)," 1 2 3 5 7 8 "[(" "_PAR3_" ") d
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 ...d ADDLINE^PRTREP00(REPID,"PRT","!")
 .f i=1:1:3 d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT","Total episodios"_$s(SITE1'="":" for "_SITE2,1:"")_" : "_epistot)
 s report="DLR"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
