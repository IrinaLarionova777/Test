<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDCR01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDCR01	; IG 19/08/99 ; DVA Coning Report
	q
	; Parameters :  1. Date From
	;               2. Date To
	;
CREATE(REPID,PAR,USER,%msql)	; create report
	n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
	k xHEADER d INIT
	s line=1,xHEADER(line)="DVA Coning Report"
	s line=2,xHEADER(line)=""
	d HEADER^PRTREP00(.xHEADER,PAR1,PAR2),BODY,PRINT
	k ^TMP($zn,$j),^TMP("PRTREP00",$j)
	q
INIT	; initialise parameters
	f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
	k xxx,xSORT d H^PRTREP00(PAR10,$zn)
	s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
	q
BODY	;; create temporary printing file
	k ^TMP($zn,$j,"REPORT")
	s pc="" f  s pc=$o(^TMCB(pc)) q:pc=""  d
	.i $$seldata^LVBCTPC(pc,28)'="V" q
	.s invb="" f  s invb=$o(^TMCB(pc,invb)) q:invb=""  d
	..s date=$p(^TMCB(pc,invb),"\",1)
	..i '$l(date)!(date<PAR1)!(date>PAR2) q
	..s InvRID=pc_"||"_invb,(ClaimID,InvDate,InvProv)="" i '$$select^LVBINVB(pc_"||"_invb) d
	...s ClaimID=PLIST(7),InvDate=$$extdate2^SSUTIL4(PLIST(4)),InvProv=PLIST(5)
	...
	...i $$open^LVBINVBP(InvRID)
	...f  q:$$fetch^LVBINVBP(InvRID)  d
	....s epis=$p(PLIST(2),$c(1),1),coldate=$$extdate2^SSUTIL4($$seldata^LVBEPVIS(epis,12)),doc=$$seldata^LVBEPVIS(epis,15),docprov=""
	....s episinv=$$seldata^LVBEPVIS(epis,57),DVANo=$$seldata^LVBEPVIS(epis,89)
	....i episinv'=InvRID q
	....i $l(doc) s docprov=$$seldata^LVBCTDR(doc,18)
	....s ^TMP($zn,$j,"REPORT",epis)=InvProv_$c(1)_ClaimID_$c(1)_docprov_$c(1)_coldate_$c(1)_DVANo_$c(1)_InvDate
	.... ; get all claimed billing items and amounts
	....i $$open^LVBVISBI(epis)
	....f  q:$$fetch^LVBVISBI(epis)  s ^TMP($zn,$j,"REPORT",epis,"BILLED",$p(PLIST(2),$c(1),1))=PLIST(3)
	....i $$close^LVBVISBI()
	.... ; get all coned items and amounts
	....s sum=$$price(epis)
	....f i=1:1:PLIST i '$d(^TMP($zn,$j,"REPORT",epis,"BILLED",$p(PLIST(i),"\",1))) s ^TMP($zn,$j,"REPORT",epis,"CONED",$p(PLIST(i),"\",1))=$p(PLIST(i),"\",2)
	...i '$$close^LVBINVBP()
	; leave only GP outpatients
	s %routine=$p($g(^TTAB("REPORT-GENERIC","PRICING")),"\",1) i $l(%routine) d
	s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  d
	.x "s inpat=$$inpat^"_%routine_"("""_epis_""")"
	.x "s conn=$$conn^"_%routine_"("""_epis_""")"
	.i inpat'="Y",conn="Y" q
	.k ^TMP($zn,$j,"REPORT",epis)
	q
price(epis)	n (epis,PLIST) s epislist=$$seldata^LVBEPVIS(epis,55)
	s Master=epis,epis=epis_","_epislist k ^TMP($zn,$j,"list")
	s pc=$$seldata^LVBEPVIS(Master,9),date=$$seldata^LVBEPVIS(Master,12),init=$$seldata^LVBEPVIS(epis,54)
	k PAYCODE i '$$select^LVBCTPC(pc,"Y") m PAYCODE=PLIST
	s xInit="" i '$$init^LVBQUOTE(init,pc) s xInit=$g(PLIST(1))
	s %routine=$p($g(^TTAB("REPORT-GENERIC","PRICING")),"\",1) i $l(%routine) d
	.f jEpis=1:1:$l(epis,",") s ep=$p(epis,",",jEpis) i $l(ep) d
	..k ListOfItems m ListOfItems=^TMP($zn,$j,"list")
	..x "d ListOfItems^"_%routine_"("""_ep_""")"
	..k ^TMP($zn,$j,"list")
	..s item="" f  s item=$o(ListOfItems(item)) q:item=""  s ^TMP($zn,$j,"list",item)=ListOfItems(item)
	s item="" f  s item=$o(^TMP($zn,$j,"list",item)) q:item=""  s ^TMP("LVBQUOTE",$j,"ITEM",item)=^TMP($zn,$j,"list",item)
	i $d(^TMP("LVBQUOTE",$j,"ITEM")) d
	.s Conning="N" d price^LVBQUOTE
	.s result="" f j=1:1:PLIST s result=result+$p(PLIST(j),"\",2)
	q result
PRINT	; print report
	i $d(^TMP($zn,$j,"REPORT")) d
	.s file=""
	.i $d(^CF("CTPCT","HIC")) s file=$p(^CF("CTPCT","HIC"),"\",5)
	.s usefile=0 i $l(file) d 
	..s file=$tr(file,$c(17),"\") f j="/","\" i file[j,$e(file,$l(file))'=j S file=file_j
	..s date=$$extdate2^SSUTIL4($p($h,",")) s file=file_"DVA"_$p(date,"/",3)_$p(date,"/",2)_$p(date,"/",1)_".csv"
	..o file:"NSW" s usefile=1
	.s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  d
	..k VISIT,PLIST i '$$select^LVBEPVIS(epis) m VISIT=PLIST
	..s row=^TMP($zn,$j,"REPORT",epis)
	..; First show all claimed items for episode
	..i $d(^TMP($zn,$j,"REPORT",epis,"BILLED")) d
	...s itm="" f  s itm=$o(^TMP($zn,$j,"REPORT",epis,"BILLED",itm)) q:itm=""  d
	....s xxx(1)=$p(row,$c(1),1),xxx(2)=$p(row,$c(1),2),xxx(3)=itm,xxx(4)=^(itm),xxx(5)="",xxx(6)=""
	....s xxx(7)=$p(row,$c(1),3),xxx(8)=$p(row,$c(1),5),xxx(9)=$p(row,$c(1),6)
	....i usefile u file w VISIT(2)_","_xxx(1)_","_xxx(2)_","_xxx(3)_","_xxx(4)_","_xxx(5)_","_xxx(6)_","_xxx(7)_","_$$extdate2^SSUTIL4(VISIT(12))_","_xxx(8)_","_xxx(9),!
	....d PRINT^PRTREP00(.VISIT,.N) k xxx
	..; Then all coned items
	..i $d(^TMP($zn,$j,"REPORT",epis,"CONED")) d
	...s itm="" f  s itm=$o(^TMP($zn,$j,"REPORT",epis,"CONED",itm)) q:itm=""  d
	....s xxx(1)=$p(row,$c(1),1),xxx(2)=$p(row,$c(1),2),xxx(3)="",xxx(4)="",xxx(5)=itm,xxx(6)=^(itm)
	....s xxx(7)=$p(row,$c(1),3),xxx(8)=$p(row,$c(1),5),xxx(9)=$p(row,$c(1),6)
	....i usefile u file w VISIT(2)_","_xxx(1)_","_xxx(2)_","_xxx(3)_","_xxx(4)_","_xxx(5)_","_xxx(6)_","_xxx(7)_","_$$extdate2^SSUTIL4(VISIT(12))_","_xxx(8)_","_xxx(9),!
	....d PRINT^PRTREP00(.VISIT,.N) k xxx
	.i usefile c file
	s report="DCR",EndLine=$$seldata^LVBCTCLR(report,9) i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
	q
]]></Routine>
</Export>
