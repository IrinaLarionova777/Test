<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF109" type="INT" languagemode="0" generated="1"><![CDATA[
MIF109(mi) ; ML 30/6/98 ; BIOMERIEUX Vitek
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s stx=$c(2),etx=$c(3),ack=$c(6),eot=$c(4),enq=$c(5),gs=$c(29),rs=$c(30)
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main s record=$$read^MIF000(stx,etx) q:'$l(record)
 d trace^MIF000(mi,record,"H<--M")
 w ack,*-3 d trace^MIF000(mi,"ACK","H-->M")
 i $e(record,2,5)'="mtrsl" q
 s (sample,epis,surname,result,date,time,QC)=""
 f j=1:1:$l(record,"|") i $e($p(record,"|",j),1,2)="pi" d  q
 .s epis=$e($p(record,"|",j),3,99),epis=$tr(epis," ")
 .i $l(epis)>7 s epis=$e(epis,$l(epis)-6,99)
 f j=1:1:$l(record,"|") d
 .i $e($p(record,"|",j),1,2)="a1" s test=$e($p(record,"|",j),3,99) q
 .i $e($p(record,"|",j),1,2)="a4" s result=result_test_$c(par10)_$e($p(record,"|",j),3,99)_$c(par11)
 i $l(result) d file^MIF000(mi,sample,epis,surname,result,date,time,QC)
 q
]]></Routine>
</Export>
