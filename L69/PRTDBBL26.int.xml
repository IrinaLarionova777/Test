<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDBBL26" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDBBL26 ; IG 07/10/98 ; Daybook Blocks (Cassetes) - APP
 Q
 ; Parameters :  1.Episode
 ;               2.Accession number
 ;               3.Block
 ;               4.pc-name
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($zn,$j)
 D INIT,PRINT
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR4_"-"_PAR1 I $$update^LVBPRPR(REPID)
 K ^TMP($zn,$j),^TMP("PRTREP00",$j)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
 ; create temporary printing file
PRINT s epis="" f  s epis=$o(^THIRi("BLOCK-PRINT",epis)) q:epis=""  d
 .f j=3,4,17 s VISIT(j)=$$seldata^LVBEPVIS(epis,j)
 .i $l(PAR1),epis'=PAR1 q
 .s urgent="" i $$seldata^LVBCTPR(VISIT(17),4)="Y" s urgent="Y"
 .s acc="" f  s acc=$o(^THIRi("BLOCK-PRINT",epis,acc))  q:acc=""  d
 ..q:$$select^LVBHIVAC(epis_"||"_acc,"Y")  m ACC=PLIST k PLIST
 ..i $l(PAR2),acc'=PAR2 q
 ..s AccNum=$p(ACC(8),$c(1))_$p(ACC(14),$c(1))_$p(ACC(9),$c(1))_"-"_$p(ACC(10),$c(1))
 ..s block="" f  s block=$o(^THIRi("BLOCK-PRINT",epis,acc,block)) q:block=""  d
 ...q:$$select^LVBHIVAB(epis_"||"_acc_"||"_block)  k BLOCK m BLOCK=PLIST k PLIST
 ...i $l(PAR3),block'=PAR3 q
 ...s hopper=$$Hopper(urgent) i hopper d
 .... ; change to multicolour
 ....d ADD^SSPRINT(REPID,"PRT","$#KC"_$c(13))
 .... ; switch to correct hopper
 ....d ADD^SSPRINT(REPID,"PRT","#H"_hopper)
 .... ; line - 1
 ....d ADD^SSPRINT(REPID,"PRT","$#2")
 ....d ADD^SSPRINT(REPID,"PRT",$e(VISIT(3),1,10)_$s($l(VISIT(3))>10:"*",1:"")_" "_$e(VISIT(4)))
 ....d ADD^SSPRINT(REPID,"PRT","#N")
 .... ; line - 2
 ....d ADD^SSPRINT(REPID,"PRT","#2")
 ....d ADD^SSPRINT(REPID,"PRT",AccNum_"."_block)
 .... ; eject cassete
 ....d ADD^SSPRINT(REPID,"PRT",$c(13))
 ...m PLIST=BLOCK s PLIST(5)="Y" i $$update^LVBHIVAB(epis_"||"_acc_"||"_block)
  Q
 ; hopper colours - 1 : orange - urgent episodes
 ;                  2 : white  - default
 ;                  3 : blue  -
Hopper(urgent) s hopper=""
 i '$l(hopper),urgent="Y" s hopper=1
 i '$l(hopper),'$$select^LVBCTSPE($p(ACC(3),$c(1))) s hopper=$p(PLIST(3),"/",2)
 s:'$l(hopper) hopper=2
 q $s(hopper:7-hopper,1:"")
]]></Routine>
</Export>
