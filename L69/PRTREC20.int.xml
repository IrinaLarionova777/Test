<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTREC20" type="INT" languagemode="0"><![CDATA[
PRTREC20 ; IG 23/11/99 ; RECEIPTS - ABBOTT Pathology
 q
 ; Parameters :  1. Receipt Number
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i '$$select^LVBPRPR(REPID) S PLIST(12)=$g(bs)_"-"_PAR1
 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
 ; create temporary printing file
BODY s epis="" i '$$select^LVBRCPT(PAR1) d
 .f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 .k REC m REC=PLIST
 .i '$$select^LVBCRBL(REC(8)) d
 ..f j=1:1:PLIST s PLIST(j)=$p($g(PLIST(j)),$c(1))
 ..k crLINE m crLINE=PLIST
 .. ; get single episodes
 ..i $l(crLINE(3)),crLINE(7) i '$l(epis) s epis=crLINE(3) d BODY1(epis,crLINE(7))
 .. ; get episode from multiple episodes
 ..i $$open^LVBCRBLB(REC(8))
 ..f  q:$$fetch^LVBCRBLB(REC(8))  i PLIST(3) s epis=$p(PLIST(2),$c(1)) d BODY1(epis,PLIST(3))
 ..i $$close^LVBCRBLB()
 ; update receipt
 i REC(3)="" d
 .s REC(3)=+$h,REC(4)=$p($h,",",2)
 .k PLIST m PLIST=REC i $$update^LVBRCPT(PAR1) 
 q
BODY1(epis,amount) s epis=$g(epis),amount=$g(amount)
 i '$$select^LVBEPVIS(epis,"Y") d
 .s ^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW")=amount
 .s xx1=$p(PLIST(57),"||",1),xx2=$p(PLIST(57),"||",2)
 .s seq=$p(^TMCB(xx1,xx2,epis),"\",2) i '$l(seq) s seq=" "
 .s credit="" i $l(PLIST(87)) s epis=PLIST(87)
 . ; get provider name and provider number
 .k INVOICE i '$$select^LVBINVB(xx1_"||"_xx2) m INVOICE=PLIST
 .s ^TMP($zn,$j,"REPORT")=$p($g(INVOICE(10)),$c(1),2)_" - "_$g(INVOICE(5))
 .s ^TMP($zn,$j,"REPORT",epis)=xx1_"||"_xx2_"||"_seq
 .s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 .s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ..s rowid=deb_"||"_date_"||"_ep
 ..i $$open^LVBDEBVT(rowid)
 ..f  q:$$fetch^LVBDEBVT(rowid)  d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k TRAN m TRAN=PLIST
 ...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) s credit=credit-PLIST(14)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) s credit=credit-PLIST(16)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) s credit=credit-PLIST(9)
 ..i $$close^LVBDEBVT()
 .i credit s ^TMP($zn,$j,"REPORT",epis,"CREDIT-TOTAL")=credit
 Q
PRINT ; print report
 d QDR
 s max=16
 s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
 .s (inpat,icDescr)="" i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y") d  k PLIST
 ..s icDescr=PLIST(5) i PLIST(4)="Y" s inpat=1
 .i icDescr="" s icDescr="Processing Fee"
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101))
 .d ITEM,ADDRESS,PHEAD
 .s (line,nextpg,item)=""
 .f  s item=$o(^TMP($zn,$j,"REPORT",epis,"ITEM",item)) q:item=""  d
 ..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2),tax=$p(det,"\",4)
 ..i item=VISIT(54),'$l(desc) s desc=icDescr
 .. ; check item desc length
 ..s x="" s xdesc=desc k desc s desc=xdesc
 ..i $l(desc)<23 s desc(1)=desc
 ..i $l(desc)>22 d
 ...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>22 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>22
 ..d ADD^PRTREP00(REPID,"PRT","?8  "_$$extdate2^SSUTIL4(VISIT(14)))
 ..d ADD^PRTREP00(REPID,"PRT","?23 "_$$extdate2^SSUTIL4(VISIT(12)))
 ..d ADD^PRTREP00(REPID,"PRT","?38 "_desc(1)),ADD^PRTREP00(REPID,"PRT","?61 "_item_" "_$s(inpat=1:"*",1:"")_$s(tax="Y":"#",1:"")),ADD^PRTREP00(REPID,"PRT","?70 "_$j(amt,7,2)),LN s line=line+1
 ..f i=2:1 q:'$d(desc(i))  d ADD^PRTREP00(REPID,"PRT","?38 "_desc(i)),LN s line=line+1
 ..i line=16,$o(^TMP($zn,$j,"REPORT",epis,"ITEM",item))'="" d
 ...d LN,ADD^PRTREP00(REPID,"PRT","?61 Continued over..")
 ...s nextpg=1 d PHEAD s (line,nextpg)=""
 . ; print LCC/SCP after billing item
 .i $l(VISIT(18)),$l(VISIT(27)) d
 ..i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y"),PLIST(7)="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1))) d
 ...d LN s line=line+1
 ...d ADD^SSPRINT(REPID,"PRT","?8  SCP Number : "_$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))),LN s line=line+1
 . ; print legend
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4680V")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD"),LN
 .d ADD^PRTREP00(REPID,"PRT","?8  LEGEND: ")
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADD^PRTREP00(REPID,"PRT","* - inpatient, # - taxable items"),LN
 .s tot=$g(^TMP($zn,$j,"REPORT",epis,"TOT")),credit=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-TOTAL"))
 .s current=$g(^TMP($zn,$j,"REPORT",epis,"CREDIT-NOW"))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h4800V")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD"),LN
 .d ADD^PRTREP00(REPID,"PRT","?52 Payment Received $"_$j(current,7,2)_" CR"),LN
 .d ADD^PRTREP00(REPID,"PRT","?8  ** Thank you for the payment **")
 .d ADD^PRTREP00(REPID,"PRT","?52 Total Amount Due $"_$j(tot-credit,7,2))
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL"),LN
 .f i=1:1:8 d LN
 .d ADD^PRTREP00(REPID,"PRT","?17 "_name),LN
 .f i=2:1:4 d ADD^PRTREP00(REPID,"PRT","?17 "_ADDRESS(i)),LN
 .f i=1:1:2 d LN
 .d ADD^PRTREP00(REPID,"PRT","?20 "_epis)
 .d ADD^PRTREP00(REPID,"PRT","?39 "_$$extdate2^SSUTIL4(+$h))
 .s episdet=$g(^TMP($zn,$j,"REPORT",epis))
 .d ADD^PRTREP00(REPID,"PRT","?70 "_$p(episdet,"||")_" "_$p(episdet,"||",2)_" "_$p(episdet,"||",3)),LN
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3300h4670V")
 q
PHEAD ; print patient details
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b4099T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h0V")
 f i=1:1:10 d LN
 d ADD^PRTREP00(REPID,"PRT","?17 "_name),LN
 d ADD^PRTREP00(REPID,"PRT","?8  Acc to:")
 d ADD^PRTREP00(REPID,"PRT","?17 "_ADDRESS(1))
 d ADD^PRTREP00(REPID,"PRT","?73 "_epis),LN
 f i=2:1:4 d ADD^PRTREP00(REPID,"PRT","?17 "_ADDRESS(i)) i i'=4 d LN
 s episdet=$g(^TMP($zn,$j,"REPORT",epis))
 d ADD^PRTREP00(REPID,"PRT","?53 "_$p(episdet,"||")_" "_$p(episdet,"||",2)_" "_$p(episdet,"||",3)),ADD^PRTREP00(REPID,"PRT","?73 "_$$extdate2^SSUTIL4(VISIT(56)))
 f i=1:1:3 d LN
 d DOC
 i drdet'="" d ADD^PRTREP00(REPID,"PRT","?8  "_drdet_$s($g(DOC(38))="Y":" (sd)",1:""))
 d ADD^PRTREP00(REPID,"PRT","?53 "_$p(^TMP($zn,$j,"REPORT")," - ",1)_" Prov No: "_$p(^TMP($zn,$j,"REPORT")," - ",2))
 f i=1:1:4 d LN
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 .s ^TMP($zn,$j,"REPORT",epis,"ITEM",item)=det
 s ^TMP($zn,$j,"REPORT",epis,"TOT")=amount
 q
DOC ; extract referring doctor details
 s drdet=""
 q:$$select^LVBCTDR(VISIT(15))  m DOC=PLIST k PLIST
 s drdet=$$NameFormat^LVBCOM03("DR",DOC(26),$e(DOC(4)),DOC(3),DOC(49),DOC(50),DOC(51))_" Prov No: "_DOC(18)
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 i '$l(ADDRESS(1)) s ADDRESS(1)=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),$e(VISIT(4)),VISIT(3),VISIT(99),VISIT(100),VISIT(101))
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
LN ; add line to report
 d ADD^PRTREP00(REPID,"PRT","!")
 q
QDR ; set header
 d ADD^PRTREP00(REPID,"HDR",$c(27)_"E")
 s x="" f  s x=$o(^SS("QDR","ABB-REC",x)) q:x=""  d ADD^PRTREP00(REPID,"HDR",^(x))
 q         
]]></Routine>
</Export>
