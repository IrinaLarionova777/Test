<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="MIF139" type="INT" languagemode="0" generated="1"><![CDATA[
MIF139(mi) ; ML 21/09/99 ; ACL 100/200/300
 s mi=$g(mi) i '$l(mi) q
 k ^TMIF(mi,10) s $zt="err^MIF000"
 i $$select^LVBMIMP(mi) q
 f j=1:1:PLIST s @("par"_j)=PLIST(j)
 s soh=$c(1),stx=$c(2),etx=$c(3),eot=$c(4),enq=$c(5),ack=$c(6),lf=$c(10),cr=$c(13),nak=$c(21),chk=0
 i $$start^MIF000() q
 f  d Main i $$stop^MIF000() q
 c par4
 q
Main i '$$READ q
 i $e(rec(1),13,14)=34 d HEAD q
 q
HEAD ; set test parameters
 s type=$e(rec(2),1,2),num=$e(rec(2),3,4),fmt=$e(rec(2),5)
 k ts,tt,se,res
 i type="03" s ts=1,test(1)="PT\PC\INR\FIB",seq(1)="1,4\6,8\10,13\15,18"
 i type="06" s ts=1,test(1)="APTT\RATIO",seq(1)="1,4\5,8"
 i type="09" s ts=1,test(1)="TT\RATIO",seq(1)="1,4\5,8"
 i type="1B" s ts=2,test(1)="PT\PC\INR\FIB",test(2)="APTT\RATIO",seq(1)="1,4\6,8\10,13\15,18",seq(2)="1,4\5,8"
 i type="1E" s ts=2,test(1)="TT\RATIO",test(2)="APTT\RATIO",(seq(1),seq(2))="1,4\5,8"
 q:'$d(test)
 s num=$zh(num)
 s err=0
 f O=1:1:num d A10 q:err>1
 i err>1 d SEND("!????!????") q
 q
A10 ; process data
 i err>1 q
 i '$$READ s err=err+1 g A10
 s err=0
 s id=$e(rec(2),25,26) q:id>18
 s zz=ts i '$d(res(+id)) s zz=1 d
 .s (sample,epis,surname,result,date,time,QC)=""
 .s epis=+id k res(+id)
 f i=1:1:$l(test(zz),"\") d RES
 i zz=ts d
 .d file^MIF000(mi,sample,epis,surname,$g(res(+id)),date,time,QC)
 .k res(+id)
 .s (sample,epis,surname,result,date,time,QC)=""
 q
RES ; extract results
 s fld=$p(seq(zz),"\",i),fldx=$p(fld,",",2),fld=+fld
 s res(+id)=$g(res(+id))_$p(test(zz),"\",i)_$c(par10)_$tr($e(rec(2),fld,fldx)," ")_$c(par11)
 q
READ() ; read in data packets from instrument
 r *x:30 e  q 0
 i $c(x)'=":" q 0
 s rec(1)=$$read^MIF000("",lf)
 i '$l(rec(1)) q 0
 d trace^MIF000(mi,rec(1),"H<--M")
 s rec(2)=$$read^MIF000("",lf)
 i '$l(rec(2)) d SEND("!????") q 0
 d trace^MIF000(mi,rec(2),"H<--M")
 r x#2:2 e  d SEND("!????") q 0
 d SEND("!"_$e(rec(1),1,4))
 q 1
SEND(str) ; send string to instrument
 w str,*-3
 d trace^MIF000(mi,str,"H-->M")
 q
]]></Routine>
</Export>
