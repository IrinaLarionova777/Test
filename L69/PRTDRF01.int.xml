<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTDRF01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTDRF01 ;IG 27/05/98 ;Doctors Request Forms for SJOG
 Q
 ; Parameters :  1.Pad Code
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 N (REPID,PAR,USER,%msql) K ^TMP($ZN,$J)
 D INIT
 I '$$select^LVBPAD(PAR1) D
 .I PLIST(5)=1 D HEADER1
 .I PLIST(5)=2 D HEADER2
 D PRINT
CREND I '$$select^LVBPRPR(REPID) S PLIST(12)=PAR1 I $$update^LVBPRPR(REPID)
 K ^TMP($ZN,$J),^TMP("PRTREP00",$J)
 Q
 ; initialize parameters
INIT F J=1:1:$L(PAR,$C(1)) S @("PAR"_J)=$P(PAR,$C(1),J)
 Q
 ; START - X,Y coordinats
 ; 4-Cols No Address
HEADER1 s START="19.0,9.9"
 i $$open^LVBPADDR(PAR1)
 f  q:$$fetch^LVBPADDR(PAR1)  d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .s cnt=PLIST(3),^TMP($zn,$j,cnt)=PLIST(2)
 i $$close^LVBPADDR()
 D ADDLINE^PRTREP00(REPID,"HDR"," !R! RES; PSRC 4; UNIT C; SPSZ 11; SPO L; EXIT;")
 s (ADDRESS1,ADDRESS2,ADDRESS3,cnt)="" f  s cnt=$o(^TMP($zn,$j,cnt)) q:cnt=""  d
 .s doc=^(cnt) i $$select^LVBCTDR(doc,"Y") q
 .s X=$s(cnt<5:$p(START,",",1),cnt<9:$p(START,",",1)+3.6,cnt<13:$p(START,",",1)+7.2,1:$p(START,",",1)+10.8)
 .s Y=$p(START,",",2)+($s(cnt#4:cnt#4,1:4)*.9)
 .s x="" f j=1:1:$l(PLIST(4)," ") s x=x_$s($l(x):" ",1:"")_$e($p(PLIST(4)," ",j))
 .s PLIST(4)=x
 .s NAME=$$NameFormat^LVBCOM03("DR",PLIST(26),$e(PLIST(4)),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 .s PROVIDER=PLIST(18),CODE=PLIST(2)
 .i '$l(ADDRESS1),$l(PLIST(33)) s ADDRESS1=PLIST(33)
 .i '$l(ADDRESS2),$l(PLIST(5)) s ADDRESS2="Ph : "_PLIST(5)
 .i '$l(ADDRESS2),$l(PLIST(6)) s ADDRESS2="Ph : "_PLIST(6)
 .i '$l(ADDRESS2),$l(PLIST(7)) s ADDRESS2="Ph : "_PLIST(7)
 .i '$l(ADDRESS3),$l(PLIST(8)) s ADDRESS3="Fax : "_PLIST(8)
 .D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_(X-.6)_","_(Y+.1)_";")
 .D ADDLINE^PRTREP00(REPID,"HDR","BOX .4,.4;")
 .D ADDLINE^PRTREP00(REPID,"HDR","FONT 74;")
 .D ADDLINE^PRTREP00(REPID,"HDR","MAP "_X_","_(Y+.1)_"; EXIT;")
 .D ADDLINE^PRTREP00(REPID,"HDR",NAME)
 . ;D ADDLINE^PRTREP00(REPID,"HDR"," !R! BARC 19,N,'"_CODE_"',.5,.5,.017,.034,.051,.068,.017,.034,.051,.068; EXIT;")
 .D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_X_","_(Y+.4)_"; EXIT;")
 .D ADDLINE^PRTREP00(REPID,"HDR",PROVIDER_" [ "_CODE_" ]")
 ; print Address
 D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_($p(START,",",1)+7.2)_","_($p(START,",",2)+.3)_"; EXIT;")
 D ADDLINE^PRTREP00(REPID,"HDR",$tr(ADDRESS1,"|"," "))
 ; print Telephone and Fax
 D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_($p(START,",",1)+7.2)_","_($p(START,",",2)+.55)_"; EXIT;")
 D ADDLINE^PRTREP00(REPID,"HDR",ADDRESS2_"  "_ADDRESS3)
 ; print Doctor Pad Code
 D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_($p(START,",",1)+12)_","_($p(START,",",2)+.55)_"; EXIT;")
 D ADDLINE^PRTREP00(REPID,"HDR",PAR1)
 D ADDLINE^PRTREP00(REPID,"HDR","!")
 Q
 ; START - X,Y coordinats
 ; 2-Cols + Address
HEADER2 s START="19.0,9.9"
 i $$open^LVBPADDR(PAR1)
 f  q:$$fetch^LVBPADDR(PAR1)  d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .s cnt=PLIST(3),^TMP($zn,$j,cnt)=PLIST(2)
 i $$close^LVBPADDR()
 D ADDLINE^PRTREP00(REPID,"HDR"," !R! RES; PSRC 4; UNIT C; SPSZ 11; SPO L; EXIT;")
 s cnt="" f  s cnt=$o(^TMP($zn,$j,cnt)) q:cnt=""  d
 .s doc=^(cnt) i $$select^LVBCTDR(doc,"Y") q
 .s X=$s(cnt<5:$p(START,",",1),1:$p(START,",",1)+7.2)
 .s Y=$p(START,",",2)+($s(cnt#4:cnt#4,1:4)*1)
 .s x="" f j=1:1:$l(PLIST(4)," ") s x=x_$s($l(x):" ",1:"")_$e($p(PLIST(4)," ",j))
 .s PLIST(4)=x F J=26,4,3 S PLIST(J)=$TR(PLIST(J),"'"," ")
 .s NAME=$$NameFormat^LVBCOM03("DR",PLIST(26),$e(PLIST(4)),PLIST(3),PLIST(49),PLIST(50),PLIST(51))
 .s PROVIDER=PLIST(18),CODE=PLIST(2),ADDRESS=PLIST(33)
 .i $l(PLIST(5)) s $p(ADDRESS,"|",4)="Ph: "_PLIST(5)
 .e  i $l(PLIST(6)) s $p(ADDRESS,"|",4)="Ph: "_PLIST(6)
 .e  i $l(PLIST(7)) s $p(ADDRESS,"|",4)="Ph: "_PLIST(7)
 .i $l(PLIST(8)) s $p(ADDRESS,"|",4)=$p(ADDRESS,"|",4)_$s($l($p(ADDRESS,"|",4)):" ",1:"")_"Fax: "_PLIST(8)
 .D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_(X-.6)_","_(Y+.1)_";")
 .D ADDLINE^PRTREP00(REPID,"HDR","BOX .4,.4;")
 .D ADDLINE^PRTREP00(REPID,"HDR","FONT 74;")
 .D ADDLINE^PRTREP00(REPID,"HDR","MAP "_X_","_(Y+.1)_"; EXIT;")
 .D ADDLINE^PRTREP00(REPID,"HDR",NAME)
 . ;D ADDLINE^PRTREP00(REPID,"HDR"," !R! BARC 19,N,'"_CODE_"',.5,.5,.017,.034,.051,.068,.017,.034,.051,.068; EXIT;")
 .D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_X_","_(Y+.4)_"; EXIT;")
 .D ADDLINE^PRTREP00(REPID,"HDR",PROVIDER_" [ "_CODE_" ]")
 .F I=1:1:4 D
 ..D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_(X+2.5)_","_(Y+(.19*I))_"; EXIT;")
 ..D ADDLINE^PRTREP00(REPID,"HDR",$P(ADDRESS,"|",I))
 ; print Doctor Pad Code
 D ADDLINE^PRTREP00(REPID,"HDR"," !R! MAP "_($p(START,",",1)+12)_","_($p(START,",",2)+.55)_"; EXIT;")
 D ADDLINE^PRTREP00(REPID,"HDR",PAR1)
 D ADDLINE^PRTREP00(REPID,"HDR","!")
 Q
PRINT i '$$select^LVBPAD(PAR1) d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 .s QUANTITY=PLIST(4),PLIST(3)=+$h i $$update^LVBPAD(PLIST(1))
 .f CNT=1:1:QUANTITY D ADDLINE^PRTREP00(REPID,"PRT","#")
 Q
]]></Routine>
</Export>
