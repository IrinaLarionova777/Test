<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTINV18" type="INT" languagemode="0" generated="1"><![CDATA[
PRTINV18 ; ML 09/05/01 ; PRIVATE INVOICES - DermPath
 q
 ; Parameters :  1.Payment Code
 ;               2.Batch Number
 ;               3.Episode Number
 ;               4.Sequence Number
 ;               5.Reminder
 ;
CREATE(REPID,PAR,USER,%msql) ; Create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 d INIT,BODY,PRINT
CREND i 'PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)=PAR1_"-"_PAR2 i $$update^LVBPRPR(REPID)
 i PAR5,'$$select^LVBPRPR(REPID) s PLIST(12)="REM-"_PAR5 i $$update^LVBPRPR(REPID)
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
 ; initialize parameters
INIT f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 q
BODY ; create temporary printing file
 ; print single patient
 i $l(PAR3),'$$select^LVBEPVIS(PAR3,"Y") d
 .m VISIT=PLIST k PLIST s epis=PAR3
 .s pc=$p(VISIT(57),"||",1),batch=$p(VISIT(57),"||",2)
 .k INVOICE i '$$select^LVBINVB(pc_"||"_batch) m INVOICE=PLIST
 .s COMP(10)=$$seldata^LVBCTCO(INVOICE(6),10)
 .s seq=$p($g(^TMCB(pc,batch,epis)),"\",2) i $l(seq) d
 ..s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq_"\"_$p(VISIT(9),$c(1))_"\"_COMP(10)
 ..d ITEM
 ; print batch
 i $l(PAR1),$l(PAR2) d
 .k INVOICE i '$$select^LVBINVB(PAR1_"||"_PAR2) m INVOICE=PLIST
 .s COMP(10)=$$seldata^LVBCTCO(INVOICE(6),10)
 .s epis="" f  s epis=$o(^TMCB(PAR1,PAR2,epis)) q:epis=""  d
 ..i $$CT007^at345(epis,PAR1_"||"_PAR2)="Y" q
 ..s seq=$p(^TMCB(PAR1,PAR2,epis),"\",2) i '$l(seq) s seq=" "
 ..i $l(PAR4),PAR4'=seq q
 ..i '$$select^LVBEPVIS(epis,"Y") d  q
 ...m VISIT=PLIST k PLIST
 ...s ^TMP($ZN,$J,"EPIS",epis)=VISIT(23)_"\"_VISIT(57)_"||"_seq_"\"_$p(VISIT(9),$c(1))_"\"_COMP(10)
 ...d ITEM 
 ; check credits
 s (credit,epis)="" f cnt=1:1 s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
 .s deb=$$seldata^LVBEPVIS(epis,20),date=$$seldata^LVBEPVIS(epis,12),list=$$seldata^LVBEPVIS(epis,55)
 .s list=epis_","_list f jMaster=1:1:$l(list,",") s ep=$p(list,",",jMaster) i $l(ep) d
 ..s rowid=deb_"||"_date_"||"_ep
 ..i $$open^LVBDEBVT(rowid)
 ..f  q:$$fetch^LVBDEBVT(rowid)  d
 ...f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 ...k TRAN m TRAN=PLIST
 ...i TRAN(5)="JOURNAL",'$$select^LVBJOU(TRAN(2)) d
 ....s credit=credit-PLIST(14)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=2,'$$select^LVBCRBL(TRAN(2)) d
 ....s credit=credit-PLIST(16)
 ...i TRAN(5)="CREDIT",$l(TRAN(2),"||")=3,'$$select^LVBCRBLB(TRAN(2)) d
 ....s credit=credit-PLIST(9)
 ..i $$close^LVBDEBVT()
 .i credit s ^TMP($zn,$j,"EPIS",epis,"CREDIT")=credit,credit=""
 ; set events
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis)) q:epis=""  d
 .s invoice=$p($p(^(epis),"\",2),"||")_"-"_$p($p(^(epis),"\",2),"||",2)_"-"_$p($p(^(epis),"\",2),"||",3)
 .i '$l(PAR5) d events^LVBCOM01(epis,"ACC","Original invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 .i 'PAR5 d events^LVBCOM01(epis,"ACC","Duplicate invoice "_invoice_$s($l(USER):" printed by "_USER,1:"")) q
 .d events^LVBCOM01(epis,"ACC","Reminder "_PAR5_$s($l(USER):" printed by "_USER,1:""))
 q
PRINT ; print report
 d QDR,PATH
 s max=15,(line,nextpg)=""
 s epis="" f  s epis=$o(^TMP($zn,$j,"EPIS",epis))  q:epis=""  d
 .k VISIT q:$$select^LVBEPVIS(epis,"Y")  m VISIT=PLIST k PLIST
 .k PAYC i '$$select^LVBCTPC(VISIT(9)) m PAYC=PLIST k PLIST
 .k IC i '$$select^LVBCTIC($p(VISIT(18),$c(1)),"Y") m IC=PLIST k PLIST
 .s lcc="" i $g(IC(7))="Y",'$$select^LVBCTCC($p(VISIT(27),$c(1)),"Y") s lcc=$s($l(PLIST(13))&(VISIT(12)<PLIST(13)):PLIST(14),1:PLIST(10))
 .s name=$$NameFormat^LVBCOM03("PT",$p(VISIT(7),$c(1),2),VISIT(4),VISIT(3),VISIT(99),VISIT(100),VISIT(101))
 .f  q:$e(name)'=" "  s name=$e(name,2,99)
 .s (line,nextpg,item)=""
 .s compabn=$p(^TMP($zn,$j,"EPIS",epis),"\",4)
 .s payable=$g(^TMP($zn,$j,"EPIS",epis,"TOT"))-$g(^TMP($zn,$j,"EPIS",epis,"CREDIT"))
 .d ADDRESS,PHEAD
 . ; print reminder notices
 .i amount,PAR5 d
 ..i PAR5=1 d
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b4101T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2400h2500V")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&d0D")
 ...d ADD^PRTREP00(REPID,"PRT","REMINDER")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&d@")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1000h2650V")
 ...d ADD^PRTREP00(REPID,"PRT","This account is now overdue. Prompt attention would be appreciated.")
 ..i PAR5=2 d
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b4101T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2150h2500V")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&d0D")
 ...d ADD^PRTREP00(REPID,"PRT","SECOND REMINDER")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&d@")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1350h2650V")
 ...d ADD^PRTREP00(REPID,"PRT","This account is still overdue. Please remit by return mail.")
 ..i PAR5=3 d
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b4101T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a2300h2500V")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&d0D")
 ...d ADD^PRTREP00(REPID,"PRT","FINAL NOTICE")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&d@")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 ...d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a750h2650V")
 ...d ADD^PRTREP00(REPID,"PRT","Please remit by return mail or this account will be forwarded to a debt collector.")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h3100V"),LN
 .d ADD^PRTREP00(REPID,"PRT","?8  "_$$extdate2^SSUTIL4(VISIT(14)))
 .d ADD^PRTREP00(REPID,"PRT","?25 "_$$extdate2^SSUTIL4(VISIT(12)))
 .s (item,Rule3Exist)="" f  s item=$o(^TMP($zn,$j,"EPIS",epis,"ITEM",item)) q:item=""  d
 ..s det=^(item),amt=$p(det,"\",1),desc=$p(det,"\",2),Rule3=$p(det,"\",3),Rule3Exist=$s(Rule3="Y":1,1:"")
 ..i item=VISIT(54),'$l(desc) s desc=$g(IC(5))
 .. ; check item desc length
 ..s x="" s xdesc=desc k desc s desc=xdesc
 ..i $l(desc)<15 s desc(1)=desc
 ..i $l(desc)>15 d
 ...f j=1:1:$l(desc,";") i $l($p(desc,";",1,j))>15 s x=x+1,desc(x)=$p(desc,";",1,j-1),(desc,desc(x+1))=$p(desc,";",j,999) q:$l(desc)'>15
 ..i $d(desc(1)) d ADD^PRTREP00(REPID,"PRT","?42 "_desc(1))
 ..d ADD^PRTREP00(REPID,"PRT","?59 "_item_$s(Rule3="Y":"*",1:"")_$s($g(IC(4))="Y":"#",1:""))
 ..d ADD^PRTREP00(REPID,"PRT","?72 "_$j(amt,7,2)),LN s line=line+1
 ..i '$d(desc(2)) d NewPage
 ..f i=2:1 q:'$d(desc(i))  d
 ...d ADD^PRTREP00(REPID,"PRT","?42 "_desc(i))
 ...d LN s line=line+1 d NewPage
 . ; print LCC/SCP after billing item
 .i lcc'="" d
 ..d LN s line=line+1
 ..d ADD^SSPRINT(REPID,"PRT","?8  SCP Number : "_lcc),LN s line=line+1
 .f i=1:1:13-line d LN
 .i $g(IC(4))="Y" d ADD^PRTREP00(REPID,"PRT","?8  # - Inpatient"),LN
 .d LN i $l(PAYC(10)) d
 ..d ADD^PRTREP00(REPID,"PRT","?8  ")
 ..d ADD^PRTREP00(REPID,"PRT","~BOLD"),ADD^PRTREP00(REPID,"PRT","~ITALIC")
 ..d ADD^PRTREP00(REPID,"PRT",PAYC(10))
 ..d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 ..d ADD^PRTREP00(REPID,"PRT",$c(13))
 .s tot=$g(^TMP($zn,$j,"EPIS",epis,"TOT")),credit=$g(^TMP($zn,$j,"EPIS",epis,"CREDIT")),GST=$g(^TMP($zn,$j,"EPIS",epis,"GST"))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3610h5020VTotal Fee:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T"),ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?71 $"_$j(tot,7,2))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3610h5140VGST Included:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T"),ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?71 $"_$j(GST,7,2))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T"),ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3610h5260VReceived:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T"),ADD^PRTREP00(REPID,"PRT",$c(13)),ADD^PRTREP00(REPID,"PRT","?71 $"_$j(credit,7,2))
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p10v0s0b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3610h5380V")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD")
 .d ADD^PRTREP00(REPID,"PRT","~UNDERLINE")
 .d ADD^PRTREP00(REPID,"PRT","Balance Due:   ")
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
 .d ADD^PRTREP00(REPID,"PRT",$c(13))
 .d ADD^PRTREP00(REPID,"PRT","?71 ")
 .d ADD^PRTREP00(REPID,"PRT","~BOLD")
 .d ADD^PRTREP00(REPID,"PRT","~UNDERLINE")
 .d ADD^PRTREP00(REPID,"PRT","$"_$j(payable,7,2))
 .d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s3b4101T")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s('PAR5:1400,1:1650)_"h6500VPlease remit "_$s('PAR5:"within 7 days",1:"")_" to:")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s('PAR5:3000,1:2600)_"h6500VDERMPATH PTY LTD")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s('PAR5:3000,1:2600)_"h6650VPO BOX 198")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a"_$s('PAR5:3000,1:2600)_"h6800VIVANHOE  3079")
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a750h2650V")
 q
PHEAD ; print patient details
 d ADD^PRTREP00(REPID,"PRT","#")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a0h0V")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p14v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1650h250VMemorandum of Fees for Histopathology")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s1p12v0s0b4101T")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3390h700V"_pathdr)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a820h850V"_compabn)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a3900h850VProvider No. "_pathprv)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(3@")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"(s0p12h0s0b5T")
 d ADD^PRTREP00(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a850h1500V"_name),ADD^PRTREP00(REPID,"PRT","~NORMAL"),LN
 f i=1:1:4 i $l(ADDRESS(i)) d ADD^PRTREP00(REPID,"PRT","?14 "_ADDRESS(i)) d LN
 d DOC
 s episdet=$g(^TMP($zn,$j,"EPIS",epis)),batch=$p(episdet,"\",2),payc=$p(episdet,"\",3)
 d ADD^PRTREP00(REPID,"PRT","~BOLD")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4350h1500V"_$p(batch,"||")_" "_$p(batch,"||",2)_" "_$p(batch,"||",3))
 d ADD^PRTREP00(REPID,"PRT","~NORMAL")
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4350h1700V"_epis)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4350h1900V"_$$extdate2^SSUTIL4(+$h))
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a1650h2300V"_drdet)
 d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4350h2100V"_drprov)
 q
ITEM ; extract billing items for episode
 s (item,amt,amount,det,GST)=""
 f  s item=$o(^TEPI(epis,7,item)) q:item=""  d
 .s det=^(item),amount=amount+$p(det,"\")
 .i $p(det,"\",4)="GST" s GST=$p(det,"\")
 .s ^TMP($zn,$j,"EPIS",epis,"ITEM",item)=det
 s ^TMP($zn,$j,"EPIS",epis,"TOT")=amount
 s ^TMP($zn,$j,"EPIS",epis,"GST")=GST
 q
DOC ; extract referring doctor details
 s (drdet,drprov)=""
 i '$$select^LVBCTDR(VISIT(15),"Y") s drdet=$$NameFormat^LVBCOM03("DR",PLIST(26),PLIST(4),PLIST(3),PLIST(49),PLIST(50),PLIST(51)),drprov=PLIST(18)
 q
ADDRESS ; if billing address different to actual address use billing address
 k ADDRESS s ADDRESS(1)=VISIT(42)
 f  q:$e(ADDRESS(1))'=" "  s ADDRESS(1)=$e(ADDRESS(1),2,99)
 f j=2:1:6 s ADDRESS(j)=VISIT(j+35)
 i '$l(ADDRESS(2)) f j=2:1:6 s ADDRESS(j)=VISIT(j+30)
 i ADDRESS(3)'="",ADDRESS(4)="",ADDRESS(3)'[ADDRESS(6) s ADDRESS(3)=ADDRESS(3)_" "_ADDRESS(6)
 i ADDRESS(4)'="",ADDRESS(4)'[ADDRESS(6) s ADDRESS(4)=ADDRESS(4)_" "_ADDRESS(5)_" "_ADDRESS(6)
 f i=1:1:4 q:i=4  s:ADDRESS(i)="" ADDRESS(i)=ADDRESS(i+1),ADDRESS(i+1)=""
 q
NewPage ; check if new page is required
 i line>15,($o(^TMP($zn,$j,"EPIS",epis,"ITEM",item))'=""!($o(^TMP($zn,$j,"EPIS",epis))'="")) d
 .d ADD^PRTREP00(REPID,"PRT",$c(27)_"&a4800h4950VCont. Over..")
 .s nextpg=1 d PHEAD s (line,nextpg)=""
 q
PATH ; extract pathology doctor details
 s (pathdr,pathprv)=""
 i '$$select^LVBINVB(PAR1_"||"_PAR2) s pathdr=$p(PLIST(10),$c(1),2)_" "_$p(PLIST(10),$c(1),3),pathprv=PLIST(5)
 q
LN ; add line to report
 d ADD^PRTREP00(REPID,"PRT","!")
 q
QDR ; set header
 s QDR="DERM-ACC",x="" f  s x=$o(^SS("QDR",QDR,x)) q:x=""  d
 .i $p(^SS("QDR",QDR,x),"-",1)="BITMAP" d  q
 ..s bitmap=$tr($p(^SS("QDR",QDR,x),"-",2,99)," ")
 ..s bit="" f  s bit=$o(^SS("QDR",bitmap,bit)) q:bit=""  d ADD^PRTREP00(REPID,"HDR",^SS("QDR",bitmap,bit))
 .d ADD^PRTREP00(REPID,"HDR",^SS("QDR",QDR,x))
 q
]]></Routine>
</Export>
