<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LBACKGR07" type="INT" languagemode="0" generated="1"><![CDATA[
LBACKGR07 ; BackGround Job - 07 - background faxes
 q
 ;
DOIT(zn,job,DATE,TIME) s zn=$g(zn),job=$g(job),DATE=$g(DATE),TIME=$g(TIME)
 l +^TMP(zn,job,"DOIT07"):0 e  q
 s zt=$zt,$zt="ERROR^"_$zn
 s $p(^TMP(zn,job,"DOIT07",DATE,TIME),"^",1)="START : "_$h
 s sort="N" i $$seldata^MVBCFLAB(1,161) s sort="Y"
 s courier="" f  s courier=$o(^TRPT("FAX",courier)) q:courier=""  d
 .l +^TRPT("F",courier):2
 .s dh="" f  s dh=$o(^TRPT("FAX",courier,dh)) q:dh=""  d
 ..s doc="" f  s doc=$o(^TRPT("FAX",courier,dh,doc)) q:doc=""  d
 ...s epis="" f  s epis=$o(^TRPT("FAX",courier,dh,doc,epis)) q:epis=""  d
 ....s fax=$tr($g(^TRPT("FAX",courier,dh,doc,epis)),"\","-") i sort="Y" s fax=$p(fax,"-")
 ....s ts="" f  s ts=$o(^TRPT("FAX",courier,dh,doc,epis,ts)) q:ts=""  d
 .....s tscnt="" f  s tscnt=$o(^TRPT("FAX",courier,dh,doc,epis,ts,tscnt)) q:tscnt=""  d
 ......s ^TRPT("F",courier_"-FAX-"_fax,dh,doc,epis,ts,tscnt)=^(tscnt)
 ......k ^TRPT("FAX",courier,dh,doc,epis,ts,tscnt)
 ....k ^TRPT("FAX",courier,dh,doc,epis)
 .l -^TRPT("F",courier)
 s fax="" f  s fax=$o(^TRPT("F",fax)) q:fax=""  d
 .l +^TRPT("F",fax):2
 .i $$START^PRTREP00("","","F\"_fax,"DRP")
 .l -^TRPT("F",fax)
 s $p(^TMP(zn,job,"DOIT07",DATE,TIME),"^",2)="FINISH : "_$h
 l -^TMP(zn,job,"DOIT07")
 s $zt=zt
 q
ERROR d BACK^%ETN
 s $zt=$g(zt)
 q
]]></Routine>
</Export>
