<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTMPL01" type="INT" languagemode="0" generated="1"><![CDATA[
PRTMPL01 ; ML 20/10/98 ; MANUAL PRICING
 q
 ; Parameters :  1. None
 ;
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 k xHEADER d INIT
 s line=1,xHEADER(line)="Manual Pricing"
 s line=2,xHEADER(line)=""
 d HEADER^PRTREP00(.xHEADER),BODY,PRINT
 k ^TMP($zn,$j),^TMP("PRTREP00",$j)
 q
INIT ; initialise parameters
 f j=1:1:$l(PAR,$c(1)) s @("PAR"_j)=$p(PAR,$c(1),j)
 k xSORT d H^PRTREP00(PAR10,$zn)
 s X=xLINE(2),MAX=$l(X),$p(SPACE," ",MAX)="",$p(LINE,"-",MAX)=""
 q
BODY ; create temporary printing file
 s epis="" f  s epis=$o(^TBIL(8,epis)) q:epis=""  d
 .k TS d TS(epis,.TS)
 .s (Episodes,ep)=""
 .f  s ep=$o(^TBIL(8,epis,"EP",ep)) q:ep=""  i ep'=epis s Episodes=Episodes_$s($l(Episodes):",",1:"")_ep d TS(ep,.TS)
 .s (Tests,ts)="" f  s ts=$o(TS(ts)) q:ts=""  s Tests=Tests_$s($l(Tests):",",1:"")_ts_$s(TS(ts)>1:"x"_TS(ts),1:"")
 .s ^TMP($zn,$j,"REPORT",epis)=Episodes_"\"_Tests
 q
TS(ep,TS) ; compile list of tests
 s ts="" f  s ts=$o(^TEPI(ep,1,ts)) q:ts=""  d
 .s tscnt="" f  s tscnt=$o(^TEPI(ep,1,ts,tscnt)) q:tscnt=""  s TS(ts)=$g(TS(ts))+1
 q
PRINT ; print report
 i $d(^TMP($zn,$j,"REPORT")) d
 .s epis="" f  s epis=$o(^TMP($zn,$j,"REPORT",epis)) q:epis=""  d
 ..s xxx(1)=$p(^(epis),"\",1),xxx(2)=$p(^(epis),"\",2) i $$select^LVBEPVIS(epis)
 ..d PRINT^PRTREP00(.PLIST,.N) k xxx,PLIST
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 .d ADDLINE^PRTREP00(REPID,"PRT",xLINE(1))
 .d ADDLINE^PRTREP00(REPID,"PRT","!")
 s report="MPL"
 s EndLine=$$seldata^LVBCTCLR(report,9)
 i EndLine="Y" D PRINTEND^PRTREP00(xLINE(1))
 q
]]></Routine>
</Export>
