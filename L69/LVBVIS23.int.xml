<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="LVBVIS23" type="INT" languagemode="0" generated="1"><![CDATA[
LVBVIS23 ; IG 2/6/98 ; Visit selection routines (for Daybook Review screen)
 ;
 ; supported calls:
 ;
 ; $$open^LVBVIS23(epis,access,user,param,p4,p5,p6,p7,p8,p9)
 ; $$fetch^LVBVIS23(epis,access,user,param,p4,p5,p6,p7,p8,p9)
 ; $$fetchbuffer^LVBVIS23(epis,access,user,param,p4,p5,p6,p7,p8,p9)
 ; $$close^LVBVIS23()
 ;
 ; param : 1.Test Set
 ;         2.User Site
 ;         3.Lab
 ;         4.Origin Site
 ;         5.Sort by : A-Accession number
 ;                     T-Test set
 ;                     E-Episode
open(epis,access,user,param,p4,p5,p6,p7,p8,p9) n (epis,access,user,param) k ^TMP($zn,$j,"create")
 s epis=$g(epis),access=$g(access),user=$g(user),param=$g(param)
 f j=1:1:$l(param,"\") s param(j)=$p(param,"\",j)
 ; check if test set - synonym
 i $l(param(1)),'$d(^TTAB("TS",param(1))) s param(1)=$$select^LVBCTSY(param(1),"TS",param(2))
 d
 .i $l(epis) d  q
 ..s x2="" f  s x2=$o(^THIRi("NotCompleted",epis,x2)) q:x2=""  d
 ...i $l(access),$e(x2,1,$l(access))'=access q
 ...d create(epis,x2)
 .s x1="" f  s x1=$o(^THIRi("NotCompleted",x1)) q:x1=""  d
 ..s x2="" f  s x2=$o(^THIRi("NotCompleted",x1,x2)) q:x2=""  d
 ...i $l(access),$e(x2,1,$l(access))'=access q
 ...d create(x1,x2)
 q 0
 ; create by episode
create(epis,access) n (epis,access,user,param)
 i $$select^LVBEPVIS(epis,"Y",user) q
 ; check user site
 i $l(param(2)),PLIST(66)'=param(2) q
 i '$$select^LVBHIVAC(epis_"||"_access,"Y") d
 .f j=1:1:PLIST s PLIST(j)=$p(PLIST(j),$c(1))
 . ; check laboratory
 .i $l(param(3)),PLIST(8)'=param(3) q
 . ; check origin site
 .i $l(param(4)),PLIST(14)'=param(4) q
 . ; check test set
 .i $l(param(1)),PLIST(6)'[("||"_param(1)_"||") q
 .s sort="" d:$l($g(param(5)))  i '$l(sort) s sort=" "
 ..i param(5)="E" s sort=epis
 ..i param(5)="A" s sort=access
 ..i param(5)="T" s sort=$s(PLIST(6)[",":"",1:$p(PLIST(6),"||",2))
 .s ^TMP($zn,$j,"create",sort,epis,access)=""
 q
 ;
fetch(epis,access,user,param,p4,p5,p6,p7,p8,p9) n (epis,access,user,param,PLIST) K PLIST
 s epis=$g(epis),access=$g(access),user=$g(user),param=$g(param)
 s line=$g(^TMP($zn,$j,"create")) f j=1:1:3 s @("line"_j)=$p(line,"||",j)
 s ok=0 f  d:$l(line1)  q:ok  s line1=$o(^TMP($zn,$j,"create",line1)),(line2,line3)="" q:line1=""
 .f  d:$l(line2)  q:ok  s line2=$o(^TMP($zn,$j,"create",line1,line2)),line3="" q:line2=""
 ..f  s line3=$o(^TMP($zn,$j,"create",line1,line2,line3)) q:line3=""  s ok=1 q
 s SQLCODE=100
 i ok s SQLCODE=$$select^LVBVIS19(line2,line3)
 s ^TMP($zn,$j,"create")=line1_"||"_line2_"||"_line3
 s PLIST=$o(PLIST(""),-1)
 q SQLCODE
 ; Fetch all data
fetchbuffer(epis,access,user,param,p4,p5,p6,p7,p8,p9) k ^TMP($zn,$j,1)
 s epis=$g(epis),access=$g(access),user=$g(user),param=$g(param)
 s max=$s($g(PLIST(1)):PLIST(1),1:100) i max>100 s max=100
 s xSUM=0
 f xxx=1:1:max s SQLCODE=$$fetch(epis,access,user,param) q:SQLCODE  d  q:xSUM>15000
 .s cnt=$o(^TMP($zn,$j,1,""),-1)+1 f j=1:1:PLIST s $p(^TMP($zn,$j,1,cnt),$c(28),j)=$g(PLIST(j))
 .s xSUM=xSUM+$l(^TMP($zn,$j,1,cnt))
 k PLIST m PLIST=^TMP($zn,$j,1) k ^TMP($zn,$j,1)
 s PLIST=$o(PLIST(""),-1)
 q SQLCODE
 ;
close() k ^TMP($zn,$j,"create")
 q 0
]]></Routine>
</Export>
