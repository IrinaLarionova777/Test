<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="at46ESP01" type="INT" languagemode="0" generated="1"><![CDATA[
at46ESP01 
 ; Custom at46 for La Paz
 q
POFILINS 
 n (%d)
 i $l($g(%d(0))) s ^TMPBQ(1,%d(0))=$h_"\"_$zn
 s vists=$g(%d(1))
 ; --------------------------------------------------------------------
 ; Put Patient Warning "CARTILLA ABIERTA" if C106 test has been ordered
 ; --------------------------------------------------------------------
 i $P(vists,"||",2)="C106" d
 . s ep=$P(%d(1),"||")
 . s mrn=$P(^TEPI(ep),"\",18)
 . s err=$$SetPatWarning("CA",mrn)
 ; --------------------------------------------------------------------
 d IDNA
 d ILEU
 s err=$$inmunocounter^UACTIONSESP01(%d(1))
 q
SetPatWarning(pwarn,mrn) 
 n (mrn,pwarn)
 q:mrn="" 100
 q:'$d(^TDEB(mrn)) 100
 s $P(^TDEB(mrn),"\",3)=pwarn
 q 0
checkaction(vts) 
 ; check on TS Add if the user adds the first test of "G" department 
 ; we add G0200 TEST SET
 n (vts)
 s epis=$P(vts,"||"),ts=$P(vts,"||",2)
 i $e(ts)="G" q "Y"
 q "N"
IDNA ; Create IDNA number
 n (%d)
 s code="ID020"
 s epis=%d(0),ts=%d(3),cnt=%d(2)
 s r=$g(^TEPI(epis))
 s mrn=$p(r,"\",18),col=$p(r,"\",10)
 i '$l(mrn) q
 i '$d(^TTAB("TC",code,1,ts)) q
 ; ts has the code tc
 i $l(col) s col=col+1
 s (swsearch,swend)=0,(wepis,wts,wcnt)=""
 f  s col=$o(^TDEB(mrn,col),-1) q:col=""  d  q:swend
 . s wepis=$o(^TDEB(mrn,col,0,wepis),-1) q:wepis=""  d  q:swend
 .. i wepis=epis s swsearch=1
 .. i 'swsearch q
 .. f  s wts=$o(^TTAB("TC",code,1,wts)) q:wts=""  d  q:swend
 ... i '$d(^TEPI(wepis,1,wts)) q
 ... f  s wcnt=$o(^TEPI(wepis,1,wts,wcnt)) q:wcnt=""  d  q:swend
 .... i wepis=epis,wts=ts,wcnt=cnt q
 .... s val=$p($g(^TEPI(wepis,1,wts,wcnt,"DATA",code)),"\",1)
 .... i $l(val) s swend=1
 i 'swend s val=$$newnum("IDNA")
 s PLIST(1)=epis_"||"_ts_"||"_cnt_"||"_code_"\"_code_"\"_val
 d save^LVBVIS4(epis_"||"_ts_"||"_cnt,"E",$$user^MVBSSUSR($j))
 q
ILEU ; Create ILEU number
 n (%d)
 s code="ID100"
 s epis=%d(0),ts=%d(3),cnt=%d(2)
 i '$d(^TTAB("TC",code,1,ts)) q
 s swend=0,(wts,wcnt)=""
 f  s wts=$o(^TTAB("TC",code,1,wts)) q:wts=""  d  q:swend
 . f  s wcnt=$o(^TEPI(epis,1,wts,wcnt)) q:wcnt=""  d  q:swend
 .. s val=$p($g(^TEPI(epis,1,wts,wcnt,"DATA",code)),"\",1)
 .. i $l(val) s swend=1
 i swend q  ; Autocopy will copy the value
 s val=$$newnum("ILEU")
 s PLIST(1)=epis_"||"_ts_"||"_cnt_"||"_code_"\"_code_"\"_val
 d save^LVBVIS4(epis_"||"_ts_"||"_cnt,"E",$$user^MVBSSUSR($j))
 q 
newnum(id) n num
 l +^CFESP01("CNT",id)
 s num=$g(^CFESP01("CNT",id))
 s ^CFESP01("CNT",id)=num+1
 l -^CFESP01("CNT",id)
 q num
newalf(id) n num
 l +^CFESP01("CNT",id)
 s num=$g(^CFESP01("CNT",id)),num=$$sum(num),^CFESP01("CNT",id)=num
 l -^CFESP01("CNT",id)
 q num
sum(txt) n (txt)
 ; Format nnnA, nnnAA, Annn, AAnnn
 i '$l(txt) q "000A"
 s format=0
 i txt?3n1a s n=$e(txt,1,3),l=$e(txt,4)
 i txt?3n2a s n=$e(txt,1,3),l=$e(txt,4,5)
 i txt?1a3n s l=$e(txt,1,1),n=$e(txt,2,4),format=1
 i txt?2a3n s l=$e(txt,1,2),n=$e(txt,3,5),format=1
 s n=n+1
 i $l(n)<4 s n=$tr($j(n,3)," ","0") q $s(format:l_n,1:n_l)
 s alf=$$sumalf(l,$l(l))
 i alf="A" s format=$s(format=1:0,1:1)
 q $s(format:alf_"000",1:"000"_alf)
sumalf(txt,pos) n (txt,pos)
 i txt="ZZ" q "A"
 i pos=0 s txt="A@",pos=2
 s c=$a($e(txt,pos))
 s c=c+1
 i c=91 s $e(txt,pos)="A" q $$sumalf(txt,pos-1)
 s $e(txt,pos)=$c(c)
 q txt
POFILUPD 
 i $l($g(%d(0))) s ^TMPBQ(1,%d(0))=$h_"\"_$zn
 q
POFILDEL 
 i $l($p($g(%old(1)),"||")) s ^TMPBQ(1,$p(%old(1),"||"))=$h_"\"_$zn
 q
]]></Routine>
</Export>
