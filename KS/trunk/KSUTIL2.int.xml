<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="KSUTIL2" type="INT" languagemode="0"><![CDATA[
KSUTIL2
	// Строка возраста на заданную дату
	// Параметры:
	// pDOB - дата рождения
	// pDate - дата, на которую необходимо определять возраст
	// pLanguage - язык результата (2-ENG, иначе-русский)
	// Примеры возвращаемого значения: 
	// 26 Y.O. или 26 ЛЕТ
	// 3 M. или 3 МЕС. (если возраст меньше 1 года)
	// 15 D. или 15 ДН. (если возраст меньше 1 месяца)
AgeString(pDOB,pDate,pLanguage) public {
	Set pLanguage=$Get(pLanguage) 
	Set result=$Select(pLanguage=2:"Unknown",1:"Неизвестен")        
	If pDOB'="" {
		Set age=$$CalAge(pDOB,pDate) ;вычисление возраста пациента  "год|месяц|день" на момент "даты забора" биоматериала
		Set ageyear=$Piece(age,"|",1)
		Set agemonth=$Piece(age,"|",2)
		Set ageday=$Piece(age,"|",3)
		If (ageyear>0) {
			 Set result=ageyear_" "_$Select(pLanguage=2:"Y.O.",1:"ЛЕТ")
		} ElseIf (agemonth>0) {
			 Set result=agemonth_" "_$Select(pLanguage=2:"M.",1:"МЕС.")
		} ElseIf (ageday>0){
			 Set result=ageday_" "_$Select(pLanguage=2:"D.",1:"ДН.")
		}			 
	}
	Quit result
}
	// Функция СП.Арма по вычислению возраста в формате "год|месяц|день" на заданную дату
	// Параметры:
	// IBirth - дата рождения
	// IToday - дата, на которую необходимо определять возраст
CalAge(IBirth, IToday) public {
 ;hack of date of birth
 s IBirth=$g(IBirth) s:IBirth>2980000 IBirth="" s:IBirth<0 IBirth="" q:'$G(IBirth) ""
 s IToday=$g(IToday) s:IToday="" IToday=$h
 s XBirth=$ZD(IBirth)
 s XToday=$ZD(IToday)
 s AgeMth=XToday-XBirth
 s AgeDay=$p(XToday,"/",2)-$p(XBirth,"/",2)
 s CurrYear=$p(XToday,"/",3) s:CurrYear<100 CurrYear=CurrYear+1900
 s BirthYear=$p(XBirth,"/",3) s:BirthYear<100 BirthYear=BirthYear+1900
 s AgeYear=CurrYear-BirthYear
 i AgeDay<0 d
 . s AgeMth=AgeMth-1
 . s AgeDay=AgeDay+$p("31,31,28,31,30,31,30,31,31,30,31,30,31",",",+XToday)
 . q:XToday'=2
 . s:((CurrYear#4)=0)&(((CurrYear#100)'=0)!((CurrYear#400)=0)) AgeDay=AgeDay+1
 i AgeMth<0 s AgeMth=AgeMth+12,AgeYear=AgeYear-1
 q AgeYear_"|"_AgeMth_"|"_AgeDay
}

]]></Routine>
</Export>
