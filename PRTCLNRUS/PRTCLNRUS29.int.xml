<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCLNRUS29" type="INT" languagemode="0"><![CDATA[
PRTCLNRUS29   ;SPARM; Сервис - Список врачей ;@gol;
 ;
 q
 ; Отчет CLNRUS29 + CLNRUS29.xsl + DLL: CLNSPARM007.clsSTART
 ; Таблица CT_Doctor 
 ;======================================================================================================
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 q
 ;======================================================================================================
 ; Формирование VB-формы для отчета (задаем входные параметры для формирования отчета)
 ; Возвращает:   имя массива  
 ;                        
FORM(USER) new (USER) s USER=$G(USER) 
 S Par="^TEMP($J)" k @Par 
 ; флаг "Выводить только активные врачи"
 s @Par@("PAR1")="CheckBox: 100 : 200 : 200  : 300 : : : 1 "
 s @Par@("PAR1a")="Label   : 500 : 200 : 5000 : 300 : Только активные врачи:"
 ; флаги для настройки вывода полей таблицы
 s @Par@("PAR2a")="Label   : 100 : 800 : 5000 : 300 : Дополнительная настройка полей таблицы:"
 s @Par@("PAR3")="CheckBox: 100 : 1100 : 200  : 300 : : : 1 "
 s @Par@("PAR3a")="Label   : 500 : 1100 : 5000 : 300 : Курьер печати:"
 s @Par@("PAR4")="CheckBox: 100 : 1400 : 200  : 300 : : : 0 "
 s @Par@("PAR4a")="Label   : 500 : 1400 : 5000 : 300 : Формат печати + Кол-во копий:"
 s @Par@("PAR5")="CheckBox: 100 : 1700 : 200  : 300 : : : 0 "
 s @Par@("PAR5a")="Label   : 500 : 1700 : 5000 : 300 : Специальность врача:"
 s @Par@("PAR6")="CheckBox: 100 : 2000 : 200  : 300 : : : 0 "
 s @Par@("PAR6a")="Label   : 500 : 2000 : 5000 : 300 : Клиент (e-mail):"
 s @Par@("PAR7")="CheckBox: 100 : 2300 : 200  : 300 : : : 0 "
 s @Par@("PAR7a")="Label   : 500 : 2300 : 5000 : 300 : Язык (для печати ответов):"
 ; настройка формы
 s @Par@("form")="Список врачей:500:300:Формиров.:Выход"
 q "^TEMP("_$J_")" 
 ;==================================================================================================================
 ; Формирование отчета согласно заданным критериям (передаются из формы VB)
 ; Формат PAR = поле $С(3) значение { $C(1) значение $C(1) значение } $C(2) поле $С(3) значение { $C(2) поле ... }
 ; Возвращает:   0^сообщение^имя файла^программ для открытия файла ( EXPLORER, EXCEL, WINWORD )
 ;             100^сообщение об ошибке
 ; 
START(USER,PAR) New (USER,PAR)  s USER=$G(USER),PAR=$G(PAR) 
 i $G(PAR)="" q "100^нет входных параметров!"    ;при выходе из DLL была нажата кнопка Отмена
 ;
 ;k ^zz   m ^zz("FORM")=^TEMP($j) s ^zz="$j="_$j_" ; $h="_$h   s ^zz("PAR")=PAR       ;@@@
 k ^TEMP($j)
 s Mas="^TEMP($zn,$J)" k @Mas
 s MSGERROR=""            
 d INIT i $g(MSGERROR)'="" quit MSGERROR
 g XML
 ;------------------------------------------------
INIT ; инициализация входных параметров
 d PARAM^REPORTSPB(PAR,"mas")
 s j="" f  s j=$O(mas(j)) q:j=""  s @j=$g(mas(j))
 ; флаг "Выводить только активных врачей "
 s FlagActive=+$G(PAR1)
 ; флаги для настройки вывода полей таблицы
 s FlagCour=+$G(PAR3)		;Курьер  ( -> CT_Courier)
 s FlagFR=+$G(PAR4)			;Формат печати + Кол-во копий
 s FlagSpec=+$G(PAR5)		;Специальность врача
 s FlagClient=+$G(PAR6)		;Клиент (e-mail) ( -> CT_Client)
 s FlagLang=+$G(PAR7)		;Язык (для печати ответов) ( -> SS_Language)
 ;
 ; log
 ;s ^zz("INIT")="PAR1="_PAR1_"  PAR2="_PAR2_"  PAR3="_PAR3_"  PAR4="_PAR4_"; PAR5="_PAR5_"; PAR6="_PAR6   ;@@@
 quit
 ;------------------------------------------------
XML    ; обработка данных и формирование XML ...
  S MasXML="^TEMP($zn,$J,""XML"")" 
  ; 
  S NN1=0,doc="" f  s doc=$O(^TTAB("DR",doc)) q:doc=""  do
  . i $$select^LVBCTDR(doc,,USER) quit
  . i $G(FlagActive)=1 i $P($G(PLIST(25)),$C(1),1)="N" quit		;CTDR_ActiveFlag
  . ;формируем данные
  . S NN1=$i(NN1)
  . s @MasXML@("User",1,"Next",NN1,"NN")=NN1
  . ; код ;CTDR_Code
  . s @MasXML@("User",1,"Next",NN1,"Code")=$G(PLIST(2))      
  . ; наименование ;CTDR_Surname + CTDR_GivenName
  . s @MasXML@("User",1,"Next",NN1,"Desc")=$G(PLIST(3))_$S($G(PLIST(4))'="":" "_$G(PLIST(4)),1:"")
  . ; флаг Active ;CTDR_ActiveFlag
  . s @MasXML@("User",1,"Next",NN1,"FlagActive")=$G(PLIST(25))  
  . i $G(FlagFR)=1 do
  .. ; Формат печати ;CTDR_PrintFormat DR -> CT_PrintFormat  
  .. s @MasXML@("User",1,"Next",NN1,"PrintFormat")=$P($G(PLIST(19)),$C(1),2)
  .. ; Кол-во копий ;CTDR_NoOfCopies
  .. s @MasXML@("User",1,"Next",NN1,"NoCopies")=$G(PLIST(14))  				 
  . ; Курьер ;CTDR_CourierRun DR -> CT_Courier
  . i $G(FlagCour)=1 do  		
  .. q:$P($G(PLIST(16)),$C(1),1)="" 
  .. s @MasXML@("User",1,"Next",NN1,"Courier")="("_$P($G(PLIST(16)),$C(1),1)_") "_$P($G(PLIST(16)),$C(1),2)	
  . ; Клиент ;CTDR_Client DR -> CT_Client
  . i $G(FlagClient)=1 do  		
  .. q:$P($G(PLIST(15)),$C(1),1)="" 
  .. s @MasXML@("User",1,"Next",NN1,"Client")="("_$P($G(PLIST(15)),$C(1),1)_") "_$P($G(PLIST(15)),$C(1),2)	
  . ; Язык ;CTDR_Language DR -> SS_Language
  . i $G(FlagLang)=1 do  
  .. s @MasXML@("User",1,"Next",NN1,"Lang")=$P($G(PLIST(46)),$C(1),2)      
  . ; Специальность ;CTDR_Specialty DR
  . i $G(FlagSpec)=1 do  		
  .. s @MasXML@("User",1,"Next",NN1,"Spec")=$P($G(PLIST(22)),$C(1),2) 
  ;
  ; входные параметры
  S @MasXML@("User",1,"FlagActive")=+$G(FlagActive)	; флаг "Выводить только активные местоположения"
  S @MasXML@("User",1,"FlagFR")=+$G(FlagFR)   		; Флаг "Формат печати + Кол-во копий"
  S @MasXML@("User",1,"FlagCour")=+$G(FlagCour)   	; Флаг "Курьер"
  S @MasXML@("User",1,"FlagSpec")=+$G(FlagSpec)   	; Флаг "Специальность"
  S @MasXML@("User",1,"FlagClient")=+$G(FlagClient) ; Флаг "Клиент"
  S @MasXML@("User",1,"FlagLang")=+$G(FlagLang) 	; Флаг "Язык"
  ;
  s @MasXML@("User",1,"NameLaboratoty")=$P($G(^CF("SM",1)),"^",5)  ; название организации (Идентификационная строка 1)
  S @MasXML@("User",1,"Footer")=$$Footer^UDFSPARM001($G(USER))     ; подвал: "дата/время печати + пользователь"
  ;
  ;log
  ;k ^zz M ^zz=@Mas     ;@@@
  ;
  S %Ret=$$WriteFileLabTrakClient^REPORTSPB($NA(@MasXML),"CLNRUS29.xsl",USER)
  i $E(%Ret)="*" q "100^Ошибка:"_$E(%Ret,2,$L(%Ret)) 
  ; Возвращаем:  0^сообщение^имя файла^программа для открытия файла ( EXPLORER, EXCEL, WINWORD ) / 100^сообщение об ошибке
  k ^TEMP($zn,$J),^TEMP($j)
  quit "0^^"_%Ret_"^"_$$GetViwer^REPORTSPB($zn)  ;"^WINWORD  ;^EXCEL"  ;^EXPLORER"
]]></Routine>
</Export>
