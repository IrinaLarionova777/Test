<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCLNRUS39" type="INT" languagemode="0"><![CDATA[
PRTCLNRUS39   ;SPARM; Сервис - Список рабочих листов;@gol;
 ;
 q
 ; Отчет CLNRUS39  +  CLNRUS39.xsl  + DLL: CLNSPARM007.clsSTART
 ;==================================================================================================================
CREATE(REPID,PAR,USER,%msql) ; create report
 n (REPID,PAR,USER,%msql) k ^TMP($zn,$j)
 q
 ;==================================================================================================================
FORM(USER) new (USER) s USER=$G(USER) 
 S Par="^TEMP($J)" k @Par 
 ; список отделов
 s @Par@("PAR1a")="Label   : 100 : 100 : 1000 : 300 : Отдел:"
 s default=$$ListDep^UDFSPARM001($G(USER),"VB","PAR1")
 s @Par@("PAR1")="ComboBox: 1000 : 100 : 4000 : 300 : :True: "_default_" :List"         ;Standart
 ; 
 ; флаг "Выводить только активные рабочие листы"
 s @Par@("PAR2")="CheckBox: 100 : 600 : 200  : 300 : : : 1 "
 s @Par@("PAR2a")="Label   : 500 : 600 : 5000 : 300 : Только активные рабочие листы:"
 ;
 ; флаги для настройки вывода полей таблицы
 s @Par@("PAR3a")="Label   : 100 : 1000 : 5000 : 300 : Дополнительная настройка вывода полей таблицы:"
 s @Par@("PAR4")="CheckBox: 100 : 1400 : 200  : 300 : : : 0 "
 s @Par@("PAR4a")="Label   : 500 : 1400 : 4000 : 300 : Ручное создание:"
 s @Par@("PAR5")="CheckBox: 100 : 1700 : 200  : 300 : : : 0 "
 s @Par@("PAR5a")="Label   : 500 : 1700 : 4000 : 300 : Максимальное кол-во образцов:"
 s @Par@("PAR6")="CheckBox: 100 : 2000 : 200  : 300 : : : 0 "
 s @Par@("PAR6a")="Label   : 500 : 2000 : 4000 : 300 : Размер столбца для печати/ввода:"
 ; 
 s @Par@("PAR7")="CheckBox: 100 : 2500 : 200  : 300 : : : 0 "
 s @Par@("PAR7a")="Label   : 500 : 2500 : 4000 : 300 : Печать вычисляемых тестов:"
 s @Par@("PAR8")="CheckBox: 100 : 2800 : 200  : 300 : : : 0 "
 s @Par@("PAR8a")="Label   : 500 : 2800 : 4000 : 300 : Печать врача:"
 s @Par@("PAR9")="CheckBox: 100 : 3100 : 200  : 300 : : : 0 "
 s @Par@("PAR9a")="Label   : 500 : 3100 : 4000 : 300 : Печать предыдущих рез-тов:"
 s @Par@("PAR10")="CheckBox: 100 : 3400 : 200  : 300 : : : 0 "
 s @Par@("PAR10a")="Label   : 500 : 3400 : 4000 : 300 : Перестроить при печати:"
 s @Par@("PAR11")="CheckBox: 100 : 3700 : 200  : 300 : : : 0 "
 s @Par@("PAR11a")="Label   : 500 : 3700 : 4000 : 300 : Печать разрешенных пациентов:"
 s @Par@("PAR12")="CheckBox: 100 : 4000 : 200  : 300 : : : 0 "
 s @Par@("PAR12a")="Label   : 500 : 4000 : 4000 : 300 : Закрыть после печати:"
 ;
 s @Par@("PAR13")="CheckBox: 4100 : 2500 : 200  : 300 : : : 0 "
 s @Par@("PAR13a")="Label   : 4500 : 2500 : 4000 : 300 : Клинические отметки:"
 s @Par@("PAR14")="CheckBox: 4100 : 2800 : 200  : 300 : : : 0 "
 s @Par@("PAR14a")="Label   : 4500 : 2800 : 4000 : 300 : Показать синоним для теста/набора:"
 s @Par@("PAR15")="CheckBox: 4100 : 3100 : 200  : 300 : : : 0 "
 s @Par@("PAR15a")="Label   : 4500 : 3100 : 4000 : 300 : Переместить ждущих пациентов в тек.РЛ:"
 s @Par@("PAR16")="CheckBox: 4100 : 3400 : 200  : 300 : : : 0 "
 s @Par@("PAR16a")="Label   : 4500 : 3400 : 4000 : 300 : Индикаторы результатов/статусов:"
 s @Par@("PAR17")="CheckBox: 4100 : 3700 : 200  : 300 : : : 0 "
 s @Par@("PAR17a")="Label   : 4500 : 3700 : 4000 : 500 : Печать списка наб.тестов без разбивки по тестам:"
 ;
 s @Par@("PAR18")="CheckBox: 100 : 4500 : 200  : 300 : : : 0 "
 s @Par@("PAR18a")="Label   : 500 : 4500 : 4000 : 300 : Расположение листа (Альбомная/Книжная):"
 s @Par@("PAR19")="CheckBox: 100 : 4800 : 200  : 300 : : : 0 "
 s @Par@("PAR19a")="Label   : 500 : 4800 : 4000 : 300 : Тип шрифта:"
 s @Par@("PAR20")="CheckBox: 100 : 5100 : 200  : 300 : : : 0 "
 s @Par@("PAR20a")="Label   : 500 : 5100 : 4000 : 300 : Комментарий:"
 ; 
 s @Par@("PAR21")="CheckBox: 4100 : 1400 : 200  : 300 : : : 1 "
 s @Par@("PAR21a")="Label   : 4500 : 1400 : 3000 : 300 : Отдел:"
 s @Par@("PAR22")="CheckBox: 4100 : 1700 : 200  : 300 : : : 0 "
 s @Par@("PAR22a")="Label   : 4500 : 1700 : 3000 : 300 : Наборы тестов:"
 s @Par@("PAR23")="CheckBox: 4100 : 2000 : 200  : 300 : : : 0 "
 s @Par@("PAR23a")="Label   : 4500 : 2000 : 3000 : 300 : Контроль Качества:"
 ;
 s @Par@("PAR24")="CheckBox: 4100 : 4500 : 200  : 300 : : : 0 "
 s @Par@("PAR24a")="Label   : 4500 : 4500 : 3000 : 300 : Дата/время последней печати РЛ:"
 s @Par@("PAR25")="CheckBox: 4100 : 4800 : 200  : 300 : : : 0 "
 s @Par@("PAR25a")="Label   : 4500 : 4800 : 3000 : 300 : Последний номер РЛ:"
 ;
 ; настройка формы
 s @Par@("form")="Список рабочих листов:500:300:Формиров.:Выход"
 q "^TEMP("_$J_")" 
 ;==================================================================================================================
 ; Формирование отчета согласно заданным критериям (передаются из формы VB)
 ; Формат PAR = поле $С(3) значение { $C(1) значение $C(1) значение } $C(2) поле $С(3) значение { $C(2) поле ... }
 ; Возвращает:   0^сообщение^имя файла^программ для открытия файла ( EXPLORER, EXCEL, WINWORD )
 ;             100^сообщение об ошибке
 ; 
START(USER,PAR) n (USER,PAR)  s USER=$G(USER),PAR=$G(PAR) 
 i $G(PAR)="" q "100^нет входных параметров!"    ;при выходе из DLL была нажата кнопка Отмена
 ;
 ;k ^zz   m ^zz("FORM")=^TEMP($j) s ^zz="$j="_$j_" ; $h="_$h   s ^zz("PAR")=PAR       ;@@@
 k ^TEMP($j),^TEMP($zn,$J)
 s Mas="^TEMP($zn,$J)" k @Mas
 s MSGERROR=""            
 d INIT i $g(MSGERROR)'="" quit MSGERROR
 g XML
 ;------------------------------------------------
INIT ; инициализация входных параметров
 New mas,str,str1,str2,i,j,pay
 d PARAM^REPORTSPB(PAR,"mas")
 s j="" f  s j=$O(mas(j)) q:j=""  s @j=$g(mas(j))
 ; отдел
 i $G(PAR1)="" s MSGERROR="100^Не определено значение <Отдел>" q
 k DEP
 if PAR1="0" do     ;* Все
 . s default=$$ListDep^UDFSPARM001($G(USER),"INIT","DEP") m DEP=^TEMP($J,"DEP")
 else  do  q:MSGERROR'=""
 . i '$D(^TTAB("DEP",PAR1)) s MSGERROR="100^В Labtrake нет кода отдела <"_DEP_">" q
 . s DEP(PAR1)=""
 ; флаг "Выводить только активные рабочие листы"
 s FlagActiveWS=+$G(PAR2)
 ; флаги для настройки вывода полей таблицы
 s FlagManual=+$G(PAR4)		; Флаг - Ручное создание					- ManualCreation
 s FlagMaxVisits=+$G(PAR5)	; Максимальное кол-во образцов				- MaxVisits
 s FlagColumn=+$G(PAR6)		; Размер столбца для печати/ввода           - Column Width For Printing
 s FlagCalculate=+$G(PAR7)	; Флаг - Печать вычисляемых тестов			- PrintCalculatedFields
 s FlagDoc=+$G(PAR8)		; Флаг - Печать врача						- PrintDoctor
 s FlagPrevResult=+$G(PAR9)	; Флаг - Печать предыдущих рез-тов			- PreviousResults
 s FlagRearrange=+$G(PAR10)	; Флаг - Перестроить при печати				- RearrangeWhenPrinting
 s FlagPAP=+$G(PAR11)		; Флаг - Печать разрешенных пациентов		- Print Authorised Patients
 s FlagClose=+$G(PAR12)		; Флаг - Закрыть после печати               - Close WS After Printing
 s FlagClinNotes=+$G(PAR13)	; Флаг - Клинические отметки				- ClinicalNotes
 s FlagSynonym=+$G(PAR14)	; Флаг - Показать синоним для теста/набора	- Use Synonyms For Test Items
 s FlagPending=+$G(PAR15)	; Флаг - Переместить ждущих пац.в тек.РЛ	- PendingToCurrent
 s FlagIndicator=+$G(PAR16)	; Флаг - Индикаторы результатов/статусов	- Flag ResultIndicators / StatusIndicators
 s FlagPrintOnlyTS=+$G(PAR17)	; Флаг - Печать списка наб.тестов без разбивки по тестам - AutomaticAllocation
 s FlagDirect=+$G(PAR18)	; Расположение листа (Альбомная/Книжная)	- Print Direction [ Horizontal||H, Vertical||V ]
 s FlagFont=+$G(PAR19)		; Тип шрифта	
 s FlagComments=+$G(PAR20)	; Комментарий								- Comments to print
 s FlagDep=+$G(PAR21)		; Отдел		
 s FlagTS=+$G(PAR22)		; Наборы тестов
 s FlagQC=+$G(PAR23)		; Контроль Качества
 s FlagLastPrint=+$G(PAR24)	; Дата/время последней печати
 s FlagLastNum=+$G(PAR25)	; Последний номер РЛ
 quit
 ;------------------------------------------------
XML    ; обработка данных и формирование XML ...
  s Mas="^TEMP($zn,$J)" k @Mas
  S MasXML="^TEMP($zn,$J,""XML"")" 
  ; 
  S (NN1,NN2)=0 do
  . s ws="" f  s ws=$O(^TTAB("WS",ws)) q:ws=""  do
  .. s str=$G(^TTAB("WS",ws)) q:str=""
  .. i $G(FlagActiveWS)=1 i $p(str,"\",2)="N" quit
  .. ; ---
  .. ; проверяем ограничение доступа по отделам
  .. k MasWS,MasDepWS
  .. s xx=0,i="" f  s i=$O(^TTAB("WS",ws,10,i)) q:i=""  do
  ... s ts=i q:ts=""  s dep=$e(ts,1) q:dep=""  q:'$D(DEP(dep))
  ... ;s ts=$P($G(^TTAB("WS",ws,10,i)),"\",1) q:ts=""  s dep=$e(ts,1) q:dep=""  q:'$D(DEP(dep))
  ... i +$G(FlagTS)=1 s xx=$i(xx),MasWS("TS",xx,"TestSet")="("_ts_") "_$P($G(^TTAB("TS",ts)),"\",1)
  ... s MasDepWS(dep)=""
  .. quit:'$D(MasDepWS)
  .. ; ---
  .. S NN1=$i(NN1)
  .. ; N п/п
  .. s @MasXML@("User",1,"Next",NN1,"NN")=NN1
  .. ; код РЛ
  .. s @MasXML@("User",1,"Next",NN1,"Code")=ws       
  .. ; наименование РЛ
  .. s @MasXML@("User",1,"Next",NN1,"Desc")=$$trans($p(str,"\",1))
  .. ; флаг Active
  .. s @MasXML@("User",1,"Next",NN1,"FlagActive")=$p(str,"\",2)         
  .. ; Флаг - Ручное создание
  .. i $G(FlagManual)=1 s @MasXML@("User",1,"Next",NN1,"ManualCreation")=$p(str,"\",15)
  .. ; Флаг - Максимальное кол-во образцов	
  .. i $G(FlagMaxVisits)=1 s @MasXML@("User",1,"Next",NN1,"MaxVisits")=$p(str,"\",3)
  .. ; Размер столбца для печати/ввод 
  .. i $G(FlagColumn)=1 do
  ... s @MasXML@("User",1,"Next",NN1,"ColumnPrinting")=$p(str,"\",4)	;WKS_ColumnWidthForPrinting
  ... s @MasXML@("User",1,"Next",NN1,"ColumnEntry")=$p(str,"\",19)		;WKS_ColumnWidthForEntry
  .. ; Флаг - Печать вычисляемых тестов
  .. i $G(FlagCalculate)=1 s @MasXML@("User",1,"Next",NN1,"PrintCalculatedFields")=$p(str,"\",12)
  .. ; Флаг - Печать врача
  .. i $G(FlagDoc)=1 s @MasXML@("User",1,"Next",NN1,"PrintDoctor")=$p(str,"\",7)
  .. ; Флаг - Печать предыдущих рез-тов
  .. i $G(FlagPrevResult)=1 s @MasXML@("User",1,"Next",NN1,"PreviousResults")=$p(str,"\",14)
  .. ; Флаг - Перестроить при печати
  .. i $G(FlagRearrange)=1 s @MasXML@("User",1,"Next",NN1,"RearrangeWhenPrinting")=$p(str,"\",13)
  .. ; Флаг - Печать разрешенных пациентов
  .. i $G(FlagPAP)=1 s @MasXML@("User",1,"Next",NN1,"PrintAuthorisedPatients")=$p(str,"\",17)
  .. ; Флаг - Закрыть после печати
  .. i $G(FlagClose)=1 s @MasXML@("User",1,"Next",NN1,"CloseAfterPrinting")=$p(str,"\",5)
  .. ; Флаг - Клинические отметки
  .. i $G(FlagClinNotes)=1 s @MasXML@("User",1,"Next",NN1,"ClinicalNotes")=$p(str,"\",10)
  .. ;  Флаг - Показать синоним для теста/набора
  .. i $G(FlagSynonym)=1 do
  ... s @MasXML@("User",1,"Next",NN1,"UseSynonymsForTI")=$p(str,"\",11)			;WKS_UseSynonymsForTI
  ... s @MasXML@("User",1,"Next",NN1,"UseSynonymsForTS")=$p(str,"\",20)			;WKS_UseSynonymsForTS
   .. ;  Флаг - Переместить ждущих пац.в тек.РЛ
  .. i $G(FlagPending)=1 s @MasXML@("User",1,"Next",NN1,"PendingToCurrent")=$p(str,"\",16)
  .. ;  Флаг - Индикаторы результатов/статусов
  .. i $G(FlagIndicator)=1 do
  ... s @MasXML@("User",1,"Next",NN1,"ResultIndicators")=$p(str,"\",23)			;WKS_ResultIndicators
  ... s @MasXML@("User",1,"Next",NN1,"StatusIndicators")=$p(str,"\",24)			;WKS_StatusIndicators
  .. ;  Флаг - Печать списка наб.тестов без разбивки по тестам  (WKS_AutomaticAllocation)
  .. i $G(FlagPrintOnlyTS)=1 s @MasXML@("User",1,"Next",NN1,"PrintOnlyTS")=$p(str,"\",29)
  .. ; Расположение листа (Альбомная/Книжная)
  .. i $G(FlagDirect)=1 do
  ... s xxx=$p(str,"\",18)								;WKS PrintDirection [Horizontal||H, Vertical||V ]  
  ... s @MasXML@("User",1,"Next",NN1,"PrintDirection")=$S(xxx="H":"Альбомная",xxx="V":"Книжная",1:xxx)	
  .. ; Тип шрифта	
  .. i $G(FlagFont)=1 s @MasXML@("User",1,"Next",NN1,"Font")=$p(str,"\",21)
  .. ;  Комментарий
  .. i $G(FlagComments)=1 do
  ... s (i,NN2)=0 f  s i=$O(^TTAB("WS",ws,"REM",i)) q:i=""  s NN2=$i(NN2),@MasXML@("User",1,"Next",NN1,"Comments",NN2,"Desc")=^TTAB("WS",ws,"REM",i)
  .. ; ---
  .. ; Отделы/Наборы тестов
  .. i +$G(FlagTS)=1 m @MasXML@("User",1,"Next",NN1,"TS")=MasWS("TS")
  .. i +$G(FlagDep)=1 do
  ... s Str="",i="" f  s i=$O(MasDepWS(i)) q:i=""  s Str=$S(Str="":"",1:Str_"; ")_"("_i_") "_$P($G(^TTAB("DEP",i)),"\",1)
  ... s @MasXML@("User",1,"Next",NN1,"Department")=Str
  .. ; Контроль Качества ;^TTAB("WS","N6",1,1)=Eiio?ieu (+)  (IgG)\\IFARubellaG\1
  .. i +$G(FlagQC)=1 do
  ... s (i,NN2)=0 f  s i=$O(^TTAB("WS",ws,1,i)) q:i=""  d
  .... f xi=1:1:4 s xstr(xi)=$P($G(^TTAB("WS",ws,1,i)),"\",xi)
  .... q:xstr(1)=""
  .... s NN2=$i(NN2)
  .... s @MasXML@("User",1,"Next",NN1,"QC",NN2,"Desc")=i_") "_$G(xstr(1))_$S($G(xstr(3))'="":" (Конт.Материал: "_xstr(3)_$S($G(xstr(4))'="":" / Уровень="_xstr(4),1:"")_")",1:"")
  .. ; Дата/время последней печати
  .. i +$G(FlagLastPrint)=1 do
  ... s strlp=""
  ... s wsnum="" f  s wsnum=$O(^TWKS(ws,2,wsnum),-1) q:wsnum=""  do  q:strlp'=""
  .... i $P($G(^TWKS(ws,2,wsnum)),"\",2)'="" do  ;^TWKS("ГЕПС",2,500)=34\61236\48859\C\\P\61234
  ..... s strlp=$TR($zd($P($G(^TWKS(ws,2,wsnum)),"\",2),4),"/",".")_" "_$zt($P($G(^TWKS(ws,2,wsnum)),"\",3),2)_" (РЛ № "_wsnum_")"
  ... s @MasXML@("User",1,"Next",NN1,"DateTimeLastPrint")=$G(strlp)
  .. ; Последний номер РЛ
  .. i +$G(FlagLastNum)=1 s @MasXML@("User",1,"Next",NN1,"LastNum")=$O(^TWKS(ws,2,""),-1)
  ;
  ; входные параметры
  S @MasXML@("User",1,"Department")=$S(PAR1="":"",PAR1="0":"* Все",1:PAR1_" - "_$P($G(^TTAB("DEP",PAR1)),"\",1))
  S @MasXML@("User",1,"FlagActiveWS")=+$G(FlagActiveWS)		; флаг "Выводить только активные рабочие листы"
  S @MasXML@("User",1,"FlagManual")=+$G(FlagManual)   		; Флаг - Ручное создание
  S @MasXML@("User",1,"FlagMaxVisits")=+$G(FlagMaxVisits)  	; Максимальное кол-во образцов
  S @MasXML@("User",1,"FlagColumn")=+$G(FlagColumn)   		; Размер столбца для печати/ввода
  S @MasXML@("User",1,"FlagCalculate")=+$G(FlagCalculate)   ; Флаг - Печать вычисляемых тестов
  S @MasXML@("User",1,"FlagDoc")=+$G(FlagDoc)   			; Флаг - Печать врача	
  S @MasXML@("User",1,"FlagPrevResult")=+$G(FlagPrevResult) ; Флаг - Печать предыдущих рез-тов
  S @MasXML@("User",1,"FlagRearrange")=+$G(FlagRearrange) 	; Флаг - Перестроить при печати	
  S @MasXML@("User",1,"FlagPAP")=+$G(FlagPAP)   			; Флаг - Печать разрешенных пациентов
  S @MasXML@("User",1,"FlagClose")=+$G(FlagClose)   		; Флаг - Закрыть после печати
  S @MasXML@("User",1,"FlagClinNotes")=+$G(FlagClinNotes)   ; Флаг - Клинические отметки
  S @MasXML@("User",1,"FlagSynonym")=+$G(FlagSynonym) 		; Флаг - Показать синоним для теста/набора
  S @MasXML@("User",1,"FlagPending")=+$G(FlagPending) 		; Флаг - Переместить ждущих пац.в тек.РЛ
  S @MasXML@("User",1,"FlagIndicator")=+$G(FlagIndicator) 	; Флаг - Индикаторы результатов/статусо
  S @MasXML@("User",1,"FlagPrintOnlyTS")=+$G(FlagPrintOnlyTS) ; Флаг - Печать списка наб.тестов без разбивки по тестам
  S @MasXML@("User",1,"FlagDirect")=+$G(FlagDirect) 		; Расположение листа (Альбомная/Книжная)
  S @MasXML@("User",1,"FlagFont")=+$G(FlagFont) 			; Тип шрифта
  S @MasXML@("User",1,"FlagComments")=+$G(FlagComments) 	; Комментарий		  	
  S @MasXML@("User",1,"FlagDep")=+$G(FlagDep) 				; Отдел		
  S @MasXML@("User",1,"FlagTS")=+$G(FlagTS) 				; Наборы тестов
  S @MasXML@("User",1,"FlagQC")=+$G(FlagQC) 				; Контроль Качества		
  S @MasXML@("User",1,"FlagLastPrint")=+$G(FlagLastPrint)	; Дата/время последней печати
  S @MasXML@("User",1,"FlagLastNum")=+$G(FlagLastNum)		; Последний номер РЛ
  ;
  s @MasXML@("User",1,"NameLaboratoty")=$P($G(^CF("SM",1)),"^",5)  ; название организации (Идентификационная строка 1)
  S @MasXML@("User",1,"Footer")=$$Footer^UDFSPARM001($G(USER))     ; подвал: "дата/время печати + пользователь"
  ;
  ;log
  ;k ^zz M ^zz("XML")=@MasXML     ;@@@
  ;
  S %Ret=$$WriteFileLabTrakClient^REPORTSPB($NA(@MasXML),"CLNRUS39.xsl",USER)
  i $E(%Ret)="*" q "100^Ошибка:"_$E(%Ret,2,$L(%Ret)) 
  ; Возвращаем:  0^сообщение^имя файла^программа для открытия файла ( EXPLORER, EXCEL, WINWORD ) / 100^сообщение об ошибке
  k ^TEMP($zn,$J),^TEMP($j)
  quit "0^^"_%Ret_"^"_$$GetViwer^REPORTSPB($zn)  ;"^WINWORD  ;^EXCEL"  ;^EXPLORER"
  ;---------------
trans(res) 
  q $tr(res,$c(16,17,18,5,6,11,12),"^\|{}[]")
]]></Routine>
</Export>
