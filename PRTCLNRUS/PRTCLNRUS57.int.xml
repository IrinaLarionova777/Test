<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Routine name="PRTCLNRUS57" type="INT" languagemode="0"><![CDATA[
PRTCLNRUS57   ; Кол-во тестов по приборам
 q
 ;
 ; Отчет CLNRUS57  +  CLNRUS57.xsl  + DLL: CLNSPARM007.clsSTART
 ; 
 ; Используется сервисная программа  ^REPORTSPB
 ;==================================================================================================================
 ; Формирование VB-формы для отчета (задаем входные параметры для формирования отчета)
 ; Возвращает:   имя массива  
FORM(USER) new (USER) s USER=$G(USER) 
 S Par="^TEMP($J)" k @Par             
 ; период дат
 s @Par@("PAR1a")="Label   : 100 : 200 : 1000 : 300 : Дата от:"
 s @Par@("PAR1")="DateText: 1300: 200 : 1100 : 300 : t-7 :True"
 s @Par@("PAR2a")="Label   : 3000: 200 : 1000 : 300 : Дата до:"
 s @Par@("PAR2")="DateText: 4100: 200 : 1100 : 300 : t:True"
 ; ---
 ; список приборов
 s @Par@("PAR3a")="Label   : 100 : 1000 : 1000 : 300 : Анализатор:"
 set default="0"
 s @Par@("PAR3")="ListBox :  100 : 1300 : 6000 : 5000 : :True: 0 : Checkbox  " 
 set id=""
 for {
	 set id=$order(^TMIF(id),1,record) if id="" quit
	 set name=$piece(record,"\",1)
	 ;set @Par@("PAR3",name_$char(4)_id)=name
	 set @Par@("PAR3",id)=name
 }
 if $data(@Par@("PAR3")) set @Par@("PAR3",0)="Все анализаторы"
 ; ---
 ; настройка формы
 s @Par@("form")="Отчет о количестве тестов по анализаторам ("_USER_"):500:300:Формиров.:Выход"
 q "^TEMP("_$J_")" 
 ;==================================================================================================================
START(USER,PAR) n (USER,PAR)  s USER=$G(USER),PAR=$G(PAR) 
 i $G(PAR)="" q "100^нет входных параметров!"    ;при выходе из DLL была нажата кнопка Отмена
 ;
 ;k ^zz  ;m ^zz("FORM")=^TEMP($j) s ^zz="$j="_$j_" ; $h="_$h   s ^zz("PAR")=PAR  ;@@@
 ; сохраняем для обработки входных данных (списка наборов тестов)
 k MasForm m MasForm=^TEMP($j) 
 k ^TEMP($J)
 s Mas="^TEMP($J,""START"")"
 s MSGERROR=""            
 d INIT i $g(MSGERROR)'="" quit MSGERROR
 set sc=$$BODY() 
 if 'sc {
	 k ^TEMP($zn,$J),^TEMP($j)
	 s MSGERROR="100^"_$System.Status.GetErrorText(sc) 
	 quit MSGERROR
 }
 d XML
 k ^TEMP($zn,$J),^TEMP($j)
 q $G(MSGERROR)  ;код^сообщение (об ошибке) для возврата в DLL^имя файла^имя проги для открытия файла
 ;------------------------------------------------
INIT ; инициализация входных параметров
 New mas,str,i,j
 d PARAM^REPORTSPB(PAR,"mas")
 s j="" f  s j=$O(mas(j)) q:j=""  s @j=$g(mas(j))
 ; период дат
 i $G(PAR1)="" s MSGERROR="100^Не определено значение <Дата от>" q
 i $G(PAR2)="" s MSGERROR="100^Не определено значение <Дата до>" q
 i PAR1>PAR2 s MSGERROR="100^Значение <Дата от> больше значения <Дата до>" q
 set dateFrom=PAR1,dateTo=PAR2
 ; Анализаторы
 i $G(PAR3)="" s MSGERROR="100^Не определено значение <Анализатор>" q
 ; ---
 kill Analyzers
 for i=1:1:$length(PAR3,$char(1)) {
	 set analyzer=$piece(PAR3,$char(1),i)
	 if analyzer=0 set Analyzers=1 quit
	 if analyzer'="" set Analyzers(analyzer)=""
 }
 quit
BODY()
 set ok=1
 try {
	 kill ^||data
	 set Analyzers=+$get(Analyzers)
	 set date=PAR1-1 
     for {
	     set date=$order(^TDAY(4,date)) quit:date=""  quit:date>PAR2  
	     set epis="" 
	     for {
		     set epis=$order(^TDAY(4,date,0,epis)) quit:epis=""  if '$Data(^TEPI(epis)) continue
		     set rec=$get(^TEPI(epis))
             set ts=""
             for {
	             set ts=$o(^TEPI(epis,1,ts)) quit:ts=""  
	             set tscnt="",cnt=0 
                 for {
	                 set tscnt=$o(^TEPI(epis,1,ts,tscnt)) quit:tscnt=""  
	                 set test=""
	                 for {
		                 set test=$order(^TEPI(epis,1,ts,tscnt,"DATA",test)) if test="" quit
	                     set ^||data("test",test)=$get(^||data("test",test))+1		                 
	                     set analyzercode=##class(TCBI.Utils).GetMachineTestData0(epis_"||"_ts_"||"_tscnt_"||"_test)
	                     if (analyzercode'="")&&((Analyzers=1)||($data(Analyzers(analyzercode)))) {
		                     set ^||data("test",test,analyzercode)=$get(^||data("test",test,analyzercode))+1
	                     }
	                 }
                 }
             }
	     }
     }
	 set test=""
	 for {
	    set test=$order(^||data("test",test)) if test="" quit
	    set countall=+^(test)
	    set analyzercode=""
	    for {
		    set analyzercode=$order(^||data("test",test,analyzercode)) if analyzercode="" quit
		    set countAnalyzer=+^(analyzercode)
		    
		    if $get(^||data("AnalyzerName",analyzercode))="" {
			   set ^||data("AnalyzerName",analyzercode)=$piece($get(^TMIF(analyzercode)),"\",1)   
		    }
		    set nameAnalyzer=$get(^||data("AnalyzerName",analyzercode))
		    set analyzer=nameAnalyzer_$char(1)_analyzercode
		    set ^||data("report",analyzer,test)=countAnalyzer_$char(0)_countall
		    set rec=$get(^||data("report",analyzer))
		    set $piece(rec,$char(0),1)=$piece(rec,$char(0),1)+countAnalyzer
		    set $piece(rec,$char(0),2)=$piece(rec,$char(0),2)+countall
		    set ^||data("report",analyzer)=rec
	    }
	 		    	 
	 } 
	 // итого тестов, сделанных на анализаторах
	 set analyzer=""
	 for {
		    set analyzer=$order(^||data("report",analyzer)) if analyzer="" quit
		    set rec=+^(analyzer)
		    set $piece(^||data("report"),$char(0),1)=$piece($get(^||data("report")),$char(0),1)+$piece(rec,$char(0),1)
	 }
	 // итого тестов всего (только среди тестов, сделанных хоть один раз на анализаторе)
	 set test=""
	 for {
	    set test=$order(^||data("test",test)) if test="" quit
	    set countall=+^(test) 
	    set countallanal=0 if $data(^||data("test",test))>10 set countallanal=countall
	    set reco=$get(^||data("report"))
		set $piece(reco,$char(0),2)=$piece(reco,$char(0),2)+countall  // всего тестов
		set $piece(reco,$char(0),3)=$piece(reco,$char(0),3)+countallanal // всего тестов (учитываются только тесты, которые хоть один раз за этот период делались на анализаторе)
		set ^||data("report")=reco 
	 }
 } catch(exception) {
	 set ok=exception.AsStatus()
 }

 quit ok
 ;------------------------------------------------
XML  ; обработка данных...
 ; Формирование XML...
 if '$D(^||data("report")) s MSGERROR="100^Нет данных!" quit
 S MasO="^TEMP($J,""XML"")"
 set analyzer="",nnAn=0
 for {
	 set analyzer=$order(^||data("report",analyzer)) if analyzer="" quit
	 set rec=$get(^(analyzer))
     set nnAn=nnAn+1
     set @MasO@("User",1,"GrAnalyzer",nnAn,"Analyzer")=$piece(analyzer,$char(1),1) 
     set @MasO@("User",1,"GrAnalyzer",nnAn,"CountTestAnalyzer")=$piece(rec,$char(0),1)
     set @MasO@("User",1,"GrAnalyzer",nnAn,"CountTestAll")=$piece(rec,$char(0),2)						
     set test="",nnTest=0
     for {
	     set test=$order(^||data("report",analyzer,test)) if test="" quit
	     set rec=^(test)
	     set nnTest=nnTest+1
	     set @MasO@("User",1,"GrAnalyzer",nnAn,"GrTest",nnTest,"Test")=test
	     set @MasO@("User",1,"GrAnalyzer",nnAn,"GrTest",nnTest,"TestName")=$piece($get(^TTAB("TC",test)),"\",1)
	     set @MasO@("User",1,"GrAnalyzer",nnAn,"GrTest",nnTest,"CountTestAnalyzer")=$piece(rec,$char(0),1) 
	     set @MasO@("User",1,"GrAnalyzer",nnAn,"GrTest",nnTest,"CountTestAll")=$piece(rec,$char(0),2)
     }
 }
 ;
 ; общие итоги по отчету
 set rec=$get(^||data("report"))
 set @MasO@("User",1,"TotalCountTestAnalyzer")=$piece(rec,$char(0),1)
 set @MasO@("User",1,"TotalCountTestAll")=$piece(rec,$char(0),2)
 ;
 ; шапка
 s @MasO@("User",1,"NameReport")="Отчет о количестве тестов, сделанных на анализаторе"
 S @MasO@("User",1,"DateFrom")=$TR($$extdate2^SSUTIL4(dateFrom),"/",".")
 S @MasO@("User",1,"DateTo")=$TR($$extdate2^SSUTIL4(dateTo),"/",".")
 S @MasO@("User",1,"Footer")=$$Footer^UDFSPARM001($G(USER))     ; подвал: "дата/время печати + пользователь"
 ;
 //kill ^zz M ^zz=@MasO     ;@@@
 ;
 S MSGERROR=$$WriteFileLabTrakClient^REPORTSPB($NA(@MasO),"CLNRUS57.xsl",USER,$get(PAR))
 ;i $E(%Ret)="*" s MSGERROR="100^Ошибка:"_$E(%Ret,2,$L(%Ret)) quit
 ;
 ; Возвращаем:  0^сообщение^имя файла^программа для открытия файла ( EXPLORER, EXCEL, WINWORD ) / 100^сообщение об ошибке
 ;s MSGERROR="0^^"_%Ret_$$GetViwer^REPORTSPB($zn)  ;^WINWORD"  ;^EXCEL"  ;^EXPLORER"
 quit

]]></Routine>
</Export>
