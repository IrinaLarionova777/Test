<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="24">
<Class name="ReportZEN.genRptNew">
<Description>
Программа генерации окна запроса параметров для отчета
предполагается %request user,reportRoutine
Отчеты типа medium - для них сделаны свои мордочки - должны вызываться напрямую в getReportURL^TCLEx.ReportVB() (для этого в классификаторе Настройке отчетов - долженбыть указан класс zen- .cls </Description>
<Super>ReportZEN.genRptAbstractNew</Super>
<TimeCreated>62409,56913.501078</TimeCreated>

<Parameter name="APPLICATION">
<Description>
Имя приложения, которому принадлежит эта страница.</Description>
<Default>ReportZEN.ReportApplication</Default>
</Parameter>

<UDLText name="T">
<Content><![CDATA[
//Parameter APPLICATION = "WebER.Application";

]]></Content>
</UDLText>

<Parameter name="PAGENAME">
<Description>
Отображаемое имя для нового приложения.</Description>
</Parameter>

<Parameter name="DOMAIN">
<Description>
Домен, используемый для локализации.</Description>
</Parameter>

<XData name="Contents">
<Description>
Этот XML блок описывает содержимое этой страницы.</Description>
<XMLNamespace>http://www.intersystems.com/zen</XMLNamespace>
<Data><![CDATA[
<page height="100%" id="page" xmlns="http://www.intersystems.com/zen" title="" xmlns:WebER="WebER" xmlns:ZenExt="ZenExt">  
<form id="mainFormForFaceReport" 
	width="50%" 
	layout="vertical"
	labelPosition="top"
	cellStyle="padding: 3px;" 
	align="center"
	>
<html id="titleFormForFaceReport">Заголовок</html>
<vgroup id="formGroupForFaceReport" align="center">
</vgroup>
</form>
</page>
]]></Data>
</XData>

<Method name="%OnAfterCreatePage">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
 set ok=$$$OK
 try {
	 
	Do ##super()
	
	set title="",refTempGlob=""
    if $IsObject(%session) {
	       if $data(%session.Data("title")) set title=%session.Data("title")
	       if $data(%session.Data("refTempGlob")) set refTempGlob=%session.Data("refTempGlob")   
    }
    
    // title
    set ctrlTitle=%page.%GetComponentById("titleFormForFaceReport")
    if title '= "",$IsObject(ctrlTitle) {
       set ctrlTitle.content=$zcvt(title,"O","HTML")
    }
    elseif $IsObject(ctrlTitle) {
	   set ctrlTitle.hidden=1 
    }
    
    set form= %page.%GetComponentById("formGroupForFaceReport")
    
    // формирование элементов экрана по описанию
    $$$ThrowOnError(..runCreatePage(refTempGlob)) 
    
    // кнопка Печать
    Set spacer = ##class(%ZEN.Component.spacer).%New()
    Set spacer.height=10
    do form.%AddChild(spacer)  
    Set button = ##class(%ZEN.Component.button).%New()
    Set button.align="center"
    Set button.caption="Печать"
    Set button.onclick="zenPage.startRptClient()"
    Do form.%AddChild(button)
    Set spacer = ##class(%ZEN.Component.spacer).%New()
    Set spacer.height=10
    Do form.%AddChild(spacer)
 
  } catch(exception) 
  {
	   set ok=exception.AsStatus()	
  } 
  quit ok
]]></Implementation>
</Method>

<Method name="runCreatePage">
<ClassMethod>1</ClassMethod>
<FormalSpec>refParam:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
 

 set ok=$$$OK
 try {
	set row="",maxWidthLabel=0 
    for {
	  set row=$order(@refParam@("layout",row)) if row="" quit 
      set rec=$get(@refParam@("layout",row))
      set description=$listget(rec,2),len=$length(description)    
	  if len>maxWidthLabel set maxWidthLabel=len
    } 
    set maxWidthLabel=(maxWidthLabel*10)_"px"
     	   
    set form= %page.%GetComponentById("formGroupForFaceReport")
    set layoutRow=##class(%ZEN.Component.vgroup).%New()
    ;set layoutRow.cellSize="" ;"same" ;"stretch"
    do form.%AddChild(layoutRow) 
    set row="",width=(1000-maxWidthLabel)_"px",size=+width*0.15\1
    for {
	    
	      set row=$order(@refParam@("layout",row)) if row="" quit 
	      ;Set spacer = ##class(%ZEN.Component.spacer).%New()
          ;Set spacer.height="1px"
          ;do layoutRow.%AddChild(spacer) 
	      set layoutCol=##class(%ZEN.Component.hgroup).%New()
	      ;set layoutCol.cellSize="" ;"same" ;"stretch"
	      do layoutRow.%AddChild(layoutCol) 
	      set rec=$get(@refParam@("layout",row))
 	      set controlName=$listget(rec,1)
 	      set description=$listget(rec,2)
 	      //Label 
 	      set layoutV=##class(%ZEN.Component.vgroup).%New()
	      set layoutV.width=maxWidthLabel
	      do layoutCol.%AddChild(layoutV)
	      set ctrl=##class(%ZEN.Component.label).%New()
	      set ctrl.id="Label"_row
	      set ctrl.value=description
	      set ctrl.width=maxWidthLabel 
	      do layoutV.%AddChild(ctrl) 
	      // 
	      set rec=@refParam@("par",controlName)
	      set typeControl=$listget(rec,1)
	      set initValue=$listget(rec,3)
		  set layoutV=##class(%ZEN.Component.vgroup).%New()
	      set layoutV.width=width
	      do layoutCol.%AddChild(layoutV)
	
	      kill ctrl set ctrl=""
	      // Text   ----------------------------------------
	      if typeControl="Text" {
		      set ctrl=..getText(controlName, initValue, rec, size)
	     	  do layoutV.%AddChild(ctrl)  
	      }
	      // DateText   ------------------------------------ 
	      if typeControl="DateText" {
	    	  set ctrl=..getDateText(controlName, initValue, rec)
	    	  do layoutV.%AddChild(ctrl)  
	      }
	      // CheckBox  -------------------------------------
	      if typeControl="CheckBox" {
		       set ctrl=..getCheckBox(controlName, initValue, rec)
               do layoutV.%AddChild(ctrl)  
	      }
	      // RadioButton  ----------------------------------
	      if typeControl="RadioButton" {
		      ;do ..getRadioButton(controlName, initValue, rec, refParam )
	          ;set rs=##class(%ZEN.Component.radioSet).%New()
	    	  ;set rs.id=controlName
	    	  ;set rs.name=controlName
	       	  set key=""
	    	  for {
		    	 set key=$order(@refParam@("par",controlName,"data",key)) if key="" quit
		    	 set text=$get(@refParam@("par",controlName,"data",key))
		    	 set ctrl=##class(%ZEN.Component.radioButton).%New()  
		    	 set ctrl.id=controlName_key
		    	 set ctrl.name=controlName
		    	 set ctrl.caption=$select(text'="":text,1:key)
		    	 set ctrl.optionValue=key
		    	 if key=initValue set ctrl.value=key ;,rs.value=key 
		    	 do layoutV.%AddChild(ctrl)
	    	  }
	      } 
	      // ComboBox  ----------------------------------
	      if typeControl="ComboBox" {
		      set ctrl=..getComboBox(controlName, initValue, rec, refParam, size )
	   	      do layoutV.%AddChild(ctrl)

	      }
	       // ListBox  ----------------------------------
	       if typeControl="ListBox" {
		      set ctrl=..getListBox(controlName, initValue, rec, refParam, width)
	   	      do layoutV.%AddChild(ctrl)
	      }
	      // Table  -------------------------------------
	      if typeControl="Table"  
	         {
		      set ctrl=..getListBox(controlName, initValue, rec, refParam, width )
	   	      do layoutV.%AddChild(ctrl)
	         }  
		 
          // onchange
	       // добавление обработчика onchange (данный элемент par является параметром для другого элемента)
	       if $IsObject(ctrl),$data(@refParam@("param",controlName)) {
		      set parChange="",stringCompChange=""
		      for {
			     set parChange=$order(@refParam@("param",controlName,parChange)) if parChange="" quit
			     set stringCompChange=stringCompChange_$select(stringCompChange'="":",",1:"")_parChange
		      }
		      if stringCompChange'="" Set ctrl.onchange="zenPage.changeParam('"_ctrl.id_"','"_stringCompChange_"');" 
	       }
	       
	      Set spacer = ##class(%ZEN.Component.spacer).%New()
	      Set spacer.width=5
          do layoutCol.%AddChild(spacer)    
	  }
   } catch(exception) 
   {
	   set ok=exception.AsStatus()	
   } 
   quit ok
]]></Implementation>
</Method>
</Class>
</Export>
